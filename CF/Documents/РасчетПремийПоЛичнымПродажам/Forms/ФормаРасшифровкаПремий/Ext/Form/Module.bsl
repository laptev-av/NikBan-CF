#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Параметры.Свойство("ФизЛицо", ФизЛицо);
	Параметры.Свойство("АдресВХранилищеПремииПоСтроке", АдресВХранилищеПремииПоСтроке);
	Заголовок = Строка(ФизЛицо);
	
	ТаблицаПремий.Загрузить(ПолучитьИзВременногоХранилища(АдресВХранилищеПремииПоСтроке));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		РезультатыРедактирования = Новый Структура;
		РезультатыРедактирования.Вставить("Модифицированность", Модифицированность);
		РезультатыРедактирования.Вставить("ФизЛицо", ФизЛицо);
		РезультатыРедактирования.Вставить("АдресВХранилищеПремииПоСтроке", АдресВХранилищеПремииПоСтроке());
		
		Модифицированность = Ложь;
		Закрыть(РезультатыРедактирования)

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция АдресВХранилищеПремииПоСтроке()
	
	ТаблицаПремийИзм = ТаблицаПремий.Выгрузить();
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаПремийИзм);
	
КонецФункции

&НаКлиенте
Процедура ТаблицаПремийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПремийПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если Элемент.ТекущиеДанные.СуммаПремии = 0 Тогда
		ИдентификаторСтроки = Элементы.ТаблицаПремий.ТекущаяСтрока;
	
		Если ИдентификаторСтроки <> Неопределено Тогда
			ЭлементКоллекции = ТаблицаПремий.НайтиПоИдентификатору(ИдентификаторСтроки);
	    	ИндексЭлементаКоллекции = ТаблицаПремий.Индекс(ЭлементКоллекции);
	    	ТаблицаПремий.Удалить(ИндексЭлементаКоллекции);
			Модифицированность = Ложь;
	   	КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти