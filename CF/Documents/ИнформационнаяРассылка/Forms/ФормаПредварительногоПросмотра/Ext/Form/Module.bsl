#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ТекстСообщения") Тогда
		ТекстСообщения = Параметры.ТекстСообщения;
	КонецЕсли;
	
	Если Параметры.Свойство("ТипРассылки") Тогда
		ТипРассылки = Параметры.ТипРассылки;
	КонецЕсли;
	
	Если ТипРассылки = Перечисления.ТипыИнформационныхРассылок.SMS Тогда
		Если Параметры.Свойство("ОтправлятьВТранслите") Тогда
			ОтправлятьВТранслите = Параметры.ОтправлятьВТранслите;
		КонецЕсли;
		Элементы.ОсталосьСимволов.Видимость = Истина;
		ОсталосьСимволов = СформироватьИнформационнуюНадписьКоличествоСимволовСообщений();
	Иначе
		Элементы.ОсталосьСимволов.Видимость = Ложь;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "Документ.ИнформационнаяРассылка.Форма.ФормаПредварительногоПросмотра.Открытие");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьИнформационнуюНадписьКоличествоСимволовСообщений()
	
	Если ОтправлятьВТранслите Тогда
		СимволовВСообщении = 140;
		НовыйТекст = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(ТекстСообщения);
		ЧислоСимволов = СтрДлина(НовыйТекст);
	Иначе
		СимволовВСообщении = 50;
		ЧислоСимволов = СтрДлина(ТекстСообщения);
	КонецЕсли;
	КоличествоСообщений   = Цел(ЧислоСимволов / СимволовВСообщении) + 1;
	ОсталосьСимволов      = СимволовВСообщении - ЧислоСимволов % СимволовВСообщении;
	ШаблонТекстаСообщения = НСтр("ru = 'Сообщение - %1, осталось символов - %2'");
	СтрокаРезультат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонТекстаСообщения,
						КоличествоСообщений,
						ОсталосьСимволов);
	Возврат СтрокаРезультат;

КонецФункции

#КонецОбласти
