&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивНомеровСтрок") Тогда
		
		Для каждого ЭлементМассива Из Параметры.МассивНомеровСтрок Цикл
			
			СтрокаТаблицы = ИдентификаторыСтрок.Добавить();
			СтрокаТаблицы.ИдентификаторСтроки = ЭлементМассива;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Параметры.Свойство("АдресТаблицы") Тогда
		ТаблицаТоварыПоДаннымПоставщика = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицы);
		ОбщегоНазначенияРТ.ЗагрузитьВТаблицуЗначений(ТаблицаТоварыПоДаннымПоставщика , ТоварыПоДаннымПоставщика, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНоменклатуру(Команда)
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("ВидНоменклатуры");
	СтруктураРеквизитов.Вставить("ЕдиницаИзмерения");
	СтруктураРеквизитов.Вставить("СтавкаНДС");
	
	Если ОбщегоНазначенияРТКлиент.ПроверитьЗаполнениеРеквизитов(ЭтотОбъект, СтруктураРеквизитов) Тогда
		АдресВХранилище = СоздатьНоменклатуруНаСервере();
		Закрыть(АдресВХранилище);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СоздатьНоменклатуруНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	СоответствиеНоменклатур = Новый Соответствие;
	Для каждого СтрокаТаблицы Из ИдентификаторыСтрок Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ИдентификаторСтроки", СтрокаТаблицы.ИдентификаторСтроки);
		
		МассивСтрок = ТоварыПоДаннымПоставщика.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСтрок.Количество() > 0 Тогда
			
			СтрокаТаблицыЕГАИС   = МассивСтрок[0];
			АлкогольнаяПродукция = СтрокаТаблицыЕГАИС.АлкогольнаяПродукция;
			
			НоменклатураСсылка = СоответствиеНоменклатур.Получить(АлкогольнаяПродукция);
			Если НоменклатураСсылка = Неопределено Тогда
				НоменклатураОбъект   = Справочники.Номенклатура.СоздатьЭлемент();
				НоменклатураОбъект.Наименование = АлкогольнаяПродукция.Наименование;
				НоменклатураОбъект.Родитель     = Родитель;
				НоменклатураОбъект.АлкогольнаяПродукция = Истина;
				НоменклатураОбъект.ВидНоменклатуры      = ВидНоменклатуры;
				НоменклатураОбъект.ЕдиницаИзмерения     = ЕдиницаИзмерения;
				НоменклатураОбъект.НаименованиеПолное   = НоменклатураОбъект.Наименование;
				НоменклатураОбъект.ТоварнаяГруппа		= ТоварнаяГруппа;
				НоменклатураОбъект.Производитель        = Контрагент;
				НоменклатураОбъект.ОбъемДАЛ             = АлкогольнаяПродукция.Объем / 10;
				НоменклатураОбъект.СтавкаНДС            = СтавкаНДС;
				НоменклатураОбъект.ВидАлкогольнойПродукцииЕГАИС = АлкогольнаяПродукция.ВидПродукции;
				НоменклатураОбъект.Записать();
				
				СоответствиеНоменклатур.Вставить(АлкогольнаяПродукция, НоменклатураОбъект.Ссылка);
				СтрокаТаблицыЕГАИС.Номенклатура = НоменклатураОбъект.Ссылка;
			Иначе
				СтрокаТаблицыЕГАИС.Номенклатура = НоменклатураСсылка;
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат АдресВХранилище();
	
КонецФункции

&НаСервере
Функция АдресВХранилище()
	
	ВозвращаемыйАдрес = ПоместитьВоВременноеХранилище(ТоварыПоДаннымПоставщика.Выгрузить(), УникальныйИдентификатор);
	
	Возврат ВозвращаемыйАдрес;

КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "Документ.ПоступлениеТоваров.Форма.ФормаСозданияНоменклатур.Открытие");

КонецПроцедуры


