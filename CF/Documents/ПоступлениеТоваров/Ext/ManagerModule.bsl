#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт расхождений
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктРасхождений";
	КомандаПечати.Представление = НСтр("ru = 'Акт расхождений'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// М-4 (Приходный ордер)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "М4";
	КомандаПечати.Представление = НСтр("ru = 'М-4 (Приходный ордер)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Приходная накладная (по розничным и ценам поступления).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Приходная накладная (по розничным и ценам поступления)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьЦены", Истина);
	
	// Приходная накладная (по розничным ценам).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Приходная накладная (по розничным ценам)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьЦены", Истина);
	КомандаПечати.ДополнительныеПараметры.Вставить("ТолькоРозничныеЦены", Истина);
	
	// Приходная накладная (по ценам поступления).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Приходная накладная (по ценам поступления)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьЦены", Ложь);
	
	// Опись номенклатуры
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ОписьНоменклатуры";
	КомандаПечати.Представление = НСтр("ru = 'Опись номенклатуры'");
	КомандаПечати.ДополнительныеПараметры.Вставить("Представление", КомандаПечати.Представление);
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// ТОРГ-12 (Товарная накладная за поставщика с услугами).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ12";
	КомандаПечати.Представление = НСтр("ru = 'ТОРГ-12 (Товарная накладная за поставщика с услугами)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Истина);
	
	// ТОРГ-12 (Товарная накладная за поставщика).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ12";
	КомандаПечати.Представление = НСтр("ru = 'ТОРГ-12 (Товарная накладная за поставщика)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
	
	// ТОРГ-2 (Акт об установленном расхождении).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ2";
	КомандаПечати.Представление = НСтр("ru = 'ТОРГ-2 (Акт об установленном расхождении)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// ТОРГ-4 (Акт о приемке товара, поступившего без счета поставщика).
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТОРГ4";
	КомандаПечати.Представление = НСтр("ru = 'ТОРГ-4 (Акт о приемке товара, поступившего без счета поставщика)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	ДоступноПечатьЭтикетокИЦенников = РольДоступна("ПолныеПрава") 
									  Или РольДоступна("ПечатьЭтикетокИЦенников");
	
	Если ДоступноПечатьЭтикетокИЦенников Тогда
	
		// Ценники
		ПараметрыПечатиЦенников = Новый Структура;
		ПараметрыПечатиЦенников.Вставить("ИмяПроцедурыПодготовкиСтруктурыДанных", "УправлениеПечатьюРТВызовСервера.ПодготовитьСтруктуруДанныхЦенниковИЭтикеток");
		ПараметрыПечатиЦенников.Вставить("УстановитьРежим", "ПечатьЦенников");
		ПараметрыПечатиЦенников.Вставить("ИмяДокумента", "ПоступлениеТоваров");
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Ценники";
		КомандаПечати.Представление = НСтр("ru = 'Ценники'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Обработчик = "УправлениеПечатьюРТКлиент.ОбработкаКомандыПечатиЦенниковИЭтикеток";
		КомандаПечати.ДополнительныеПараметры.Вставить("ПараметрыПечатиЦенниковИЭтикеток", ПараметрыПечатиЦенников);
		
		// Этикетки
		ПараметрыПечатиЭтикеток = Новый Структура;
		ПараметрыПечатиЭтикеток.Вставить("ИмяПроцедурыПодготовкиСтруктурыДанных", "УправлениеПечатьюРТВызовСервера.ПодготовитьСтруктуруДанныхЦенниковИЭтикеток");
		ПараметрыПечатиЭтикеток.Вставить("УстановитьРежим", "ПечатьЭтикеток");
		ПараметрыПечатиЭтикеток.Вставить("ИмяДокумента", "ПоступлениеТоваров");
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "Этикетки";
		КомандаПечати.Представление = НСтр("ru = 'Этикетки'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
		КомандаПечати.Обработчик = "УправлениеПечатьюРТКлиент.ОбработкаКомандыПечатиЦенниковИЭтикеток";
		КомандаПечати.ДополнительныеПараметры.Вставить("ПараметрыПечатиЦенниковИЭтикеток", ПараметрыПечатиЭтикеток);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//	Объект - ДокументОбъект или ДанныеФормыСтруктура - документ, для которого нужно сформировать параметры проверки.
//
// Возвращаемое значение:
//	Структура - Состав полей определяется требованиями функции
//	            ОбработкаТабличнойЧастиСервер.ЗаполнитьСтатусыУказанияСерий.
//
Функция ПараметрыУказанияСерий(Объект)Экспорт
	
	ПоляСвязи = Новый Массив;
	
	ПараметрыУказанияСерий = Новый Структура;
	ИспользованиеСерийСклад = Ложь;
	
	ИспользованиеСерийСклад = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	ПараметрыУказанияСерий.Вставить("ИспользоватьСерииНоменклатуры", ИспользованиеСерийСклад);
	ПараметрыУказанияСерий.Вставить("ПоляСвязи",ПоляСвязи);
	ПараметрыУказанияСерий.Вставить("ЭтоНакладная", Истина);
	
	СкладскиеОперации = Новый Массив;
	СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаОтПоставщика);
	
	ПараметрыУказанияСерий.Вставить("СкладскиеОперации", СкладскиеОперации);
	
	ПараметрыУказанияСерий.Вставить("ОперацияДокумента", Объект.ХозяйственнаяОперация);
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Инициализирует таблицы значений, содержащие данные документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	ИспользоватьКомиссионнуюТорговлю = ПолучитьФункциональнуюОпцию("ИспользоватьКомиссионнуюТорговлю");
	ХозяйственнаяОперация            = ДокументСсылка.ХозяйственнаяОперация;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст ="ВЫБРАТЬ
	|	ДанныеДокумента.Дата                                            КАК Период,
	|	ДанныеДокумента.Ссылка                                          КАК Ссылка,
	|	ДанныеДокумента.Организация                                     КАК Организация,
	|	ДанныеДокумента.Склад                                           КАК Склад,
	|	ДанныеДокумента.Магазин                                         КАК Магазин,
	|	ДанныеДокумента.Контрагент                                      КАК Контрагент,
	|	ДанныеДокумента.Договор                                         КАК Договор, 	
	|	ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПоступлениеТоваров)   КАК АналитикаХозяйственнойОперации,
	|	ДанныеДокумента.Ссылка                                          КАК Основание,
	|	ДанныеДокумента.Магазин.ИспользоватьОрдернуюСхемуПриПоступлении КАК ПоступлениеПоОрдернойСхеме,
	|	ДанныеДокумента.ЗаказПоставщику                                 КАК ЗаказПоставщику,
	|	ДанныеДокумента.ЦенаВключаетНДС                                 КАК ЦенаВключаетНДС,
	|	ДанныеДокумента.УчитыватьНДС                                    КАК УчитыватьНДС,
	|	ДанныеДокумента.СуммаДокумента                                  КАК СуммаДокумента,
	|	(НЕ ДанныеДокумента.Магазин.СкладУправляющейСистемы)            КАК ФормироватьДвижения
	|ИЗ
	|	Документ.ПоступлениеТоваров                                     КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	РезультатЗапроса = Запрос.Выполнить();
	
	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий(); 
	
	ОбщегоНазначенияРТ.ПеренестиСтрокуВыборкиВПараметрыЗапроса(РезультатЗапроса, Реквизиты, Запрос);
	Запрос.УстановитьПараметр("ПоступлениеПоЗаказу",                     ЗначениеЗаполнено(Реквизиты.ЗаказПоставщику));
	
	
	НастройкаСпособаУчета      = ЗапасыСервер.ПолучитьСпособУчетаСебестоимости(Реквизиты.Период);
	СпособУчетаСебестоимости   = НастройкаСпособаУчета.СпособУчета;
	РаспределятьСтоимостьУслуг = НастройкаСпособаУчета.РаспределятьСтоимостьУслуг;
	ПравилоРаспределения       = НастройкаСпособаУчета.ПравилоРаспределения;
	
	Запрос.УстановитьПараметр("ИспользоватьУчетСебестоимости", ПолучитьФункциональнуюОпцию("ИспользоватьУчетСебестоимости"));
	Запрос.УстановитьПараметр("СпособУчетаСебестоимости",      СпособУчетаСебестоимости);
	Запрос.УстановитьПараметр("СебестоимостьВключаетНДС",      Константы.СебестоимостьВключаетНДС.Получить());
	
	ПериодОбновления = Константы.ПериодОбновленияПоставляемойНоменклатуры.Получить();
	Запрос.УстановитьПараметр("ПериодОбновления", ПериодОбновления);
	
	ТекстЗапроса =
	"//0 ВтТаблицаТовары
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки                  КАК НомерСтроки,
	|	ТаблицаТовары.Ссылка                       КАК Ссылка,
	|	ТаблицаТовары.Номенклатура                 КАК Номенклатура,
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТовары.Характеристика               КАК Характеристика,
	|	ТаблицаТовары.НомерГТД                     КАК НомерГТД,
	|	ТаблицаТовары.Количество                   КАК Количество,
	|	&Склад КАК Склад,
	|	&АналитикаХозяйственнойОперации            КАК АналитикаХозяйственнойОперации,
	|	ТаблицаТовары.КлючСвязиСерийныхНомеров     КАК КлючСвязиСерийныхНомеров,
	|	&Основание                                 КАК Основание,
	|	&Контрагент                                КАК Контрагент,
	|	&Магазин                                   КАК Магазин,
	|	ТаблицаТовары.Цена                         КАК Цена,
	|	ВЫБОР
	|		КОГДА НЕ &УчитыватьНДС
	|				ИЛИ &ЦенаВключаетНДС
	|			ТОГДА ТаблицаТовары.Цена
	|		ИНАЧЕ ТаблицаТовары.Цена + (ВЫРАЗИТЬ(ТаблицаТовары.СуммаНДС / ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(15, 2)))
	|	КОНЕЦ КАК ЦенаСНДС,
	|	ТаблицаТовары.Упаковка                     КАК Упаковка,
	|	ВЫБОР
	|		КОГДА НЕ &УчитыватьНДС
	|			ТОГДА ТаблицаТовары.Сумма
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ЦенаВключаетНДС
	|					ТОГДА ТаблицаТовары.Сумма
	|				ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
	|			КОНЕЦ
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетСебестоимости ТОГДА
	|			0
	|		КОГДА &СебестоимостьВключаетНДС = &ЦенаВключаетНДС
	|			ИЛИ (НЕ &УчитыватьНДС)
	|			ТОГДА ТаблицаТовары.Сумма
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &ЦенаВключаетНДС
	|					ТОГДА ТаблицаТовары.Сумма - ТаблицаТовары.СуммаНДС
	|				ИНАЧЕ ТаблицаТовары.Сумма + ТаблицаТовары.СуммаНДС
	|			КОНЕЦ
	|	КОНЕЦ                                      КАК Себестоимость
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары         КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И &ФормироватьДвижения
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 1 Услуги к распределению на себестоимость.
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Услуга,
	|	Товары.Сумма КАК Сумма
	|	ИЗ
	|		ВтТаблицаТовары КАК Товары
	|	ГДЕ
	|	&ФормироватьДвижения
	|	И &ИспользоватьУчетСебестоимости
	|	И Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//2 ТаблицаТоварыНаСкладах
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаТовары.Склад                    КАК Склад,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК Количество,
	|	ТаблицаТовары.АналитикаХозяйственнойОперации КАК АналитикаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаТовары                        КАК ТаблицаТовары
	|ГДЕ 
	|   НЕ &ПоступлениеПоОрдернойСхеме
	|   И &ФормироватьДвижения
	|   И НЕ ТаблицаТовары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	//3 ТаблицаТоварыКПоступлению
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаТовары.Склад                    КАК Склад,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК Количество,
	|	ВЫБОР
	|		КОГДА
	|			&ПоступлениеПоЗаказу
	|		ТОГДА
	|			&ЗаказПоставщику
	|		ИНАЧЕ
	|			ТаблицаТовары.Ссылка
	|	КОНЕЦ                                  КАК ДокументОснование
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	&ПоступлениеПоОрдернойСхеме
	|	И &ФормироватьДвижения
	|	И НЕ ТаблицаТовары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	//4 ТаблицаСебестоимостьНоменклатуры
	|ВЫБРАТЬ
	|   &Период                               КАК Период,
	|	ТаблицаТовары.Номенклатура            КАК Номенклатура,
	|	ТаблицаТовары.Характеристика          КАК Характеристика,
	|	ТаблицаТовары.Магазин                 КАК Магазин,
	|	СУММА(ТаблицаТовары.Количество)       КАК Количество,
	|	СУММА(ТаблицаТовары.Себестоимость) / ВЫБОР
	|		КОГДА СУММА(ТаблицаТовары.Количество) = 0
	|			ТОГДА 1
	|		ИНАЧЕ СУММА(ТаблицаТовары.Количество)
	|	КОНЕЦ                                 КАК Цена,
	|	СУММА(ТаблицаТовары.Сумма)            КАК Сумма
	|ИЗ
	|	ВтТаблицаТовары                       КАК ТаблицаТовары
	|ГДЕ 
	|    &ИспользоватьУчетСебестоимости
	|    И &СпособУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыУчетаСебестоимости.АвтономныйУчет)
	|    И &ФормироватьДвижения
	|    И НЕ ТаблицаТовары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Магазин
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура, Характеристика
	|;
	|////////////////////////////////////////////////////////////////////////////////
	//5 ТаблицаНоменклатураПоставщиков
	|ВЫБРАТЬ
	|	&Период КАК ДатаПоследнегоПоступления,
	|	ТаблицаТовары.Магазин КАК Магазин,
	|	ТаблицаТовары.Контрагент КАК Поставщик,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(СПРАВОЧНИК.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.ЦенаСНДС
	|		ИНАЧЕ ТаблицаТовары.ЦенаСНДС / ТаблицаТовары.Упаковка.Коэффициент
	|	КОНЕЦ КАК Цена
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураПоставщиков КАК НоменклатураПоставщиков
	|		ПО (НоменклатураПоставщиков.Поставщик = ТаблицаТовары.Контрагент)
	|			И (НоменклатураПоставщиков.Магазин = ТаблицаТовары.Магазин)
	|			И (НоменклатураПоставщиков.Номенклатура = ТаблицаТовары.Номенклатура)
	|			И (НоменклатураПоставщиков.Характеристика = ТаблицаТовары.Характеристика)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПоставщикиНоменклатуры КАК ПоставщикиНоменклатуры
	|		ПО ТаблицаТовары.Контрагент = ПоставщикиНоменклатуры.Поставщик
	|ГДЕ
	|	(НЕ ЕСТЬNULL(ТаблицаТовары.Контрагент.УчастникЭДО, Истина))
	|	И (НоменклатураПоставщиков.Поставщик ЕСТЬ NULL ИЛИ НоменклатураПоставщиков.ДатаПоследнегоПоступления <= &Период) 
	|	И &ФормироватьДвижения И &ПериодОбновления <= &Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Магазин,
	|	ТаблицаТовары.Контрагент,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.НомерСтроки,
	|	ТаблицаТовары.Упаковка,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(СПРАВОЧНИК.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.ЦенаСНДС
	|		ИНАЧЕ ТаблицаТовары.ЦенаСНДС / ТаблицаТовары.Упаковка.Коэффициент
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	//6 ТаблицаТоварыОрганизаций
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	&Склад                                 КАК Склад,";
	Если ИспользоватьКомиссионнуюТорговлю
		И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
		ТекстЗапроса = ТекстЗапроса + "
								|	&Контрагент  КАК Поставщик,
								|	&Договор     КАК Договор,";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.НомерГТД                 КАК НомерГТД,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК Количество
	|ИЗ
	|	ВтТаблицаТовары                        КАК ТаблицаТовары
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И &ФормироватьДвижения
	|	И НЕ ТаблицаТовары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	//7 ТаблицаЗаказыТоваров
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&ЗаказПоставщику                       КАК Заказ,
	|	ТаблицаТовары.Магазин                  КАК Магазин,
	|	ТаблицаТовары.Контрагент               КАК Поставщик,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК Количество
	|ИЗ
	|	ВтТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|   &ФормироватьДвижения
	|   И &ПоступлениеПоЗаказу
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////
	//8 ТаблицаЗакупкиТоваров
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаТовары.Магазин                  КАК Магазин,
	|	ТаблицаТовары.Контрагент               КАК Поставщик,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК Количество,
	|	ТаблицаТовары.Сумма                    КАК Сумма,
	|	ТаблицаТовары.АналитикаХозяйственнойОперации КАК АналитикаХозяйственнойОперации
	|ИЗ
	|ВтТаблицаТовары КАК ТаблицаТовары
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 9 ТаблицаДвиженияСерийТоваров
	|ВЫБРАТЬ
	|	ТаблицаСерии.Номенклатура КАК Номенклатура,
	|	ТаблицаСерии.Характеристика КАК Характеристика,
	|	ТаблицаСерии.Серия КАК Серия,
	|	ТаблицаСерии.Количество КАК Количество,
	|	&Магазин КАК Магазин,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаОтПоставщика) КАК СкладскаяОперация,
	|	&Ссылка КАК Документ,
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ТаблицаСерии.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.ПоступлениеТоваров.Серии КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|	И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	И &ФормироватьДвижения
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 10 ТаблицаРасчетыСПоставщиками
	|	ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Магазин                               КАК Магазин,
	|	&Контрагент                            КАК Поставщик,
	|	ВЫБОР КОГДА &ПоступлениеПоЗаказу ТОГДА
	|			&ЗаказПоставщику 
	|		  ИНАЧЕ &Ссылка КОНЕЦ              КАК ДокументРасчета,
	|	&СуммаДокумента                        КАК Сумма,
	|	&СуммаДокумента                        КАК КОплате,
	|	0                                      КАК КПоступлению,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПустаяСсылка) КАК ФормаОплаты,
	|	ЛОЖЬ                                   КАК Взаимозачет
	|ГДЕ
	|	&ФормироватьДвижения
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Магазин                               КАК Магазин,
	|	&Контрагент                            КАК Поставщик,
	|	&Ссылка                                КАК ДокументРасчета,
	|	ЭтапыОплат.Сумма                       КАК Сумма,
	|	ЭтапыОплат.Сумма                       КАК КОплате,
	|	0                                      КАК КПоступлению,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Взаимозачет) КАК ФормаОплаты,
	|	ЛОЖЬ                                   КАК Взаимозачет
	|ИЗ
	|	Документ.ПоступлениеТоваров.ЭтапыОплат КАК ЭтапыОплат
	|ГДЕ
	|	ЭтапыОплат.Ссылка = &Ссылка
	|	И ЭтапыОплат.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Взаимозачет)
	|	И &ФормироватьДвижения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Магазин                               КАК Магазин,
	|	&Контрагент                            КАК Поставщик,
	|	ЭтапыОплат.ДокументВзаимозачета        КАК ДокументРасчета,
	|	ЭтапыОплат.Сумма                       КАК Сумма,
	|	ЭтапыОплат.Сумма                       КАК КОплате,
	|	0                                      КАК КПоступлению,
	|	ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.ПустаяСсылка) КАК ФормаОплаты,
	|	ИСТИНА                                 КАК Взаимозачет
	|ИЗ
	|	Документ.ПоступлениеТоваров.ЭтапыОплат КАК ЭтапыОплат
	|ГДЕ
	|	ЭтапыОплат.Ссылка = &Ссылка
	|	И ЭтапыОплат.ФормаОплаты = ЗНАЧЕНИЕ(Перечисление.ФормыОплаты.Взаимозачет)
	|	И &ФормироватьДвижения
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 11 ТаблицаСебестоимостьПоставкиТоваров
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	&Период                                КАК Период,
	|	ТаблицаТовары.Магазин                  КАК Магазин,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК Количество,
	|	ТаблицаТовары.Цена                     КАК Цена,
	|	ТаблицаТовары.Себестоимость            КАК Сумма
	|ИЗ
	|ВтТаблицаТовары КАК ТаблицаТовары
	|ГДЕ 
	|	&ИспользоватьУчетСебестоимости
	|	И &СпособУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыУчетаСебестоимости.АвтономныйУчетСредняяВзвешенная)
	|	И &ФормироватьДвижения
	|	И НЕ ТаблицаТовары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|";
	
	ТекстЗапроса = ТекстЗапроса + ТекстЗапросаТаблицаСерийныхНомеров();
	Запрос.Текст = ТекстЗапроса;
	Результат    = Запрос.ВыполнитьПакет();
	
	ТаблицаСебестоимостьНоменклатуры    = Результат[4].Выгрузить();
	ТаблицаСебестоимостьПоставкиТоваров = Результат[11].Выгрузить();
	ТаблицаУслугНаРаспределение         = Результат[1].Выгрузить();
	
	Если РаспределятьСтоимостьУслуг Тогда
		ЗапасыСервер.РаспределитьУслугиНаСебестоимость(ТаблицаУслугНаРаспределение, ПравилоРаспределения, ТаблицаСебестоимостьНоменклатуры, СпособУчетаСебестоимости, ТаблицаСебестоимостьПоставкиТоваров);
	КонецЕсли;
	
	ТаблицыДляДвижений = ДополнительныеСвойства.ТаблицыДляДвижений;
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыНаСкладах"              , Результат[2].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыКПоступлению"           , Результат[3].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСебестоимостьНоменклатуры"    , ТаблицаСебестоимостьНоменклатуры);
	ТаблицыДляДвижений.Вставить("ТаблицаНоменклатураПоставщиков"      , Результат[5].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаТоварыОрганизаций"            , Результат[6].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаЗаказыТоваров"                , Результат[7].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаЗакупкиТоваров"               , Результат[8].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаДвиженияСерийТоваров"         , Результат[9].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками"         , Результат[10].Выгрузить());
	ТаблицыДляДвижений.Вставить("ТаблицаСебестоимостьПоставкиТоваров" , ТаблицаСебестоимостьПоставкиТоваров);
	ТаблицыДляДвижений.Вставить("ТаблицаСерийныхНомеров"              , Результат[18].Выгрузить());
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую.
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", 
			НСтр("ru = 'Приходная накладная'") + ?(ПараметрыПечати["ВыводитьЦены"], " " + НСтр("ru = '(с розничными ценами)'"), ""), 
			ПечатьНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати["ВыводитьЦены"], ПараметрыПечати.Свойство("ТолькоРозничныеЦены")));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОписьНоменклатуры") Тогда
	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОписьНоменклатуры",
				ПараметрыПечати.Представление,
				ПечатьОписиНоменклатуры(МассивОбъектов, ОбъектыПечати));
	
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ12") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"ТОРГ12",
			НСтр("ru = 'ТОРГ-12 (Товарная накладная за поставщика'") + ?(ПараметрыПечати["ВыводитьУслуги"], "", " " + НСтр("ru = 'без услуг'")) + ")", 
			ПечатьТОРГ12(МассивОбъектов, ОбъектыПечати, ПараметрыПечати["ВыводитьУслуги"]));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "М4") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,"М4",
			НСтр("ru = 'М-4 (Приходный ордер)'"),
			ПечатьМ4(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ4") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ4",
			НСтр("ru = 'ТОРГ-4 (Акт о приемке товара, поступившего без счета поставщика)'"),
			ПечатьТОРГ4(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТОРГ2") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТОРГ2",
			НСтр("ru = 'ТОРГ-2 (Акт о расхождении)'"),
			ПечатьТОРГ2(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктРасхождений") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктРасхождений",
			НСтр("ru = 'Акт расхождений'"),
			ПечатьАктРасхождений(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;

КонецПроцедуры

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// СтандартныеПодсистемы.ШаблоныСообщений

////////////////////////////////////////////////////////////////////////////////
// Шаблоны сообщений

// Вызывается при подготовке шаблонов сообщений и позволяет переопределить список реквизитов и вложений.
//
// Параметры:
//  Реквизиты               - ДеревоЗначений - список реквизитов шаблона.
//         ** Имя            - Строка - Уникальное имя общего реквизита.
//         ** Представление  - Строка - Представление общего реквизита.
//         ** Тип            - Тип    - Тип реквизита. По умолчанию строка.
//         ** Подсказка      - Строка - Расширенная информация о реквизите.
//         ** Формат         - Строка - Формат вывода значения для чисел, дат, строк и булевых значений.
//  Вложения                - ТаблицаЗначений - печатные формы и вложения
//         ** Имя            - Строка - Уникальное имя вложения.
//         ** Представление  - Строка - Представление варианта.
//         ** Подсказка      - Строка - Расширенная информация о вложении.
//         ** ТипФайла       - Строка - Тип вложения, который соответствует расширению файла: "pdf", "png", "jpg", mxl" и др.
//  ДополнительныеПараметры - Структура - дополнительные сведения о шаблоне сообщений.
//
Процедура ПриПодготовкеШаблонаСообщения(Реквизиты, Вложения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Вызывается в момент создания сообщений по шаблону для заполнения значений реквизитов и вложений.
//
// Параметры:
//  Сообщение - Структура - структура с ключами:
//    * ЗначенияРеквизитов - Соответствие - список используемых в шаблоне реквизитов.
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * ЗначенияОбщихРеквизитов - Соответствие - список используемых в шаблоне общих реквизитов.
//      ** Ключ     - Строка - имя реквизита в шаблоне;
//      ** Значение - Строка - значение заполнения в шаблоне.
//    * Вложения - Соответствие - значения реквизитов 
//      ** Ключ     - Строка - имя вложения в шаблоне;
//      ** Значение - ДвоичныеДанные, Строка - двоичные данные или адрес во временном хранилище вложения.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//  ДополнительныеПараметры - Структура -  Дополнительная информация о шаблоне сообщения.
//
Процедура ПриФормированииСообщения(Сообщение, ПредметСообщения, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

// Заполняет список получателей SMS при отправке сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиSMS - ТаблицаЗначений - список получается SMS.
//     * НомерТелефона - Строка - номер телефона, куда будет отправлено сообщение SMS.
//     * Представление - Строка - представление получателя сообщения SMS.
//     * Контакт       - СправочникСсылка - контакт, которому принадлежит номер телефона.
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииТелефоновПолучателейВСообщении(ПолучателиSMS, ПредметСообщения) Экспорт
	
	ШаблоныСообщений.ЗаполнитьПолучателей(ПолучателиSMS, ПредметСообщения, "Контрагент", Перечисления.ТипыКонтактнойИнформации.Телефон);
	
КонецПроцедуры

// Заполняет список получателей письма при отправки сообщения сформированного по шаблону.
//
// Параметры:
//   ПолучателиПисьма - ТаблицаЗначений - список получается письма.
//     * Адрес           - Строка - адрес электронной почты получателя.
//     * Представление   - Строка - представление получается письма.
//     * ВариантОтправки - Строка - Варианты отправки письма: "Кому", "Копия", "СкрытаяКопия", "ОбратныйАдреса";
//  ПредметСообщения - ЛюбаяСсылка - ссылка на объект являющийся источником данных.
//
Процедура ПриЗаполненииПочтыПолучателейВСообщении(ПолучателиПисьма, ПредметСообщения) Экспорт
	
	ШаблоныСообщений.ЗаполнитьПолучателей(ПолучателиПисьма, ПредметСообщения, "Контрагент");
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ШаблоныСообщений

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаТаблицаСерийныхНомеров()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Товары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	Товары.Количество
	|ПОМЕСТИТЬ ТабТоварыВСЕ
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура,
	|	ТабТовары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	ТабТовары.Количество
	|ПОМЕСТИТЬ ТабТовары
	|ИЗ
	|	ТабТоварыВСЕ КАК ТабТовары
	|ГДЕ
	|	ТабТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СерийныеНомера.СерийныйНомер КАК Справочник.СерийныеНомера) КАК СерийныйНомер,
	|	СерийныеНомера.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	1 КАК Количество
	|ПОМЕСТИТЬ ТабСерийныеНомера
	|ИЗ
	|	Документ.ПоступлениеТоваров.СерийныеНомера КАК СерийныеНомера
	|ГДЕ
	|	СерийныеНомера.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабСерийныеНомера.СерийныйНомер.Владелец КАК Номенклатура,
	|	ТабСерийныеНомера.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	СУММА(ТабСерийныеНомера.Количество) КАК Количество
	|ПОМЕСТИТЬ ТабСвернутыхСерийныхНомеров
	|ИЗ
	|	ТабСерийныеНомера КАК ТабСерийныеНомера
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабСерийныеНомера.СерийныйНомер.Владелец,
	|	ТабСерийныеНомера.КлючСвязиСерийныхНомеров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура,
	|	ТабТовары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	ТабТовары.Количество - ЕСТЬNULL(ТабСвернутыхСерийныхНомеров.Количество, 0) КАК Количество
	|ПОМЕСТИТЬ ТабОстатокНоменклатур
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТабСвернутыхСерийныхНомеров КАК ТабСвернутыхСерийныхНомеров
	|		ПО ТабТовары.КлючСвязиСерийныхНомеров = ТабСвернутыхСерийныхНомеров.КлючСвязиСерийныхНомеров
	|ГДЕ
	|	(НЕ ТабСвернутыхСерийныхНомеров.КлючСвязиСерийныхНомеров ЕСТЬ NULL )
	|	И ТабТовары.Количество - ЕСТЬNULL(ТабСвернутыхСерийныхНомеров.Количество, 0) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура,
	|	ЕСТЬNULL(ТабСерийныеНомера.СерийныйНомер, ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка)) КАК СерийныйНомер,
	|	ВЫБОР
	|		КОГДА ТабСерийныеНомера.СерийныйНомер ЕСТЬ NULL 
	|			ТОГДА ТабТовары.Количество
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ ТабОбщая
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТабСерийныеНомера КАК ТабСерийныеНомера
	|		ПО ТабТовары.КлючСвязиСерийныхНомеров = ТабСерийныеНомера.КлючСвязиСерийныхНомеров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабОстатокНоменклатур.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка),
	|	ТабОстатокНоменклатур.Количество
	|ИЗ
	|	ТабОстатокНоменклатур КАК ТабОстатокНоменклатур
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерийныеНомера.Номенклатура  КАК Номенклатура,
	|	ТаблицаСерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|	ТаблицаСерийныеНомера.Количество    КАК Количество,
	|	&Склад                              КАК Получатель,
	|	&Организация                        КАК Организация,
	|	&АналитикаХозяйственнойОперации     КАК АналитикаХозяйственнойОперации,
	|	&Период КАК Период
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТабОбщая.Номенклатура КАК Номенклатура,
	|		ТабОбщая.СерийныйНомер КАК СерийныйНомер,
	|		СУММА(ТабОбщая.Количество) КАК Количество
	|	ИЗ
	|		ТабОбщая КАК ТабОбщая
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТабОбщая.Номенклатура,
	|		ТабОбщая.СерийныйНомер)         КАК ТаблицаСерийныеНомера
	|ГДЕ
	|	(НЕ &ПоступлениеПоОрдернойСхеме)
	|	И &ФормироватьДвижения";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Выводит многострочное поле в табличный документ.
//
// Параметры:
// 		ТабличныйДокумент - ТабличныйДокумент
// 		Макет - ТабличныйДокумент - Макет табличного документа.
// 		ДанныеПечати - ВыборкаИзРезультатаЗапроса
// 		ИмяПоля - Строка - Имя реквизита результата запроса, имя области макета, имя параметра области макета.
// 		КоличествоСтрокРучногоВвода - Число - Количество строк ручного заполнения.
//
Процедура ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, ИмяПоля, КоличествоСтрокРучногоВвода = 4)
	
	Область = Макет.ПолучитьОбласть(ИмяПоля);
	ТабличныйДокумент.Вывести(Область);
	
	ОбластьСтрокиРучногоЗаполнения = Макет.ПолучитьОбласть("СтрокаРучногоЗаполнения");
	Для Счет =1 По КоличествоСтрокРучногоВвода Цикл
		ТабличныйДокумент.Вывести(ОбластьСтрокиРучногоЗаполнения);
	КонецЦикла;
	
	ОбластьСтрокиПробела = Макет.ПолучитьОбласть("СтрокаПробела");
	ТабличныйДокумент.Вывести(ОбластьСтрокиПробела);
	
КонецПроцедуры

Процедура СформироватьТекстЗапросаТОРГ2(ТекстЗапроса)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПоступлениеТоваров.Ссылка,
	|	ПоступлениеТоваров.Номер КАК НомерДокумента,
	|	ПоступлениеТоваров.Дата КАК ДатаДокумента,
	|	ПоступлениеТоваров.Организация КАК Организация,
	|	ПоступлениеТоваров.Организация.Префикс КАК Префикс,
	|	ПоступлениеТоваров.Магазин КАК ПредставлениеПодразделения,
	|	ПоступлениеТоваров.Контрагент КАК Поставщик,
	|	ПоступлениеТоваров.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	НЕ ПоступлениеТоваров.ЕстьРасхождения КАК ОшибкаНетРасхождений
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровТовары.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровТовары.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровТовары.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТовары.КоличествоУпаковок, 0)) КАК Количество,
	|	ЕСТЬNULL(ПоступлениеТоваровТовары.Цена, 0) КАК Цена,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТовары.Сумма, 0)) КАК Сумма,
	|	ПоступлениеТоваровТовары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТовары.СуммаНДС, 0)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ПоступлениеТоваровТовары.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК НоменклатураНаименование,
	|	ПоступлениеТоваровТовары.Характеристика.Наименование КАК ХарактеристикаНаименование,
	|	ПоступлениеТоваровТовары.Номенклатура.Код КАК Код,
	|	ПоступлениеТоваровТовары.Номенклатура.Артикул КАК Артикул,
	|	ПоступлениеТоваровТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	МИНИМУМ(ПоступлениеТоваровТовары.НомерСтроки + 1000) КАК НомерСтроки
	|ПОМЕСТИТЬ ТоварыПоФакту
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
	|ГДЕ
	|	(ПоступлениеТоваровТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|			ИЛИ ПоступлениеТоваровТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|	И ПоступлениеТоваровТовары.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровТовары.Номенклатура,
	|	ПоступлениеТоваровТовары.Характеристика,
	|	ЕСТЬNULL(ПоступлениеТоваровТовары.Цена, 0),
	|	ПоступлениеТоваровТовары.СтавкаНДС,
	|	ВЫРАЗИТЬ(ПоступлениеТоваровТовары.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)),
	|	ПоступлениеТоваровТовары.Характеристика.Наименование,
	|	ПоступлениеТоваровТовары.Ссылка.ЦенаВключаетНДС,
	|	ПоступлениеТоваровТовары.Номенклатура.Код,
	|	ПоступлениеТоваровТовары.Номенклатура.Артикул,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровТовары.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровТовары.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровТовары.Упаковка
	|	КОНЕЦ,
	|	ПоступлениеТоваровТовары.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.КоличествоУпаковок, 0)) КАК Количество,
	|	ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Цена, 0) КАК Цена,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Сумма, 0)) КАК Сумма,
	|	ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.СтавкаНДС, 0) КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.СуммаНДС, 0)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК НоменклатураНаименование,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика.Наименование КАК ХарактеристикаНаименование,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.Код КАК Код,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.Артикул КАК Артикул,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	МИНИМУМ(ПоступлениеТоваровТоварыПоДаннымПоставщика.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ТоварыПоДокументам
	|ИЗ
	|	Документ.ПоступлениеТоваров.ТоварыПоДаннымПоставщика КАК ПоступлениеТоваровТоварыПоДаннымПоставщика
	|ГДЕ
	|	(ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|			ИЛИ ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|	И ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика,
	|	ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Цена, 0),
	|	ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.СтавкаНДС, 0),
	|	ВЫРАЗИТЬ(ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)),
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика.Наименование,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка.ЦенаВключаетНДС,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.Код,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.Артикул,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка
	|	КОНЕЦ,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТоварыПоДокументам.Ссылка, ТоварыПоФакту.Ссылка) КАК Ссылка,
	|	ЕСТЬNULL(ТоварыПоДокументам.Номенклатура, ТоварыПоФакту.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ТоварыПоДокументам.Характеристика, ТоварыПоФакту.Характеристика) КАК Характеристика,
	|	ЕСТЬNULL(ТоварыПоДокументам.Упаковка, ТоварыПоФакту.Упаковка) КАК Упаковка,
	|	ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) КАК КоличествоПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.Цена, 0) КАК ЦенаПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0) КАК СуммаПоДокументам,
	|	ТоварыПоДокументам.СтавкаНДС КАК СтавкаНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.СуммаНДС, 0) КАК СуммаНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоФакту.Количество, 0) КАК КоличествоПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.Цена, 0) КАК ЦенаПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) КАК СуммаПоФакту,
	|	ТоварыПоФакту.СтавкаНДС КАК СтавкаНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0) КАК СуммаНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоДокументам.Код, ТоварыПоФакту.Код) КАК Код,
	|	ЕСТЬNULL(ТоварыПоДокументам.Артикул, ТоварыПоФакту.Артикул) КАК Артикул,
	|	ЕСТЬNULL(ТоварыПоДокументам.ЕдиницаИзмерения, ТоварыПоФакту.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ТоварыПоДокументам.ЕдиницаИзмерения.Код, ТоварыПоФакту.ЕдиницаИзмерения.Код) КАК КодПоОКЕИ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоФакту.Количество, 0) - ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоФакту.Количество, 0) - ЕСТЬNULL(ТоварыПоДокументам.Количество, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоИзлишек,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) - ЕСТЬNULL(ТоварыПоФакту.Количество, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) - ЕСТЬNULL(ТоварыПоФакту.Количество, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоНедостача,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) - ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) - ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаИзлишек,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0) - ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0) - ЕСТЬNULL(ТоварыПоФакту.Сумма, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНедостача,
	|	ЕСТЬNULL(ТоварыПоДокументам.НоменклатураНаименование, ТоварыПоФакту.НоменклатураНаименование) КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТоварыПоДокументам.ХарактеристикаНаименование, ТоварыПоФакту.ХарактеристикаНаименование) КАК ХарактеристикаНаименование,
	|	ЕСТЬNULL(ТоварыПоФакту.ЦенаВключаетНДС, ЛОЖЬ) КАК ЦенаВключаетНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоДокументам.ЦенаВключаетНДС, ЛОЖЬ) КАК ЦенаВключаетНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.НомерСтроки, ТоварыПоФакту.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	ТоварыПоДокументам КАК ТоварыПоДокументам
	|		ПОЛНОЕ СОЕДИНЕНИЕ ТоварыПоФакту КАК ТоварыПоФакту
	|		ПО ТоварыПоДокументам.Ссылка = ТоварыПоФакту.Ссылка
	|			И ТоварыПоДокументам.Номенклатура = ТоварыПоФакту.Номенклатура
	|			И ТоварыПоДокументам.Характеристика = ТоварыПоФакту.Характеристика
	|			И ТоварыПоДокументам.Упаковка = ТоварыПоФакту.Упаковка
	|			И ТоварыПоДокументам.Цена = ТоварыПоФакту.Цена
	|			И ТоварыПоДокументам.ЕдиницаИзмерения = ТоварыПоФакту.ЕдиницаИзмерения
	|			И ТоварыПоДокументам.СтавкаНДС = ТоварыПоФакту.СтавкаНДС
	|ГДЕ
	|	ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) <> ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыПоДокументам.Ссылка КАК Ссылка,
	|	ТоварыПоДокументам.Упаковка,
	|	ТоварыПоДокументам.ЕдиницаИзмерения,
	|	ТоварыПоДокументам.НоменклатураНаименование,
	|	ТоварыПоДокументам.ХарактеристикаНаименование,
	|	ТоварыПоДокументам.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ТоварыПоДокументам КАК ТоварыПоДокументам
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
КонецПроцедуры

Процедура СформироватьТекстЗапросаАктРасхождений(ТекстЗапроса)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПоступлениеТоваров.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ПоступлениеТоваров.Ссылка) КАК ОснованиеПредставление,
	|	ПоступлениеТоваров.Номер КАК Номер,
	|	ПоступлениеТоваров.Дата КАК ДатаДокумента,
	|	ПоступлениеТоваров.Дата КАК Дата,
	|	ПоступлениеТоваров.Магазин КАК Магазин,
	|	ПРЕДСТАВЛЕНИЕ(ПоступлениеТоваров.Магазин) КАК МагазинПредставление,
	|	ПоступлениеТоваров.Склад КАК Склад,
	|	ПРЕДСТАВЛЕНИЕ(ПоступлениеТоваров.Склад) КАК СкладПредставление,
	|	ПоступлениеТоваров.Организация КАК Организация,
	|	НЕ ПоступлениеТоваров.ЕстьРасхождения КАК ОшибкаНетРасхождений,
	|	ПоступлениеТоваров.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ПоступлениеТоваров.Организация.Префикс КАК Префикс,
	|	ПоступлениеТоваров.Контрагент КАК Поставщик,
	|	ПоступлениеТоваров.УчитыватьНДС КАК УчитыватьНДС
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровТовары.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровТовары.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровТовары.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТовары.Количество, 0)) КАК Количество,
	|	ЕСТЬNULL(ПоступлениеТоваровТовары.Цена, 0) КАК Цена,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТовары.Сумма, 0)) КАК Сумма,
	|	ПоступлениеТоваровТовары.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТовары.СуммаНДС, 0)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ПоступлениеТоваровТовары.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК НоменклатураНаименование,
	|	ПоступлениеТоваровТовары.Характеристика.Наименование КАК ХарактеристикаНаименование,
	|	ПоступлениеТоваровТовары.Номенклатура.Код КАК Код,
	|	ПоступлениеТоваровТовары.Номенклатура.Артикул КАК Артикул,
	|	ПоступлениеТоваровТовары.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПоступлениеТоваровТовары.Ссылка.ЦенаВключаетНДС
	|				ТОГДА ЕСТЬNULL(ПоступлениеТоваровТовары.Сумма, 0)
	|			ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровТовары.Сумма, 0) + ЕСТЬNULL(ПоступлениеТоваровТовары.СуммаНДС, 0)
	|		КОНЕЦ) КАК СуммаСНДС,
	|	МИНИМУМ(ПоступлениеТоваровТовары.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ТоварыПоФакту
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
	|ГДЕ
	|	(ПоступлениеТоваровТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|			ИЛИ ПоступлениеТоваровТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|	И ПоступлениеТоваровТовары.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровТовары.Номенклатура,
	|	ПоступлениеТоваровТовары.Характеристика,
	|	ЕСТЬNULL(ПоступлениеТоваровТовары.Цена, 0),
	|	ПоступлениеТоваровТовары.СтавкаНДС,
	|	ВЫРАЗИТЬ(ПоступлениеТоваровТовары.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)),
	|	ПоступлениеТоваровТовары.Характеристика.Наименование,
	|	ПоступлениеТоваровТовары.Ссылка.ЦенаВключаетНДС,
	|	ПоступлениеТоваровТовары.Номенклатура.Код,
	|	ПоступлениеТоваровТовары.Номенклатура.Артикул,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровТовары.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровТовары.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровТовары.Упаковка
	|	КОНЕЦ,
	|	ПоступлениеТоваровТовары.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка КАК Ссылка,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Количество, 0)) КАК Количество,
	|	ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Цена, 0) КАК Цена,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Сумма, 0)) КАК Сумма,
	|	ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.СтавкаНДС, 0) КАК СтавкаНДС,
	|	СУММА(ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.СуммаНДС, 0)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)) КАК НоменклатураНаименование,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика.Наименование КАК ХарактеристикаНаименование,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.Код КАК Код,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.Артикул КАК Артикул,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	СУММА(ВЫБОР
	|			КОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка.ЦенаВключаетНДС
	|				ТОГДА ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Сумма, 0)
	|			ИНАЧЕ ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Сумма, 0) + ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.СуммаНДС, 0)
	|		КОНЕЦ) КАК СуммаСНДС,
	|	МИНИМУМ(ЕСТЬNULL(ТоварыПоФакту.НомерСтроки, 1000)) КАК НомерСтроки
	|ПОМЕСТИТЬ ТоварыПоДокументам
	|ИЗ
	|	Документ.ПоступлениеТоваров.ТоварыПоДаннымПоставщика КАК ПоступлениеТоваровТоварыПоДаннымПоставщика
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ТоварыПоФакту.Ссылка КАК Ссылка,
	|			ТоварыПоФакту.Номенклатура КАК Номенклатура,
	|			ТоварыПоФакту.Характеристика КАК Характеристика,
	|			МИНИМУМ(ТоварыПоФакту.НомерСтроки) КАК НомерСтроки
	|		ИЗ
	|			ТоварыПоФакту КАК ТоварыПоФакту
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ТоварыПоФакту.Ссылка,
	|			ТоварыПоФакту.Номенклатура,
	|			ТоварыПоФакту.Характеристика) КАК ТоварыПоФакту
	|		ПО ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура = ТоварыПоФакту.Номенклатура
	|			И ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика = ТоварыПоФакту.Характеристика
	|			И ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка = ТоварыПоФакту.Ссылка
	|ГДЕ
	|	(ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|			ИЛИ ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат))
	|	И ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика,
	|	ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Цена, 0),
	|	ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.СтавкаНДС, 0),
	|	ВЫРАЗИТЬ(ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.НаименованиеПолное КАК СТРОКА(1000)),
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Характеристика.Наименование,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка.ЦенаВключаетНДС,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.Код,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.Артикул,
	|	ВЫБОР
	|		КОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
	|			ТОГДА ПоступлениеТоваровТоварыПоДаннымПоставщика.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка.ЕдиницаИзмерения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка.Коэффициент, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ПоступлениеТоваровТоварыПоДаннымПоставщика.Упаковка
	|	КОНЕЦ,
	|	ПоступлениеТоваровТоварыПоДаннымПоставщика.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТоварыПоДокументам.Ссылка, ТоварыПоФакту.Ссылка) КАК Ссылка,
	|	ЕСТЬNULL(ТоварыПоДокументам.Номенклатура, ТоварыПоФакту.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ТоварыПоДокументам.Характеристика, ТоварыПоФакту.Характеристика) КАК Характеристика,
	|	ЕСТЬNULL(ТоварыПоДокументам.Упаковка, ТоварыПоФакту.Упаковка) КАК Упаковка,
	|	ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) КАК КоличествоПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.Цена, 0) КАК ЦенаПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0) КАК СуммаПоДокументам,
	|	ТоварыПоДокументам.СтавкаНДС КАК СтавкаНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.СуммаНДС, 0) КАК СуммаНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.СуммаСНДС, 0) КАК СуммаCНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоФакту.Количество, 0) КАК КоличествоПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.Цена, 0) КАК ЦенаПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) КАК СуммаПоФакту,
	|	ТоварыПоФакту.СтавкаНДС КАК СтавкаНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0) КАК СуммаНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоФакту.СуммаСНДС, 0) КАК СуммаCНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоДокументам.Код, ТоварыПоФакту.Код) КАК Код,
	|	ЕСТЬNULL(ТоварыПоДокументам.Артикул, ТоварыПоФакту.Артикул) КАК Артикул,
	|	ЕСТЬNULL(ТоварыПоДокументам.Номенклатура.ЕдиницаИзмерения, ТоварыПоФакту.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоФакту.Количество, 0) - ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоФакту.Количество, 0) - ЕСТЬNULL(ТоварыПоДокументам.Количество, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоИзлишек,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) - ЕСТЬNULL(ТоварыПоФакту.Количество, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоДокументам.Количество, 0) - ЕСТЬNULL(ТоварыПоФакту.Количество, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоНедостача,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоФакту.СуммаСНДС, 0) - ЕСТЬNULL(ТоварыПоДокументам.СуммаСНДС, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоФакту.СуммаСНДС, 0) - ЕСТЬNULL(ТоварыПоДокументам.СуммаСНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаИзлишек,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоДокументам.СуммаСНДС, 0) - ЕСТЬNULL(ТоварыПоФакту.СуммаСНДС, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоДокументам.СуммаСНДС, 0) - ЕСТЬNULL(ТоварыПоФакту.СуммаСНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНедостача,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0) - ЕСТЬNULL(ТоварыПоДокументам.СуммаНДС, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0) - ЕСТЬNULL(ТоварыПоДокументам.СуммаНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДСИзлишек,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПоДокументам.СуммаНДС, 0) - ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0) > 0
	|			ТОГДА ЕСТЬNULL(ТоварыПоДокументам.СуммаНДС, 0) - ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаНДСНедостача,
	|	ЕСТЬNULL(ТоварыПоДокументам.НоменклатураНаименование, ТоварыПоФакту.НоменклатураНаименование) КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТоварыПоДокументам.ХарактеристикаНаименование, ТоварыПоФакту.ХарактеристикаНаименование) КАК ХарактеристикаНаименование,
	|	ЕСТЬNULL(ТоварыПоФакту.ЦенаВключаетНДС, ЛОЖЬ) КАК ЦенаВключаетНДСПоФакту,
	|	ЕСТЬNULL(ТоварыПоДокументам.ЦенаВключаетНДС, ЛОЖЬ) КАК ЦенаВключаетНДСПоДокументам,
	|	ЕСТЬNULL(ТоварыПоДокументам.НомерСтроки, ТоварыПоФакту.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	ТоварыПоДокументам КАК ТоварыПоДокументам
	|		ПОЛНОЕ СОЕДИНЕНИЕ ТоварыПоФакту КАК ТоварыПоФакту
	|		ПО ТоварыПоДокументам.Ссылка = ТоварыПоФакту.Ссылка
	|			И ТоварыПоДокументам.Номенклатура = ТоварыПоФакту.Номенклатура
	|			И ТоварыПоДокументам.Характеристика = ТоварыПоФакту.Характеристика
	|			И ТоварыПоДокументам.Цена = ТоварыПоФакту.Цена
	|			И ТоварыПоДокументам.СтавкаНДС = ТоварыПоФакту.СтавкаНДС
	|ГДЕ
	|	(ЕСТЬNULL(ТоварыПоФакту.Сумма, 0) <> ЕСТЬNULL(ТоварыПоДокументам.Сумма, 0)
	|			ИЛИ ЕСТЬNULL(ТоварыПоФакту.СуммаНДС, 0) <> ЕСТЬNULL(ТоварыПоДокументам.СуммаНДС, 0)
	|			ИЛИ ЕСТЬNULL(ТоварыПоФакту.Количество, 0) <> ЕСТЬNULL(ТоварыПоДокументам.Количество, 0))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки";
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// Печать

Функция ПечатьНакладная(МассивОбъектов, ОбъектыПечати, ВыводитьЦены = Ложь, ТолькоПоРозничнымЦенам = Ложь)
	
	КолонкаКодов       = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	ВыводитьКоды       = НЕ ПустаяСтрока(КолонкаКодов);
	ВыводитьУпаковки   = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	РеквизитыДокумента = Новый Структура("Номер, Дата, Префикс");
	СинонимДокумента   = НСтр("ru='Поступление товаров'");

	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваров_ПоступлениеТоваров";
	
	Если ВыводитьЦены Тогда
		ИндексРезультатаТовары=2;
	Иначе
		ИндексРезультатаТовары=1;
	КонецЕсли;
	
	УстанавливатьВидыЦенВАссортименте = ПолучитьФункциональнуюОпцию("УстанавливатьВидыЦенВАссортименте");
	
	Если УстанавливатьВидыЦенВАссортименте Тогда
		Если ВыводитьЦены Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Документ.Ссылка                               КАК Ссылка,
			|	Документ.Номер                                КАК Номер,
			|	Документ.Дата                                 КАК Дата,
			|	Документ.Организация                          КАК Покупатель,
			|	Документ.Контрагент                           КАК Поставщик,
			|	Документ.Ответственный.ФизическоеЛицо         КАК Получил,
			|	Документ.Организация.Префикс                  КАК Префикс,
			|	Документ.УчитыватьНДС                         КАК УчитыватьНДС,
			|	Документ.ЦенаВключаетНДС                      КАК ЦенаВключаетНДС,
			|	ПРЕДСТАВЛЕНИЕ(Документ.Контрагент)            КАК ПоставщикПредставление,
			|	ПРЕДСТАВЛЕНИЕ(Документ.Организация)           КАК ПокупательПредставление,
			|	ПРЕДСТАВЛЕНИЕ(Документ.Ответственный.ФизическоеЛицо) КАК ПолучилПредставление
			|ИЗ
			|	Документ.ПоступлениеТоваров КАК Документ
			|ГДЕ
			|	Документ.Ссылка В(&МассивОбъектов)
			|	И Документ.Проведен
			|УПОРЯДОЧИТЬ ПО
			|	Дата, Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
			|	" + ?(ВыводитьКоды, "ТаблицаТовары.Номенклатура." + КолонкаКодов +" КАК КолонкаКодов,", "") + "
			|	ТаблицаТовары.Номенклатура.ЦеноваяГруппа КАК ЦеноваяГруппа,
			|	ТаблицаТовары.Характеристика КАК Характеристика,
			|	ТаблицаТовары.Номенклатура.ЕдиницаИзмерения КАК БазоваяЕдиницаИзмерения,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
			|			ТОГДА ТаблицаТовары.Упаковка.ЕдиницаИзмерения
			|		ИНАЧЕ ТаблицаТовары.Номенклатура.ЕдиницаИзмерения
			|	КОНЕЦ КАК ЕдиницаИзмеренияУпаковки,
			|	ТаблицаТовары.Количество КАК Количество,
			|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ТаблицаТовары.Цена КАК Цена,
			|	ТаблицаТовары.Сумма КАК Сумма,
			|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.Ссылка.Дата КАК Дата,
			|	ТаблицаТовары.Ссылка.Магазин КАК Магазин,
			|	ТаблицаТовары.Ссылка.Магазин.ФорматМагазина КАК ФорматМагазина
			|ПОМЕСТИТЬ ТаблицаТовары
			|ИЗ
			|	Документ.ПоступлениеТоваров.Товары КАК ТаблицаТовары
			|ГДЕ
			|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
			|	И ТаблицаТовары.Ссылка.Проведен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Магазины.Ссылка КАК Магазин
			|ПОМЕСТИТЬ втМагазиныСПрименениемЦен
			|ИЗ
			|	Справочник.Магазины КАК Магазины
			|ГДЕ
			|	&ИспользоватьПрименениеЦен
			|	И Магазины.ИспользоватьПрименениеЦен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Магазины.Ссылка КАК Магазин,
			|	Магазины.ФорматМагазина КАК ФорматМагазина
			|ПОМЕСТИТЬ втМагазиныСАссортиментом
			|ИЗ
			|	Справочник.Магазины КАК Магазины
			|ГДЕ
			|	Магазины.КонтролироватьАссортимент
			|	И НЕ(&ИспользоватьПрименениеЦен
			|				И Магазины.ИспользоватьПрименениеЦен)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Магазины.Ссылка КАК Магазин,
			|	Магазины.ПравилоЦенообразования КАК ПравилоЦенообразования,
			|	Магазины.ПравилоЦенообразования.ВидЦен КАК ВидЦены
			|ПОМЕСТИТЬ втМагазиныОбычные
			|ИЗ
			|	Справочник.Магазины КАК Магазины
			|ГДЕ
			|	НЕ(Магазины.КонтролироватьАссортимент)
			|	И НЕ(&ИспользоватьПрименениеЦен
			|				И Магазины.ИспользоватьПрименениеЦен)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МагазиныОбычные.Магазин КАК Магазин,
			|	ПравилаЦенообразованияЦеновыеГруппы.ЦеноваяГруппа КАК ЦеноваяГруппа,
			|	ПравилаЦенообразованияЦеновыеГруппы.ВидЦен КАК ВидЦены
			|ПОМЕСТИТЬ втПравилаОбычныхМагазинов
			|ИЗ
			|	Справочник.ПравилаЦенообразования.ЦеновыеГруппы КАК ПравилаЦенообразованияЦеновыеГруппы
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМагазиныОбычные КАК МагазиныОбычные
			|		ПО (МагазиныОбычные.ПравилоЦенообразования = ПравилаЦенообразованияЦеновыеГруппы.Ссылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ТаблицаТовары.Характеристика КАК Характеристика,
			|	ТаблицаТовары.Магазин КАК Магазин,
			|	МАКСИМУМ(ДействующиеЦены.Период) КАК ДатаЦены,
			|	ТаблицаТовары.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ втТоварыСПериодамиДействующихЦен
			|ИЗ
			|	ТаблицаТовары КАК ТаблицаТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМагазиныСПрименениемЦен КАК МагазиныСПрименениемЦен
			|		ПО (МагазиныСПрименениемЦен.Магазин = ТаблицаТовары.Магазин)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДействующиеЦеныНоменклатуры КАК ДействующиеЦены
			|		ПО (ДействующиеЦены.ОбъектЦенообразования = ТаблицаТовары.Магазин)
			|			И (ДействующиеЦены.Период <= ТаблицаТовары.Дата)
			|			И (ДействующиеЦены.Номенклатура = ТаблицаТовары.Номенклатура)
			|			И (ДействующиеЦены.Характеристика = ТаблицаТовары.Характеристика)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТовары.Номенклатура,
			|	ТаблицаТовары.Характеристика,
			|	ТаблицаТовары.Магазин,
			|	ТаблицаТовары.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыСПериодамиДействующихЦен.Номенклатура КАК Номенклатура,
			|	ТоварыСПериодамиДействующихЦен.Характеристика КАК Характеристика,
			|	ТоварыСПериодамиДействующихЦен.Ссылка КАК Ссылка,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ДействующиеЦены.Упаковка.Коэффициент, 0) = 0
			|			ТОГДА ДействующиеЦены.Цена
			|		ИНАЧЕ ВЫРАЗИТЬ(ДействующиеЦены.Цена / ДействующиеЦены.Упаковка.Коэффициент КАК ЧИСЛО(15, 2))
			|	КОНЕЦ КАК Цена
			|ПОМЕСТИТЬ втЦеныДействующиеПоДокументам
			|ИЗ
			|	втТоварыСПериодамиДействующихЦен КАК ТоварыСПериодамиДействующихЦен
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДействующиеЦеныНоменклатуры КАК ДействующиеЦены
			|		ПО (ДействующиеЦены.ОбъектЦенообразования = ТоварыСПериодамиДействующихЦен.Магазин)
			|			И (ДействующиеЦены.Период = ТоварыСПериодамиДействующихЦен.ДатаЦены)
			|			И (ДействующиеЦены.Номенклатура = ТоварыСПериодамиДействующихЦен.Номенклатура)
			|			И (ДействующиеЦены.Характеристика = ТоварыСПериодамиДействующихЦен.Характеристика)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ТаблицаТовары.Характеристика КАК Характеристика,
			|	ТаблицаТовары.ФорматМагазина КАК ФорматМагазина,
			|	ТаблицаТовары.Магазин КАК Магазин,
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.Дата КАК Дата,
			|	МАКСИМУМ(Ассортимент.Период) КАК ДатаАссортимента
			|ПОМЕСТИТЬ втТоварыСПериодамиАссортимента
			|ИЗ
			|	ТаблицаТовары КАК ТаблицаТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМагазиныСАссортиментом КАК МагазиныСАссортиментом
			|		ПО (МагазиныСАссортиментом.Магазин = ТаблицаТовары.Магазин)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Ассортимент КАК Ассортимент
			|		ПО (Ассортимент.ОбъектПланирования = ТаблицаТовары.ФорматМагазина)
			|			И (Ассортимент.Период <= КОНЕЦПЕРИОДА(ТаблицаТовары.Дата, ДЕНЬ))
			|			И (Ассортимент.Номенклатура = ТаблицаТовары.Номенклатура)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТовары.Номенклатура,
			|	ТаблицаТовары.Характеристика,
			|	ТаблицаТовары.ФорматМагазина,
			|	ТаблицаТовары.Магазин,
			|	ТаблицаТовары.Ссылка,
			|	ТаблицаТовары.Дата
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыСПериодамиАссортимента.Номенклатура КАК Номенклатура,
			|	ТоварыСПериодамиАссортимента.Характеристика КАК Характеристика,
			|	ТоварыСПериодамиАссортимента.Ссылка КАК Ссылка,
			|	ТоварыСПериодамиАссортимента.ФорматМагазина КАК ФорматМагазина,
			|	ТоварыСПериодамиАссортимента.Магазин КАК Магазин,
			|	ТоварыСПериодамиАссортимента.Дата КАК Дата,
			|	Ассортимент.ВидЦен КАК ВидЦен
			|ПОМЕСТИТЬ втВидыЦенАссортиментаПоДокументам
			|ИЗ
			|	втТоварыСПериодамиАссортимента КАК ТоварыСПериодамиАссортимента
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Ассортимент КАК Ассортимент
			|		ПО (Ассортимент.ОбъектПланирования = ТоварыСПериодамиАссортимента.ФорматМагазина)
			|			И (Ассортимент.Период = ТоварыСПериодамиАссортимента.ДатаАссортимента)
			|			И (Ассортимент.Номенклатура = ТоварыСПериодамиАссортимента.Номенклатура)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЦенАссортиментаПоДокументам.Номенклатура КАК Номенклатура,
			|	ВидыЦенАссортиментаПоДокументам.Характеристика КАК Характеристика,
			|	ВидыЦенАссортиментаПоДокументам.Ссылка КАК Ссылка,
			|	ВидыЦенАссортиментаПоДокументам.Магазин КАК Магазин,
			|	ВидыЦенАссортиментаПоДокументам.ВидЦен КАК ВидЦен,
			|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК ДатаЦены
			|ПОМЕСТИТЬ втПериодыЦенПоАссортименту
			|ИЗ
			|	втВидыЦенАссортиментаПоДокументам КАК ВидыЦенАссортиментаПоДокументам
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
			|		ПО (ЦеныНоменклатуры.Период <= ВидыЦенАссортиментаПоДокументам.Дата)
			|			И (ЦеныНоменклатуры.ВидЦены = ВидыЦенАссортиментаПоДокументам.ВидЦен)
			|			И (ЦеныНоменклатуры.Номенклатура = ВидыЦенАссортиментаПоДокументам.Номенклатура)
			|			И (ЦеныНоменклатуры.Характеристика = ВидыЦенАссортиментаПоДокументам.Характеристика)
			|
			|СГРУППИРОВАТЬ ПО
			|	ВидыЦенАссортиментаПоДокументам.Номенклатура,
			|	ВидыЦенАссортиментаПоДокументам.Характеристика,
			|	ВидыЦенАссортиментаПоДокументам.Ссылка,
			|	ВидыЦенАссортиментаПоДокументам.Магазин,
			|	ВидыЦенАссортиментаПоДокументам.ВидЦен
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПериодыЦенПоАссортименту.Номенклатура КАК Номенклатура,
			|	ПериодыЦенПоАссортименту.Характеристика КАК Характеристика,
			|	ПериодыЦенПоАссортименту.Ссылка КАК Ссылка,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ЦеныНоменклатуры.Упаковка.Коэффициент, 0) = 0
			|			ТОГДА ЦеныНоменклатуры.Цена
			|		ИНАЧЕ ВЫРАЗИТЬ(ЦеныНоменклатуры.Цена / ЦеныНоменклатуры.Упаковка.Коэффициент КАК ЧИСЛО(15, 2))
			|	КОНЕЦ КАК Цена
			|ПОМЕСТИТЬ втЦеныАссортиментныеПоДокументам
			|ИЗ
			|	втПериодыЦенПоАссортименту КАК ПериодыЦенПоАссортименту
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
			|		ПО (ЦеныНоменклатуры.Период = ПериодыЦенПоАссортименту.ДатаЦены)
			|			И (ЦеныНоменклатуры.ВидЦены = ПериодыЦенПоАссортименту.ВидЦен)
			|			И (ЦеныНоменклатуры.Номенклатура = ПериодыЦенПоАссортименту.Номенклатура)
			|			И (ЦеныНоменклатуры.Характеристика = ПериодыЦенПоАссортименту.Характеристика)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ТаблицаТовары.Характеристика КАК Характеристика,
			|	ТаблицаТовары.Магазин КАК Магазин,
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.Дата КАК Дата,
			|	ЕСТЬNULL(ПравилаОбычныхМагазинов.ВидЦены, МагазиныОбычные.ВидЦены) КАК ВидЦены
			|ПОМЕСТИТЬ втВидыЦенПоПравилам
			|ИЗ
			|	ТаблицаТовары КАК ТаблицаТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втМагазиныОбычные КАК МагазиныОбычные
			|		ПО (МагазиныОбычные.Магазин = ТаблицаТовары.Магазин)
			|		ЛЕВОЕ СОЕДИНЕНИЕ втПравилаОбычныхМагазинов КАК ПравилаОбычныхМагазинов
			|		ПО (ПравилаОбычныхМагазинов.Магазин = ТаблицаТовары.Магазин)
			|			И (ПравилаОбычныхМагазинов.ЦеноваяГруппа = ТаблицаТовары.ЦеноваяГруппа)
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТовары.Номенклатура,
			|	ТаблицаТовары.Характеристика,
			|	ТаблицаТовары.Магазин,
			|	ТаблицаТовары.Ссылка,
			|	ТаблицаТовары.Дата,
			|	ЕСТЬNULL(ПравилаОбычныхМагазинов.ВидЦены, МагазиныОбычные.ВидЦены)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЦенПоПравилам.Номенклатура КАК Номенклатура,
			|	ВидыЦенПоПравилам.Характеристика КАК Характеристика,
			|	ВидыЦенПоПравилам.Ссылка КАК Ссылка,
			|	ВидыЦенПоПравилам.Магазин КАК Магазин,
			|	ВидыЦенПоПравилам.ВидЦены КАК ВидЦены,
			|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК ДатаЦены
			|ПОМЕСТИТЬ втПериодыЦенПоПравилам
			|ИЗ
			|	втВидыЦенПоПравилам КАК ВидыЦенПоПравилам
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
			|		ПО (ЦеныНоменклатуры.Период <= ВидыЦенПоПравилам.Дата)
			|			И (ЦеныНоменклатуры.ВидЦены = ВидыЦенПоПравилам.ВидЦены)
			|			И (ЦеныНоменклатуры.Номенклатура = ВидыЦенПоПравилам.Номенклатура)
			|			И (ЦеныНоменклатуры.Характеристика = ВидыЦенПоПравилам.Характеристика)
			|
			|СГРУППИРОВАТЬ ПО
			|	ВидыЦенПоПравилам.Номенклатура,
			|	ВидыЦенПоПравилам.Характеристика,
			|	ВидыЦенПоПравилам.Ссылка,
			|	ВидыЦенПоПравилам.Магазин,
			|	ВидыЦенПоПравилам.ВидЦены
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПериодыЦенПоПравилам.Номенклатура КАК Номенклатура,
			|	ПериодыЦенПоПравилам.Характеристика КАК Характеристика,
			|	ПериодыЦенПоПравилам.Ссылка КАК Ссылка,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ЦеныНоменклатуры.Упаковка.Коэффициент, 0) = 0
			|			ТОГДА ЦеныНоменклатуры.Цена
			|		ИНАЧЕ ВЫРАЗИТЬ(ЦеныНоменклатуры.Цена / ЦеныНоменклатуры.Упаковка.Коэффициент КАК ЧИСЛО(15, 2))
			|	КОНЕЦ КАК Цена
			|ПОМЕСТИТЬ втЦеныПравильныеПоДокументам
			|ИЗ
			|	втПериодыЦенПоПравилам КАК ПериодыЦенПоПравилам
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
			|		ПО (ЦеныНоменклатуры.Период = ПериодыЦенПоПравилам.ДатаЦены)
			|			И (ЦеныНоменклатуры.ВидЦены = ПериодыЦенПоПравилам.ВидЦены)
			|			И (ЦеныНоменклатуры.Номенклатура = ПериодыЦенПоПравилам.Номенклатура)
			|			И (ЦеныНоменклатуры.Характеристика = ПериодыЦенПоПравилам.Характеристика)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	ТаблицаТовары.НоменклатураПредставление КАК НоменклатураПредставление,
			|	" + ?(ВыводитьКоды, "ТаблицаТовары.КолонкаКодов КАК КолонкаКодов,", "") + "
			|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
			|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.БазоваяЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.ЕдиницаИзмеренияУпаковки) КАК ПредставлениеЕдиницыИзмеренияУпаковки,
			|	ТаблицаТовары.Количество КАК Количество,
			|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ТаблицаТовары.Цена КАК Цена,
			|	ТаблицаТовары.Сумма КАК Сумма,
			|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
			|	ВЫБОР
			|		КОГДА НЕ ЦеныДействующиеПоДокументам.Цена ЕСТЬ NULL 
			|			ТОГДА ЦеныДействующиеПоДокументам.Цена
			|		КОГДА НЕ ЦеныАссортиментныеПоДокументам.Цена ЕСТЬ NULL 
			|			ТОГДА ЦеныАссортиментныеПоДокументам.Цена
			|		ИНАЧЕ ЕСТЬNULL(ЦеныПравильныеПоДокументам.Цена, 0)
			|	КОНЕЦ КАК ЦенаРозничная,
			|	ВЫРАЗИТЬ(ВЫБОР
			|		КОГДА НЕ ЦеныДействующиеПоДокументам.Цена ЕСТЬ NULL 
			|			ТОГДА ЦеныДействующиеПоДокументам.Цена * ТаблицаТовары.Количество
			|		КОГДА НЕ ЦеныАссортиментныеПоДокументам.Цена ЕСТЬ NULL 
			|			ТОГДА ЦеныАссортиментныеПоДокументам.Цена * ТаблицаТовары.Количество
			|		ИНАЧЕ ЕСТЬNULL(ЦеныПравильныеПоДокументам.Цена, 0) * ТаблицаТовары.Количество
			|	КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРозничная,
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.Дата КАК Дата
			|ИЗ
			|	ТаблицаТовары КАК ТаблицаТовары
			|		ЛЕВОЕ СОЕДИНЕНИЕ втЦеныДействующиеПоДокументам КАК ЦеныДействующиеПоДокументам
			|		ПО (ЦеныДействующиеПоДокументам.Номенклатура = ТаблицаТовары.Номенклатура)
			|			И (ЦеныДействующиеПоДокументам.Характеристика = ТаблицаТовары.Характеристика)
			|			И (ЦеныДействующиеПоДокументам.Ссылка = ТаблицаТовары.Ссылка)
			|		ЛЕВОЕ СОЕДИНЕНИЕ втЦеныАссортиментныеПоДокументам КАК ЦеныАссортиментныеПоДокументам
			|		ПО (ЦеныАссортиментныеПоДокументам.Номенклатура = ТаблицаТовары.Номенклатура)
			|			И (ЦеныАссортиментныеПоДокументам.Характеристика = ТаблицаТовары.Характеристика)
			|			И (ЦеныАссортиментныеПоДокументам.Ссылка = ТаблицаТовары.Ссылка)
			|		ЛЕВОЕ СОЕДИНЕНИЕ втЦеныПравильныеПоДокументам КАК ЦеныПравильныеПоДокументам
			|		ПО (ЦеныПравильныеПоДокументам.Номенклатура = ТаблицаТовары.Номенклатура)
			|			И (ЦеныПравильныеПоДокументам.Характеристика = ТаблицаТовары.Характеристика)
			|			И (ЦеныПравильныеПоДокументам.Ссылка = ТаблицаТовары.Ссылка)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	Ссылка,
			|	НомерСтроки
			|ИТОГИ
			|	МАКСИМУМ(Дата)
			|ПО
			|	Ссылка");
			ИндексРезультатаТовары = 15;
			Запрос.УстановитьПараметр("ИспользоватьПрименениеЦен", ПолучитьФункциональнуюОпцию("ИспользоватьПрименениеЦен"));
		Иначе
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Документ.Ссылка                               КАК Ссылка,
			|	Документ.Номер                                КАК Номер,
			|	Документ.Дата                                 КАК Дата,
			|	Документ.Организация                          КАК Покупатель,
			|	Документ.Контрагент                           КАК Поставщик,
			|	Документ.Ответственный.ФизическоеЛицо         КАК Получил,
			|	Документ.Организация.Префикс                  КАК Префикс,
			|	Документ.УчитыватьНДС                         КАК УчитыватьНДС,
			|	Документ.ЦенаВключаетНДС                      КАК ЦенаВключаетНДС,
			|	ПРЕДСТАВЛЕНИЕ(Документ.Контрагент)            КАК ПоставщикПредставление,
			|	ПРЕДСТАВЛЕНИЕ(Документ.Организация)           КАК ПокупательПредставление,
			|	ПРЕДСТАВЛЕНИЕ(Документ.Ответственный.ФизическоеЛицо) КАК ПолучилПредставление
			|ИЗ
			|	Документ.ПоступлениеТоваров КАК Документ
			|ГДЕ
			|	Документ.Ссылка В(&МассивОбъектов)
			|	И Документ.Проведен
			|УПОРЯДОЧИТЬ ПО
			|	Дата, Ссылка
			|;
			|
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
			|	ТаблицаТовары.Номенклатура КАК Номенклатура,
			|	" + ?(ВыводитьКоды, "ТаблицаТовары.Номенклатура." + КолонкаКодов +" КАК КолонкаКодов,", "") + "
			|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
			|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
			|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
			|	ВЫБОР
			|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
			|			ТОГДА ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Упаковка.ЕдиницаИзмерения)
			|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
			|	КОНЕЦ КАК ПредставлениеЕдиницыИзмеренияУпаковки,
			|	ТаблицаТовары.Количество КАК Количество,
			|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
			|	ТаблицаТовары.Цена КАК Цена,
			|	ТаблицаТовары.Сумма КАК Сумма,
			|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
			|	ТаблицаТовары.Ссылка КАК Ссылка,
			|	ТаблицаТовары.Ссылка.Дата КАК Дата
			|ИЗ
			|	Документ.ПоступлениеТоваров.Товары КАК ТаблицаТовары
			|
			|ГДЕ
			|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
			|	И ТаблицаТовары.Ссылка.Проведен
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	Ссылка,
			|	НомерСтроки
			|ИТОГИ МАКСИМУМ(Дата) ПО
			|	Ссылка");
		КонецЕсли;
	Иначе //НЕ УстанавливатьВидыЦенВАссортименте
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Документ.Ссылка                               КАК Ссылка,
		|	Документ.Номер                                КАК Номер,
		|	Документ.Дата                                 КАК Дата,
		|	Документ.Организация                          КАК Покупатель,
		|	Документ.Контрагент                           КАК Поставщик,
		|	Документ.Ответственный.ФизическоеЛицо         КАК Получил,
		|	Документ.Организация.Префикс                  КАК Префикс,
		|	Документ.УчитыватьНДС                         КАК УчитыватьНДС,
		|	Документ.ЦенаВключаетНДС                      КАК ЦенаВключаетНДС,
		|	ПРЕДСТАВЛЕНИЕ(Документ.Контрагент)            КАК ПоставщикПредставление,
		|	ПРЕДСТАВЛЕНИЕ(Документ.Организация)           КАК ПокупательПредставление,
		|	ПРЕДСТАВЛЕНИЕ(Документ.Ответственный.ФизическоеЛицо) КАК ПолучилПредставление
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК Документ
		|ГДЕ
		|	Документ.Ссылка В(&МассивОбъектов)
		|	И Документ.Проведен
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|" + ?(ВыводитьЦены, "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Ссылка КАК СсылкаДокумента,
		|	ТаблицаТовары.Ссылка.Дата КАК Период,
		|	ТаблицаТовары.Ссылка.Магазин КАК Магазин,
		|	ТаблицаТовары.Характеристика КАК Характеристика,
		|   Магазины.ИспользоватьПрименениеЦен КАК ИспользоватьПрименениеЦен,
		|   Магазины.ПравилоЦенообразования КАК ПравилоЦенообразования,
		|   СправочникНоменклатура.ЦеноваяГруппа КАК ЦеноваяГруппа
		|ПОМЕСТИТЬ ВТТовары
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ТаблицаТовары
	    |ЛЕВОЕ СОЕДИНЕНИЕ 
		|   Справочник.Магазины КАК Магазины
		|   ПО ТаблицаТовары.Ссылка.Магазин = Магазины.Ссылка
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|   Справочник.Номенклатура КАК СправочникНоменклатура
		|   ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
		|	И ТаблицаТовары.Ссылка.Проведен
		|;
		|
		|","") + "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	" + ?(ВыводитьКоды, "ТаблицаТовары.Номенклатура." + КолонкаКодов +" КАК КолонкаКодов,", "") + "
		|	ТаблицаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
		|	ВЫБОР
		|		КОГДА ТаблицаТовары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|			ТОГДА ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Упаковка.ЕдиницаИзмерения)
		|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ТаблицаТовары.Номенклатура.ЕдиницаИзмерения)
		|	КОНЕЦ КАК ПредставлениеЕдиницыИзмеренияУпаковки,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаТовары.Цена КАК Цена,
		|	ТаблицаТовары.Сумма КАК Сумма,
		|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
		|	ТаблицаТовары.Ссылка КАК Ссылка " + ?(ВыводитьЦены, ",
		|   ЕСТЬNULL(РозничныеЦены.Цена, 0) КАК ЦенаРозничная,
		|	ВЫРАЗИТЬ(ТаблицаТовары.Количество * ЕСТЬNULL(РозничныеЦены.Цена, 0) КАК ЧИСЛО(15, 2)) КАК СуммаРозничная
		|","") + "
		|ИЗ
		|	Документ.ПоступлениеТоваров.Товары КАК ТаблицаТовары
		|" + ?(ВыводитьЦены, "
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|	ПодЗапрос.Период,
		|	ПодЗапрос.Номенклатура,
		|	ПодЗапрос.Характеристика,
		|	ПодЗапрос.Магазин,
		|	ПодЗапрос.Цена
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТоварыПериодыЦен.Период КАК Период,
		|		ТоварыПериодыЦен.Номенклатура КАК Номенклатура,
		|		ТоварыПериодыЦен.Характеристика КАК Характеристика,
		|		ТоварыПериодыЦен.Магазин КАК Магазин,
		|		ТоварыПериодыЦен.ИспользоватьПрименениеЦен КАК ИспользоватьПрименениеЦен,
		|		ТоварыПериодыЦен.ПериодЦены КАК ПериодЦены,
		|       ВЫБОР
		|			КОГДА ТоварыПериодыЦен.ИспользоватьПрименениеЦен
		|				ТОГДА ЕСТЬNULL(ДействующиеЦены.Цена, 0) / ЕСТЬNULL(ДействующиеЦены.Упаковка.Коэффициент, 1)
		|			ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / ЕСТЬNULL(ЦеныНоменклатуры.Упаковка.Коэффициент, 1)

		|		КОНЕЦ КАК Цена
		|	ИЗ
		|		(ВЫБРАТЬ
		|			Товары.Период КАК Период,
		|			Товары.Номенклатура КАК Номенклатура,
		|			Товары.Характеристика КАК Характеристика,
		|			Товары.Магазин КАК Магазин,
		|			Товары.ИспользоватьПрименениеЦен КАК ИспользоватьПрименениеЦен,
		|			МАКСИМУМ(ВЫБОР
		|					КОГДА Товары.ИспользоватьПрименениеЦен = ИСТИНА
		|						ТОГДА ДействующиеЦены.Период
		|					ИНАЧЕ ЦеныНоменклатуры.Период
		|				КОНЕЦ) КАК ПериодЦены,
		|			Товары.ПравилоЦенообразования КАК ПравилоЦенообразования,
		|			Товары.ЦеноваяГруппа КАК ЦеноваяГруппа
		|		ИЗ
		|			ВТТовары КАК Товары
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаЦенообразования КАК ПравилоЦенообразованияШапка
		|				ПО (ПравилоЦенообразованияШапка.Ссылка = Товары.ПравилоЦенообразования)
		|					И ((НЕ Товары.ИспользоватьПрименениеЦен))
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаЦенообразования.ЦеновыеГруппы КАК ПравилоЦенообразованияЦеновыеГруппы
		|				ПО Товары.ПравилоЦенообразования = ПравилоЦенообразованияЦеновыеГруппы.Ссылка
		|					И Товары.ЦеноваяГруппа = ПравилоЦенообразованияЦеновыеГруппы.ЦеноваяГруппа
		|					И ((НЕ Товары.ИспользоватьПрименениеЦен))
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействующиеЦеныНоменклатуры КАК ДействующиеЦены
		|				ПО Товары.Номенклатура = ДействующиеЦены.Номенклатура
		|					И Товары.Характеристика = ДействующиеЦены.Характеристика
		|					И Товары.Магазин = ДействующиеЦены.ОбъектЦенообразования
		|					И (Товары.ИспользоватьПрименениеЦен)
		|					И (ДействующиеЦены.Период <= Товары.Период)
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|				ПО Товары.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|					И Товары.Характеристика = ЦеныНоменклатуры.Характеристика
		|					И (ЦеныНоменклатуры.Период <= Товары.Период)
		|					И ((НЕ Товары.ИспользоватьПрименениеЦен))
		|					И (ЦеныНоменклатуры.ВидЦены = ВЫБОР
		|						КОГДА ПравилоЦенообразованияЦеновыеГруппы.ВидЦен ЕСТЬ НЕ NULL 
		|								И ПравилоЦенообразованияЦеновыеГруппы.ВидЦен <> ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
		|							ТОГДА ПравилоЦенообразованияЦеновыеГруппы.ВидЦен
		|						КОГДА (ПравилоЦенообразованияЦеновыеГруппы.ВидЦен ЕСТЬ NULL 
		|								ИЛИ ПравилоЦенообразованияЦеновыеГруппы.ВидЦен = ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка))
		|								И ПравилоЦенообразованияШапка.ВидЦен <> ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
		|							ТОГДА ПравилоЦенообразованияШапка.ВидЦен
		|					КОНЕЦ)
		|		
		|		СГРУППИРОВАТЬ ПО
		|			Товары.Период,
		|			Товары.Номенклатура,
		|			Товары.Характеристика,
		|			Товары.Магазин,
		|			Товары.ИспользоватьПрименениеЦен,
		|			Товары.ПравилоЦенообразования,
		|			Товары.ЦеноваяГруппа) КАК ТоварыПериодыЦен
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаЦенообразования КАК ПравилоЦенообразованияШапка
		|			ПО (ПравилоЦенообразованияШапка.Ссылка = ТоварыПериодыЦен.ПравилоЦенообразования)
		|				И ((НЕ ТоварыПериодыЦен.ИспользоватьПрименениеЦен))
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаЦенообразования.ЦеновыеГруппы КАК ПравилоЦенообразованияЦеновыеГруппы
		|			ПО ТоварыПериодыЦен.ПравилоЦенообразования = ПравилоЦенообразованияЦеновыеГруппы.Ссылка
		|				И ТоварыПериодыЦен.ЦеноваяГруппа = ПравилоЦенообразованияЦеновыеГруппы.ЦеноваяГруппа
		|				И ((НЕ ТоварыПериодыЦен.ИспользоватьПрименениеЦен))
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДействующиеЦеныНоменклатуры КАК ДействующиеЦены
		|			ПО ТоварыПериодыЦен.Номенклатура = ДействующиеЦены.Номенклатура
		|				И ТоварыПериодыЦен.Характеристика = ДействующиеЦены.Характеристика
		|				И ТоварыПериодыЦен.Магазин = ДействующиеЦены.ОбъектЦенообразования
		|				И (ТоварыПериодыЦен.ИспользоватьПрименениеЦен)
		|				И (ДействующиеЦены.Период = ТоварыПериодыЦен.ПериодЦены)
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|			ПО ТоварыПериодыЦен.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|				И ТоварыПериодыЦен.Характеристика = ЦеныНоменклатуры.Характеристика
		|				И (ЦеныНоменклатуры.Период = ТоварыПериодыЦен.ПериодЦены)
		|				И ((НЕ ТоварыПериодыЦен.ИспользоватьПрименениеЦен))
		|				И (ЦеныНоменклатуры.ВидЦены = ВЫБОР
		|					КОГДА ПравилоЦенообразованияЦеновыеГруппы.ВидЦен ЕСТЬ НЕ NULL 
		|							И ПравилоЦенообразованияЦеновыеГруппы.ВидЦен <> ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
		|						ТОГДА ПравилоЦенообразованияЦеновыеГруппы.ВидЦен
		|					КОГДА (ПравилоЦенообразованияЦеновыеГруппы.ВидЦен ЕСТЬ NULL 
		|							ИЛИ ПравилоЦенообразованияЦеновыеГруппы.ВидЦен = ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка))
		|							И ПравилоЦенообразованияШапка.ВидЦен <> ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
		|						ТОГДА ПравилоЦенообразованияШапка.ВидЦен
		|				КОНЕЦ)) КАК ПодЗапрос) КАК РозничныеЦены
		|		ПО (РозничныеЦены.Магазин = ТаблицаТовары.Ссылка.Магазин)
		|			И (РозничныеЦены.Характеристика = ТаблицаТовары.Характеристика)
		|			И (РозничныеЦены.Номенклатура = ТаблицаТовары.Номенклатура)
		|            И (РозничныеЦены.Период = ТаблицаТовары.Ссылка.Дата)
		|","") + "
		|ГДЕ
		|	ТаблицаТовары.Ссылка В(&МассивОбъектов)
		|	И ТаблицаТовары.Ссылка.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки
		|ИТОГИ ПО
		|	Ссылка");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваров.ПФ_MXL_ПоступлениеТоваров");

	ОкончаниеИмениОбласти = ?(ВыводитьЦены И НЕ ТолькоПоРозничнымЦенам,"РозничныеЦены","");
	
	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок|Основная" + ОкончаниеИмениОбласти);
	ОбластьШапка 			= Макет.ПолучитьОбласть("Шапка|Основная" + ОкончаниеИмениОбласти);
	
	ОбластьШапкаТаблицыНачало 	= Макет.ПолучитьОбласть("ШапкаТаблицы|НачалоСтроки");
	ОбластьСтрокаТаблицыНачало 	= Макет.ПолучитьОбласть("СтрокаТаблицы|НачалоСтроки");
	ОбластьПодвалТаблицыНачало 	= Макет.ПолучитьОбласть("ПодвалТаблицы|НачалоСтроки");
	
	ОбластьШапкаТаблицыКолонкаКодов 	= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
	ОбластьСтрокаТаблицыКолонкаКодов 	= Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаКодов");
	ОбластьПодвалТаблицыКолонкаКодов 	= Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаКодов");
	
	ОбластьШапкаТаблицыКолонкаКодов.Параметры.ИмяКолонкиКодов = КолонкаКодов; 
	
	ОбластьШапкаТаблицыКолонкаУпаковок 		= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаУпаковок");
	ОбластьСтрокаТаблицыКолонкаУпаковок 	= Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаУпаковок");
	ОбластьПодвалТаблицыКолонкаУпаковок		= Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаУпаковок");
	
	ОбластьШапкаТаблицыКолонкаКоличество 	= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКоличество");
	ОбластьСтрокаТаблицыКолонкаКоличество 	= Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаКоличество");
	ОбластьПодвалТаблицыКолонкаКоличество	= Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаКоличество");
	
	ОбластьШапкаТаблицыЦеныПоступления  = Макет.ПолучитьОбласть("ШапкаТаблицы|ЦеныПоступления");
	ОбластьСтрокаТаблицыЦеныПоступления = Макет.ПолучитьОбласть("СтрокаТаблицы|ЦеныПоступления");
	ОбластьПодвалТаблицыЦеныПоступления = Макет.ПолучитьОбласть("ПодвалТаблицы|ЦеныПоступления");
	
	ОбластьШапкаТаблицыРозничныеЦены  = Макет.ПолучитьОбласть("ШапкаТаблицы|РозничныеЦены");
	ОбластьСтрокаТаблицыРозничныеЦены = Макет.ПолучитьОбласть("СтрокаТаблицы|РозничныеЦены");
	ОбластьПодвалТаблицыРозничныеЦены = Макет.ПолучитьОбласть("ПодвалТаблицы|РозничныеЦены");
	
	
	
	ОбластьКолонкаТоваров = Макет.Область("КолонкаТоваров");
	
	Если НЕ ВыводитьКоды Тогда
		
		ОбластьКолонкаТоваров.ШиринаКолонки = ОбластьКолонкаТоваров.ШиринаКолонки + Макет.Область("КолонкаКодов").ШиринаКолонки;
		
	КонецЕсли;
	
	Если НЕ ВыводитьУпаковки Тогда
		
		ОбластьКолонкаТоваров.ШиринаКолонки = ОбластьКолонкаТоваров.ШиринаКолонки + Макет.Область("КолонкаУпаковокКоличество").ШиринаКолонки
		+ Макет.Область("КолонкаУпаковокПредставление").ШиринаКолонки;
		
	КонецЕсли;
	
	ОбластьШапкаТаблицыКолонкаТоваров 	= Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаТоваров");
	ОбластьСтрокаТаблицыКолонкаТоваров 	= Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаТоваров");
	ОбластьПодвалТаблицыКолонкаТоваров 	= Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаТоваров");
	
	ОбластьПодписей       = Макет.ПолучитьОбласть("Подписи|Основная" + ОкончаниеИмениОбласти);
	ОбластьИтого          = Макет.ПолучитьОбласть("Итого|Основная" + ОкончаниеИмениОбласти);
	ОбластьИтогоНДС       = Макет.ПолучитьОбласть("ИтогоНДС|Основная" + ОкончаниеИмениОбласти);
	ОбластьСуммаПрописью  = Макет.ПолучитьОбласть("СуммаПрописью|Основная" + ОкончаниеИмениОбласти);
	
	ВыборкаПоДокументам = Результаты[0].Выбрать();
	
	ВыборкаПоТабличнымЧастям = Результаты[ИндексРезультатаТовары].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоДокументам.Следующий() Цикл
		
		Если НЕ ВыборкаПоТабличнымЧастям.НайтиСледующий(Новый Структура("Ссылка",ВыборкаПоДокументам.Ссылка)) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ВыборкаПоСтрокамТЧ = ВыборкаПоТабличнымЧастям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если НЕ ПервыйДокумент Тогда
			
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// ЗАГОЛОВОК
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ВыборкаПоДокументам);
		ОбластьЗаголовок.Параметры.Заполнить(ВыборкаПоДокументам);
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = ФормированиеПечатныхФормСервер.СформироватьЗаголовокДокумента(РеквизитыДокумента, СинонимДокумента);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		// ШАПКА
		ОбластьШапка.Параметры.Заполнить(ВыборкаПоДокументам);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицыНачало);
		
		Если ВыводитьКоды Тогда
			
			ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКолонкаКодов);
			
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКолонкаТоваров);
		
		Если ВыводитьУпаковки Тогда
			
			ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКолонкаУпаковок);
			
		КонецЕсли;
		
		ПараметрыШапкаКолонкаКоличество = Новый Структура;
		Если ВыводитьУпаковки Тогда
			ПараметрыШапкаКолонкаКоличество.Вставить("ЕдиницаКоличество", НСтр("ru = '(Ед. изм.)'"));
		Иначе
			ПараметрыШапкаКолонкаКоличество.Вставить("ЕдиницаКоличество", "");
		КонецЕсли;
		ОбластьШапкаТаблицыКолонкаКоличество.Параметры.Заполнить(ПараметрыШапкаКолонкаКоличество);
		
		ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКолонкаКоличество);
		
		Если НЕ ВыводитьУпаковки Тогда
			ТабличныйДокумент.Область("ОбластьЕдиницаКоличество").Объединить();
			ТабличныйДокумент.Область("ОбластьЕдиницаКоличество").ВертикальноеПоложение = ВертикальноеПоложение.Центр;
		КонецЕсли;
		
		Если НЕ ТолькоПоРозничнымЦенам Тогда 
			ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыЦеныПоступления);
		КонецЕсли;
		
		Если ВыводитьЦены Тогда
			
			ПараметрыШапкаРозничныеЦены = Новый Структура;
			Если ВыводитьУпаковки Тогда
				ПараметрыШапкаРозничныеЦены.Вставить("РозничнаяЦенаЕдиница", НСтр("ru = '(Ед. изм.)'"));
			Иначе
				ПараметрыШапкаРозничныеЦены.Вставить("РозничнаяЦенаЕдиница", "");
			КонецЕсли;
			ОбластьШапкаТаблицыРозничныеЦены.Параметры.Заполнить(ПараметрыШапкаРозничныеЦены);
			
			ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыРозничныеЦены);
			
			Если НЕ ВыводитьУпаковки Тогда
				ТабличныйДокумент.Область("РозничнаяЦенаЕдиница").Объединить();
				ТабличныйДокумент.Область("РозничнаяЦенаЕдиница").ВертикальноеПоложение = ВертикальноеПоложение.Центр;
			КонецЕсли;
			
		КонецЕсли;
		
		ВсегоНаименований  = 0;
		Итого              = 0;
		ИтогоНДС           = 0;
		ИтогоРозничныеЦены = 0;
		
		// СТРОКИ ТЧ
		Пока ВыборкаПоСтрокамТЧ.Следующий() Цикл
			
			ОбластьСтрокаТаблицыНачало.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицыНачало);
			
			Если ВыводитьКоды Тогда
				
				ОбластьСтрокаТаблицыКолонкаКодов.Параметры.Артикул = ВыборкаПоСтрокамТЧ["КолонкаКодов"];
				ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКолонкаКодов);
				
			КонецЕсли;
			
			ОбластьСтрокаТаблицыКолонкаТоваров.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			ОбластьСтрокаТаблицыКолонкаТоваров.Параметры.Товар = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(ВыборкаПоСтрокамТЧ.НоменклатураПредставление,ВыборкаПоСтрокамТЧ.ХарактеристикаПредставление);
			ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКолонкаТоваров);
			
			Если ВыводитьУпаковки Тогда
				
				ОбластьСтрокаТаблицыКолонкаУпаковок.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
				ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКолонкаУпаковок);
				
			КонецЕсли;
			
			ОбластьСтрокаТаблицыКолонкаКоличество.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
			ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКолонкаКоличество);
			
			Если НЕ ТолькоПоРозничнымЦенам Тогда
				ОбластьСтрокаТаблицыЦеныПоступления.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
				ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыЦеныПоступления);
				
			КонецЕсли;
			
			Если ВыводитьЦены Тогда
				ОбластьСтрокаТаблицыРозничныеЦены.Параметры.Заполнить(ВыборкаПоСтрокамТЧ);
				ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыРозничныеЦены);
			КонецЕсли;
			
			
			ВсегоНаименований = ВсегоНаименований + 1;
			Итого             = Итого + ВыборкаПоСтрокамТЧ.Сумма;
			ИтогоНДС          = ИтогоНДС + ВыборкаПоСтрокамТЧ.СуммаНДС;
			
			Если ВыводитьЦены Тогда
				
				ИтогоРозничныеЦены = ИтогоРозничныеЦены  +ВыборкаПоСтрокамТЧ.СуммаРозничная;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьПодвалТаблицыНачало);
		
		Если ВыводитьКоды Тогда
			
			ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыКолонкаКодов);
			
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыКолонкаТоваров);
		
		Если ВыводитьУпаковки Тогда
			ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыКолонкаУпаковок);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыКолонкаКоличество);
		
		Если НЕ ТолькоПоРозничнымЦенам Тогда
			ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыЦеныПоступления);
		КонецЕсли;
		
		Если ВыводитьЦены Тогда
			ТабличныйДокумент.Присоединить(ОбластьПодвалТаблицыРозничныеЦены);
		КонецЕсли;
		
		// ИТОГО
		
		
		Если ТолькоПоРозничнымЦенам Тогда
			
			ТекстИтоговойСтроки = НСтр("ru = '%Итого%'");
			ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%Итого%",ФормированиеПечатныхФормСервер.ФорматСумм(ИтогоРозничныеЦены));
			ОбластьИтого.Параметры.Итого = ТекстИтоговойСтроки;
			
		Иначе
			
			ТекстИтоговойСтроки = НСтр("ru = '%Итого%'");
			ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%Итого%",ФормированиеПечатныхФормСервер.ФорматСумм(Итого));
			ОбластьИтого.Параметры.Итого = ТекстИтоговойСтроки;
			
		КонецЕсли;
		
		
		
		Если ВыводитьЦены И НЕ ТолькоПоРозничнымЦенам Тогда
			ТекстИтоговойСтроки = НСтр("ru = '%Итого%'");
			ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%Итого%",ФормированиеПечатныхФормСервер.ФорматСумм(ИтогоРозничныеЦены));
			ОбластьИтого.Параметры.ИтогоРозничныеЦены = ТекстИтоговойСтроки;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		Если НЕ ТолькоПоРозничнымЦенам Тогда
			ТекстИтоговойСтроки = НСтр("ru = '%Итого%'");
			ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%Итого%",ФормированиеПечатныхФормСервер.ФорматСумм(ИтогоНДС));
			
			Если НЕ ВыборкаПоДокументам.УчитыватьНДС Тогда
				ТекстИтоговойСтроки = НСтр("ru = 'Без НДС'");
			КонецЕсли;
			
			ОбластьИтогоНДС.Параметры.ИтогоНДС = ТекстИтоговойСтроки;
			
			ОбластьИтогоНДС.Параметры.НДС = ?(ВыборкаПоДокументам.ЦенаВключаетНДС, НСтр("ru='В том числе НДС:'"), НСтр("ru='Сумма НДС:'"));
			
			ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
		КонецЕсли;
		
		
		// СУММА ПРОПИСЬЮ
		
		ТекстИтоговойСтроки = НСтр("ru = 'Всего наименований %ВсегоНаименований%, на сумму %Итого%'");
		Если ТолькоПоРозничнымЦенам Тогда
			СуммаКПрописи = ИтогоРозничныеЦены;
		Иначе
			СуммаКПрописи = Итого + ?(ВыборкаПоДокументам.ЦенаВключаетНДС, 0, ИтогоНДС);
		КонецЕсли;
		
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%ВсегоНаименований%", ВсегоНаименований);
		ТекстИтоговойСтроки = СтрЗаменить(ТекстИтоговойСтроки,"%Итого%", ФормированиеПечатныхФормСервер.ФорматСумм(СуммаКПрописи));
		
		ОбластьСуммаПрописью.Параметры.ИтоговаяСтрока = ТекстИтоговойСтроки;
		ОбластьСуммаПрописью.Параметры.СуммаПрописью = ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(СуммаКПрописи);
		
		ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		
		// ПОДПИСИ
		ОбластьПодписей.Параметры.Заполнить(ВыборкаПоДокументам);
		ОбластьПодписей.Параметры.Получил = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(ВыборкаПоДокументам.Получил);
		ТабличныйДокумент.Вывести(ОбластьПодписей);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаПоДокументам.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьТОРГ12(МассивОбъектов, ОбъектыПечати,ВыводитьУслуги = Истина)
	
	КолонкаКодов = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	
	Если ПустаяСтрока(КолонкаКодов) Тогда
		
		КолонкаКодов = "Код";
		
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокументКПечати.Ссылка                                                 КАК Ссылка,
		|	ДокументКПечати.Номер                                                  КАК Номер,
		|	ДокументКПечати.Дата                                                   КАК Дата,
		|	ДокументКПечати.Контрагент                                             КАК Контрагент,
		|	ДокументКПечати.Организация                                            КАК Организация,
		|	ДокументКПечати.БанковскийСчетОрганизации                              КАК БанковскийСчетОрганизации,
		|	ДокументКПечати.БанковскийСчетКонтрагента                              КАК БанковскийСчетКонтрагента,
		|	ДокументКПечати.Контрагент                                             КАК Руководители,
		|	ДокументКПечати.Организация.Префикс                                    КАК Префикс,
		|	ДокументКПечати.Организация                                            КАК Грузополучатель,
		|	ДокументКПечати.Контрагент                                             КАК Грузоотправитель,
		|	ДокументКПечати.Контрагент                                             КАК Поставщик,
		|	ДокументКПечати.Организация                                            КАК Покупатель,
		|	ДокументКПечати.Организация                                            КАК Плательщик,
		|	ДокументКПечати.Ответственный.ФизическоеЛицо                           КАК ОтветственноеЛицо,
		|	ДокументКПечати.ЦенаВключаетНДС                                        КАК ЦенаВключаетНДС,
		|	ДокументКПечати.УчитыватьНДС                                           КАК УчитыватьНДС,
		|	ДокументКПечати.ДатаВходящегоДокумента                                 КАК ДатаВходящегоДокумента,
		|	ДокументКПечати.НомерВходящегоДокумента                                КАК НомерВходящегоДокумента
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК ДокументКПечати
		|
		|ГДЕ
		|	ДокументКПечати.Ссылка В (&МассивДокументов)
		|	И ДокументКПечати.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;		
		|
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВложенныйЗапрос.Ссылка                                                     КАК Ссылка,
		|	ВложенныйЗапрос.Номенклатура                                               КАК Номенклатура,
		|	ВложенныйЗапрос.Номенклатура.НаименованиеПолное                            КАК ТоварНаименование,
		|	ВложенныйЗапрос.Номенклатура." + КолонкаКодов + "                          КАК ТоварКод,
		|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление                             КАК БазоваяЕдиницаНаименование,
		|	ВложенныйЗапрос.ЕдиницаИзмерения.Код                                       КАК БазоваяЕдиницаКодПоОКЕИ,
		|	ВложенныйЗапрос.ЕдиницаИзмерения                                           КАК ЕдиницаИзмерения,
		|	ВложенныйЗапрос.ЕдиницаИзмеренияМест.Представление                         КАК ВидУпаковки,
		|	ВложенныйЗапрос.КоэффициентМест / ВложенныйЗапрос.Коэффициент              КАК КоличествоВОдномМесте,
		|	ВложенныйЗапрос.Характеристика                                             КАК Характеристика,
		|	ВложенныйЗапрос.СтавкаНДС                                                  КАК СтавкаНДС,
		|	ВложенныйЗапрос.Цена                                                       КАК Цена,
		|	ВложенныйЗапрос.Количество                                                 КАК Количество,
		|	ЕСТЬNULL(ВложенныйЗапрос.КоличествоМест, 0)                                КАК КоличествоМест,
		|	ВложенныйЗапрос.Сумма                                                      КАК Сумма,
		|	ВложенныйЗапрос.СуммаНДС                                                   КАК СуммаНДС,
		|	ВложенныйЗапрос.НомерСтроки                                                КАК НомерСтроки,
		|	ВложенныйЗапрос.МассаБрутто                                                КАК МассаБрутто,
		|	ЛОЖЬ                                                                       КАК ЭтоВозвратнаяТара
		|ИЗ (
		|	ВЫБРАТЬ
		|		Товары.Ссылка КАК Ссылка,
		|		Товары.Номенклатура КАК Номенклатура,
		|
		|		ВЫБОР КОГДА Товары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
		|			1
		|		ИНАЧЕ
		|			Товары.Упаковка.Коэффициент
		|		КОНЕЦ КАК Коэффициент,
		|
		|		ВЫБОР КОГДА Товары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
		|			Товары.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ
		|			Товары.Упаковка.ЕдиницаИзмерения
		|		КОНЕЦ КАК ЕдиницаИзмерения,
		|
		|		ВЫБОР КОГДА Товары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
		|			1
		|		ИНАЧЕ
		|			Товары.Упаковка.Коэффициент
		|		КОНЕЦ КАК КоэффициентМест,
		|
		|		ВЫБОР КОГДА Товары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
		|			Товары.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ
		|			Товары.Упаковка.ЕдиницаИзмерения
		|		КОНЕЦ КАК ЕдиницаИзмеренияМест,
		|
		|		ВЫБОР КОГДА Товары.Упаковка.Вес ЕСТЬ NULL ТОГДА
		|			Товары.Количество * Товары.Номенклатура.Вес
		|		ИНАЧЕ
		|			Товары.КоличествоУпаковок * Товары.Упаковка.Вес
		|		КОНЕЦ КАК МассаБрутто,
		|
		|		Товары.Характеристика     КАК Характеристика,
		|		Товары.СтавкаНДС          КАК СтавкаНДС,
		|		ВЫБОР
		|			КОГДА
		|				Товары.КоличествоУпаковок = 0
		|			ТОГДА
		|				Товары.Цена
		|			ИНАЧЕ
		|				Товары.Сумма / Товары.КоличествоУпаковок
		|		КОНЕЦ КАК Цена,
		|		Товары.КоличествоУпаковок КАК Количество,
		|		Товары.КоличествоУпаковок КАК КоличествоМест,
		|		Товары.Сумма              КАК Сумма,
		|		Товары.СуммаНДС           КАК СуммаНДС,
		|		Товары.НомерСтроки        КАК НомерСтроки
		|	ИЗ
		|		Документ.ПоступлениеТоваров.Товары КАК Товары
		|	ГДЕ
		|		Товары.Ссылка В (&МассивДокументов)
		|		И Товары.Ссылка.Проведен
		|		" + ?(Не ВыводитьУслуги, " И Товары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга) ", "") + "
		|	
		|	) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВложенныйЗапрос.Ссылка,
		|	НомерСтроки
		|
		|ИТОГИ ПО
		|	Ссылка
		|");
		
		Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваров_ТОРГ12";
			
		ЕдиницаИзмеренияВеса           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("ЕдиницаИзмеренияВеса");
		
		ТабличныйДокумент.ПолеСлева = 5;
		ТабличныйДокумент.ПолеСправа = 5;
		ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
		ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		ДанныеПечати = МассивРезультатов[0].Выбрать();
		ВыборкаПоДокументам = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ТОРГ12");
		
		ПервыйДокумент = Истина;
		Пока ДанныеПечати.Следующий() Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент = Ложь;
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
			
			СведенияОПоставщике       = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Поставщик,        ДанныеПечати.Дата,, ДанныеПечати.БанковскийСчетКонтрагента);
			СведенияОПокупателе       = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Покупатель,       ДанныеПечати.Дата,, ДанныеПечати.БанковскийСчетОрганизации);
			СведенияОГрузополучателе  = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Грузополучатель,  ДанныеПечати.Дата,, ДанныеПечати.БанковскийСчетОрганизации);
			СведенияОГрузоотправитель = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Грузоотправитель, ДанныеПечати.Дата,, ДанныеПечати.БанковскийСчетКонтрагента);
			
			ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
			ОбластьШапка.Параметры.Заполнить(ДанныеПечати);
			ОбластьШапка.Параметры.НомерДокумента = ДанныеПечати.НомерВходящегоДокумента;
			ОбластьШапка.Параметры.ДатаДокумента  = ДанныеПечати.ДатаВходящегоДокумента;
			
			Если ДанныеПечати.Организация = ДанныеПечати.Грузоотправитель Тогда
				
				ОбластьШапка.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, 
				"ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет");
				
			Иначе
				
				ОбластьШапка.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОГрузоотправитель, 
				"ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет");
				
			КонецЕсли;
			
			ОбластьШапка.Параметры.ПредставлениеГрузополучателя = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОГрузополучателе, 
			"ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет");
			
			ОбластьШапка.Параметры.ПредставлениеПоставщика  = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, 
			"ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет");
			
			ОбластьШапка.Параметры.ПредставлениеПлательщика = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе, 
			"ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет");
			
			// Выводим коды
			ОбластьШапка.Параметры.ОрганизацияПоОКПО          = СведенияОПоставщике.КодПоОКПО;
			ОбластьШапка.Параметры.ВидДеятельностиПоОКДП      = "";
			ОбластьШапка.Параметры.ГрузополучательПоОКПО      = СведенияОПоставщике.КодПоОКПО;
			ОбластьШапка.Параметры.ПоставщикПоОКПО            = СведенияОПоставщике.КодПоОКПО;
			ОбластьШапка.Параметры.ПлательщикПоОКПО           = СведенияОПокупателе.КодПоОКПО;
			ОбластьШапка.Параметры.ОснованиеНомер             = "";
			ОбластьШапка.Параметры.ОснованиеДата              = "";
			ОбластьШапка.Параметры.ТранспортнаяНакладнаяНомер = "";
			ОбластьШапка.Параметры.ТранспортнаяНакладнаяДата  = "";
			
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			НомерСтраницы = 1;
			
			ИтоговыеСуммы = Новый Структура;
			
			// Инициализация итогов по странице.
			ИтоговыеСуммы.Вставить("ИтогоМассаБруттоНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоМестНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоКоличествоНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоСуммаНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоНДСНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоСуммаСНДСНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоМассаБруттоНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоМассаНеттоНаСтранице", 0);
			
			// Инициализация итогов по документу.
			ИтоговыеСуммы.Вставить("ИтогоМассаБрутто", 0);
			ИтоговыеСуммы.Вставить("ИтогоМест", 0);
			ИтоговыеСуммы.Вставить("ИтогоКоличество", 0);
			ИтоговыеСуммы.Вставить("ИтогоСуммаСНДС", 0);
			ИтоговыеСуммы.Вставить("ИтогоСумма", 0);
			ИтоговыеСуммы.Вставить("ИтогоНДС", 0);
			ИтоговыеСуммы.Вставить("ИтогоМассаБрутто", 0);
			ИтоговыеСуммы.Вставить("ИтогоМассаНетто", 0);
			
			ИтоговыеСуммы.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", 0);
			ИтоговыеСуммы.Вставить("СуммаПрописью", "");
			
			ДанныеСтроки = Новый Структура;
			ДанныеСтроки.Вставить("Номер", 0);
			ДанныеСтроки.Вставить("Мест", 0);
			ДанныеСтроки.Вставить("Количество", 0);
			ДанныеСтроки.Вставить("Цена", 0);
			ДанныеСтроки.Вставить("СуммаБезНДС", 0);
			ДанныеСтроки.Вставить("СуммаНДС", 0);
			ДанныеСтроки.Вставить("СуммаСНДС", 0);
			ДанныеСтроки.Вставить("МассаБрутто", 0);
			ДанныеСтроки.Вставить("МассаНетто", 0);
			
			
			// Создаем массив для проверки вывода.
			МассивВыводимыхОбластей = Новый Массив;
			
			// Выводим многострочную часть документа.
			ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаб");
			ОбластьСтрока           = Макет.ПолучитьОбласть("Строка");
			ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
			ОбластьВсего            = Макет.ПолучитьОбласть("Всего");
			ОбластьПодвала          = Макет.ПолучитьОбласть("Подвал");
			
			СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
			ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
			
			КоличествоСтрок = ВыборкаПоДокументам.Количество();
			
			СтрокаТовары = ВыборкаПоДокументам.Выбрать();
			Пока СтрокаТовары.Следующий() Цикл
				
				ДанныеСтроки.Номер = ДанныеСтроки.Номер + 1;
								
				ОбластьСтрока.Параметры.Заполнить(СтрокаТовары);
				ОбластьСтрока.Параметры.ТоварНаименование = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
					СтрокаТовары.ТоварНаименование,
					СтрокаТовары.Характеристика);
				
				ДанныеСтроки.Мест = СтрокаТовары.КоличествоМест;
				ДанныеСтроки.Количество  = СтрокаТовары.Количество;
				
				Если ЕдиницаИзмеренияВеса <> Неопределено Тогда
					Если Не ЗначениеЗаполнено(ЕдиницаИзмеренияВеса) Тогда
						ДанныеСтроки.МассаБрутто = 0;
					Иначе
						ДанныеСтроки.МассаБрутто = СтрокаТовары.МассаБрутто;
					КонецЕсли;
				КонецЕсли;
				
				ДанныеСтроки.СуммаСНДС   = Окр((СтрокаТовары.Сумма + ?(ДанныеПечати.ЦенаВключаетНДС, 0, СтрокаТовары.СуммаНДС)), 2);
				ДанныеСтроки.СуммаНДС    = СтрокаТовары.СуммаНДС;
				ДанныеСтроки.СуммаБезНДС = ДанныеСтроки.СуммаСНДС - ДанныеСтроки.СуммаНДС;
				
				Если ДанныеПечати.ЦенаВключаетНДС Тогда
					ДанныеСтроки.Цена = ?(ДанныеСтроки.Количество = 0, 0, ДанныеСтроки.СуммаБезНДС / ДанныеСтроки.Количество);
				Иначе
					ДанныеСтроки.Цена = СтрокаТовары.Цена;
				КонецЕсли;
				
				ОбластьСтрока.Параметры.Заполнить(ДанныеСтроки);
				
				Если ДанныеСтроки.Номер = 1 Тогда // первая строка
					
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
					ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
					
				Иначе
					
					МассивВыводимыхОбластей.Очистить();
					МассивВыводимыхОбластей.Добавить(ОбластьШапка);
					МассивВыводимыхОбластей.Добавить(ОбластьСтрока);
					МассивВыводимыхОбластей.Добавить(ОбластьИтоговПоСтранице);
					
					Если ДанныеСтроки.Номер = КоличествоСтрок Тогда
						
						МассивВыводимыхОбластей.Добавить(ОбластьВсего);
						МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
						
					КонецЕсли;
					
					Если ДанныеСтроки.Номер <> 1 И Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
						
						ОбластьИтоговПоСтранице.Параметры.Заполнить(ИтоговыеСуммы);
						ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
						
						ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице = 0;
						ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  = 0;
						ИтоговыеСуммы.ИтогоМестНаСтранице        = 0;
						ИтоговыеСуммы.ИтогоКоличествоНаСтранице  = 0;
						ИтоговыеСуммы.ИтогоСуммаНаСтранице       = 0;
						ИтоговыеСуммы.ИтогоНДСНаСтранице         = 0;
						ИтоговыеСуммы.ИтогоСуммаСНДСНаСтранице   = 0;
						
						НомерСтраницы = НомерСтраницы + 1;
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
						ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
						
					КонецЕсли;
					
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(ОбластьСтрока);
								
				// Увеличим итоги по странице.
				ИтоговыеСуммы.ИтогоМестНаСтранице        = ИтоговыеСуммы.ИтогоМестНаСтранице        + ДанныеСтроки.Мест;
				ИтоговыеСуммы.ИтогоКоличествоНаСтранице  = ИтоговыеСуммы.ИтогоКоличествоНаСтранице  + ДанныеСтроки.Количество;
				ИтоговыеСуммы.ИтогоСуммаНаСтранице       = ИтоговыеСуммы.ИтогоСуммаНаСтранице       + ДанныеСтроки.СуммаБезНДС;
				ИтоговыеСуммы.ИтогоНДСНаСтранице         = ИтоговыеСуммы.ИтогоНДСНаСтранице         + ДанныеСтроки.СуммаНДС;
				ИтоговыеСуммы.ИтогоСуммаСНДСНаСтранице   = ИтоговыеСуммы.ИтогоСуммаСНДСНаСтранице   + ДанныеСтроки.СуммаСНДС;
				ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице = ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице + ДанныеСтроки.МассаБрутто;
				ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  = ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  + ДанныеСтроки.МассаНетто;
				
				// Увеличим итоги по документу.
				ИтоговыеСуммы.ИтогоМест        = ИтоговыеСуммы.ИтогоМест        + ДанныеСтроки.Мест;
				ИтоговыеСуммы.ИтогоКоличество  = ИтоговыеСуммы.ИтогоКоличество  + ДанныеСтроки.Количество;
				ИтоговыеСуммы.ИтогоСумма       = ИтоговыеСуммы.ИтогоСумма       + ДанныеСтроки.СуммаБезНДС;
				ИтоговыеСуммы.ИтогоНДС         = ИтоговыеСуммы.ИтогоНДС         + ДанныеСтроки.СуммаНДС;
				ИтоговыеСуммы.ИтогоСуммаСНДС   = ИтоговыеСуммы.ИтогоСуммаСНДС   + ДанныеСтроки.СуммаСНДС;
				ИтоговыеСуммы.ИтогоМассаБрутто = ИтоговыеСуммы.ИтогоМассаБрутто + ДанныеСтроки.МассаБрутто;
				ИтоговыеСуммы.ИтогоМассаНетто  = ИтоговыеСуммы.ИтогоМассаНетто  + ДанныеСтроки.МассаНетто;
				
			КонецЦикла;
			
			// Выводим итоги по последней странице.
			ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
			ОбластьИтоговПоСтранице.Параметры.Заполнить(ИтоговыеСуммы);
			
			ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
			
			// Выводим итоги по документу в целом.
			ОбластьМакета = Макет.ПолучитьОбласть("Всего");
			ОбластьМакета.Параметры.Заполнить(ИтоговыеСуммы);
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			// Выводим подвал документа
			
			ИтоговыеСуммы.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", ЧислоПрописью(ДанныеСтроки.Номер, ,",,,,,,,,0"));
			ИтоговыеСуммы.Вставить("СуммаПрописью", ФормированиеПечатныхФормСервер.СформироватьСуммуПрописью(ИтоговыеСуммы.ИтогоСуммаСНДС));
						
			ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
			
			ПолнаяДатаДокумента = Формат(ДанныеПечати.Дата, "ДФ=""дд ММММ гггг """"года""""""");
			ДлинаСтроки = СтрДлина(ПолнаяДатаДокумента);
			ПервыйРазделитель = Найти(ПолнаяДатаДокумента, " ");
			ВторойРазделитель = Найти(Прав(ПолнаяДатаДокумента, ДлинаСтроки - ПервыйРазделитель), " ") + ПервыйРазделитель;
			ОбластьМакета.Параметры.ДатаДокументаДень = """" + Лев(ПолнаяДатаДокумента, ПервыйРазделитель -1 ) + """";
			ОбластьМакета.Параметры.ДатаДокументаМесяц = Сред(ПолнаяДатаДокумента, ПервыйРазделитель + 1, ВторойРазделитель - ПервыйРазделитель - 1);
			ОбластьМакета.Параметры.ДатаДокументаГод = Прав(ПолнаяДатаДокумента, ДлинаСтроки - ВторойРазделитель);
						
			Руководители = ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(ДанныеПечати.Руководители, ДанныеПечати.Дата);
						
			ОбластьМакета.Параметры.ФИОГлавБухгалтера     = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(Руководители.ГлавныйБухгалтер);
			ОбластьМакета.Параметры.ФИОРуководителя       = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(Руководители.Руководитель);
			ОбластьМакета.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжность;
			СтруктураФИО = ФормированиеПечатныхФормСервер.ФамилияИмяОтчество(ДанныеПечати.ОтветственноеЛицо, ДанныеПечати.Дата);
			ОбластьМакета.Параметры.ФИОКладовщика         = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(Неопределено,СтруктураФИО.Фамилия,СтруктураФИО.Имя,СтруктураФИО.Отчество);
			
			Если ИтоговыеСуммы.ИтогоМест > 0 Тогда
				
				ОбластьМакета.Параметры.ВсегоМестПрописью = ЧислоПрописью(ИтоговыеСуммы.ИтогоМест, ,",,,,,,,,0");
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЕдиницаИзмеренияВеса) И ИтоговыеСуммы.ИтогоМассаБрутто > 0 Тогда
				
				ОбластьМакета.Параметры.МассаГрузаПрописью = ЧислоПрописью(ИтоговыеСуммы.ИтогоМассаБрутто, ,",,,,,,,,0")+ " " + СокрЛП(ЕдиницаИзмеренияВеса) + ".";
				
			КонецЕсли;
			
			ОбластьМакета.Параметры.КоличествоПорядковыхНомеровЗаписейПрописью = ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписейПрописью;
			ОбластьМакета.Параметры.СуммаПрописью = ИтоговыеСуммы.СуммаПрописью;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
						
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
			
		КонецЦикла;
		
		Возврат ТабличныйДокумент;
		
КонецФункции

Функция ПечатьМ4(МассивОбъектов, ОбъектыПечати)
	
	КолонкаКодов = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	
	Если ПустаяСтрока(КолонкаКодов) Тогда
		
		КолонкаКодов = "Код";
		
	КонецЕсли;

	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Документ.Ссылка                                  КАК Ссылка,
		|	Документ.Номер                                   КАК Номер,
		|	Документ.Дата                                    КАК Дата,
		|	Документ.Дата                                    КАК ДатаСоставления,
		|	Документ.НомерВходящегоДокумента                 КАК НомерСопроводительногоДокумента,
		|	Документ.Контрагент                              КАК Поставщик,
		|   Документ.Контрагент.Код                          КАК ПоставщикКод,
		|	Документ.Организация                             КАК Организация,
		|	Документ.Организация                             КАК ЮрФизЛицо,
		|	Документ.Организация.Префикс                     КАК Префикс,
		|	Документ.Склад.Наименование                      КАК СкладНаименование,
		|	Документ.Склад                                   КАК МестоПриемки,
		|	Документ.ЦенаВключаетНДС                         КАК ЦенаВключаетНДС
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК Документ
		|
		|ГДЕ
		|	Документ.Ссылка В (&МассивДокументов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВложенныйЗапрос.Ссылка                                                     КАК Ссылка,
		|	ВложенныйЗапрос.Номенклатура                                               КАК Номенклатура,
		|	ВложенныйЗапрос.Номенклатура.НаименованиеПолное                            КАК ТоварНаименование,
		|	ВложенныйЗапрос.Номенклатура." + КолонкаКодов + "                          КАК ТоварКод,
		|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление                             КАК ЕдиницаИзмеренияНаименование,
		|	ВложенныйЗапрос.ЕдиницаИзмерения.Код                                       КАК ЕдиницаИзмеренияКод,
		|	ВложенныйЗапрос.Характеристика                                             КАК Характеристика,
		|	ВложенныйЗапрос.Количество                                                 КАК Количество,
		|	ВложенныйЗапрос.Количество                                                 КАК КоличествоПринято,
		|	ВложенныйЗапрос.Цена                                                       КАК Цена,
		|	ВложенныйЗапрос.Сумма                                                      КАК Сумма,
		|	ВложенныйЗапрос.СуммаНДС                                                   КАК СуммаНДС,
		|	ВложенныйЗапрос.НомерСтроки                                                КАК НомерСтроки
		|ИЗ (
		|	ВЫБРАТЬ
		|		Документ.Ссылка             КАК Ссылка,
		|		Документ.Номенклатура       КАК Номенклатура,
		|		ВЫБОР КОГДА Документ.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
		|			Документ.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ
		|			Документ.Упаковка.ЕдиницаИзмерения
		|		КОНЕЦ                       КАК ЕдиницаИзмерения,
		|		Документ.Характеристика     КАК Характеристика,
		|		ВЫБОР
		|			КОГДА Документ.КоличествоУпаковок = 0
		|			ТОГДА
		|				Документ.Цена
		|			ИНАЧЕ
		|				Документ.Сумма / Документ.КоличествоУпаковок
		|		КОНЕЦ КАК Цена,
		|		Документ.КоличествоУпаковок КАК Количество,
		|		Документ.Сумма              КАК Сумма,
		|		Документ.СуммаНДС           КАК СуммаНДС,
		|		Документ.НомерСтроки        КАК НомерСтроки
		|	ИЗ
		|		Документ.ПоступлениеТоваров.Товары КАК Документ
		|	ГДЕ
		|		Документ.Ссылка В (&МассивДокументов)
		|		И Документ.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|
		|	) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки
		|
		|ИТОГИ ПО
		|	Ссылка
		|");
		
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
		
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваров_М4";
		
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	// Зададим параметры макета
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ДанныеПечати      = МассивРезультатов[0].Выбрать();
	ВыборкаПоДокументам = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_М4");
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
		// Выводим общие реквизиты шапки.
		СведенияОПокупателе = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.ДатаСоставления);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе);
		ОбластьМакета.Параметры.ОрганизацияПоОКПО        = СведенияОПокупателе.КодПоОКПО;
		ОбластьМакета.Параметры.НомерДокумента           = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПечати.Номер, Ложь, Истина);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим заголовок документа.
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокДокумента");
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		ОбластьМакета.Параметры.ДатаСоставления = ДанныеПечати.ДатаСоставления;
		
		СведенияОПоставщике     = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Поставщик, ДанныеПечати.ДатаСоставления);
		ПредставлениеПоставщика = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ПолноеНаименование");
		ОбластьМакета.Параметры.ПоставщикНаименование = ПредставлениеПоставщика;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
			
		// Выводим заголовок таблицы
		ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
		ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);

		НомерСтраницы = 1;
				
		ИтоговыеСуммы = Новый Структура;
	
		ИтоговыеСуммы.Вставить("ИтогоКоличество", 0);
		ИтоговыеСуммы.Вставить("ИтогоСуммаСНДС", 0);
		ИтоговыеСуммы.Вставить("ИтогоСумма", 0);
		ИтоговыеСуммы.Вставить("ИтогоНДС", 0);
		
		ДанныеСтроки = Новый Структура;
		
		ДанныеСтроки.Вставить("Номер", 0);
		ДанныеСтроки.Вставить("Количество", 0);
		ДанныеСтроки.Вставить("Цена", 0);
		ДанныеСтроки.Вставить("СуммаБезНДС", 0);
		ДанныеСтроки.Вставить("СуммаНДС", 0);
		ДанныеСтроки.Вставить("СуммаСНДС", 0);
			
		// Создаем массив для проверки вывода.
		МассивВыводимыхОбластей = Новый Массив;
		
		// Выводим многострочную часть документа.
		ОбластьМакета           = Макет.ПолучитьОбласть("Строка");
		ОбластьПодвалСтрок      = Макет.ПолучитьОбласть("ПодвалСтрок");
		ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("Итого");
		ОбластьПодвала          = Макет.ПолучитьОбласть("Подвал");
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
		
		КоличествоСтрок = ВыборкаПоДокументам.Количество();
		
		СтрокаТовары = ВыборкаПоДокументам.Выбрать();
		
		Пока СтрокаТовары.Следующий() Цикл
			
			ОбластьМакета.Параметры.Заполнить(СтрокаТовары);
			
			ДанныеСтроки.Номер       = ДанныеСтроки.Номер + 1;
			ДанныеСтроки.Количество  = СтрокаТовары.Количество;
			
			ДанныеСтроки.СуммаСНДС   = Окр((СтрокаТовары.Сумма + ?(ДанныеПечати.ЦенаВключаетНДС, 0, СтрокаТовары.СуммаНДС)), 2);
			ДанныеСтроки.СуммаНДС    = СтрокаТовары.СуммаНДС;
			ДанныеСтроки.СуммаБезНДС = ДанныеСтроки.СуммаСНДС - ДанныеСтроки.СуммаНДС;
			
			Если ДанныеПечати.ЦенаВключаетНДС Тогда
				ДанныеСтроки.Цена = ?(ДанныеСтроки.Количество = 0, 0, ДанныеСтроки.СуммаБезНДС / ДанныеСтроки.Количество);
			Иначе
				ДанныеСтроки.Цена = СтрокаТовары.Цена;
			КонецЕсли;
			
			ОбластьМакета.Параметры.Заполнить(ДанныеСтроки);
			
			ОбластьМакета.Параметры.ТоварНаименование = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
				СтрокаТовары.ТоварНаименование,
				СтрокаТовары.Характеристика);
						
			МассивВыводимыхОбластей.Очистить();
			МассивВыводимыхОбластей.Добавить(ОбластьМакета);
			МассивВыводимыхОбластей.Добавить(ОбластьИтоговПоСтранице);
			
			Если ДанныеСтроки.Номер = КоличествоСтрок Тогда
				МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
			КонецЕсли;

			Если Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
				НомерСтраницы = НомерСтраницы + 1;
				ПодвалСтрок   = Макет.ПолучитьОбласть("ПодвалСтрок");
				ТабличныйДокумент.Вывести(ПодвалСтрок);
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
				ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			// Увеличим итоги по документу.
			ИтоговыеСуммы.ИтогоКоличество  = ИтоговыеСуммы.ИтогоКоличество  + ДанныеСтроки.Количество;
			ИтоговыеСуммы.ИтогоСумма       = ИтоговыеСуммы.ИтогоСумма       + ДанныеСтроки.СуммаБезНДС;
			ИтоговыеСуммы.ИтогоНДС         = ИтоговыеСуммы.ИтогоНДС         + ДанныеСтроки.СуммаНДС;
			ИтоговыеСуммы.ИтогоСуммаСНДС   = ИтоговыеСуммы.ИтогоСуммаСНДС   + ДанныеСтроки.СуммаСНДС;
				
		КонецЦикла;
		
		// Выводим итоги по документу
		ОбластьМакета = Макет.ПолучитьОбласть("Итого");

		ОбластьМакета.Параметры.ИтогоКоличествоПринято = ИтоговыеСуммы.ИтогоКоличество;
		ОбластьМакета.Параметры.ИтогоСуммаБезНДС = ИтоговыеСуммы.ИтогоСумма;
		ОбластьМакета.Параметры.ИтогоСуммаНДС    = ИтоговыеСуммы.ИтогоНДС;
		ОбластьМакета.Параметры.ИтогоСуммаСНДС   = ИтоговыеСуммы.ИтогоСуммаСНДС;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим подвал документа
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
			
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьТОРГ4(МассивОбъектов, ОбъектыПечати)
	
	КолонкаКодов = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	
	Если ПустаяСтрока(КолонкаКодов) Тогда
		
		КолонкаКодов = "Код";
		
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Документ.Ссылка                                       КАК Ссылка,
		|	Документ.Номер                                        КАК Номер,
		|	Документ.Дата                                         КАК Дата,
		|	Документ.Дата                                         КАК ДатаДокумента,
		|	Документ.ДатаВходящегоДокумента                       КАК ДатаДокументаСопровождения,
		|	Документ.НомерВходящегоДокумента                      КАК НомерДокументаСопровождения,
		|	Документ.Контрагент                                   КАК Поставщик,
		|	Документ.Организация                                  КАК Организация,
		|	Документ.БанковскийСчетОрганизации                    КАК БанковскийСчетОрганизации,
		|	Документ.БанковскийСчетКонтрагента                    КАК БанковскийСчетКонтрагента,
		|	Документ.Организация.Префикс                          КАК Префикс,
		|	Документ.Склад.Наименование                           КАК МестоПриемки,
		|	Документ.Контрагент                                   КАК Грузоотправитель,
		|	Документ.Организация                            КАК Руководители,
		|	Документ.ЦенаВключаетНДС                              КАК ЦенаВключаетНДС
		|ИЗ
		|	Документ.ПоступлениеТоваров КАК Документ
		|
		|ГДЕ
		|	Документ.Ссылка В (&МассивДокументов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////
		|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
		|
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВложенныйЗапрос.Ссылка                                                     КАК Ссылка,
		|	ВложенныйЗапрос.Номенклатура                                               КАК Номенклатура,
		|	ВложенныйЗапрос.Номенклатура.НаименованиеПолное                            КАК ТоварНаименование,
		|	ВложенныйЗапрос.Номенклатура." + КолонкаКодов + "                          КАК ТоварКод,
		|	ВложенныйЗапрос.ЕдиницаИзмерения.Представление                             КАК ЕдиницаИзмеренияНаименование,
		|	ВложенныйЗапрос.ЕдиницаИзмерения.Код                                       КАК ЕдиницаИзмеренияКод,
		|	ВложенныйЗапрос.Характеристика                                             КАК Характеристика,
		|	ВложенныйЗапрос.КоэффициентМест / ВложенныйЗапрос.Коэффициент              КАК КоличествоВОдномМесте,
		|	ВложенныйЗапрос.Количество                                                 КАК Количество,
		|	ЕСТЬNULL(ВложенныйЗапрос.КоличествоМест, 0)                                КАК КоличествоМест,
		|	ВложенныйЗапрос.МассаБрутто                                                КАК МассаБрутто,
		|	ВложенныйЗапрос.МассаНетто                                                 КАК МассаНетто,
		|	ВложенныйЗапрос.Цена                                                       КАК Цена,
		|	ВложенныйЗапрос.Сумма                                                      КАК Сумма,
		|	ВложенныйЗапрос.СуммаНДС                                                   КАК СуммаНДС,
		|	ВложенныйЗапрос.НомерСтроки                                                КАК НомерСтроки,
		|	ЛОЖЬ                                                                       КАК ЭтоВозвратнаяТара
		|ИЗ (
		|	ВЫБРАТЬ
		|		Документ.Ссылка             КАК Ссылка,
		|		Документ.Номенклатура       КАК Номенклатура,
		|		ВЫБОР КОГДА Документ.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
		|			Документ.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ
		|			Документ.Упаковка.ЕдиницаИзмерения
		|		КОНЕЦ                                      КАК ЕдиницаИзмерения,
		|		ВЫБОР КОГДА Документ.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
		|			1
		|		ИНАЧЕ
		|			Документ.Упаковка.Коэффициент
		|		КОНЕЦ КАК КоэффициентМест,
		|		ВЫБОР КОГДА Документ.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка) ТОГДА
		|			1
		|		ИНАЧЕ
		|			Документ.Упаковка.Коэффициент
		|		КОНЕЦ                                      КАК Коэффициент,
		|		ВЫБОР КОГДА Документ.Упаковка.Вес ЕСТЬ NULL ТОГДА
		|			Документ.Количество * Документ.Номенклатура.Вес
		|		ИНАЧЕ
		|			Документ.КоличествоУпаковок * Документ.Упаковка.Вес
		|		КОНЕЦ                                      КАК МассаБрутто,
		|		Документ.Количество * Документ.Номенклатура.Вес КАК МассаНетто,
		|		Документ.Характеристика     КАК Характеристика,
		|		ВЫБОР
		|			КОГДА
		|				Документ.КоличествоУпаковок = 0
		|			ТОГДА
		|				Документ.Цена
		|			ИНАЧЕ
		|				Документ.Сумма / Документ.КоличествоУпаковок
		|		КОНЕЦ КАК Цена,
		|		Документ.КоличествоУпаковок КАК Количество,
		|		Документ.КоличествоУпаковок КАК КоличествоМест,
		|		Документ.Сумма              КАК Сумма,
		|		Документ.СуммаНДС           КАК СуммаНДС,
		|		Документ.НомерСтроки        КАК НомерСтроки
		|	ИЗ
		|		Документ.ПоступлениеТоваров.Товары КАК Документ
		|	ГДЕ
		|		Документ.Ссылка В (&МассивДокументов)
		|		И Документ.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|
		|	) КАК ВложенныйЗапрос
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки
		|
		|ИТОГИ ПО
		|	Ссылка
		|");
		
		Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
				
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваров_ТОРГ4";
		
		ЕдиницаИзмеренияВеса           = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("ЕдиницаИзмеренияВеса");
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		// Зададим параметры макета
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		ДанныеПечати      = МассивРезультатов[0].Выбрать();
		ВыборкаПоДокументам = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_ТОРГ4");
		
		ПервыйДокумент = Истина;
		
		Пока ДанныеПечати.Следующий() Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент = Ложь;
			НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
						
			// Выводим общие реквизиты шапки.
			ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
			
			СведенияОПокупателе       = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Организация,      ДанныеПечати.ДатаДокумента,, ДанныеПечати.БанковскийСчетОрганизации);
			СведенияОПоставщике       = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Поставщик ,       ДанныеПечати.ДатаДокумента,, ДанныеПечати.БанковскийСчетКонтрагента);
			СведенияОГрузоотправителе = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Грузоотправитель, ДанныеПечати.ДатаДокумента,, ДанныеПечати.БанковскийСчетКонтрагента);
			
			ОбластьМакета.Параметры.ПредставлениеПоставщика       = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике,"ПолноеНаименование,ИНН,ЮридическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет");
			ОбластьМакета.Параметры.ОписаниеПоставщика            = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПоставщике, "ПолноеНаименование,ЮридическийАдрес,Телефоны");
			ОбластьМакета.Параметры.ПредставлениеГрузоотправителя = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОГрузоотправителе, "ПолноеНаименование,ИНН,ФактическийАдрес,Телефоны,НомерСчета,Банк,БИК,КоррСчет");
			ОбластьМакета.Параметры.ПоставщикПоОКПО               = СведенияОПоставщике.КодПоОКПО;
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ОбластьМакета.Параметры.ДатаДокумента            = ДанныеПечати.ДатаДокумента;
			ОбластьМакета.Параметры.НомерДокумента           = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПечати.Номер, Ложь, Истина);
			ОбластьМакета.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОПокупателе);
			ОбластьМакета.Параметры.ОрганизацияПоОКПО        = СведенияОПокупателе.КодПоОКПО;
			ОбластьМакета.Параметры.ГрузополучательПоОКПО    = СведенияОПокупателе.КодПоОКПО;
						
			Руководители = ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(ДанныеПечати.Руководители, ДанныеПечати.Дата);
			ОбластьМакета.Параметры.ФИОРуководителя       = ФормированиеПечатныхФормСервер.ФамилияИнициалыФизЛица(Руководители.Руководитель);
			ОбластьМакета.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжность;
						
			РеквизитыСопроводительногоДокумента = НСтр("ru='сопроводительный документ № %НомерДокументаСопровождения% от %ДатаДокументаСопровождения%'");
			РеквизитыСопроводительногоДокумента = СтрЗаменить(РеквизитыСопроводительногоДокумента, "%НомерДокументаСопровождения%", ДанныеПечати.НомерДокументаСопровождения);
			РеквизитыСопроводительногоДокумента = СтрЗаменить(РеквизитыСопроводительногоДокумента, "%ДатаДокументаСопровождения%",  Формат(ДанныеПечати.ДатаДокументаСопровождения, "ДФ='дд ММММ гггг'"));
			
			ОбластьМакета.Параметры.РеквизитыСопроводительногоДокумента = РеквизитыСопроводительногоДокумента;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
						
			НомерСтраницы = 1;
			
			ИтоговыеСуммы = Новый Структура;
			
			// Инициализация итогов по странице.
			ИтоговыеСуммы.Вставить("ИтогоМассаБруттоНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоМестНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоКоличествоНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоСуммаНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоНДСНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоСуммаСНДСНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоМассаБруттоНаСтранице", 0);
			ИтоговыеСуммы.Вставить("ИтогоМассаНеттоНаСтранице", 0);
			
			// Инициализация итогов по документу.
			ИтоговыеСуммы.Вставить("ИтогоМассаБрутто", 0);
			ИтоговыеСуммы.Вставить("ИтогоМест", 0);
			ИтоговыеСуммы.Вставить("ИтогоКоличество", 0);
			ИтоговыеСуммы.Вставить("ИтогоСуммаСНДС", 0);
			ИтоговыеСуммы.Вставить("ИтогоСумма", 0);
			ИтоговыеСуммы.Вставить("ИтогоНДС", 0);
			ИтоговыеСуммы.Вставить("ИтогоМассаБрутто", 0);
			ИтоговыеСуммы.Вставить("ИтогоМассаНетто", 0);
			
			ИтоговыеСуммы.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", 0);
			ИтоговыеСуммы.Вставить("СуммаПрописью", "");
			
			ДанныеСтроки = Новый Структура;
			ДанныеСтроки.Вставить("Номер", 0);
			ДанныеСтроки.Вставить("Мест", 0);
			ДанныеСтроки.Вставить("Количество", 0);
			ДанныеСтроки.Вставить("Цена", 0);
			ДанныеСтроки.Вставить("СуммаБезНДС", 0);
			ДанныеСтроки.Вставить("СуммаНДС", 0);
			ДанныеСтроки.Вставить("СуммаСНДС", 0);
			ДанныеСтроки.Вставить("МассаБрутто", 0);
			ДанныеСтроки.Вставить("МассаНетто", 0);
						
			// Создаем массив для проверки вывода.
			МассивВыводимыхОбластей = Новый Массив;
			
			// Выводим многострочную часть документа.
			ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
			ОбластьМакета           = Макет.ПолучитьОбласть("Строка");
			ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогиПоСтранице");
			ОбластьВсего            = Макет.ПолучитьОбласть("Всего");
			ОбластьПодвала          = Макет.ПолучитьОбласть("Подвал");
			
			СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
			ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
			
			КоличествоСтрок = ВыборкаПоДокументам.Количество();
			
			СтрокаТовары = ВыборкаПоДокументам.Выбрать();
			Пока СтрокаТовары.Следующий() Цикл
				
				ДанныеСтроки.Номер = ДанныеСтроки.Номер + 1;
				
				ОбластьМакета.Параметры.Заполнить(СтрокаТовары);
				ОбластьМакета.Параметры.ТоварНаименование = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
				СтрокаТовары.ТоварНаименование,
				СтрокаТовары.Характеристика);
				
				ДанныеСтроки.Мест = СтрокаТовары.КоличествоМест;
				ДанныеСтроки.Количество  = СтрокаТовары.Количество;
				
				Если ЕдиницаИзмеренияВеса <> Неопределено Тогда
					
					Если Не ЗначениеЗаполнено(ЕдиницаИзмеренияВеса) Тогда
						ДанныеСтроки.МассаБрутто = 0;
						ДанныеСтроки.МассаНетто = 0;
					Иначе
						ДанныеСтроки.МассаБрутто = СтрокаТовары.МассаБрутто;
						ДанныеСтроки.МассаНетто = СтрокаТовары.МассаНетто;
					КонецЕсли;
					
				КонецЕсли;
				
				ДанныеСтроки.СуммаБезНДС   = СтрокаТовары.Сумма;
				
				Если ДанныеПечати.ЦенаВключаетНДС Тогда
					ДанныеСтроки.Цена = ?(ДанныеСтроки.Количество = 0, 0, ДанныеСтроки.СуммаБезНДС / ДанныеСтроки.Количество);
				Иначе
					ДанныеСтроки.Цена = СтрокаТовары.Цена;
				КонецЕсли;
				
				ОбластьМакета.Параметры.Заполнить(ДанныеСтроки);
				ОбластьМакета.Параметры.Стоимость      = ДанныеСтроки.СуммаБезНДС;
				ОбластьМакета.Параметры.КоличествоШтук = ДанныеСтроки.Количество;
				
				Если ДанныеСтроки.Номер = 1 Тогда // первая строка
					
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
					ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
					
				Иначе
					
					МассивВыводимыхОбластей.Очистить();
					МассивВыводимыхОбластей.Добавить(ОбластьМакета);
					МассивВыводимыхОбластей.Добавить(ОбластьИтоговПоСтранице);
					
					Если ДанныеСтроки.Номер = КоличествоСтрок Тогда
						
						МассивВыводимыхОбластей.Добавить(ОбластьВсего);
						МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
						
					КонецЕсли;
					
					Если ДанныеСтроки.Номер <> 1 И Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
						
						ОбластьИтоговПоСтранице.Параметры.Заполнить(ИтоговыеСуммы);
						ОбластьИтоговПоСтранице.Параметры.ИтогоСтоимостьНаСтранице = ИтоговыеСуммы.ИтогоСуммаНаСтранице;
						ОбластьИтоговПоСтранице.Параметры.ИтогоШтукНаСтранице      = ИтоговыеСуммы.ИтогоКоличествоНаСтранице;
						ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
						
						// Очистим итоги по странице.
						ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице = 0;
						ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  = 0;
						ИтоговыеСуммы.ИтогоМестНаСтранице        = 0;
						ИтоговыеСуммы.ИтогоКоличествоНаСтранице  = 0;
						ИтоговыеСуммы.ИтогоСуммаНаСтранице       = 0;
						ИтоговыеСуммы.ИтогоНДСНаСтранице         = 0;
						ИтоговыеСуммы.ИтогоСуммаСНДСНаСтранице   = 0;
						
						
						
						НомерСтраницы = НомерСтраницы + 1;
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
						ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
						
					КонецЕсли;
					
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(ОбластьМакета);
				
				// Увеличим итоги по странице.
				ИтоговыеСуммы.ИтогоМестНаСтранице        = ИтоговыеСуммы.ИтогоМестНаСтранице        + ДанныеСтроки.Мест;
				ИтоговыеСуммы.ИтогоКоличествоНаСтранице  = ИтоговыеСуммы.ИтогоКоличествоНаСтранице  + ДанныеСтроки.Количество;
				ИтоговыеСуммы.ИтогоСуммаНаСтранице       = ИтоговыеСуммы.ИтогоСуммаНаСтранице       + ДанныеСтроки.СуммаБезНДС;
				ИтоговыеСуммы.ИтогоНДСНаСтранице         = ИтоговыеСуммы.ИтогоНДСНаСтранице         + ДанныеСтроки.СуммаНДС;
				ИтоговыеСуммы.ИтогоСуммаСНДСНаСтранице   = ИтоговыеСуммы.ИтогоСуммаСНДСНаСтранице   + ДанныеСтроки.СуммаСНДС;
				ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице = ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице + ДанныеСтроки.МассаБрутто;
				ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  = ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  + ДанныеСтроки.МассаНетто;
				
				// Увеличим итоги по документу.
				ИтоговыеСуммы.ИтогоМест        = ИтоговыеСуммы.ИтогоМест        + ДанныеСтроки.Мест;
				ИтоговыеСуммы.ИтогоКоличество  = ИтоговыеСуммы.ИтогоКоличество  + ДанныеСтроки.Количество;
				ИтоговыеСуммы.ИтогоСумма       = ИтоговыеСуммы.ИтогоСумма       + ДанныеСтроки.СуммаБезНДС;
				ИтоговыеСуммы.ИтогоНДС         = ИтоговыеСуммы.ИтогоНДС         + ДанныеСтроки.СуммаНДС;
				ИтоговыеСуммы.ИтогоСуммаСНДС   = ИтоговыеСуммы.ИтогоСуммаСНДС   + ДанныеСтроки.СуммаСНДС;
				ИтоговыеСуммы.ИтогоМассаБрутто = ИтоговыеСуммы.ИтогоМассаБрутто + ДанныеСтроки.МассаБрутто;
				ИтоговыеСуммы.ИтогоМассаНетто  = ИтоговыеСуммы.ИтогоМассаНетто  + ДанныеСтроки.МассаНетто;
				
				
			КонецЦикла;
			
			// Выводим итоги по последней странице.
			ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогиПоСтранице");
			ОбластьИтоговПоСтранице.Параметры.Заполнить(ИтоговыеСуммы);
			ОбластьИтоговПоСтранице.Параметры.ИтогоСтоимостьНаСтранице  = ИтоговыеСуммы.ИтогоСуммаНаСтранице;
			ОбластьИтоговПоСтранице.Параметры.ИтогоШтукНаСтранице       = ИтоговыеСуммы.ИтогоКоличествоНаСтранице;
			
			ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
			
			// Выводим итоги по документу в целом.
			ОбластьМакета = Макет.ПолучитьОбласть("Всего");
			ОбластьМакета.Параметры.Заполнить(ИтоговыеСуммы);
			ОбластьМакета.Параметры.ИтогоСтоимость = ИтоговыеСуммы.ИтогоСумма;
			ОбластьМакета.Параметры.ИтогоШтук = ИтоговыеСуммы.ИтогоКоличество;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			
			// Выводим подвал документа
						
			ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
						
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
			
		КонецЦикла;
				
		Возврат ТабличныйДокумент;
			
КонецФункции

Функция ПечатьТОРГ2(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	СформироватьТекстЗапросаТОРГ2(Запрос.Текст);
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваров_ТОРГ2";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваров.ПФ_MXL_ТОРГ2");
	
	ПакетРезультатов 			= Запрос.ВыполнитьПакет();
	ДанныеПечати 				= ПакетРезультатов[0].Выбрать();
	ТаблицаТоваров 				= ПакетРезультатов[3].Выгрузить();
	ТаблицаТоваровПоДокументам 	= ПакетРезультатов[4].Выгрузить();
	
	ПервыйДокумент = Истина;
	КолонкаКодов = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	
	Если НЕ ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Артикул";
	КонецЕсли;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если ДанныеПечати.ОшибкаНетРасхождений Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В документе %1 нет расхождений. Печать акта об установленном расхождении по количеству и качеству при приемке %2 не требуется.'"),
			ДанныеПечати.Ссылка,
			НСтр("ru='товарно-материальных ценностей'"));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ДанныеПечати.Ссылка);
			Продолжить;
		КонецЕсли;
		
		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ТекстНДСВШапкеТаблицы = ?(ДанныеПечати.ЦенаВключаетНДС, "", " " + НСтр("ru='(Без НДС)'"));
		
		// Страница №1 ////////////////////////////////////////////////////////////////////////////////////////////////////
		// Вывод шапки
		Область = Макет.ПолучитьОбласть("ШапкаПервойСтраницы");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПечати.НомерДокумента, Ложь, Истина);
		Область.Параметры.ПредставлениеОрганизации = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.ДатаДокумента), "ПолноеНаименование,ЮридическийАдрес,Телефоны,");
		Область.Параметры.Поставщик = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Поставщик, ДанныеПечати.ДатаДокумента), "ПолноеНаименование,ЮридическийАдрес,Телефоны,");
		Область.Параметры.ОснованиеДляСоставленияАкта = НСтр("ru='приказ, распоряжение'");
		
		Руководители = ФормированиеПечатныхФормСервер.ОтветственныеЛицаОрганизаций(ДанныеПечати.Организация, ДанныеПечати.ДатаДокумента);
		Область.Параметры.РуководительФИО = Руководители.Руководитель;
		Область.Параметры.ДолжностьРуководителя = Руководители.РуководительДолжность;
		
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод таблицы приемки товаров.
		
		Область = Макет.ПолучитьОбласть("ТаблицаПриемкиТоваровШапка");
		ТабличныйДокумент.Вывести(Область);
		КоличествоСтрокРучногоВвода = 4;
		Для Счетчик =1 По КоличествоСтрокРучногоВвода Цикл
			Область = Макет.ПолучитьОбласть("ТаблицаПриемкиТоваровСтрока");
			ТабличныйДокумент.Вывести(Область);
		КонецЦикла;
		Область = Макет.ПолучитьОбласть("ТаблицаПриемкиТоваровПодвал");
		ТабличныйДокумент.Вывести(Область);
		
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Страница №2 ////////////////////////////////////////////////////////////////////////////////////////////////////
		
		// Вывод шапки
		Область = Макет.ПолучитьОбласть("ШапкаВторойСтраницы");
		ТабличныйДокумент.Вывести(Область);
		
		
		// Вывод состояния транспорта по документам.
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "СостояниеТранспортаПоДокументам", 4);
		
		// Вывод таблицы товаров по документам.
		Область = Макет.ПолучитьОбласть("ТаблицаТоваровПоДокументамШапка");
		ТабличныйДокумент.Вывести(Область);
		Строки = ТаблицаТоваровПоДокументам.НайтиСтроки(Новый Структура("Ссылка",ДанныеПечати.Ссылка));
		Для Каждого Строка Из Строки Цикл
			Область = Макет.ПолучитьОбласть("ТаблицаТоваровПоДокументамСтрока");
			Область.Параметры.Заполнить(Строка);
			Область.Параметры.Товар = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
			Строка.НоменклатураНаименование,
			Строка.ХарактеристикаНаименование);
			ТабличныйДокумент.Вывести(Область);
		КонецЦикла;
		Область = Макет.ПолучитьОбласть("ТаблицаТоваровПоДокументамПодвал");
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод состояния транспорта по факту.
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "СостояниеТранспортаПоФакту", 4);
		
		// Вывод таблицы итогов
		Область = Макет.ПолучитьОбласть("ТаблицаИтогов");
		Область.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(Область);
		
		
		// Вывод первой части таблицы товаров.
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров1Шапка");
		Область.Параметры.НДС = ТекстНДСВШапкеТаблицы;
		ТабличныйДокумент.Вывести(Область);
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров1Строка");
		Товары = ТаблицаТоваров.НайтиСтроки(Новый Структура("Ссылка", ДанныеПечати.Ссылка));
		Для Каждого Товар Из Товары Цикл
			Область.Параметры.Заполнить(Товар);
			Область.Параметры.Товар = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
			Товар.НоменклатураНаименование,
			Товар.ХарактеристикаНаименование);
			Область.Параметры.ЗначениеКода = Товар[КолонкаКодов];
			ТабличныйДокумент.Вывести(Область);
		КонецЦикла;
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров1Подвал");
		ТабличныйДокумент.Вывести(Область);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Страница №3 ////////////////////////////////////////////////////////////////////////////////////////////////////
		
		// Вывод шапки
		Область = Макет.ПолучитьОбласть("ШапкаТретьейСтраницы");
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод Условий Хранения Товара До Вскрытия
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "УсловияХраненияТовараДоВскрытия", 4);
		
		// Вывод Температуры При Разгрузке
		Область = Макет.ПолучитьОбласть("ТемператураПриРазгрузке");
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод Состояния Тары И Упаковки
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "СостояниеТарыИУпаковки", 4);
		
		// Вывод содержания наружной маркировки.
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "СодержаниеНаружнойМаркировки", 4);
		
		// Вывод даты вскрытия тары
		Область = Макет.ПолучитьОбласть("ДатаВскрытияТары");
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод Организации Взвесившей И Опломбировавшей Товар
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "ОрганизацияВзвесившаяИОпломбировавшаяТовар", 4);
		
		// Вывод порядка Отбора Товара Для Выборочной Проверки
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "ПорядокОтбораТовараДляВыборочнойПроверки", 4);
		
		// Вывод второй части таблицы товаров.
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров2Шапка");
		Область.Параметры.НДС = ТекстНДСВШапкеТаблицы;
		ТабличныйДокумент.Вывести(Область);
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров2Строка");
		Для Каждого Товар Из Товары Цикл
			Область.Параметры.Заполнить(Товар);
			Если ЗначениеЗаполнено(КолонкаКодов) Тогда
				Область.Параметры.ЗначениеКода = Товар[КолонкаКодов];
			КонецЕсли;
			ТабличныйДокумент.Вывести(Область);
		КонецЦикла;
		Область = Макет.ПолучитьОбласть("ТаблицаТоваров2Подвал");
		ТабличныйДокумент.Вывести(Область);
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Страница №4 ////////////////////////////////////////////////////////////////////////////////////////////////////
		
		// Вывод шапки
		Область = Макет.ПолучитьОбласть("ШапкаЧетвертойСтраницы");
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод способа определения количества.
		Область = Макет.ПолучитьОбласть("СпособОпределенияКоличества");
		Область.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(Область);
		// Вывод места определения количества.
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "МестоОпределенияКоличества", 4);
		// Вывод сведений об исправности весоизмерительных приборов.
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "СведенияОбИсправностиВесоизмерительныхПриборов", 4);
		// Вывод прочих данных
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "ПрочиеДанные", 4);
		// Вывод подробного описания дефектов.
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "ПодробноеОписаниеДефектов", 4);
		// Вывод заключения комиссии
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "ЗаключениеКомиссии", 4);
		// Вывод приложения
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "Приложение", 4);
		
		// Вывод членов комиссии
		Область = Макет.ПолучитьОбласть("ЧленыКомиссии");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.ГлавныйБухгалтерФИО = Руководители.ГлавныйБухгалтер;
		ТабличныйДокумент.Вывести(Область);
		
		// Вывод решения руководителя
		ВывестиМногострочноеПоле(ТабличныйДокумент, Макет, ДанныеПечати, "РешениеРуководителя", 4);
		
		// Вывод кладовщика
		Область = Макет.ПолучитьОбласть("Кладовщик");
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;

	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция ПечатьАктРасхождений(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	УстановитьПривилегированныйРежим(Истина);
	
	КолонкаКодов 		= ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	ВыводитьКоды		= ЗначениеЗаполнено(КолонкаКодов);
	СинонимДокумента 	= НСтр("ru='Акт расхождений по поступлению'");
	РеквизитыДокумента 	= Новый Структура("Номер, Дата, Префикс");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	СформироватьТекстЗапросаАктРасхождений(Запрос.Текст);
	
	ПакетРезультатов 			= Запрос.ВыполнитьПакет();
	ДанныеПечати 				= ПакетРезультатов[0].Выбрать();
	ТаблицаТоваровПоДокументам	= ПакетРезультатов[3].Выгрузить();
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеТоваров_АктРасхождений";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеТоваров.ПФ_MXL_АктРасхождений");
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если ДанныеПечати.ОшибкаНетРасхождений Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'В документе %1 нет расхождений. Печать акта расхождении не требуется.'"),
			ДанныеПечати.Ссылка);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ДанныеПечати.Ссылка);
			Продолжить;
		КонецЕсли;
		
		
		УчитыватьНДС = ?(ДанныеПечати.УчитыватьНДС, "", "БезНДС");
		
		ОбластьШапкаТаблицыНачало 	= Макет.ПолучитьОбласть("ШапкаТаблицы"+УчитыватьНДС+"|НачалоСтроки"+УчитыватьНДС);
		ОбластьСтрокаТаблицыНачало 	= Макет.ПолучитьОбласть("СтрокаТаблицы"+УчитыватьНДС+"|НачалоСтроки"+УчитыватьНДС);
		ОбластьСтрокаИтогоНачало 	= Макет.ПолучитьОбласть("СтрокаИтого"+УчитыватьНДС+"|НачалоСтроки"+УчитыватьНДС);
		
		ОбластьШапкаТаблицыКолонкаКодов 	= Макет.ПолучитьОбласть("ШапкаТаблицы"+УчитыватьНДС+"|КолонкаКодов"+УчитыватьНДС);
		ОбластьСтрокаТаблицыКолонкаКодов 	= Макет.ПолучитьОбласть("СтрокаТаблицы"+УчитыватьНДС+"|КолонкаКодов"+УчитыватьНДС);
		ОбластьСтрокаИтогоКолонкаКодов 		= Макет.ПолучитьОбласть("СтрокаИтого"+УчитыватьНДС+"|КолонкаКодов"+УчитыватьНДС);
		
		ОбластьШапкаТаблицыКолонкаКодов.Параметры.ИмяКолонкиКодов = КолонкаКодов;
		
		ОбластьКолонкаТовары = Макет.Область("КолонкаТовары"+УчитыватьНДС);
		
		Если НЕ ВыводитьКоды Тогда
			ОбластьКолонкаТовары.ШиринаКолонки = ОбластьКолонкаТовары.ШиринаКолонки + Макет.Область("КолонкаКодов"+УчитыватьНДС).ШиринаКолонки;
		КонецЕсли;
		
		ОбластьШапкаТаблицыКонец 	= Макет.ПолучитьОбласть("ШапкаТаблицы"+УчитыватьНДС+"|КонецСтроки"+УчитыватьНДС);
		ОбластьСтрокаТаблицыКонец 	= Макет.ПолучитьОбласть("СтрокаТаблицы"+УчитыватьНДС+"|КонецСтроки"+УчитыватьНДС);
		ОбластьСтрокаИтогоКонец 	= Макет.ПолучитьОбласть("СтрокаИтого"+УчитыватьНДС+"|КонецСтроки"+УчитыватьНДС);
		
		
		СтрокиТЧ = ТаблицаТоваровПоДокументам.НайтиСтроки(Новый Структура("Ссылка",ДанныеПечати.Ссылка));
		
		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Если ДанныеПечати.УчитыватьНДС Тогда
			ТекстНДСВШапкеТаблицы = ?(ДанныеПечати.ЦенаВключаетНДС, "", " " + НСтр("ru='(Без НДС)'"));
			ОбластьШапкаТаблицыКонец.Параметры.НДС = ТекстНДСВШапкеТаблицы;
		КонецЕсли;
		
		// Заголовок
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ДанныеПечати);
		Область = Макет.ПолучитьОбласть("Заголовок");
		Область.Параметры.ТекстЗаголовка = ФормированиеПечатныхФормСервер.СформироватьЗаголовокДокумента(ДанныеПечати, СинонимДокумента);
		ТабличныйДокумент.Вывести(Область);
		
		// Шапка
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Заполнить(ДанныеПечати);
		Область.Параметры.ПоставщикПредставление = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Поставщик, ДанныеПечати.ДатаДокумента), "ПолноеНаименование,ЮридическийАдрес,Телефоны,");
		Область.Параметры.ПокупательПредставление = ФормированиеПечатныхФормСервер.ОписаниеОрганизации(ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.ДатаДокумента), "ПолноеНаименование,ЮридическийАдрес,Телефоны,");
		ТабличныйДокумент.Вывести(Область);
		
		// Таблица
		ТабличныйДокумент.Вывести(ОбластьШапкаТаблицыНачало);
		
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКолонкаКодов);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьШапкаТаблицыКонец);
		
		КоличествоИзлишекИтого		= 0;
		СуммаИзлишекИтого			= 0;
		КоличествоНедостачаИтого	= 0;
		СуммаНедостачаИтого			= 0;
		СуммаНДСИзлишекИтого		= 0;
		СуммаНДСНедостачаИтого		= 0;
		НомерСтроки = 1;
		
		Для Каждого СтрокаТЧ Из СтрокиТЧ Цикл
			
			ОбластьСтрокаТаблицыНачало.Параметры.НомерСтроки = НомерСтроки;
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицыНачало);
			
			Если ВыводитьКоды Тогда
				
				ОбластьСтрокаТаблицыКолонкаКодов.Параметры.Артикул = СтрокаТЧ[КолонкаКодов];
				ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКолонкаКодов);
				
			КонецЕсли;
			
			ОбластьСтрокаТаблицыКонец.Параметры.Заполнить(СтрокаТЧ);
			ОбластьСтрокаТаблицыКонец.Параметры.Товар = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика);
			ТабличныйДокумент.Присоединить(ОбластьСтрокаТаблицыКонец);
			
			КоличествоИзлишекИтого		= КоличествоИзлишекИтого + СтрокаТЧ.КоличествоИзлишек;
			СуммаИзлишекИтого			= СуммаИзлишекИтого + СтрокаТЧ.СуммаИзлишек;
			КоличествоНедостачаИтого	= КоличествоНедостачаИтого + СтрокаТЧ.КоличествоНедостача;
			СуммаНедостачаИтого			= СуммаНедостачаИтого + СтрокаТЧ.СуммаНедостача;
			СуммаНДСИзлишекИтого		= СуммаНДСИзлишекИтого + СтрокаТЧ.СуммаНДСИзлишек;
			СуммаНДСНедостачаИтого		= СуммаНДСНедостачаИтого + СтрокаТЧ.СуммаНДСНедостача;
			НомерСтроки 				= НомерСтроки + 1;
			
		КонецЦикла;
		
		// Итого
		ОбластьСтрокаИтогоКонец.Параметры.КоличествоИзлишекИтого	= КоличествоИзлишекИтого;
		ОбластьСтрокаИтогоКонец.Параметры.СуммаИзлишекИтого			= СуммаИзлишекИтого;
		ОбластьСтрокаИтогоКонец.Параметры.КоличествоНедостачаИтого	= КоличествоНедостачаИтого;
		ОбластьСтрокаИтогоКонец.Параметры.СуммаНедостачаИтого		= СуммаНедостачаИтого;
		Если ДанныеПечати.УчитыватьНДС Тогда
			ОбластьСтрокаИтогоКонец.Параметры.СуммаНДСИзлишекИтого		= СуммаНДСИзлишекИтого;
			ОбластьСтрокаИтогоКонец.Параметры.СуммаНДСНедостачаИтого	= СуммаНДСНедостачаИтого;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьСтрокаИтогоНачало);
		
		Если ВыводитьКоды Тогда
			ТабличныйДокумент.Присоединить(ОбластьСтрокаИтогоКолонкаКодов);
		КонецЕсли;
		
		ТабличныйДокумент.Присоединить(ОбластьСтрокаИтогоКонец);
		
		// Подписи
		Область = Макет.ПолучитьОбласть("Подписи");
		ТабличныйДокумент.Вывести(Область);
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
	
КонецФункции

Функция ПолучитьПоступлениеТоваровПоЗаказуПоставщику(ЗаказПоставщику)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступлениеТоваров.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ГДЕ
	|	ПоступлениеТоваров.ЗаказПоставщику = &ЗаказПоставщику
	|	И НЕ ПоступлениеТоваров.ЗаказПоставщику = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	И ПоступлениеТоваров.Проведен";
	
	Запрос.УстановитьПараметр("ЗаказПоставщику", ЗаказПоставщику);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПечатьОписиНоменклатуры(МассивОбъектов, ОбъектыПечати)
	
	КолонкаКодов       = ФормированиеПечатныхФормСервер.ИмяДополнительнойКолонки();
	ВыводитьКоды       = ЗначениеЗаполнено(КолонкаКодов);
	ТабличныйДокумент  = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДокументов.Организация КАК Покупатель,
	|	ТаблицаДокументов.Контрагент КАК Поставщик,
	|	ТаблицаДокументов.Магазин,
	|	ТаблицаДокументов.Склад,
	|	ТаблицаДокументов.Ссылка
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СтруктураПечати = Новый Структура;
	СтруктураПечати.Вставить("ИмяРеквизита1", "Поставщик");
	СтруктураПечати.Вставить("ИмяРеквизита2", "Покупатель");
	СтруктураПечати.Вставить("ИмяРеквизита3", "Магазин");
	СтруктураПечати.Вставить("ИмяРеквизита4", "Склад");
	СтруктураПечати.Вставить("КоличествоРеквизитов", 4);
	СтруктураПечати.Вставить("ВыводитьКоды", ВыводитьКоды);
	СтруктураПечати.Вставить("КолонкаКодов", КолонкаКодов);
	
	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураПечати.Вставить("Документ", Выборка.Ссылка);
		СтруктураПечати.Вставить("Реквизит1", Выборка.Поставщик);
		СтруктураПечати.Вставить("Реквизит2", Выборка.Покупатель);
		СтруктураПечати.Вставить("Реквизит3", Выборка.Магазин);
		СтруктураПечати.Вставить("Реквизит4", Выборка.Склад);
		
		УправлениеПечатьюРТ.ПечатьОписиНоменклатуры(ТабличныйДокумент, ОбъектыПечати, СтруктураПечати, ПервыйДокумент);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#КонецЕсли
