
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(ПараметрКоманды);
	
	МассивНепроведенныхДокументов = ФормированиеПечатныхФормВызовСервера.ПолучитьМассивНепроведенныхДокументов(МассивДокументов);
	
	Если МассивНепроведенныхДокументов.Количество() > 0 Тогда
		
		Если МассивНепроведенныхДокументов.Количество() = 1 Тогда
			ТекстВопроса = НСтр("ru = 'Дальнейшая работа возможна только после проведения документа, провести документ?'")
		Иначе
			ТекстВопроса = НСтр("ru = 'Дальнейшая работа возможна только после проведения документов, провести документы?'")
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("МассивНепроведенныхДокументов", МассивНепроведенныхДокументов);
		ДополнительныеПараметры.Вставить("ПараметрКоманды", ПараметрКоманды);
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросОПроведенииДокументов", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ОткрытьФормуПричинСписания(ПараметрКоманды);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПричинСписания(ПараметрКоманды)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "Обработка.ПомощникЗаполненияПричинСписания.Форма.Форма.Открытие");

	ПараметрыФормы = Новый Структура("ДокументОснование", ПараметрКоманды);
	ОткрытьФорму("Обработка.ПомощникЗаполненияПричинСписания.Форма.Форма", ПараметрыФормы)
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВопросОПроведенииДокументов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		МассивНепроведенныхДокументов = ФормированиеПечатныхФормВызовСервера.ПровестиДокументы(ДополнительныеПараметры.МассивНепроведенныхДокументов);
		ПараметрКоманды = ДополнительныеПараметры.ПараметрКоманды;
		Если МассивНепроведенныхДокументов.Количество() = 0 Тогда
			
			ОткрытьФормуПричинСписания(ПараметрКоманды);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

