
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ПараметрыОтбора;
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Параметры.Свойство("СтруктураПараметрыОтбора", ПараметрыОтбора)  Тогда
		
		ДанныеВыбора = СписокЧековДляВыбора(ПараметрыОтбора);
		
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Ссылка", ДанныеВыбора , Истина, ВидСравненияКомпоновкиДанных.ВСписке);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получаем список чеков для выбора по параметрам.
//
// Параметры:
//  СтруктураПараметров - Структура
//
// Возвращаемое значение:
// Список значений
//
&НаСервере
Функция СписокЧековДляВыбора(СтруктураПараметров)

	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЧекККМТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЧекККМ.Товары КАК ЧекККМТовары
	|ГДЕ
	|	ЧекККМТовары.Ссылка.СтатусЧекаККМ = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Архивный)
	|	И ЧекККМТовары.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|	И ЧекККМТовары.Ссылка.Магазин = &Магазин
	|	%ОтборПоДисконтнойКарте%
	|	%ОтборПоНоменклатуре%
	|	%ОтборПоХарактеристике%
	|	%ОтборПоНомеру%
	|	%ОтборПоРеквизитуЦенаВключаетНДС%
	|	%ОтборПоРеквизитуОперацияСДенежнымиСредствами%
	|	%ОтборПоДате%";
	
	Запрос.УстановитьПараметр("Магазин", СтруктураПараметров.Магазин);
	
	Если СтруктураПараметров.Свойство("ДисконтнаяКарта") Тогда
		Запрос.УстановитьПараметр("ДисконтнаяКарта", СтруктураПараметров.ДисконтнаяКарта);
		ОтборПоДисконтнойКарте = " И ЧекККМТовары.Ссылка.ДисконтнаяКарта = &ДисконтнаяКарта";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоДисконтнойКарте%", ОтборПоДисконтнойКарте);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоДисконтнойКарте%", "");
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Номенклатура") Тогда
		Запрос.УстановитьПараметр("Номенклатура", СтруктураПараметров.Номенклатура);
		ОтборПоНоменклатуре = " И ЧекККМТовары.Номенклатура = &Номенклатура";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоНоменклатуре%", ОтборПоНоменклатуре);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоНоменклатуре%", "");
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Характеристика") Тогда
		Запрос.УстановитьПараметр("Характеристика", СтруктураПараметров.Характеристика);
		ОтборПоХарактеристике = " И ЧекККМТовары.Характеристика = &Характеристика";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоХарактеристике%", ОтборПоХарактеристике);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоХарактеристике%", "");
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Номер") Тогда
		Запрос.УстановитьПараметр("Номер", СтруктураПараметров.Номер);
		ОтборПоНомеру = " И ЧекККМТовары.Ссылка.НомерЧекаККМ = &Номер";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоНомеру%", ОтборПоНомеру);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоНомеру%", "");
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Дата") Тогда
		Запрос.УстановитьПараметр("Дата", СтруктураПараметров.Дата);
		ОтборПоДате = " И ЧекККМТовары.Ссылка.Дата >= НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
					   |И ЧекККМТовары.Ссылка.Дата <= КОНЕЦПЕРИОДА(&Дата, ДЕНЬ)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоДате%", ОтборПоДате);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоДате%", "");
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("ЦенаВключаетНДС") Тогда
		Запрос.УстановитьПараметр("ЦенаВключаетНДС", СтруктураПараметров.ЦенаВключаетНДС);
		ОтборПоЦене = " И ЧекККМТовары.Ссылка.ЦенаВключаетНДС = &ЦенаВключаетНДС";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоРеквизитуЦенаВключаетНДС%", ОтборПоЦене);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоРеквизитуЦенаВключаетНДС%", "");
	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("ОперацияСДенежнымиСредствами") Тогда
		Запрос.УстановитьПараметр("ОперацияСДенежнымиСредствами", СтруктураПараметров.ОперацияСДенежнымиСредствами);
		ОтборПоОтгрузке = " И ЧекККМТовары.Ссылка.ОперацияСДенежнымиСредствами = &ОперацияСДенежнымиСредствами";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоРеквизитуОперацияСДенежнымиСредствами%", ОтборПоОтгрузке);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ОтборПоРеквизитуОперацияСДенежнымиСредствами%", "");
	КонецЕсли;
	
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти