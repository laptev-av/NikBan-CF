
#Область СлужебныйПрограммныйИнтерфейс

#Область ПодпискиНаСобытия

// См. процедуру ИнтеграцияГИСМПереопределяемый.ВлияющийНаСтатусПоступленияКиЗДокументПередЗаписью
//
Процедура ВлияющийНаСтатусПоступленияКиЗДокументПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Запрос = Новый Запрос(ТекстЗапросаПоступившиеКиЗ());
	Запрос.УстановитьПараметр("ДокументПоступления", Источник.Ссылка);
	Результат = Запрос.ВыполнитьПакет();
	
	Если Не Результат[0].Пустой() Тогда
		Источник.ДополнительныеСвойства.Вставить("МассивПоступившихКиЗ",          Результат[0].Выгрузить().ВыгрузитьКолонку("НомерКиЗ"));
	КонецЕсли;
	Если Не Результат[1].Пустой() Тогда
		Источник.ДополнительныеСвойства.Вставить("МассивПоступившихКиЗПродукции", Результат[1].Выгрузить().ВыгрузитьКолонку("НомерКиЗ"));
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("Организация", Источник.Организация);
	Источник.ДополнительныеСвойства.Вставить("Контрагент",  Источник.Контрагент);
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ВлияющийНаСтатусПоступленияКиЗДокументПриПроведении
//
Процедура ВлияющийНаСтатусПоступленияКиЗДокументПриПроведении(Источник, Отказ, РежимПроведения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивПоступившихКиЗПредыдущееСостояние = Новый Массив;
	МассивПоступившихКиЗТекущееСостояние    = Новый Массив;
	
	Если Источник.ДополнительныеСвойства.Свойство("МассивПоступившихКиЗ") Тогда
		МассивПоступившихКиЗПредыдущееСостояние = Источник.ДополнительныеСвойства.МассивПоступившихКиЗ;
	КонецЕсли;
	
	МассивПоступившихКиЗПродукцииТекущееСостояние    = Новый Массив;
	МассивПоступившихКиЗПродукцииПредыдущееСостояние = Новый Массив;
	
	Если Источник.ДополнительныеСвойства.Свойство("МассивПоступившихКиЗПродукции") Тогда
		МассивПоступившихКиЗПродукцииПредыдущееСостояние = Источник.ДополнительныеСвойства.МассивПоступившихКиЗПродукции;
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапросаПоступившиеКиЗ());
	Запрос.УстановитьПараметр("ДокументПоступления", Источник.Ссылка);
	Результат = Запрос.ВыполнитьПакет();
	
	Если Не Результат[0].Пустой() Тогда
		МассивПоступившихКиЗТекущееСостояние = Результат[0].Выгрузить().ВыгрузитьКолонку("НомерКиЗ");
	КонецЕсли;
	
	Если Не Результат[1].Пустой() Тогда
		МассивПоступившихКиЗПродукцииТекущееСостояние = Результат[1].Выгрузить().ВыгрузитьКолонку("НомерКиЗ");
	КонецЕсли;
	
	Если МассивПоступившихКиЗТекущееСостояние.Количество() = 0
		И МассивПоступившихКиЗПредыдущееСостояние.Количество() = 0
		И МассивПоступившихКиЗПродукцииТекущееСостояние.Количество() = 0
		И МассивПоступившихКиЗПродукцииПредыдущееСостояние.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ПоступлениеТоваров")
		И Источник.ЕстьКиЗГИСМ Тогда
			МассивАнализируемыхНомеровКиЗ = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивПоступившихКиЗПредыдущееСостояние);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивАнализируемыхНомеровКиЗ, МассивПоступившихКиЗТекущееСостояние, Истина);
			
			АнализЗаявокНаВыпускКиЗ(МассивАнализируемыхНомеровКиЗ, Источник);
	КонецЕсли;
	
	Если Источник.ЕстьМаркируемаяПродукцияГИСМ Тогда
		МассивАнализируемыхНомеровКиЗ = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивПоступившихКиЗПродукцииПредыдущееСостояние);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивАнализируемыхНомеровКиЗ, МассивПоступившихКиЗПродукцииТекущееСостояние, Истина);
		
		АнализУведомленийОПоступлении(МассивАнализируемыхНомеровКиЗ, Источник)
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусЗаявкиНаВыпускКиЗ
//
Процедура РассчитатьСтатусЗаявкиНаВыпускКиЗ(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЗаказПоставщику") Тогда
		
		РассчитатьСтатусЗаявкиНаВыпускКиЗПоЗаказуПоставщику(Источник);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ЗаявкаНаВыпускКиЗГИСМ") Тогда
		
		РассчитатьСтатусЗаявкиНаВыпускКиЗПоЗаявкеНаВыпускКиЗ(Источник);
		
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусУведомленияОбОтгрузке
//
Процедура РассчитатьСтатусУведомленияОбОтгрузке(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровПоставщику") Тогда
		
		РассчитатьСтатусУведомленияОбОтгрузкеПоВозвратуТоваровПоставщику(Источник);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.РеализацияТоваров") Тогда
		
		РассчитатьСтатусУведомленияОбОтгрузкеПоРеализацииТоваровУслуг(Источник);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ") Тогда
		
		РассчитатьСтатусУведомленияОбОтгрузкеПоУведомлениюОбОтгрузке(Источник);
		
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусИнформированияГИСМ
//
Процедура РассчитатьСтатусИнформированияГИСМ(Источник, Отказ) Экспорт
	
	// Переделать на списание
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.СписаниеТоваров") Тогда
		
		ЗаписьТребуется = Источник.ЕстьКиЗГИСМ Или Источник.ЕстьМаркируемаяПродукцияГИСМ;
		РассчитатьСтатусИнформированияГИСМДляДокументаРаспоряжения(Источник, ЗаписьТребуется);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПоступлениеТоваров") Тогда
		
		ЗаписьТребуется = (Источник.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту
		                   Или Источник.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
		                   И Источник.ЕстьМаркируемаяПродукцияГИСМ;
		РассчитатьСтатусИнформированияГИСМДляДокументаРаспоряжения(Источник, ЗаписьТребуется);

		РассчитатьСтатусИнформированияГИСМДляДокументаРаспоряжения(Источник, Истина);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.УведомлениеОСписанииКиЗГИСМ")
		  Или ТипЗнч(Источник) = Тип("ДокументОбъект.УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ")
		  Или ТипЗнч(Источник) = Тип("ДокументОбъект.УведомлениеОбИмпортеМаркированныхТоваровГИСМ") Тогда
		
		РассчитатьСтатусИнформированияГИСМДляДокументаСРаспоряжением(Источник);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ОтчетОРозничныхПродажах")
		  Или ТипЗнч(Источник) = Тип("ДокументОбъект.ВозвратТоваровОтПокупателя") Тогда
		  
		ЗаписьТребуется = Источник.ЕстьМаркируемаяПродукцияГИСМ;
		РассчитатьСтатусИнформированияГИСМДляДокумента(Источник, ЗаписьТребуется);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.МаркировкаТоваровГИСМ") Тогда
		
		РассчитатьСтатусИнформированияГИСМДляДокумента(Источник, Истина);
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ПеремаркировкаТоваровГИСМ") Тогда
		
		РассчитатьСтатусИнформированияГИСМДляДокумента(Источник, Истина, Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанныеСписаниеКиЗ);
		
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусИнформированияГИСМДляДокумента
//
Процедура РассчитатьСтатусИнформированияГИСМДляДокумента(Источник, ЗаписьТребуется, ДальнейшееДействие = Неопределено)

	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыИнформированияГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусыИнформированияГИСМ.Документ,
	|	СтатусыИнформированияГИСМ.ТекущееУведомление,
	|	СтатусыИнформированияГИСМ.Статус,
	|	СтатусыИнформированияГИСМ.ДальнейшееДействие
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ = &Основание";
	
	Запрос.УстановитьПараметр("Основание", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если (Не Источник.Проведен Или Не ЗаписьТребуется) Тогда
			РегистрыСведений.СтатусыИнформированияГИСМ.УдалитьЗаписьИзРегистра(Источник.Ссылка);
			Возврат;
		КонецЕсли;
		
	Иначе
		
		Если Источник.Проведен И ЗаписьТребуется Тогда
			
			ВыполнятьЗаписьВРегистр = Истина;
			
			ДанныеЗаписи.Документ = Источник.Ссылка;
			Если ДальнейшееДействие = Неопределено Тогда
				ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			Иначе
				ДанныеЗаписи.ДальнейшееДействие = ДальнейшееДействие;
			КонецЕсли;
			ДанныеЗаписи.Статус = Перечисления.СтатусыИнформированияГИСМ.Черновик;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыИнформированияГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;

КонецПроцедуры

Процедура РассчитатьСтатусУведомленияОПоступлении(Источник, Отказ) Экспорт
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.УведомлениеОПоступленииМаркированныхТоваровГИСМ") Тогда
		
		РассчитатьСтатусУведомленияОПоступленииПоУведомлениюОПоступлении(Источник);
		
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусИнформированияГИСМДляДокументаРаспоряжения
//
Процедура РассчитатьСтатусИнформированияГИСМДляДокументаРаспоряжения(Источник, ЗаписьТребуется)

	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыИнформированияГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусыИнформированияГИСМ.Документ,
	|	СтатусыИнформированияГИСМ.ТекущееУведомление,
	|	СтатусыИнформированияГИСМ.Статус,
	|	СтатусыИнформированияГИСМ.ДальнейшееДействие
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ = &Основание";
	
	Запрос.УстановитьПараметр("Основание", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если (Не Источник.Проведен Или Не ЗаписьТребуется)
			И Не ЗначениеЗаполнено(Выборка.ТекущееУведомление) Тогда
			
			РегистрыСведений.СтатусыИнформированияГИСМ.УдалитьЗаписьИзРегистра(Источник.Ссылка);
			Возврат;
			
		КонецЕсли;
		
	ИначеЕсли Источник.Проведен И ЗаписьТребуется Тогда
		
		ВыполнятьЗаписьВРегистр = Истина;
		
		ДанныеЗаписи.Документ            = Источник.Ссылка;
		ДанныеЗаписи.ДальнейшееДействие  = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется;
		ДанныеЗаписи.Статус              = Перечисления.СтатусыИнформированияГИСМ.Отсутствует;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыИнформированияГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусИнформированияГИСМДляДокументаСРаспоряжением
//
Процедура РассчитатьСтатусИнформированияГИСМДляДокументаСРаспоряжением(Источник)

	ЗаписьНового = Источник.ДополнительныеСвойства.Свойство("ЗаписьНового")
	             И Источник.ДополнительныеСвойства.ЗаписьНового;
	
	Если Не ЗаписьНового Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыИнформированияГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусыИнформированияГИСМ.Документ,
	|	СтатусыИнформированияГИСМ.ТекущееУведомление,
	|	СтатусыИнформированияГИСМ.Статус,
	|	СтатусыИнформированияГИСМ.ДальнейшееДействие
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Источник.Основание);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ВыполнятьЗаписьВРегистр         = Истина;
		ДанныеЗаписи.Документ           = Выборка.Документ;
		ДанныеЗаписи.ТекущееУведомление = Источник.Ссылка;
		ДанныеЗаписи.Статус             = Перечисления.СтатусыИнформированияГИСМ.Черновик;
		ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыИнформированияГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаявкаНаВыпускКиЗГИСМ

#Область ЗаполнениеНаОсновании

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусУведомленияОПоступлении
//
Процедура ЗаполнитьЗаявкуНаВыпускКиЗНаОснованииЗаказаПоставщику(ЗаявкаОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт

	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументЗаказПоставщику.Контрагент,
	|	ДокументЗаказПоставщику.Организация,
	|	ДокументЗаказПоставщику.Ссылка КАК Основание,
	|	НЕ ДокументЗаказПоставщику.Проведен КАК ЕстьОшибкиПроведен,
	|	ИСТИНА КАК ЕстьОшибкиСтатус,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ, ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.Отсутствует)) = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОбрабатываетсяЭмитентом)
	|			ТОГДА ВЫБОР
	|					КОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусОбработкиЭмитентом = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиЭмитентомКиЗГИСМ.ПустаяСсылка)
	|						ТОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ
	|					ИНАЧЕ СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусОбработкиЭмитентом
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ, ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.Отсутствует))
	|	КОНЕЦ КАК СтатусЗаявкиНаВыпускКиЗ,
	|	ЕСТЬNULL(СтатусыЗаявокНаВыпускКиЗГИСМ.ТекущаяЗаявкаНаВыпускКиЗ, ЗНАЧЕНИЕ(Документ.ЗаявкаНаВыпускКиЗГИСМ.ПустаяСсылка)) КАК ТекущаяЗаявкаНаВыпускКиЗ,
	|	ДокументЗаказПоставщику.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ДокументЗаказПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК СтатусыЗаявокНаВыпускКиЗГИСМ
	|		ПО (СтатусыЗаявокНаВыпускКиЗГИСМ.Документ = ДокументЗаказПоставщику.Ссылка)
	|ГДЕ
	|	ДокументЗаказПоставщику.Ссылка = &ЗаказПоставщику
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПоставщикуТовары.Номенклатура,
	|	ЗаказПоставщикуТовары.Характеристика,
	|	ЗаказПоставщикуТовары.Количество
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ЗаказПоставщикуТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.КиЗГИСМ
	|	И ЗаказПоставщикуТовары.Ссылка = &ЗаказПоставщику";
	
	Запрос.УстановитьПараметр("ЗаказПоставщику", ДанныеЗаполнения);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыЗаказа = РезультатЗапроса[0].Выбрать();
	РеквизитыЗаказа.Следующий();
	
	ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(
		РеквизитыЗаказа.Основание);
		
	Если Не РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ЭтоСтатусНеАктуальнойЗаявки(РеквизитыЗаказа.СтатусЗаявкиНаВыпускКиЗ) Тогда
		
		ТекстОшибки = НСтр("ru='Для %Документ% уже существует актуальная заявка на выпуск КиЗ - %Заявка%.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Заявка%", РеквизитыЗаказа.ТекущаяЗаявкаНаВыпускКиЗ);
	
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если РезультатЗапроса[1].Пустой() Тогда
		
		ТекстОшибки = НСтр("ru='В %Документ% отсутствуют контрольные (идентификационные) знаки.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
	
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
		
	ЗаполнитьЗначенияСвойств(ЗаявкаОбъект, РеквизитыЗаказа);
	ЗаявкаОбъект.ЗаказанныеКиЗ.Загрузить(РезультатЗапроса[1].Выгрузить());
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьУведомлениеОбОтгрузкеГИСМНаОснованииРеализации
//
Процедура ЗаполнитьУведомлениеОбОтгрузкеГИСМНаОснованииРеализации(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Дата");
	Запрос.УстановитьПараметр("ДатаОснования", РеквизитыОснования.Дата);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваров.Организация КАК Организация,
	|	РеализацияТоваров.Контрагент КАК Контрагент,
	|	РеализацияТоваров.Ссылка КАК Основание,
	|	ЗНАЧЕНИЕ(Перечисление.ВидОборотаТовараГИСМ.Продажа) КАК ВидОборотаТовара,
	|	НЕ РеализацияТоваров.Проведен КАК ЕстьОшибкиПроведен,
	|	ЕСТЬNULL(СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ТекущееУведомлениеОбОтгрузке, ЗНАЧЕНИЕ(Документ.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.ПустаяСсылка)) КАК АктуальноеУведомлениеОбОтгрузкеГИСМ,
	|	ЕСТЬNULL(СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ПустаяСсылка)) КАК Статус
	|ИЗ
	|	Документ.РеализацияТоваров КАК РеализацияТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ
	|		ПО (СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ = РеализацияТоваров.Ссылка)
	|ГДЕ
	|	РеализацияТоваров.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровСерии.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровСерии.Характеристика КАК Характеристика,
	|	КОЛИЧЕСТВО(РеализацияТоваровСерии.Серия) КАК КоличествоСерий
	|ПОМЕСТИТЬ втСерии
	|ИЗ
	|	Документ.РеализацияТоваров.Серии КАК РеализацияТоваровСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО РеализацияТоваровСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И РеализацияТоваровСерии.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровСерии.Номенклатура,
	|	РеализацияТоваровСерии.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровТовары.Номенклатура КАК Номенклатура,
	|	РеализацияТоваровТовары.Характеристика КАК Характеристика,
	|	СУММА(РеализацияТоваровТовары.Сумма) КАК СуммаСНДС,
	|	СУММА(РеализацияТоваровТовары.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО РеализацияТоваровТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И РеализацияТоваровТовары.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияТоваровТовары.Номенклатура,
	|	РеализацияТоваровТовары.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втТовары.СуммаНДС, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ втТовары.СуммаНДС / ВЫБОР
	|				КОГДА ЕСТЬNULL(втСерии.КоличествоСерий, 0) = 0
	|					ТОГДА 1
	|				ИНАЧЕ втСерии.КоличествоСерий
	|			КОНЕЦ
	|	КОНЕЦ КАК НДСКиЗ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втТовары.СуммаСНДС, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ втТовары.СуммаСНДС / ВЫБОР
	|				КОГДА ЕСТЬNULL(втСерии.КоличествоСерий, 0) = 0
	|					ТОГДА 1
	|				ИНАЧЕ втСерии.КоличествоСерий
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьКиЗ
	|ПОМЕСТИТЬ втСтоимостьКиЗ
	|ИЗ
	|	втСерии КАК втСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТовары КАК втТовары
	|		ПО втСерии.Номенклатура = втТовары.Номенклатура
	|			И втСерии.Характеристика = втТовары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровСерии.Серия.НомерКиЗГИСМ КАК НомерКиЗ,
	|	РеализацияТоваровСерии.Серия.RFIDTID      КАК RFIDTID,
	|	РеализацияТоваровСерии.Серия.RFIDEPC      КАК RFIDEPC,
	|	втСтоимостьКиЗ.НДСКиЗ КАК СуммаНДС,
	|	втСтоимостьКиЗ.СтоимостьКиЗ КАК Стоимость
	|ИЗ
	|	Документ.РеализацияТоваров.Серии КАК РеализацияТоваровСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО РеализацияТоваровСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСтоимостьКиЗ КАК втСтоимостьКиЗ
	|		ПО (втСтоимостьКиЗ.Номенклатура = РеализацияТоваровСерии.Номенклатура)
	|			И (втСтоимостьКиЗ.Характеристика = РеализацияТоваровСерии.Характеристика)
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И РеализацияТоваровСерии.Ссылка = &ДокументОснование";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыОснования = РезультатЗапроса[0].Выбрать();
	РеквизитыОснования.Следующий();
	
	ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыОснования.Основание);
	
	Если Не РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ЭтоСтатусНеАктуальногоУведомления(РеквизитыОснования.Статус) Тогда
		
		ТекстОшибки = НСтр("ru='Для %Документ% уже существует актуальное уведомление об отгрузке ГИСМ - %Уведомление%.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Уведомление%", РеквизитыОснования.АктуальноеУведомлениеОбОтгрузкеГИСМ);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если РезультатЗапроса[4].Пустой() Тогда
		
		ТекстОшибки = НСтр("ru='В %Документ% отсутствуют товары, маркированные контрольными (идентификационными) знаками.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(УведомлениеОбъект, РеквизитыОснования);
	УведомлениеОбъект.НомераКиЗ.Загрузить(РезультатЗапроса[4].Выгрузить());
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьУведомлениеОбОтгрузкеГИСМНаОснованииВозврата
//
Процедура ЗаполнитьУведомлениеОбОтгрузкеГИСМНаОснованииВозвратаТоваровПоставщику(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Дата");
	Запрос.УстановитьПараметр("ДатаОснования", РеквизитыОснования.Дата);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВозвратТоваровПоставщику.Организация КАК Организация,
	|	ВозвратТоваровПоставщику.Контрагент КАК Контрагент,
	|	ВозвратТоваровПоставщику.Ссылка КАК Основание,
	|	ЗНАЧЕНИЕ(Перечисление.ВидОборотаТовараГИСМ.Продажа) КАК ВидОборотаТовара,
	|	НЕ ВозвратТоваровПоставщику.Проведен КАК ЕстьОшибкиПроведен,
	|	ЕСТЬNULL(СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ТекущееУведомлениеОбОтгрузке, ЗНАЧЕНИЕ(Документ.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.ПустаяСсылка)) КАК АктуальноеУведомлениеОбОтгрузкеГИСМ,
	|	ЕСТЬNULL(СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ПустаяСсылка)) КАК Статус
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ
	|		ПО (СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ = ВозвратТоваровПоставщику.Ссылка)
	|ГДЕ
	|	ВозвратТоваровПоставщику.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровПоставщикуСерии.Номенклатура КАК Номенклатура,
	|	ВозвратТоваровПоставщикуСерии.Характеристика КАК Характеристика,
	|	КОЛИЧЕСТВО(ВозвратТоваровПоставщикуСерии.Серия) КАК КоличествоСерий
	|ПОМЕСТИТЬ втСерии
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Серии КАК ВозвратТоваровПоставщикуСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВозвратТоваровПоставщикуСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И ВозвратТоваровПоставщикуСерии.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровПоставщикуСерии.Номенклатура,
	|	ВозвратТоваровПоставщикуСерии.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровПоставщикуТовары.Номенклатура КАК Номенклатура,
	|	ВозвратТоваровПоставщикуТовары.Характеристика КАК Характеристика,
	|	СУММА(ВозвратТоваровПоставщикуТовары.Сумма) КАК СуммаСНДС,
	|	СУММА(ВозвратТоваровПоставщикуТовары.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК ВозвратТоваровПоставщикуТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВозвратТоваровПоставщикуТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И ВозвратТоваровПоставщикуТовары.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ВозвратТоваровПоставщикуТовары.Номенклатура,
	|	ВозвратТоваровПоставщикуТовары.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втТовары.СуммаНДС, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ втТовары.СуммаНДС / ВЫБОР
	|				КОГДА ЕСТЬNULL(втСерии.КоличествоСерий, 0) = 0
	|					ТОГДА 1
	|				ИНАЧЕ втСерии.КоличествоСерий
	|			КОНЕЦ
	|	КОНЕЦ КАК НДСКиЗ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втТовары.СуммаСНДС, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ втТовары.СуммаСНДС / ВЫБОР
	|				КОГДА ЕСТЬNULL(втСерии.КоличествоСерий, 0) = 0
	|					ТОГДА 1
	|				ИНАЧЕ втСерии.КоличествоСерий
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьКиЗ
	|ПОМЕСТИТЬ втСтоимостьКиЗ
	|ИЗ
	|	втСерии КАК втСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТовары КАК втТовары
	|		ПО втСерии.Номенклатура = втТовары.Номенклатура
	|			И втСерии.Характеристика = втТовары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровПоставщикуСерии.Серия.НомерКиЗГИСМ КАК НомерКиЗ,
	|	ВозвратТоваровПоставщикуСерии.Серия.RFIDTID      КАК RFIDTID,
	|	ВозвратТоваровПоставщикуСерии.Серия.RFIDEPC      КАК RFIDEPC,
	|	втСтоимостьКиЗ.НДСКиЗ КАК СуммаНДС,
	|	втСтоимостьКиЗ.СтоимостьКиЗ КАК Стоимость
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Серии КАК ВозвратТоваровПоставщикуСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВозвратТоваровПоставщикуСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСтоимостьКиЗ КАК втСтоимостьКиЗ
	|		ПО (втСтоимостьКиЗ.Номенклатура = ВозвратТоваровПоставщикуСерии.Номенклатура)
	|			И (втСтоимостьКиЗ.Характеристика = ВозвратТоваровПоставщикуСерии.Характеристика)
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И ВозвратТоваровПоставщикуСерии.Ссылка = &ДокументОснование";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыОснования = РезультатЗапроса[0].Выбрать();
	РеквизитыОснования.Следующий();
	
	ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыОснования.Основание);
	
	Если Не РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ЭтоСтатусНеАктуальногоУведомления(РеквизитыОснования.Статус) Тогда
		
		ТекстОшибки = НСтр("ru='Для %Документ% уже существует актуальное уведомление об отгрузке ГИСМ - %Уведомление%.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Уведомление%", РеквизитыОснования.АктуальноеУведомлениеОбОтгрузкеГИСМ);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если РезультатЗапроса[4].Пустой() Тогда
		
		ТекстОшибки = НСтр("ru='В %Документ% отсутствуют товары, маркированные контрольными (идентификационными) знаками.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(УведомлениеОбъект, РеквизитыОснования);
	УведомлениеОбъект.НомераКиЗ.Загрузить(РезультатЗапроса[4].Выгрузить());
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьУведомлениеОВвозеИзЕАЭСГИСМНаОснованииПоступления
//
Процедура ЗаполнитьУведомлениеОВвозеИзЕАЭСГИСМНаОснованииПоступления(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Дата");
	Запрос.УстановитьПараметр("ДатаОснования", РеквизитыОснования.Дата);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеТоваров.Организация КАК Организация,
	|	ПоступлениеТоваров.Контрагент КАК Контрагент,
	|	ПоступлениеТоваров.Ссылка КАК Основание,
	|	НЕ ПоступлениеТоваров.Проведен КАК ЕстьОшибкиПроведен,
	|	ЕСТЬNULL(СтатусыИнформированияГИСМ.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка)) КАК Статус
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|		ПО (СтатусыИнформированияГИСМ.Документ = ПоступлениеТоваров.Ссылка)
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровСерии.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровСерии.Характеристика КАК Характеристика,
	|	КОЛИЧЕСТВО(ПоступлениеТоваровСерии.Серия) КАК КоличествоСерий
	|ПОМЕСТИТЬ втСерии
	|ИЗ
	|	Документ.ПоступлениеТоваров.Серии КАК ПоступлениеТоваровСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПоступлениеТоваровСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И ПоступлениеТоваровСерии.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровСерии.Номенклатура,
	|	ПоступлениеТоваровСерии.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровТовары.Характеристика КАК Характеристика,
	|	СУММА(ПоступлениеТоваровТовары.Сумма) КАК СуммаСНДС,
	|	СУММА(ПоступлениеТоваровТовары.СуммаНДС) КАК СуммаНДС
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПоступлениеТоваровТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И ПоступлениеТоваровТовары.Ссылка = &ДокументОснование
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступлениеТоваровТовары.Номенклатура,
	|	ПоступлениеТоваровТовары.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втТовары.СуммаНДС, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ втТовары.СуммаНДС / ВЫБОР
	|				КОГДА ЕСТЬNULL(втСерии.КоличествоСерий, 0) = 0
	|					ТОГДА 1
	|				ИНАЧЕ втСерии.КоличествоСерий
	|			КОНЕЦ
	|	КОНЕЦ КАК НДСКиЗ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втТовары.СуммаСНДС, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ втТовары.СуммаСНДС / ВЫБОР
	|				КОГДА ЕСТЬNULL(втСерии.КоличествоСерий, 0) = 0
	|					ТОГДА 1
	|				ИНАЧЕ втСерии.КоличествоСерий
	|			КОНЕЦ
	|	КОНЕЦ КАК СтоимостьКиЗ
	|ПОМЕСТИТЬ втСтоимостьКиЗ
	|ИЗ
	|	втСерии КАК втСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТовары КАК втТовары
	|		ПО втСерии.Номенклатура = втТовары.Номенклатура
	|			И втСерии.Характеристика = втТовары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровСерии.Серия.RFIDTID КАК RFIDTID,
	|	ПоступлениеТоваровСерии.Серия.RFIDEPC КАК RFIDEPC,
	|	ПоступлениеТоваровСерии.Серия.НомерКиЗГИСМ КАК НомерКиЗ,
	|	втСтоимостьКиЗ.НДСКиЗ КАК СуммаНДС,
	|	втСтоимостьКиЗ.СтоимостьКиЗ КАК Стоимость
	|ИЗ
	|	Документ.ПоступлениеТоваров.Серии КАК ПоступлениеТоваровСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПоступлениеТоваровСерии.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСтоимостьКиЗ КАК втСтоимостьКиЗ
	|		ПО (втСтоимостьКиЗ.Номенклатура = ПоступлениеТоваровСерии.Номенклатура)
	|			И (втСтоимостьКиЗ.Характеристика = ПоступлениеТоваровСерии.Характеристика)
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И ПоступлениеТоваровСерии.Ссылка = &ДокументОснование";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыОснования = РезультатЗапроса[0].Выбрать();
	РеквизитыОснования.Следующий();
	
	ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыОснования.Основание);
	
	Если Не РегистрыСведений.СтатусыИнформированияГИСМ.ЭтоСтатусНеАктуальногоУведомления(РеквизитыОснования.Статус) Тогда
		
		ТекстОшибки = НСтр("ru='Для %Документ% уже существует актуальное уведомление о ввозе маркированных товаров ГИСМ - %Уведомление%.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Уведомление%", РеквизитыОснования.АктуальноеУведомлениеОбОтгрузкеГИСМ);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если РезультатЗапроса[4].Пустой() Тогда
		
		ТекстОшибки = НСтр("ru='В %Документ% отсутствуют товары, маркированные контрольными (идентификационными) знаками.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(УведомлениеОбъект, РеквизитыОснования);
	УведомлениеОбъект.НомераКиЗ.Загрузить(РезультатЗапроса[4].Выгрузить());
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьУведомлениеОбИмпортеГИСМНаОснованииПоступления
//
Процедура ЗаполнитьУведомлениеОбИмпортеГИСМНаОснованииПоступления(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоступлениеТоваров.Организация КАК Организация,
	|	ПоступлениеТоваров.Контрагент КАК Контрагент,
	|	ПоступлениеТоваров.Ссылка КАК Основание,
	|	НЕ ПоступлениеТоваров.Проведен КАК ЕстьОшибкиПроведен,
	|	ЕСТЬNULL(СтатусыИнформированияГИСМ.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка)) КАК Статус
	|ИЗ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|		ПО (СтатусыИнформированияГИСМ.Документ = ПоступлениеТоваров.Ссылка)
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоступлениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	ПоступлениеТоваровТовары.Характеристика КАК Характеристика,
	|	ПоступлениеТоваровТовары.Количество КАК Количество
	|ИЗ
	|	Документ.ПоступлениеТоваров.Товары КАК ПоступлениеТоваровТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ПоступлениеТоваровТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И ПоступлениеТоваровТовары.Ссылка = &ДокументОснование";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыОснования = РезультатЗапроса[0].Выбрать();
	РеквизитыОснования.Следующий();
	
	ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыОснования.Основание);
	
	Если Не РегистрыСведений.СтатусыИнформированияГИСМ.ЭтоСтатусНеАктуальногоУведомления(РеквизитыОснования.Статус) Тогда
		
		ТекстОшибки = НСтр("ru='Для %Документ% уже существует актуальное уведомление об импорте маркированных товаров ГИСМ - %Уведомление%.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Уведомление%", РеквизитыОснования.АктуальноеУведомлениеОбОтгрузкеГИСМ);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если РезультатЗапроса[1].Пустой() Тогда
		
		ТекстОшибки = НСтр("ru='В %Документ% отсутствуют товары, маркированные контрольными (идентификационными) знаками.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(УведомлениеОбъект, РеквизитыОснования);
	УведомлениеОбъект.Товары.Загрузить(РезультатЗапроса[1].Выгрузить());
	
	Индекс = 0;
	Для Каждого СтрокаТЧ Из УведомлениеОбъект.Товары Цикл
		Индекс = Индекс + 1;
		СтрокаТЧ.КлючСвязи = Индекс;
	КонецЦикла;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьУведомлениеОСписанииКиЗНаОснованииВнутреннегоПотребления
//
Процедура ЗаполнитьУведомлениеОСписанииКиЗНаОснованииВнутреннегоПотребления(УведомлениеОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СписаниеТоваров.Ссылка КАК Основание,
	|	НЕ СписаниеТоваров.Проведен КАК ЕстьОшибкиПроведен,
	|	СписаниеТоваров.Организация,
	|	ЕСТЬNULL(СтатусыИнформированияГИСМ.ТекущееУведомление, ЗНАЧЕНИЕ(Документ.УведомлениеОСписанииКизГИСМ.ПустаяСсылка)) КАК ТекущееУведомление,
	|	ЕСТЬNULL(СтатусыИнформированияГИСМ.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка)) КАК Статус
	|ИЗ
	|	Документ.СписаниеТоваров КАК СписаниеТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|		ПО СписаниеТоваров.Ссылка = СтатусыИнформированияГИСМ.Документ
	|ГДЕ
	|	СписаниеТоваров.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ,
	|	СерииНоменклатуры.RFIDTID КАК RFIDTID,
	|	СерииНоменклатуры.RFIDEPC КАК RFIDEPC,
	|	ВЫБОР
	|		КОГДА СписаниеТоваровСерии.Номенклатура.КиЗГИСМ ТОГДА
	|			ЛОЖЬ
	|		КОГДА СписаниеТоваровСерии.Номенклатура.ПродукцияМаркируемаяДляГИСМ ТОГДА
	|			ИСТИНА
	|	КОНЕЦ КАК Индивидуализирован,
	|	ЗаявкаНаВыпускКиЗГИСМВыпущенныеКиЗ.Ссылка КАК ЗаявкаНаВыпускКиЗ
	|ИЗ
	|	Документ.СписаниеТоваров.Серии КАК СписаниеТоваровСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО СписаниеТоваровСерии.Серия = СерииНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК ЗаявкаНаВыпускКиЗГИСМВыпущенныеКиЗ
	|		ПО ЗаявкаНаВыпускКиЗГИСМВыпущенныеКиЗ.НомерКиЗ = СерииНоменклатуры.НомерКиЗГИСМ
	|		 И СписаниеТоваровСерии.Номенклатура.КиЗГИСМ
	|ГДЕ
	|	(СписаниеТоваровСерии.Номенклатура.КиЗГИСМ
	|			ИЛИ СписаниеТоваровСерии.Номенклатура.ПродукцияМаркируемаяДляГИСМ)
	|	И (СерииНоменклатуры.НомерКиЗГИСМ <> """"
	|			ИЛИ СерииНоменклатуры.RFIDTID <> """"
	|			ИЛИ СерииНоменклатуры.RFIDEPC <> """")
	|	И СписаниеТоваровСерии.Ссылка = &ДокументОснование";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыОснования = РезультатЗапроса[0].Выбрать();
	РеквизитыОснования.Следующий();
	
	ОбщегоНазначенияРТ.ПроверитьВозможностьВводаНаОсновании(РеквизитыОснования.Основание);
		
	Если Не РегистрыСведений.СтатусыИнформированияГИСМ.ЭтоСтатусНеАктуальногоУведомления(РеквизитыОснования.Статус) Тогда
		
		ТекстОшибки = НСтр("ru='Для %Документ% уже существует актуальное уведомление об списании КиЗ - %Уведомление%.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Уведомление%", РеквизитыОснования.ТекущееУведомление);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если РезультатЗапроса[1].Пустой() Тогда
		
		ТекстОшибки = НСтр("ru='В %Документ% отсутствуют товары, маркированные контрольными (идентификационными) знаками и контрольные (идентификационные) знаки.'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДанныеЗаполнения);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(УведомлениеОбъект, РеквизитыОснования);
	УведомлениеОбъект.НомераКиЗ.Загрузить(РезультатЗапроса[1].Выгрузить());
	
КонецПроцедуры

// См. функцию ИнтеграцияГИСМПереопределяемый.ЗапросПоПоступившимКиЗ
//
Функция ЗапросПоПоступившимКиЗ(Объект) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления,
	|	НомераКиЗ.СостояниеПодтверждения,
	|	НомераКиЗ.НомерСтроки
	|ПОМЕСТИТЬ НомераКиЗ
	|ИЗ
	|	&НомераКиЗ КАК НомераКиЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления,
	|	НомераКиЗ.СостояниеПодтверждения,
	|	НомераКиЗ.НомерСтроки
	|ПОМЕСТИТЬ НеподтвержденныеКиЗ
	|ИЗ
	|	НомераКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НомераКиЗ.СостояниеПодтверждения В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОжидаетсяПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ВыбратьПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Подтвердить),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.КПередаче))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ПОМЕСТИТЬ ВсеДокументыПоступленияПоКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ПО ДвиженияСерийТоваров.Регистратор = ПоступлениеТоваров.Ссылка
	|	                         И &Организация = ПоступлениеТоваров.Организация
	|	                          И &Контрагент = ПоступлениеТоваров.Контрагент
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО ДвиженияСерийТоваров.Регистратор = ВозвратТоваровОтПокупателя.Ссылка
	|	                         И &Организация = ВозвратТоваровОтПокупателя.Организация
	|	                          И &Контрагент = ВозвратТоваровОтПокупателя.Контрагент
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления
	|ПОМЕСТИТЬ РанееСопоставленныеПоступления
	|ИЗ
	|	Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НЕ НомераКиЗ.ДокументПоступления В (&ПустыеЗначенияДокументовПоступления)
	|	И НомераКиЗ.НомерКиЗ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|	И НомераКиЗ.Ссылка <> &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НЕ НомераКиЗ.ДокументПоступления В (&ПустыеЗначенияДокументовПоступления)
	|	И НомераКиЗ.НомерКиЗ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|	И НомераКиЗ.Ссылка <> &ДокументСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеДокументыПоступленияПоКиЗ.ДокументПоступления,
	|	ВсеДокументыПоступленияПоКиЗ.НомерКиЗ
	|ПОМЕСТИТЬ ДокументыПоступленияКандидаты
	|ИЗ
	|	ВсеДокументыПоступленияПоКиЗ КАК ВсеДокументыПоступленияПоКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РанееСопоставленныеПоступления КАК РанееСопоставленныеПоступления
	|		ПО ВсеДокументыПоступленияПоКиЗ.НомерКиЗ = РанееСопоставленныеПоступления.НомерКиЗ
	|			И ВсеДокументыПоступленияПоКиЗ.ДокументПоступления = РанееСопоставленныеПоступления.ДокументПоступления
	|ГДЕ
	|	РанееСопоставленныеПоступления.ДокументПоступления ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеподтвержденныеКиЗ.НомерКиЗ КАК НомерКиЗ,
	|	НеподтвержденныеКиЗ.ДокументПоступления,
	|	НеподтвержденныеКиЗ.НомерСтроки,
	|	НеподтвержденныеКиЗ.СостояниеПодтверждения,
	|	ЕстьNULL(ДокументыПоступленияКандидаты.ДокументПоступления, ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)) КАК ДокументПоступленияКандидат
	|ИЗ
	|	НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыПоступленияКандидаты КАК ДокументыПоступленияКандидаты
	|		ПО НеподтвержденныеКиЗ.НомерКиЗ = ДокументыПоступленияКандидаты.НомерКиЗ
	|ИТОГИ ПО
	|	НомерКиЗ");

	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЗаявкаНаВыпускКиЗГИСМ") Тогда
		Запрос.УстановитьПараметр("НомераКиЗ", Объект.ВыпущенныеКиЗ.Выгрузить());
	ИначеЕсли ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.УведомлениеОПоступленииМаркированныхТоваровГИСМ") Тогда
		Запрос.УстановитьПараметр("НомераКиЗ", Объект.НомераКиЗ.Выгрузить());
	КонецЕсли;
	
	ПустыеЗначенияДокументовПоступления = ИнтеграцияГИСМ.МассивПустыхЗначенийДокументовПоступленияМаркированнойПродукции();
	
	Запрос.УстановитьПараметр("ДокументСсылка",                      Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация",                         Объект.Организация);
	Запрос.УстановитьПараметр("Контрагент",                          Объект.Контрагент);
	Запрос.УстановитьПараметр("ПустыеЗначенияДокументовПоступления", ПустыеЗначенияДокументовПоступления);
	
	Возврат Запрос;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ИННКППGLNОрганизации
//
Функция ИННКППGLNОрганизации(Организация, Подразделение) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ИНН", "");
	ВозвращаемоеЗначение.Вставить("КПП", "");
	ВозвращаемоеЗначение.Вставить("GLN", "");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Организации.ИНН КАК ИНН,
	|	Организации.КПП КАК КПП,
	|	ЕСТЬNULL(ОрганизацииДляОбменаГИСМ.GLN, """") КАК GLN
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииДляОбменаГИСМ КАК ОрганизацииДляОбменаГИСМ
	|			ПО Организации.Ссылка = ОрганизацииДляОбменаГИСМ.Организация
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, Выборка);
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ИспользоватьПодразделения
//
Функция ИспользоватьПодразделения() Экспорт
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

// См. процедуру ИнтеграцияГИСМПереопределяемый.ПроверитьКорректностьПерсонифицованностиЗаказываемыхКиЗ
//
Процедура ПроверитьКорректностьПерсонифицованностиЗаказываемыхКиЗ(Объект, ИмяТабличнойЧасти, Отказ) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказанныеКиЗ.Номенклатура,
	|	ЗаказанныеКиЗ.Характеристика
	|ПОМЕСТИТЬ ЗаказанныеКиЗ
	|ИЗ
	|	&ЗаказанныеКиЗ КАК ЗаказанныеКиЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ХарактеристикиНоменклатуры.КиЗГИСМGTIN, СправочникНоменклатура.КиЗГИСМGTIN) = """"
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Персонифицирован
	|ИЗ
	|	ЗаказанныеКиЗ КАК ЗаказанныеКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ЗаказанныеКиЗ.Характеристика = ХарактеристикиНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ЗаказанныеКиЗ.Номенклатура = СправочникНоменклатура.Ссылка";
	
	ЗаказанныеКиЗ = Объект[ИмяТабличнойЧасти].Выгрузить(, "Номенклатура, Характеристика");
	Запрос.УстановитьПараметр("ЗаказанныеКиЗ", ЗаказанныеКиЗ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 1 Тогда
		
		ТекстОшибки = НСтр("ru='Для заказа персонифицированных и неперсонифицированных КиЗ нужно использовать разные документы.'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				,ИмяТабличнойЧасти
				,"Объект"
				,
				Отказ);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РасчетСостоянийЗаявокНаВыпускКиЗ

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаПоступившиеКиЗ
//
Функция ТекстЗапросаПоступившиеКиЗ()
	
	Возврат
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ДвиженияСерийТоваров.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ДвиженияСерийТоваров.Регистратор = &ДокументПоступления
	|	И СправочникНоменклатура.КиЗГИСМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ДвиженияСерийТоваров.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ДвиженияСерийТоваров.Регистратор = &ДокументПоступления
	|	И СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ";
	
КонецФункции

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусЗаявкиНаВыпускКиЗПоЗаявкеНаВыпускКиЗ
//
Процедура РассчитатьСтатусЗаявкиНаВыпускКиЗПоЗаявкеНаВыпускКиЗ(Источник)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьНового = Источник.ДополнительныеСвойства.Свойство("ЗаписьНового")
	             И Источник.ДополнительныеСвойства.ЗаписьНового;
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статусы.Документ,
	|	Статусы.ТекущаяЗаявкаНаВыпускКиЗ,
	|	Статусы.СтатусЗаявкиНаВыпускКиЗ,
	|	Статусы.СтатусОбработкиЭмитентом,
	|	Статусы.ДальнейшееДействие,
	|	Статусы.ПоступлениеТоваров,
	|	Статусы.КоличествоПоступленийТоваров,
	|	Статусы.СтатусПоступления
	|ИЗ
	|	РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК Статусы
	|ГДЕ
	|	    Статусы.Документ = &Основание
	|	ИЛИ Статусы.Документ = &Заявка");
	
	Запрос.УстановитьПараметр("Основание", Источник.Основание);
	Запрос.УстановитьПараметр("Заявка", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если ЗаписьНового Тогда
			
			Если НЕ ЗначениеЗаполнено(Выборка.ТекущаяЗаявкаНаВыпускКиЗ) 
				Или РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ЭтоСтатусНеАктуальнойЗаявки(Выборка.СтатусЗаявкиНаВыпускКиЗ)  Тогда
				
				ВыполнятьЗаписьВРегистр = Истина;
				
				ДанныеЗаписи.Документ                 = Выборка.Документ;
				ДанныеЗаписи.ТекущаяЗаявкаНаВыпускКиЗ = Источник.Ссылка;
				ДанныеЗаписи.СтатусЗаявкиНаВыпускКиЗ  = Перечисления.СтатусыЗаявокНаВыпускКиЗГИСМ.Черновик;
				ДанныеЗаписи.ДальнейшееДействие       = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
				
			КонецЕсли;
			
		Иначе
			
			ЗаполнитьЗначенияСвойств(ДанныеЗаписи, Выборка);
			
			Регистр = РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ;
			Если Регистр.СтатусТребуетРасчетаПоступления(ДанныеЗаписи.СтатусЗаявкиНаВыпускКиЗ,
				                                         ДанныеЗаписи.СтатусОбработкиЭмитентом) Тогда
				
				ВыполнятьЗаписьВРегистр = Истина;
				РассчитатьСтатусПоступленияПоДокументу(Источник, ДанныеЗаписи);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ЗаписьНового Тогда
			
			ВыполнятьЗаписьВРегистр = Истина;
			
			ДанныеЗаписи.Документ                    = Источник.Ссылка;
			ДанныеЗаписи.ТекущаяЗаявкаНаВыпускКиЗ = Источник.Ссылка;
			ДанныеЗаписи.СтатусЗаявкиНаВыпускКиЗ  = Перечисления.СтатусыЗаявокНаВыпускКиЗГИСМ.Черновик;
			ДанныеЗаписи.ДальнейшееДействие          = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусЗаявкиНаВыпускКиЗПоЗаказуПоставщику
//
Процедура РассчитатьСтатусЗаявкиНаВыпускКиЗПоЗаказуПоставщику(Источник) 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статусы.Документ,
	|	Статусы.ТекущаяЗаявкаНаВыпускКиЗ,
	|	Статусы.СтатусЗаявкиНаВыпускКиЗ,
	|	Статусы.СтатусОбработкиЭмитентом,
	|	Статусы.ДальнейшееДействие,
	|	Статусы.ПоступлениеТоваров,
	|	Статусы.КоличествоПоступленийТоваров,
	|	Статусы.СтатусПоступления
	|ИЗ
	|	РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК Статусы
	|ГДЕ
	|	Статусы.Документ = &ЗаказПоставщику
	|");
	
	Запрос.УстановитьПараметр("ЗаказПоставщику", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если (Не Источник.Проведен Или Не Источник.ЕстьКиЗГИСМ)
			И Не ЗначениеЗаполнено(Выборка.ТекущаяЗаявкаНаВыпускКиЗ) Тогда
			РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.УдалитьЗаписьИзРегистра(Источник.Ссылка);
			Возврат;
		КонецЕсли;
		
	ИначеЕсли Источник.ЕстьКиЗГИСМ Тогда
		
		ВыполнятьЗаписьВРегистр = Истина;
		
		ДанныеЗаписи.Документ                   = Источник.Ссылка;
		ДанныеЗаписи.ДальнейшееДействие         = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется;
		ДанныеЗаписи.СтатусЗаявкиНаВыпускКиЗ = Перечисления.СтатусыЗаявокНаВыпускКиЗГИСМ.Отсутствует;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусУведомленияОбОтгрузкеПоУведомлениюОбОтгрузке
//
Процедура РассчитатьСтатусУведомленияОбОтгрузкеПоУведомлениюОбОтгрузке(Источник)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьНового = Источник.ДополнительныеСвойства.Свойство("ЗаписьНового")
	             И Источник.ДополнительныеСвойства.ЗаписьНового;
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статусы.Документ,
	|	Статусы.ТекущееУведомлениеОбОтгрузке,
	|	Статусы.Статус,
	|	Статусы.ДальнейшееДействие
	|ИЗ
	|	РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК Статусы
	|ГДЕ
	|	    Статусы.Документ = &Основание
	|	ИЛИ Статусы.Документ = &Уведомление");
	
	Запрос.УстановитьПараметр("Основание", Источник.Основание);
	Запрос.УстановитьПараметр("Уведомление", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если ЗаписьНового Тогда
			
			Если НЕ ЗначениеЗаполнено(Выборка.ТекущееУведомлениеОбОтгрузке) Тогда
				
				ВыполнятьЗаписьВРегистр = Истина;
				
			ИначеЕсли РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ЭтоСтатусНеАктуальногоУведомления(Выборка.Статус) Тогда
				
				ВыполнятьЗаписьВРегистр = Истина;
				
			КонецЕсли;
			
			ДанныеЗаписи.Документ                     = Выборка.Документ;
			ДанныеЗаписи.ТекущееУведомлениеОбОтгрузке = Источник.Ссылка;
			ДанныеЗаписи.Статус                       = Перечисления.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Черновик;
			ДанныеЗаписи.ДальнейшееДействие           = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	Иначе
		
		Если ЗаписьНового Тогда
			
			ВыполнятьЗаписьВРегистр = Истина;
			
			ДанныеЗаписи.Документ                     = Источник.Основание;
			ДанныеЗаписи.ТекущееУведомлениеОбОтгрузке = Источник.Ссылка;
			ДанныеЗаписи.Статус                       = Перечисления.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Черновик;
			ДанныеЗаписи.ДальнейшееДействие           = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусУведомленияОбОтгрузкеПоРеализацииТоваровУслуг
//
Процедура РассчитатьСтатусУведомленияОбОтгрузкеПоРеализацииТоваровУслуг(Источник)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статусы.Документ,
	|	Статусы.ТекущееУведомлениеОбОтгрузке,
	|	Статусы.Статус,
	|	Статусы.ДальнейшееДействие
	|ИЗ
	|	РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК Статусы
	|ГДЕ
	|	Статусы.Документ = &Реализация
	|");
	
	Запрос.УстановитьПараметр("Реализация", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если (Не Источник.Проведен Или Не Источник.ЕстьМаркируемаяПродукцияГИСМ)
			И Не ЗначениеЗаполнено(Выборка.ТекущееУведомлениеОбОтгрузке) Тогда
			РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.УдалитьЗаписьИзРегистра(Источник.Ссылка);
			Возврат;
		КонецЕсли;
		
	ИначеЕсли Источник.ЕстьМаркируемаяПродукцияГИСМ Тогда
		
		ВыполнятьЗаписьВРегистр = Истина;
		
		ДанныеЗаписи.Документ           = Источник.Ссылка;
		ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется;
		ДанныеЗаписи.Статус             = Перечисления.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Отсутствует;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусУведомленияОбОтгрузкеПоВозвратуТоваровПоставщику
//
Процедура РассчитатьСтатусУведомленияОбОтгрузкеПоВозвратуТоваровПоставщику(Источник)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статусы.Документ,
	|	Статусы.ТекущееУведомлениеОбОтгрузке,
	|	Статусы.Статус,
	|	Статусы.ДальнейшееДействие
	|ИЗ
	|	РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК Статусы
	|ГДЕ
	|	Статусы.Документ = &ВозвратПоставщику
	|");
	
	Запрос.УстановитьПараметр("ВозвратПоставщику", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если (Не Источник.Проведен Или Не Источник.ЕстьМаркируемаяПродукцияГИСМ)
			И Не ЗначениеЗаполнено(Выборка.ТекущееУведомлениеОбОтгрузке) Тогда
			РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.УдалитьЗаписьИзРегистра(Источник.Ссылка);
			Возврат;
		КонецЕсли;
		
	ИначеЕсли Источник.ЕстьМаркируемаяПродукцияГИСМ Тогда
		
		ВыполнятьЗаписьВРегистр = Истина;
		
		ДанныеЗаписи.Документ           = Источник.Ссылка;
		ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется;
		ДанныеЗаписи.Статус             = Перечисления.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Отсутствует;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусУведомленияОПоступлении
//
Процедура РассчитатьСтатусУведомленияОПоступленииПоУведомлениюОПоступлении(Источник)
	
	ЗаписьНового = Источник.ДополнительныеСвойства.Свойство("ЗаписьНового")
	             И Источник.ДополнительныеСвойства.ЗаписьНового;
	
	ВыполнятьЗаписьВРегистр = Ложь;
	ДанныеЗаписи = РегистрыСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.ЗначенияПоУмолчанию();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Статусы.Документ,
	|	Статусы.Статус,
	|	Статусы.ДальнейшееДействие,
	|	Статусы.ПоступлениеТоваров,
	|	Статусы.КоличествоПоступленийТоваров,
	|	Статусы.СтатусПоступления
	|ИЗ
	|	РегистрСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ КАК Статусы
	|ГДЕ
	|	Статусы.Документ = &УведомлениеОПоступлении");
	
	Запрос.УстановитьПараметр("УведомлениеОПоступлении", Источник.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ДанныеЗаписи, Выборка);
		
		Регистр = РегистрыСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ;
		Если Регистр.СтатусТребуетРасчетаПоступления(ДанныеЗаписи.Статус) Тогда
			
			ВыполнятьЗаписьВРегистр = Истина;
			РассчитатьСтатусПоступленияПоДокументу(Источник, ДанныеЗаписи);
			
		КонецЕсли;
		
	Иначе
		
		Если ЗаписьНового Тогда
			
			ВыполнятьЗаписьВРегистр = Истина;
			
			ДанныеЗаписи.Документ           = Источник.Ссылка;
			ДанныеЗаписи.Статус             = Перечисления.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.Получено;
			ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПоступлениеТоваров;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВыполнятьЗаписьВРегистр Тогда
		РегистрыСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусПоступленияПоДокументу
//
Процедура РассчитатьСтатусПоступленияПоДокументу(Источник, ДанныеЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ЗаявкаНаВыпускКиЗГИСМ") Тогда
		
		ДокументСсылка      = ДанныеЗаписи.ТекущаяЗаявкаНаВыпускКиЗ;
		ЗначенияПоУмолчанию = РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ЗначенияПоУмолчанию();
		
		Текст = 
		"ВЫБРАТЬ
		|	НомераКиЗ.НомерКиЗ               КАК НомерКиЗ,
		|	НомераКиЗ.СостояниеПодтверждения КАК СостояниеПодтверждения,
		|	НомераКиЗ.ДокументПоступления    КАК ДокументПоступления
		|ПОМЕСТИТЬ НомераКиЗ
		|ИЗ
		|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК НомераКиЗ
		|ГДЕ
		|	НомераКиЗ.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.УведомлениеОПоступленииМаркированныхТоваровГИСМ") Тогда
		
		ДокументСсылка      = ДанныеЗаписи.Документ;
		ЗначенияПоУмолчанию = РегистрыСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.ЗначенияПоУмолчанию();
		
		Текст = 
		"ВЫБРАТЬ
		|	НомераКиЗ.НомерКиЗ               КАК НомерКиЗ,
		|	НомераКиЗ.СостояниеПодтверждения КАК СостояниеПодтверждения,
		|	НомераКиЗ.ДокументПоступления    КАК ДокументПоступления
		|ПОМЕСТИТЬ НомераКиЗ
		|ИЗ
		|	Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ КАК НомераКиЗ
		|ГДЕ
		|	НомераКиЗ.Ссылка = &ДокументСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(
		ДанныеЗаписи,
		ЗначенияПоУмолчанию,
		"ПоступлениеТоваров, КоличествоПоступленийТоваров, СтатусПоступления, КПередачеПодтверждения, ПроцентПодтвержденныхКиЗ");
	
	Подтверждено = 0;
	КПередаче    = 0;
	Передано     = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = Текст +
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НомераКиЗ.НомерКиЗ) КАК ВсегоСтрок,
	|	СУММА(ВЫБОР
	|			КОГДА НомераКиЗ.ДокументПоступления В (&МассивПустыхЗначенийДокументовПоступления)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Поступило,
	|	СУММА(ВЫБОР
	|			КОГДА НомераКиЗ.СостояниеПодтверждения В (ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ПринятоГИСМ),
	|			                                          ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Списано))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Подтверждено,
	|	СУММА(ВЫБОР
	|			КОГДА НомераКиЗ.СостояниеПодтверждения В (ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.КПередаче))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КПередаче,
	|	СУММА(ВЫБОР
	|			КОГДА НомераКиЗ.СостояниеПодтверждения В (ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Передано))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Передано
	|ИЗ
	|	НомераКиЗ КАК НомераКиЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////1
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НомераКиЗ.ДокументПоступления
	|ИЗ
	|	НомераКиЗ КАК НомераКиЗ
	|ГДЕ
	|	Не НомераКиЗ.ДокументПоступления В (&МассивПустыхЗначенийДокументовПоступления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////2
	|ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ
	|ПОМЕСТИТЬ НеподтвержденныеКиЗ
	|ИЗ
	|	НомераКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НомераКиЗ.СостояниеПодтверждения В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Подтвердить),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОжидаетсяПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ВыбратьПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОтклоненоГИСМ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////3
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ПОМЕСТИТЬ ВсеДокументыПоступленияПоКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ПО ДвиженияСерийТоваров.Регистратор = ПоступлениеТоваров.Ссылка
	|		                     И &Организация = ПоступлениеТоваров.Организация
	|		                      И &Контрагент = ПоступлениеТоваров.Контрагент
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО ДвиженияСерийТоваров.Регистратор = ВозвратТоваровОтПокупателя.Ссылка
	|		                     И &Организация = ВозвратТоваровОтПокупателя.Организация
	|		                      И &Контрагент = ВозвратТоваровОтПокупателя.Контрагент
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////4
	|ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления
	|ПОМЕСТИТЬ РанееСопоставленныеПоступления
	|ИЗ
	|	НомераКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НомераКиЗ.ДокументПоступления НЕ В (
	|		ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(Документ.ВозвратТоваровОтПокупателя.ПустаяСсылка),
	|		Неопределено)
	|	И НомераКиЗ.НомерКиЗ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////5
	|ВЫБРАТЬ
	|	ВсеДокументыПоступленияПоКиЗ.ДокументПоступления,
	|	ВсеДокументыПоступленияПоКиЗ.НомерКиЗ
	|ПОМЕСТИТЬ ДокументыПоступленияКандидаты
	|ИЗ
	|	ВсеДокументыПоступленияПоКиЗ КАК ВсеДокументыПоступленияПоКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РанееСопоставленныеПоступления КАК РанееСопоставленныеПоступления
	|		ПО ВсеДокументыПоступленияПоКиЗ.НомерКиЗ = РанееСопоставленныеПоступления.НомерКиЗ
	|			И ВсеДокументыПоступленияПоКиЗ.ДокументПоступления = РанееСопоставленныеПоступления.ДокументПоступления
	|ГДЕ
	|	РанееСопоставленныеПоступления.ДокументПоступления ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////6
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НеподтвержденныеКиЗ.НомерКиЗ КАК НомерКиЗ
	|ИЗ
	|	НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыПоступленияКандидаты КАК ДокументыПоступленияКандидаты
	|		ПО НеподтвержденныеКиЗ.НомерКиЗ = ДокументыПоступленияКандидаты.НомерКиЗ";
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация",    Источник.Организация);
	Запрос.УстановитьПараметр("Контрагент",     Источник.Контрагент);
	Запрос.УстановитьПараметр("МассивПустыхЗначенийДокументовПоступления", ИнтеграцияГИСМ.МассивПустыхЗначенийДокументовПоступленияМаркированнойПродукции());
	
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаПоступилоПодтверждено = Результат[1].Выбрать();
	
	Если ВыборкаПоступилоПодтверждено.Следующий() Тогда
		
		ВсегоСтрок   = ?(ЗначениеЗаполнено(ВыборкаПоступилоПодтверждено.ВсегоСтрок)  , ВыборкаПоступилоПодтверждено.ВсегоСтрок  , 0);
		Поступило    = ?(ЗначениеЗаполнено(ВыборкаПоступилоПодтверждено.Поступило)   , ВыборкаПоступилоПодтверждено.Поступило   , 0);
		Подтверждено = ?(ЗначениеЗаполнено(ВыборкаПоступилоПодтверждено.Подтверждено), ВыборкаПоступилоПодтверждено.Подтверждено, 0);
		КПередаче    = ?(ЗначениеЗаполнено(ВыборкаПоступилоПодтверждено.КПередаче)   , ВыборкаПоступилоПодтверждено.КПередаче   , 0);
		Передано     = ?(ЗначениеЗаполнено(ВыборкаПоступилоПодтверждено.Передано)    , ВыборкаПоступилоПодтверждено.Передано    , 0);
		
		Если ВсегоСтрок <> 0 Тогда
		
			Если Поступило = ВсегоСтрок Тогда
				ДанныеЗаписи.СтатусПоступления = Перечисления.СтатусыПоступленийГИСМ.Поступило;
			ИначеЕсли Поступило > 0 Тогда
				ДанныеЗаписи.СтатусПоступления = Перечисления.СтатусыПоступленийГИСМ.ПоступилоЧастично;
			Иначе
				ДанныеЗаписи.СтатусПоступления = Перечисления.СтатусыПоступленийГИСМ.ОжидаетсяПоступление;
			КонецЕсли;
			
			ДанныеЗаписи.ПроцентПодтвержденныхКиЗ = Окр(Подтверждено / ВсегоСтрок, 2) * 100;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВыборкаДокументыПоступления = Результат[2].Выбрать();
	
	КоличествоДокументовПоступления = ВыборкаДокументыПоступления.Количество();
	Если ВыборкаДокументыПоступления.Количество() = 1 Тогда
		Если ВыборкаДокументыПоступления.Следующий() Тогда
			ДанныеЗаписи.ПоступлениеТоваров = ВыборкаДокументыПоступления.ДокументПоступления;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеЗаписи.КоличествоПоступленийТоваров = КоличествоДокументовПоступления;
	
	ВыборкаДокументыЕстьЧтоПодтверждать = Результат[7].Выбрать();
	Если ВыборкаДокументыЕстьЧтоПодтверждать.Следующий() Тогда
		
		ДанныеЗаписи.КПередачеПодтверждения  = Истина;
		ДанныеЗаписи.ДальнейшееДействие      = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПодтвердитеПолучение;
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаявкаНаВыпускКиЗГИСМ") Тогда
			ДанныеЗаписи.СтатусЗаявкиНаВыпускКиЗ = Перечисления.СтатусыЗаявокНаВыпускКиЗГИСМ.ОбрабатываетсяПоступление;
		ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.УведомлениеОПоступленииМаркированныхТоваровГИСМ") Тогда
			ДанныеЗаписи.Статус = Перечисления.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.ОбрабатываетсяПоступление;
		КонецЕсли;
		
	Иначе
		
		ДанныеЗаписи.КПередачеПодтверждения = Ложь;
		
		Если КПередаче > 0 Тогда
			
			ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПодтвердитеПолучение;
			
		ИначеЕсли Передано > 0 Тогда
			
			ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПолучениеКвитанцииОФиксации;
			
		Иначе
			
			Если Подтверждено = ВсегоСтрок И Подтверждено > 0 Тогда
				ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ЗакройтеЗаявку;
			Иначе
				ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПоступлениеТоваров;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.АнализЗаявокНаВыпускКиЗ
//
Процедура АнализЗаявокНаВыпускКиЗ(МассивАнализируемыхНомеровКиЗ, Источник)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НомераКиЗ.Ссылка КАК Ссылка,
	|	Статусы.КПередачеПодтверждения КАК КПередачеПодтверждения
	|ПОМЕСТИТЬ ДокументыДляАнализа
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК НомераКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК Статусы
	|		ПО НомераКиЗ.Ссылка = Статусы.ТекущаяЗаявкаНаВыпускКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВыпускКиЗГИСМ КАК ДокументыГИСМ
	|		ПО НомераКиЗ.Ссылка = ДокументыГИСМ.Ссылка
	|		     И &Контрагент  = ДокументыГИСМ.Контрагент
	|		     И &Организация = ДокументыГИСМ.Организация
	|ГДЕ
	|	НомераКиЗ.НомерКиЗ В(&МассивАнализируемыхНомеровКиЗ)
	|	И НЕ НомераКиЗ.СостояниеПодтверждения В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.КПередаче),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Передано),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ПринятоГИСМ))
	|	И (Статусы.СтатусЗаявкиНаВыпускКиЗ = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОбрабатываетсяПоступление)
	|		ИЛИ Статусы.СтатусОбработкиЭмитентом В (
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиЭмитентомКиЗГИСМ.ЖдетСамовывоза),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиЭмитентомКиЗГИСМ.ОтгруженоЗаказчику),
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиЭмитентомКиЗГИСМ.ПереданоГрузоперевозчику)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераКиЗ.Ссылка                           КАК Ссылка,
	|	ДокументыДляАнализа.КПередачеПодтверждения КАК КПередачеПодтверждения,
	|	НомераКиЗ.НомерКиЗ                         КАК НомерКиЗ
	|ПОМЕСТИТЬ НеподтвержденныеКиЗ
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК НомераКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыДляАнализа КАК ДокументыДляАнализа
	|		ПО НомераКиЗ.Ссылка = ДокументыДляАнализа.Ссылка
	|ГДЕ
	|	НомераКиЗ.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыДляАнализа.Ссылка
	|			ИЗ
	|				ДокументыДляАнализа)
	|	И НомераКиЗ.СостояниеПодтверждения В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Подтвердить),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОжидаетсяПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ВыбратьПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОтклоненоГИСМ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ПОМЕСТИТЬ ВсеДокументыПоступленияПоКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ПО ДвиженияСерийТоваров.Регистратор = ПоступлениеТоваров.Ссылка
	|		                     И &Контрагент  = ПоступлениеТоваров.Контрагент
	|		                     И &Организация = ПоступлениеТоваров.Организация
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления
	|ПОМЕСТИТЬ РанееСопоставленныеПоступления
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НомераКиЗ.ДокументПоступления <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	|	И НомераКиЗ.НомерКиЗ В
	|		(ВЫБРАТЬ
	|			НеподтвержденныеКиЗ.НомерКиЗ
	|		ИЗ
	|			НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|	И НЕ НомераКиЗ.Ссылка В
	|		(ВЫБРАТЬ
	|			ДокументыДляАнализа.Ссылка
	|		ИЗ
	|			ДокументыДляАнализа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеДокументыПоступленияПоКиЗ.ДокументПоступления,
	|	ВсеДокументыПоступленияПоКиЗ.НомерКиЗ
	|ПОМЕСТИТЬ ДокументыПоступленияКандидаты
	|ИЗ
	|	ВсеДокументыПоступленияПоКиЗ КАК ВсеДокументыПоступленияПоКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РанееСопоставленныеПоступления КАК РанееСопоставленныеПоступления
	|		ПО ВсеДокументыПоступленияПоКиЗ.НомерКиЗ = РанееСопоставленныеПоступления.НомерКиЗ
	|			И ВсеДокументыПоступленияПоКиЗ.ДокументПоступления = РанееСопоставленныеПоступления.ДокументПоступления
	|ГДЕ
	|	РанееСопоставленныеПоступления.ДокументПоступления ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НеподтвержденныеКиЗ.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДокументыПоступленияКандидаты.ДокументПоступления ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК ЕстьКПодтвеждению,
	|	МАКСИМУМ(НеподтвержденныеКиЗ.КПередачеПодтверждения) КАК КПередачеПодтвержденияРегистр
	|ПОМЕСТИТЬ ДокументыУКоторыхИзменилсяПризнакКПодтверждению
	|ИЗ
	|	НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыПоступленияКандидаты КАК ДокументыПоступленияКандидаты
	|		ПО НеподтвержденныеКиЗ.НомерКиЗ = ДокументыПоступленияКандидаты.НомерКиЗ
	|
	|СГРУППИРОВАТЬ ПО
	|	НеподтвержденныеКиЗ.Ссылка
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДокументыПоступленияКандидаты.ДокументПоступления ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) <> МАКСИМУМ(НеподтвержденныеКиЗ.КПередачеПодтверждения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.Документ,
	|	Статусы.ТекущаяЗаявкаНаВыпускКиЗ,
	|	Статусы.СтатусОбработкиЭмитентом,
	|	Статусы.СтатусЗаявкиНаВыпускКиЗ,
	|	Статусы.ПоступлениеТоваров,
	|	Статусы.КоличествоПоступленийТоваров,
	|	Статусы.СтатусПоступления,
	|	Статусы.ДальнейшееДействие,
	|	Статусы.ПроцентПодтвержденныхКиЗ,
	|	ТаблицаДокументы.ЕстьКПодтвеждению КАК КПередачеПодтверждения
	|ИЗ
	|	ДокументыУКоторыхИзменилсяПризнакКПодтверждению КАК ТаблицаДокументы,
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК Статусы
	|		ПО Статусы.ТекущаяЗаявкаНаВыпускКиЗ = ТаблицаДокументы.Ссылка";
	
	Запрос.УстановитьПараметр("МассивАнализируемыхНомеровКиЗ", МассивАнализируемыхНомеровКиЗ);
	Запрос.УстановитьПараметр("Контрагент",                    Источник.Контрагент);
	Запрос.УстановитьПараметр("Организация",                   Источник.Организация);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
	
		ДанныеЗаписи = РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ЗначенияПоУмолчанию();
		
		ЗаполнитьЗначенияСвойств(ДанныеЗаписи, Выборка);
		Если Выборка.КПередачеПодтверждения Тогда
			
			ДанныеЗаписи.ДальнейшееДействие         = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПодтвердитеПолучение;
			ДанныеЗаписи.СтатусЗаявкиНаВыпускКиЗ = Перечисления.СтатусыЗаявокНаВыпускКиЗГИСМ.ОбрабатываетсяПоступление;
			
		Иначе
			
			ДанныеЗаписи.ДальнейшееДействие         = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ЗакройтеЗаявку;
			ДанныеЗаписи.СтатусЗаявкиНаВыпускКиЗ = Перечисления.СтатусыЗаявокНаВыпускКиЗГИСМ.ОбрабатываетсяПоступление;
			
		КонецЕсли;
		
		РегистрыСведений.СтатусыЗаявокНаВыпускКиЗГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
	
	КонецЦикла;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.АнализУведомленийОПоступлении
//
Процедура АнализУведомленийОПоступлении(МассивАнализируемыхНомеровКиЗ, Источник)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Ссылка                 КАК Ссылка,
	|	Статусы.КПередачеПодтверждения КАК КПередачеПодтверждения
	|ПОМЕСТИТЬ ДокументыДляАнализа
	|ИЗ
	|	Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ КАК Статусы
	|		ПО Таблица.Ссылка = Статусы.Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ КАК ДокументыГИСМ
	|		ПО Таблица.Ссылка  = ДокументыГИСМ.Ссылка
	|		    И &Контрагент  = ДокументыГИСМ.Контрагент
	|		    И &Организация = ДокументыГИСМ.Организация
	|ГДЕ
	|	Таблица.НомерКиЗ В(&МассивАнализируемыхНомеровКиЗ)
	|	И НЕ Таблица.СостояниеПодтверждения В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.КПередаче),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Передано),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ПринятоГИСМ))
	|	И (Статусы.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.Получено),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.ОбрабатываетсяПоступление)))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераКиЗ.Ссылка                           КАК Ссылка,
	|	ДокументыДляАнализа.КПередачеПодтверждения КАК КПередачеПодтверждения,
	|	НомераКиЗ.НомерКиЗ                         КАК НомерКиЗ
	|ПОМЕСТИТЬ НеподтвержденныеКиЗ
	|ИЗ
	|	Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ КАК НомераКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыДляАнализа КАК ДокументыДляАнализа
	|		ПО НомераКиЗ.Ссылка = ДокументыДляАнализа.Ссылка
	|ГДЕ
	|	НомераКиЗ.Ссылка В
	|			(ВЫБРАТЬ
	|				ДокументыДляАнализа.Ссылка
	|			ИЗ
	|				ДокументыДляАнализа)
	|	И НомераКиЗ.СостояниеПодтверждения В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Подтвердить),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОжидаетсяПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ВыбратьПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОтклоненоГИСМ))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ПОМЕСТИТЬ ВсеДокументыПоступленияПоКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ПО ДвиженияСерийТоваров.Регистратор = ПоступлениеТоваров.Ссылка
	|		                     И &Контрагент  = ПоступлениеТоваров.Контрагент
	|		                     И &Организация = ПоступлениеТоваров.Организация
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ КАК НомерКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО ДвиженияСерийТоваров.Регистратор = ВозвратТоваровОтПокупателя.Ссылка
	|		                     И &Контрагент  = ВозвратТоваровОтПокупателя.Контрагент
	|		                     И &Организация = ВозвратТоваровОтПокупателя.Организация
	|		
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления
	|ПОМЕСТИТЬ РанееСопоставленныеПоступления
	|ИЗ
	|	Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НомераКиЗ.ДокументПоступления НЕ В (
	|		ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(Документ.ВозвратТоваровОтПокупателя.ПустаяСсылка),
	|		Неопределено)
	|	И НомераКиЗ.НомерКиЗ В
	|		(ВЫБРАТЬ
	|			НеподтвержденныеКиЗ.НомерКиЗ
	|		ИЗ
	|			НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|	И НЕ НомераКиЗ.Ссылка В
	|		(ВЫБРАТЬ
	|			ДокументыДляАнализа.Ссылка
	|		ИЗ
	|			ДокументыДляАнализа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеДокументыПоступленияПоКиЗ.ДокументПоступления,
	|	ВсеДокументыПоступленияПоКиЗ.НомерКиЗ
	|ПОМЕСТИТЬ ДокументыПоступленияКандидаты
	|ИЗ
	|	ВсеДокументыПоступленияПоКиЗ КАК ВсеДокументыПоступленияПоКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РанееСопоставленныеПоступления КАК РанееСопоставленныеПоступления
	|		ПО ВсеДокументыПоступленияПоКиЗ.НомерКиЗ = РанееСопоставленныеПоступления.НомерКиЗ
	|			И ВсеДокументыПоступленияПоКиЗ.ДокументПоступления = РанееСопоставленныеПоступления.ДокументПоступления
	|ГДЕ
	|	РанееСопоставленныеПоступления.ДокументПоступления ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НеподтвержденныеКиЗ.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДокументыПоступленияКандидаты.ДокументПоступления ЕСТЬ NULL 
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК ЕстьКПодтвеждению,
	|	МАКСИМУМ(НеподтвержденныеКиЗ.КПередачеПодтверждения) КАК КПередачеПодтвержденияРегистр
	|ПОМЕСТИТЬ ДокументыУКоторыхИзменилсяПризнакКПодтверждению
	|ИЗ
	|	НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыПоступленияКандидаты КАК ДокументыПоступленияКандидаты
	|		ПО НеподтвержденныеКиЗ.НомерКиЗ = ДокументыПоступленияКандидаты.НомерКиЗ
	|
	|СГРУППИРОВАТЬ ПО
	|	НеподтвержденныеКиЗ.Ссылка
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.Документ,
	|	Статусы.Статус,
	|	Статусы.ДальнейшееДействие,
	|	Статусы.ПоступлениеТоваров,
	|	Статусы.КоличествоПоступленийТоваров,
	|	Статусы.СтатусПоступления,
	|	ТаблицаДокументы.ЕстьКПодтвеждению КАК КПередачеПодтверждения,
	|	Статусы.ПроцентПодтвержденныхКиЗ
	|ИЗ
	|	ДокументыУКоторыхИзменилсяПризнакКПодтверждению КАК ТаблицаДокументы,
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ КАК Статусы
	|		ПО Статусы.Документ = ТаблицаДокументы.Ссылка";
	
	Запрос.УстановитьПараметр("МассивАнализируемыхНомеровКиЗ", МассивАнализируемыхНомеровКиЗ);
	Запрос.УстановитьПараметр("Контрагент",                    Источник.Контрагент);
	Запрос.УстановитьПараметр("Организация",                   Источник.Организация);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДанныеЗаписи = РегистрыСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.ЗначенияПоУмолчанию();
		
		ЗаполнитьЗначенияСвойств(ДанныеЗаписи, Выборка);
		Если Выборка.КПередачеПодтверждения Тогда
			
			ДанныеЗаписи.Статус             = Перечисления.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.ОбрабатываетсяПоступление;
			ДанныеЗаписи.ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПодтвердитеПолучение;
		КонецЕсли;
		
		РегистрыСведений.СтатусыУведомленийОПоступленииМаркированныхТоваровГИСМ.ВыполнитьЗаписьВРегистрПоДаннымСтруктура(ДанныеЗаписи);
		
	КонецЦикла;
	
КонецПроцедуры


// См. функцию ИнтеграцияГИСМПереопределяемый.ДанныеПоЗаявкамНаВыпускКиЗ
//
Функция ДанныеПоЗаявкамНаВыпускКиЗ(ДокументыКПодтверждению) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НомераКиЗ.НомерКиЗ,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ ДублирующиесяНомераКиЗ
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НомераКиЗ.Ссылка В(&ДокументыКПодтверждению)
	|
	|СГРУППИРОВАТЬ ПО
	|	НомераКиЗ.НомерКиЗ
	|
	|ИМЕЮЩИЕ
	|	СУММА(1) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////1
	|ВЫБРАТЬ
	|	ВыпущенныеКиЗ.ВидКиЗ               КАК ВидКиЗ,
	|	ВыпущенныеКиЗ.СпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|	ВыпущенныеКиЗ.РазмерКиЗ            КАК РазмерКиЗ,
	|	ВыпущенныеКиЗ.GTIN                 КАК GTIN
	|ПОМЕСТИТЬ втВыпущенныеКиЗ
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК ВыпущенныеКиЗ
	|ГДЕ
	|	ВыпущенныеКиЗ.Ссылка В(&ДокументыКПодтверждению)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////2
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	Номенклатура.КиЗГИСМВид КАК ВидКиЗ,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер КАК РазмерКиЗ,
	|	Номенклатура.КиЗГИСМGTIN КАК GTIN
	|ПОМЕСТИТЬ НоменклатураИХарактеристикиБД
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ЭтоГруппа
	|	И НЕ Номенклатура.ВидНоменклатуры.ИспользоватьХарактеристики
	|	И (Номенклатура.КиЗГИСМВид,
	|	   Номенклатура.КиЗГИСМСпособВыпускаВОборот,
	|	   Номенклатура.КиЗГИСМРазмер,
	|	   Номенклатура.КиЗГИСМ,
	|	   Номенклатура.КиЗГИСМGTIN) В
	|		(ВЫБРАТЬ
	|			втВыпущенныеКиЗ.ВидКиЗ               КАК ВидКиЗ,
	|			втВыпущенныеКиЗ.СпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|			втВыпущенныеКиЗ.РазмерКиЗ            КАК РазмерКиЗ,
	|			ИСТИНА                               КАК КиЗГИСМ,
	|			втВыпущенныеКиЗ.GTIN                 КАК GTIN
	|		ИЗ втВыпущенныеКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	ХарактеристикиНоменклатуры.Ссылка,
	|	Номенклатура.КиЗГИСМВид КАК ВидКиЗ,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер КАК РазмерКиЗ,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.КиЗГИСМGTIN <> """" ТОГДА
	|			ХарактеристикиНоменклатуры.КиЗГИСМGTIN
	|		ИНАЧЕ
	|			Номенклатура.КиЗГИСМGTIN
	|	КОНЕЦ КАК GTIN
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО Номенклатура.Ссылка = ХарактеристикиНоменклатуры.Владелец
	|ГДЕ
	|	Номенклатура.ВидНоменклатуры.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ИндивидуальныеДляНоменклатуры)
	|	И (
	|	Номенклатура.КиЗГИСМВид,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер,
	|	Номенклатура.КиЗГИСМ,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.КиЗГИСМGTIN <> """" ТОГДА
	|			ХарактеристикиНоменклатуры.КиЗГИСМGTIN
	|		ИНАЧЕ
	|			Номенклатура.КиЗГИСМGTIN
	|	КОНЕЦ) В
	|		(ВЫБРАТЬ
	|			втВыпущенныеКиЗ.ВидКиЗ               КАК ВидКиЗ,
	|			втВыпущенныеКиЗ.СпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|			втВыпущенныеКиЗ.РазмерКиЗ            КАК РазмерКиЗ,
	|			ИСТИНА                               КАК КиЗГИСМ,
	|			втВыпущенныеКиЗ.GTIN                 КАК GTIN
	|		ИЗ втВыпущенныеКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	ХарактеристикиНоменклатуры.Ссылка,
	|	Номенклатура.КиЗГИСМВид КАК ВидКиЗ,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер КАК РазмерКиЗ,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.КиЗГИСМGTIN <> """" ТОГДА
	|			ХарактеристикиНоменклатуры.КиЗГИСМGTIN
	|		ИНАЧЕ
	|			Номенклатура.КиЗГИСМGTIN
	|	КОНЕЦ КАК GTIN
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО Номенклатура.ВидНоменклатуры = ХарактеристикиНоменклатуры.Владелец
	|ГДЕ
	|	Номенклатура.ВидНоменклатуры.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры)
	|	И (
	|	Номенклатура.КиЗГИСМВид,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер,
	|	Номенклатура.КиЗГИСМ,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.КиЗГИСМGTIN <> """" ТОГДА
	|			ХарактеристикиНоменклатуры.КиЗГИСМGTIN
	|		ИНАЧЕ
	|			Номенклатура.КиЗГИСМGTIN
	|	КОНЕЦ) В
	|		(ВЫБРАТЬ
	|			втВыпущенныеКиЗ.ВидКиЗ               КАК ВидКиЗ,
	|			втВыпущенныеКиЗ.СпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|			втВыпущенныеКиЗ.РазмерКиЗ            КАК РазмерКиЗ,
	|			ИСТИНА                               КАК КиЗГИСМ,
	|			втВыпущенныеКиЗ.GTIN                 КАК GTIN
	|		ИЗ втВыпущенныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////3
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыпущенныеКиЗ.НомерКиЗ                                                            КАК НомерКиЗ,
	|	ВыпущенныеКиЗ.Ссылка                                                              КАК Документ,
	|	ЕСТЬNULL(Таблица.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))                 КАК Номенклатура,
	|	ЕСТЬNULL(Таблица.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика
	|ПОМЕСТИТЬ НомераКиЗНоменклатура
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК ВыпущенныеКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураИХарактеристикиБД КАК Таблица
	|		ПО ВыпущенныеКиЗ.ВидКиЗ                  = Таблица.ВидКиЗ
	|			И ВыпущенныеКиЗ.СпособВыпускаВОборот = Таблица.СпособВыпускаВОборот
	|			И ВыпущенныеКиЗ.РазмерКиЗ            = Таблица.РазмерКиЗ
	|			И ВыпущенныеКиЗ.GTIN                 = Таблица.GTIN
	|ГДЕ
	|	ВыпущенныеКиЗ.Ссылка В(&ДокументыКПодтверждению)
	|	И НЕ ВыпущенныеКиЗ.НомерКиЗ В
	|				(ВЫБРАТЬ
	|					ДублирующиесяНомераКиЗ.НомерКиЗ
	|				ИЗ
	|					ДублирующиесяНомераКиЗ КАК ДублирующиесяНомераКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////4
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НомераКиЗНоменклатура.НомерКиЗ,
	|	НомераКиЗНоменклатура.Документ,
	|	НомераКиЗНоменклатура.Номенклатура,
	|	НомераКиЗНоменклатура.Характеристика
	|ПОМЕСТИТЬ НомераКиЗПроблемыСопоставления
	|ИЗ
	|	НомераКиЗНоменклатура КАК НомераКиЗНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВыпускКиЗГИСМ.ЗаказанныеКиЗ КАК ТаблицаЗаказанныеКиЗ
	|		ПО НомераКиЗНоменклатура.Документ = ТаблицаЗаказанныеКиЗ.Ссылка
	|			И НомераКиЗНоменклатура.Номенклатура = ТаблицаЗаказанныеКиЗ.Номенклатура
	|			И НомераКиЗНоменклатура.Характеристика = ТаблицаЗаказанныеКиЗ.Характеристика
	|ГДЕ
	|	ТаблицаЗаказанныеКиЗ.Номенклатура ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////5
	|ВЫБРАТЬ
	|	НомераКиЗ.Ссылка                 КАК Документ,
	|	НомераКиЗ.НомерКиЗ               КАК НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления    КАК ДокументПоступления,
	|	НомераКиЗ.СостояниеПодтверждения КАК СостояниеПодтверждения,
	|	Документы.Организация КАК Организация,
	|	Документы.Контрагент  КАК Контрагент
	|ПОМЕСТИТЬ НеподтвержденныеКиЗ
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК НомераКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВыпускКиЗГИСМ КАК Документы
	|		ПО НомераКиЗ.Ссылка = Документы.Ссылка
	|ГДЕ
	|	НомераКиЗ.Ссылка В(&ДокументыКПодтверждению)
	|	И НомераКиЗ.СостояниеПодтверждения В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОжидаетсяПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ВыбратьПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Подтвердить),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.КПередаче))
	|	И НЕ НомераКиЗ.НомерКиЗ В
	|				(ВЫБРАТЬ
	|					ДублирующиесяНомераКиЗ.НомерКиЗ
	|				ИЗ
	|					ДублирующиесяНомераКиЗ КАК ДублирующиесяНомераКиЗ)
	|	И НЕ (НомераКиЗ.НомерКиЗ, НомераКиЗ.Ссылка) В
	|				(ВЫБРАТЬ
	|					НомераКиЗПроблемыСопоставления.НомерКиЗ,
	|					НомераКиЗПроблемыСопоставления.Документ
	|				ИЗ
	|					НомераКиЗПроблемыСопоставления КАК НомераКиЗПроблемыСопоставления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////6
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор    КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ      КАК НомерКиЗ,
	|	ПоступлениеТоваров.Контрагент  КАК Контрагент,
	|	ПоступлениеТоваров.Организация КАК Организация
	|ПОМЕСТИТЬ ВсеДокументыПоступленияПоКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ПО ДвиженияСерийТоваров.Регистратор = ПоступлениеТоваров.Ссылка
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////7
	|ВЫБРАТЬ
	|	НомераКиЗ.НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления,
	|	НомераКиЗ.Ссылка,
	|	Документы.Организация,
	|	Документы.Контрагент
	|ПОМЕСТИТЬ РанееСопоставленныеПоступления
	|ИЗ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ КАК НомераКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВыпускКиЗГИСМ КАК Документы
	|		ПО НомераКиЗ.Ссылка = Документы.Ссылка
	|ГДЕ
	|	НомераКиЗ.ДокументПоступления <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	|	И НомераКиЗ.НомерКиЗ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////8
	|ВЫБРАТЬ
	|	НеподтвержденныеКиЗ.Документ               КАК Документ,
	|	НеподтвержденныеКиЗ.НомерКиЗ               КАК НомерКиЗ,
	|	НеподтвержденныеКиЗ.ДокументПоступления    КАК ДокументПоступления,
	|	НеподтвержденныеКиЗ.СостояниеПодтверждения КАК СостояниеПодтверждения,
	|	ЕСТЬNULL(ВсеДокументыПоступленияПоКиЗ.ДокументПоступления, ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка))   КАК ДокументПоступленияКандидат,
	|	ЕСТЬNULL(РанееСопоставленныеПоступления.ДокументПоступления, ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)) КАК ДокументПоступленияУжеСопоставлено
	|ИЗ
	|	НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РанееСопоставленныеПоступления КАК РанееСопоставленныеПоступления
	|		ПО НеподтвержденныеКиЗ.НомерКиЗ = РанееСопоставленныеПоступления.НомерКиЗ
	|			И НеподтвержденныеКиЗ.Организация = РанееСопоставленныеПоступления.Организация
	|			И НеподтвержденныеКиЗ.НомерКиЗ    = РанееСопоставленныеПоступления.НомерКиЗ
	|			И НеподтвержденныеКиЗ.Документ <> РанееСопоставленныеПоступления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеДокументыПоступленияПоКиЗ КАК ВсеДокументыПоступленияПоКиЗ
	|		ПО НеподтвержденныеКиЗ.НомерКиЗ = ВсеДокументыПоступленияПоКиЗ.НомерКиЗ
	|			И НеподтвержденныеКиЗ.Контрагент  = ВсеДокументыПоступленияПоКиЗ.Контрагент
	|			И НеподтвержденныеКиЗ.Организация = ВсеДокументыПоступленияПоКиЗ.Организация
	|ИТОГИ ПО
	|	Документ,
	|	НомерКиЗ,
	|	ДокументПоступления,
	|	СостояниеПодтверждения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////9
	|ВЫБРАТЬ
	|	ДублирующиесяНомераКиЗ.НомерКиЗ
	|ИЗ
	|	ДублирующиесяНомераКиЗ КАК ДублирующиесяНомераКиЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////10
	|ВЫБРАТЬ
	|	НомераКиЗПроблемыСопоставления.Документ,
	|	НомераКиЗПроблемыСопоставления.НомерКиЗ
	|ИЗ
	|	НомераКиЗПроблемыСопоставления КАК НомераКиЗПроблемыСопоставления";
	
	Если ДокументыКПодтверждению.Количество() > 0 Тогда
		
		Запрос.УстановитьПараметр("ДокументыКПодтверждению", ДокументыКПодтверждению);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДокументыКПодтверждению", Обработки.ПодтверждениеПоступившихКиЗГИСМ.ТекстУсловияПоЗаявкамНаВыпускКиЗ());
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("НомераКиЗКПодтверждению", РезультатЗапроса[8]);
	ВозвращаемоеЗначение.Вставить("ПроблемыДублиКиЗ",        РезультатЗапроса[9]);
	ВозвращаемоеЗначение.Вставить("ПроблемыСопоставления",   РезультатЗапроса[10]);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ДанныеПоУведомлениямОПоступлении
//
Функция ДанныеПоУведомлениямОПоступлении(ДокументыКПодтверждению) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НомераКиЗ.НомерКиЗ,
	|	СУММА(1) КАК Количество
	|ПОМЕСТИТЬ ДублирующиесяНомераКиЗ
	|ИЗ
	|	Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ КАК НомераКиЗ
	|ГДЕ
	|	НомераКиЗ.Ссылка В(&ДокументыКПодтверждению)
	|
	|СГРУППИРОВАТЬ ПО
	|	НомераКиЗ.НомерКиЗ
	|
	|ИМЕЮЩИЕ
	|	СУММА(1) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////1
	|ВЫБРАТЬ
	|	ТаблицаНомераКиЗ.Ссылка                 КАК Документ,
	|	ТаблицаНомераКиЗ.НомерКиЗ               КАК НомерКиЗ,
	|	ТаблицаНомераКиЗ.ДокументПоступления    КАК ДокументПоступления,
	|	ТаблицаНомераКиЗ.СостояниеПодтверждения КАК СостояниеПодтверждения,
	|	Документы.Организация  КАК Организация,
	|	Документы.Контрагент   КАК Контрагент
	|ПОМЕСТИТЬ НеподтвержденныеКиЗ
	|ИЗ
	|	Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ КАК ТаблицаНомераКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ КАК Документы
	|		ПО ТаблицаНомераКиЗ.Ссылка = Документы.Ссылка
	|ГДЕ
	|	ТаблицаНомераКиЗ.Ссылка В(&ДокументыКПодтверждению)
	|	И ТаблицаНомераКиЗ.СостояниеПодтверждения В (
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ОжидаетсяПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.ВыбратьПоступление),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.Подтвердить),
	|		ЗНАЧЕНИЕ(Перечисление.СостоянияОтправкиПодтвержденияГИСМ.КПередаче))
	|	И НЕ ТаблицаНомераКиЗ.НомерКиЗ В
	|				(ВЫБРАТЬ
	|					ДублирующиесяНомераКиЗ.НомерКиЗ
	|				ИЗ
	|					ДублирующиесяНомераКиЗ КАК ДублирующиесяНомераКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////2
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор    КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ      КАК НомерКиЗ,
	|	ПоступлениеТоваров.Контрагент  КАК Контрагент,
	|	ПоступлениеТоваров.Организация КАК Организация
	|ПОМЕСТИТЬ ВсеДокументыПоступленияПоКиЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|		ПО ДвиженияСерийТоваров.Регистратор = ПоступлениеТоваров.Ссылка
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор    КАК ДокументПоступления,
	|	СерииНоменклатуры.НомерКиЗГИСМ      КАК НомерКиЗ,
	|	ВозвратТоваровОтПокупателя.Контрагент  КАК Контрагент,
	|	ВозвратТоваровОтПокупателя.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО ДвиженияСерийТоваров.Серия = СерииНоменклатуры.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|		ПО ДвиженияСерийТоваров.Регистратор = ВозвратТоваровОтПокупателя.Ссылка
	|ГДЕ
	|	СерииНоменклатуры.НомерКиЗГИСМ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////3
	|ВЫБРАТЬ
	|	ТаблицаНомераКиЗ.НомерКиЗ,
	|	ТаблицаНомераКиЗ.ДокументПоступления,
	|	ТаблицаНомераКиЗ.Ссылка,
	|	Документы.Организация,
	|	Документы.Контрагент
	|ПОМЕСТИТЬ РанееСопоставленныеПоступления
	|ИЗ
	|	Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ КАК ТаблицаНомераКиЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ КАК Документы
	|		ПО ТаблицаНомераКиЗ.Ссылка = Документы.Ссылка
	|ГДЕ
	|	ТаблицаНомераКиЗ.ДокументПоступления <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)
	|	И ТаблицаНомераКиЗ.НомерКиЗ В
	|			(ВЫБРАТЬ
	|				НеподтвержденныеКиЗ.НомерКиЗ
	|			ИЗ
	|				НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НеподтвержденныеКиЗ.Документ               КАК Документ,
	|	НеподтвержденныеКиЗ.НомерКиЗ               КАК НомерКиЗ,
	|	НеподтвержденныеКиЗ.ДокументПоступления    КАК ДокументПоступления,
	|	НеподтвержденныеКиЗ.СостояниеПодтверждения КАК СостояниеПодтверждения,
	|	ЕСТЬNULL(ВсеДокументыПоступленияПоКиЗ.ДокументПоступления, ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка))   КАК ДокументПоступленияКандидат,
	|	ЕСТЬNULL(РанееСопоставленныеПоступления.ДокументПоступления, ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка)) КАК ДокументПоступленияУжеСопоставлено
	|ИЗ
	|	НеподтвержденныеКиЗ КАК НеподтвержденныеКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РанееСопоставленныеПоступления КАК РанееСопоставленныеПоступления
	|		ПО НеподтвержденныеКиЗ.НомерКиЗ = РанееСопоставленныеПоступления.НомерКиЗ
	|			И НеподтвержденныеКиЗ.Организация = РанееСопоставленныеПоступления.Организация
	|			И НеподтвержденныеКиЗ.НомерКиЗ    = РанееСопоставленныеПоступления.НомерКиЗ
	|			И НеподтвержденныеКиЗ.Документ <> РанееСопоставленныеПоступления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеДокументыПоступленияПоКиЗ КАК ВсеДокументыПоступленияПоКиЗ
	|		ПО НеподтвержденныеКиЗ.НомерКиЗ = ВсеДокументыПоступленияПоКиЗ.НомерКиЗ
	|			И НеподтвержденныеКиЗ.Контрагент = ВсеДокументыПоступленияПоКиЗ.Контрагент
	|			И НеподтвержденныеКиЗ.Организация = ВсеДокументыПоступленияПоКиЗ.Организация
	|ИТОГИ ПО
	|	Документ,
	|	НомерКиЗ,
	|	ДокументПоступления,
	|	СостояниеПодтверждения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////5
	|ВЫБРАТЬ
	|	ДублирующиесяНомераКиЗ.НомерКиЗ
	|ИЗ
	|	ДублирующиесяНомераКиЗ КАК ДублирующиесяНомераКиЗ";
	
	Если ДокументыКПодтверждению.Количество() > 0 Тогда
		
		Запрос.УстановитьПараметр("ДокументыКПодтверждению", ДокументыКПодтверждению);
		
	Иначе
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДокументыКПодтверждению", Обработки.ПодтверждениеПоступившихКиЗГИСМ.ТекстУсловияПоУведомлениямОПоступленииМаркированныхТоваровГИСМ());
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("НомераКиЗКПодтверждению", РезультатЗапроса[4]);
	ВозвращаемоеЗначение.Вставить("ПроблемыДублиКиЗ",        РезультатЗапроса[5]);
	ВозвращаемоеЗначение.Вставить("ПроблемыСопоставления",   Неопределено);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ДокументОснованиеВозвратПоставщику
//
Функция ДокументОснованиеВозвратПоставщику(ДокументСсылка) Экспорт
	
	Возврат ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику");
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.РеквизитыКонтрагента
//
Функция РеквизитыКонтрагента(Контрагент) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("Страна", "");
	ВозвращаемоеЗначение.Вставить("РегистрационныйНомер", "");
	ВозвращаемоеЗначение.Вставить("Наименование", "");
	ВозвращаемоеЗначение.Вставить("НаименованиеПолное", "");
	ВозвращаемоеЗначение.Вставить("ИНН", "");
	ВозвращаемоеЗначение.Вставить("КПП", "");
	ВозвращаемоеЗначение.Вставить("ЭтоФизическоеЛицо", Ложь);
	ВозвращаемоеЗначение.Вставить("ЮридическийАдрес", "");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Контрагенты.СтранаРегистрации.КодАльфа2, """") КАК Страна,
	|	Контрагенты.РегистрационныйНомер КАК РегистрационныйНомер,
	|	Контрагенты.Наименование КАК Наименование,
	|	Контрагенты.НаименованиеПолное КАК НаименованиеПолное,
	|	Контрагенты.ИНН,
	|	Контрагенты.КПП,
	|	ВЫБОР КОГДА Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ЭтоФизическоеЛицо
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка = &Контрагент");
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение, Выборка);
	КонецЕсли;
	
	ЮридическийАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(
		Контрагент,
		Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента,
		ТекущаяДатаСеанса());
	ВозвращаемоеЗначение.ЮридическийАдрес = ЮридическийАдрес;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область РаботаСПрикладнымиДокументами

// См. функцию ИнтеграцияГИСМПереопределяемый.ЕстьМаркируемаяПродукцияГИСМ
//
Функция ЕстьМаркируемаяПродукцияГИСМ(Товары) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	&Товары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаТовары.Номенклатура
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.ПродукцияМаркируемаяДляГИСМ";
	
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ЕстьКиЗГИСМ
//
Функция ЕстьКиЗГИСМ(Товары) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	&Товары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаТовары.Номенклатура
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.КиЗГИСМ";
	
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

// См. процедуру ИнтеграцияГИСМПереопределяемый.РассчитатьСтатусИнформированияГИСМДляДокумента
//
Процедура СформироватьТекстУведомлениеОбОтгрузкеМаркированнойПродукции(Форма) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ТекстУведомленияОбОтгрузкеГИСМ", "Видимость", Форма.Объект.ЕстьМаркируемаяПродукцияГИСМ);
	Если Не Форма.Объект.ЕстьМаркируемаяПродукцияГИСМ Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеУведомления = Метаданные.Документы.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ;
	ПравоДобавления       = ПравоДоступа("Добавление", МетаданныеУведомления);
	ПравоЧтения           = ПравоДоступа("Чтение", МетаданныеУведомления);

	Если Не ПравоЧтения Тогда
		Возврат;
	КонецЕсли;
	
	Если ПравоДобавления Тогда
		ТекстНадписи =  НСтр("ru = 'Создать уведомление об отгрузке ГИСМ'");
		ИмяКоманды   = "СоздатьУведомлениеГИСМ";
	Иначе
		ТекстНадписи = НСтр("ru = 'Уведомление об отгрузке ГИСМ не создано'");
		ИмяКоманды   = "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ТекущееУведомлениеОбОтгрузке,
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Статус
	|ИЗ
	|	РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ КАК УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ
	|		ПО (СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ТекущееУведомлениеОбОтгрузке = УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.Ссылка)
	|ГДЕ
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ = &Документ
	|	И СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ТекущееУведомлениеОбОтгрузке <> ЗНАЧЕНИЕ(Документ.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Документ", Форма.Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ТекстНадписи = СтрШаблон(НСтр("ru = 'Уведомление об отгрузке ГИСМ: %1'"), Выборка.Статус);
		ИмяКоманды   = "ОткрытьПротоколОбмена";
		
	КонецЕсли;
	
	Форма.ТекстУведомленияОбОтгрузкеГИСМ = Новый ФорматированнаяСтрока(
	                                  ТекстНадписи,,
	                                  ЦветаСтиля.ЦветГиперссылкиГИСМ,,
	                                  ИмяКоманды);

КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.СформироватьТекстУведомлениеОСписанииКиЗ
//
Процедура СформироватьТекстУведомлениеОСписанииКиЗ(Форма) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ТекстУведомленияОСписанииГИСМ",
		"Видимость",
		Форма.Объект.ЕстьКиЗГИСМ ИЛИ Форма.Объект.ЕстьМаркируемаяПродукцияГИСМ);
		
	Если Не Форма.Объект.ЕстьКиЗГИСМ И Не Форма.Объект.ЕстьМаркируемаяПродукцияГИСМ Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеУведомления = Метаданные.Документы.УведомлениеОСписанииКиЗГИСМ;
	ПравоДобавления      = ПравоДоступа("Добавление", МетаданныеУведомления);
	ПравоЧтения          = ПравоДоступа("Чтение",     МетаданныеУведомления);

	Если Не ПравоЧтения Тогда
		Возврат;
	КонецЕсли;
	
	Если ПравоДобавления Тогда
		ТекстНадписи =  НСтр("ru = 'Создать уведомление о списании КиЗ ГИСМ'");
		ИмяКоманды   = "СоздатьУведомлениеГИСМ";
	Иначе
		ТекстНадписи = НСтр("ru = 'Уведомление о списании КиЗ ГИСМ не создано'");
		ИмяКоманды   = "";
	КонецЕсли;
	
	ТекстНадписи =  НСтр("ru = 'Создать уведомление о списании КиЗ ГИСМ'");
	ИмяКоманды   = "СоздатьУведомлениеГИСМ";
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СтатусыИнформированияГИСМ.ТекущееУведомление,
	|	СтатусыИнформированияГИСМ.ДальнейшееДействие,
	|	СтатусыИнформированияГИСМ.Статус
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ = &ДокументОснование
	|	И СтатусыИнформированияГИСМ.ТекущееУведомление <> НЕОПРЕДЕЛЕНО";
	
	Запрос.УстановитьПараметр("ДокументОснование", Форма.Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ТекстНадписи  = СтрШаблон(НСтр("ru = 'Уведомление о списании КиЗ ГИСМ: %1'"), Выборка.Статус);
		ИмяКоманды    = "ОткрытьПротоколОбмена";
		
	КонецЕсли;
	
	
	Форма.ТекстУведомленияОСписанииГИСМ = Новый ФорматированнаяСтрока(
	                                        ТекстНадписи,,
	                                        ЦветаСтиля.ЦветГиперссылкиГИСМ,,
	                                        ИмяКоманды)
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.СформироватьТекстУведомлениеОбИмпортеВвозеИзЕАЭС
//
Процедура СформироватьТекстУведомлениеОбИмпортеВвозеИзЕАЭС(Форма) Экспорт
	
	ВидимостьУведомления = Истина;
	Если Форма.Объект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту И 
		Форма.Объект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС Тогда
		ВидимостьУведомления = Ложь;
	ИначеЕсли НЕ Форма.Объект.ЕстьМаркируемаяПродукцияГИСМ Тогда
		ВидимостьУведомления = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ТекстУведомленияГИСМ", "Видимость", ВидимостьУведомления);
	Если НЕ ВидимостьУведомления Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеУведомлениеОбИпморте      = Метаданные.Документы.УведомлениеОбИмпортеМаркированныхТоваровГИСМ;
	ПравоДобавленияУведомлениеОбИпморте = ПравоДоступа("Добавление", МетаданныеУведомлениеОбИпморте);
	ПравоЧтенияУведомлениеОбИпморте     = ПравоДоступа("Чтение", МетаданныеУведомлениеОбИпморте);
	
	МетаданныеУведомлениеОВвозеЕАЭС  = Метаданные.Документы.УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ;
	ПравоДобавленияУведомлениеОВвозе = ПравоДоступа("Добавление", МетаданныеУведомлениеОВвозеЕАЭС);
	ПравоЧтенияУведомлениеОВвозе     = ПравоДоступа("Чтение", МетаданныеУведомлениеОВвозеЕАЭС);
	
	Если Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
		Если Не ПравоЧтенияУведомлениеОбИпморте Тогда
			Возврат;
		КонецЕсли;
		Если ПравоДобавленияУведомлениеОбИпморте Тогда
			ТекстНадписи = НСтр("ru = 'Создать уведомление об импорте ГИСМ'");
			ИмяКоманды   = "СоздатьУведомлениеГИСМ";
		Иначе
			ТекстНадписи = НСтр("ru = 'Уведомление об импорте ГИСМ не созадано'");
			ИмяКоманды   = "";
		КонецЕсли;
	ИначеЕсли Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС Тогда
		Если Не ПравоЧтенияУведомлениеОВвозе Тогда
			Возврат;
		КонецЕсли;
		Если ПравоДобавленияУведомлениеОВвозе Тогда
			ТекстНадписи = НСтр("ru = 'Создать уведомление о ввозе из ЕАЭС ГИСМ'");
			ИмяКоманды   = "СоздатьУведомлениеГИСМ";
		Иначе
			ТекстНадписи = НСтр("ru = 'Уведомление о ввозе из ЕАЭС ГИСМ не созадано'");
			ИмяКоманды   = "";
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СтатусыИнформированияГИСМ.ТекущееУведомление,
	|	СтатусыИнформированияГИСМ.ДальнейшееДействие,
	|	СтатусыИнформированияГИСМ.Статус
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ = &ДокументОснование
	|	И СтатусыИнформированияГИСМ.ТекущееУведомление <> НЕОПРЕДЕЛЕНО";
	
	Запрос.УстановитьПараметр("ДокументОснование", Форма.Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда
			ТекстНадписи = СтрШаблон(НСтр("ru = 'Уведомление об импорте ГИСМ: %1'"), Выборка.Статус);
		ИначеЕсли Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС Тогда
			ТекстНадписи = СтрШаблон(НСтр("ru = 'Уведомление о ввозе из ЕАЭС ГИСМ: %1'"), Выборка.Статус);
		КонецЕсли;
		
	
		ИмяКоманды    = "ОткрытьПротоколОбмена";
		
	КонецЕсли;
	
	Форма.ТекстУведомленияГИСМ = Новый ФорматированнаяСтрока(
		ТекстНадписи,,
		ЦветаСтиля.ЦветГиперссылкиГИСМ,,
		ИмяКоманды);
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.СформироватьТекстЗаявкаНаВыпускКиЗ
//
Процедура СформироватьТекстЗаявкаНаВыпускКиЗ(Форма) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "ТекстЗаявкаНаВыпускКиЗ", "Видимость", Форма.Объект.ЕстьКиЗГИСМ);
	Если НЕ Форма.Объект.ЕстьКиЗГИСМ Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеЗаявки = Метаданные.Документы.ЗаявкаНаВыпускКиЗГИСМ;
	ПравоДобавления = ПравоДоступа("Добавление", МетаданныеЗаявки);
	ПравоЧтения     = ПравоДоступа("Чтение", МетаданныеЗаявки);

	Если Не ПравоЧтения Тогда
		Возврат;
	КонецЕсли;
	
	Если ПравоДобавления Тогда
		ТекстНадписи =  НСтр("ru = 'Создать заявку на выпуск КиЗ'");
		ИмяКоманды   = "СоздатьЗаявкуНаВыпускКиЗ";
	Иначе
		ТекстНадписи =  НСтр("ru = 'Заявка на выпуск КиЗ не создана'");
		ИмяКоманды   = "";
	КонецЕсли;
	
		
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЗаявкаНаВыпускКиЗГИСМ.Ссылка,
	|	ВЫБОР
	|		КОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОбрабатываетсяЭмитентом)
	|			ТОГДА ВЫБОР
	|					КОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусОбработкиЭмитентом = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиЭмитентомКиЗГИСМ.ПустаяСсылка)
	|						ТОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ
	|					ИНАЧЕ СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусОбработкиЭмитентом
	|				КОНЕЦ
	|		ИНАЧЕ СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ
	|	КОНЕЦ КАК СостояниеЗаявкиНаВыпускКиЗ
	|ИЗ
	|	РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК СтатусыЗаявокНаВыпускКиЗГИСМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаВыпускКиЗГИСМ КАК ЗаявкаНаВыпускКиЗГИСМ
	|		ПО (СтатусыЗаявокНаВыпускКиЗГИСМ.ТекущаяЗаявкаНаВыпускКиЗ = ЗаявкаНаВыпускКиЗГИСМ.Ссылка)
	|ГДЕ
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.Документ = &ЗаказПоставщику
	|	И СтатусыЗаявокНаВыпускКиЗГИСМ.ТекущаяЗаявкаНаВыпускКиЗ <> ЗНАЧЕНИЕ(Документ.ЗаявкаНаВыпускКиЗГИСМ.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ЗаказПоставщику", Форма.Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ТекстНадписи = СтрШаблон(НСтр("ru = 'Заявка на выпуск КиЗ: %1'"), Выборка.СостояниеЗаявкиНаВыпускКиЗ);
		ИмяКоманды   = "ОткрытьПротоколОбмена";
		
	КонецЕсли;
	
	Форма.ТекстЗаявкаНаВыпускКиЗ = Новый ФорматированнаяСтрока(
	                                     ТекстНадписи, ,
	                                     ЦветаСтиля.ЦветГиперссылкиГИСМ, ,
	                                     ИмяКоманды);

КонецПроцедуры

// См. функцию ИнтеграцияГИСМПереопределяемый.ЗаполнитьПризнакиЕстьМаркируемаяПродукцияИЕстьКиЗ
//
Функция ЗаполнитьПризнакиЕстьМаркируемаяПродукцияИЕстьКиЗ(Объект, ИмяТабличнойЧасти) Экспорт
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ЕстьМаркируемаяПродукцияГИСМ", Ложь);
	СтруктураВозврата.Вставить("ЕстьКиЗГИСМ", Ложь);
	
	Если Не ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ") Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	Справочник.Номенклатура КАК ТаблицаНоменклатура
	|ГДЕ
	|	ТаблицаНоменклатура.ПродукцияМаркируемаяДляГИСМ
	|	И ТаблицаНоменклатура.Ссылка В(&МассивНоменклатуры)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	Справочник.Номенклатура КАК ТаблицаНоменклатура
	|ГДЕ
	|	ТаблицаНоменклатура.КиЗГИСМ
	|	И ТаблицаНоменклатура.Ссылка В(&МассивНоменклатуры)");
	
	Запрос.УстановитьПараметр("МассивНоменклатуры", Объект[ИмяТабличнойЧасти].ВыгрузитьКолонку("Номенклатура"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	СтруктураВозврата.ЕстьМаркируемаяПродукцияГИСМ = НЕ Результат[0].Пустой();
	СтруктураВозврата.ЕстьКиЗГИСМ                  = НЕ Результат[1].Пустой();
	
	Возврат СтруктураВозврата;
	
КонецФункции

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьПредставлениеТоваровУведомленияОПоступлении
//
Процедура ЗаполнитьПредставлениеТоваровУведомленияОПоступлении(НомераКиЗ) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НомераКиЗ.НомерСтроки         КАК НомерСтроки,
	|	НомераКиЗ.НомерКиЗ            КАК НомерКиЗ,
	|	НомераКиЗ.ДокументПоступления КАК ДокументПоступления
	|ПОМЕСТИТЬ
	|	втНомераКиЗ
	|ИЗ
	|	&НомераКиЗ КАК НомераКиЗ
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерКиЗ,
	|	ДокументПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СерииОснования.Номенклатура       КАК Номенклатура,
	|	СерииОснования.Характеристика     КАК Характеристика,
	|	СерииОснования.Серия              КАК Серия,
	|	СерииОснования.Ссылка             КАК ДокументПоступления,
	|	СерииОснования.Серия.НомерКиЗГИСМ КАК НомерКиЗ
	|ПОМЕСТИТЬ
	|	втСерииОснования
	|ИЗ
	|	Документ.ПоступлениеТоваров.Серии КАК СерииОснования
	|ГДЕ
	|	СерииОснования.Ссылка В (
	|		ВЫБРАТЬ
	|			втНомераКиЗ.ДокументПоступления КАК ДокументПоступления
	|		ИЗ
	|			втНомераКиЗ КАК втНомераКиЗ)
	|	И СерииОснования.Серия.НомерКиЗГИСМ В (
	|		ВЫБРАТЬ
	|			втНомераКиЗ.НомерКиЗ КАК НомерКиЗ
	|		ИЗ
	|			втНомераКиЗ КАК втНомераКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СерииОснования.Номенклатура       КАК Номенклатура,
	|	СерииОснования.Характеристика     КАК Характеристика,
	|	СерииОснования.Серия              КАК Серия,
	|	СерииОснования.Ссылка             КАК ДокументПоступления,
	|	СерииОснования.Серия.НомерКиЗГИСМ КАК НомерКиЗ
	|ИЗ
	|	Документ.ВозвратТоваровОтПокупателя.Серии КАК СерииОснования
	|ГДЕ
	|	СерииОснования.Ссылка В (
	|		ВЫБРАТЬ
	|			втНомераКиЗ.ДокументПоступления КАК ДокументПоступления
	|		ИЗ
	|			втНомераКиЗ КАК втНомераКиЗ)
	|	И СерииОснования.Серия.НомерКиЗГИСМ В (
	|		ВЫБРАТЬ
	|			втНомераКиЗ.НомерКиЗ КАК НомерКиЗ
	|		ИЗ
	|			втНомераКиЗ КАК втНомераКиЗ)
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерКиЗ,
	|	ДокументПоступления
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНомераКиЗ.НомерСтроки         КАК НомерСтроки,
	|	втСерииОснования.Номенклатура   КАК Номенклатура,
	|	втСерииОснования.Характеристика КАК Характеристика,
	|	втСерииОснования.Номенклатура.Представление   КАК НоменклатураПредставление,
	|	втСерииОснования.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	втСерииОснования.Серия          КАК Серия
	|ИЗ
	|	втСерииОснования КАК втСерииОснования
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	втНомераКиЗ КАК втНомераКиЗ
	|ПО
	|	втНомераКиЗ.НомерКиЗ = втСерииОснования.НомерКиЗ
	|	И втНомераКиЗ.ДокументПоступления = втСерииОснования.ДокументПоступления
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки ВОЗР");
	
	Запрос.УстановитьПараметр("НомераКиЗ", НомераКиЗ.Выгрузить(, "НомерСтроки, НомерКиЗ, ДокументПоступления"));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого ТекСтрока Из НомераКиЗ Цикл
		
		Если Выборка.НайтиСледующий(ТекСтрока.НомерСтроки, "НомерСтроки") Тогда
			
			ТекСтрока.Номенклатура   = Выборка.Номенклатура;
			ТекСтрока.Характеристика = Выборка.Характеристика;
			ТекСтрока.Серия          = Выборка.Серия;
			ТекСтрока.НоменклатураПредставление = ОбщегоНазначенияРТ.ПолучитьПредставлениеНоменклатуры(
					Выборка.НоменклатураПредставление,
					Выборка.ХарактеристикаПредставление);
			
		Иначе
			
			ТекСтрока.Номенклатура   = Неопределено;
			ТекСтрока.Характеристика = Неопределено;
			ТекСтрока.Серия                     = Неопределено;
			ТекСтрока.НоменклатураПредставление = Неопределено;
			
		КонецЕсли;
		
		Выборка.Сбросить();
		
	КонецЦикла;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьПредставлениеТоваровУведомленияПоНомерамКиЗОснования
//
Процедура ЗаполнитьПредставлениеТоваровУведомленияПоНомерамКиЗОснования(Основание, НомераКиЗ) Экспорт
	
	Если Не ЗначениеЗаполнено(Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НомераКиЗ.НомерСтроки КАК НомерСтроки,
	|	НомераКиЗ.НомерКиЗ    КАК НомерКиЗ
	|ПОМЕСТИТЬ
	|	втНомераКиЗ
	|ИЗ
	|	&НомераКиЗ КАК НомераКиЗ
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерКиЗ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СерииОснования.Номенклатура       КАК Номенклатура,
	|	СерииОснования.Характеристика     КАК Характеристика,
	|	СерииОснования.Серия              КАК Серия,
	|	СерииОснования.Серия.НомерКиЗГИСМ КАК НомерКиЗ
	|ПОМЕСТИТЬ
	|	втСерииОснования
	|ИЗ
	|	Документ.#ДокументОснование#.Серии КАК СерииОснования
	|ГДЕ
	|	СерииОснования.Ссылка = &Основание
	|	И СерииОснования.Серия.НомерКиЗГИСМ В (
	|		ВЫБРАТЬ
	|			втНомераКиЗ.НомерКиЗ КАК НомерКиЗ
	|		ИЗ
	|			втНомераКиЗ КАК втНомераКиЗ)
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерКиЗ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втНомераКиЗ.НомерСтроки         КАК НомерСтроки,
	|	втСерииОснования.Номенклатура   КАК Номенклатура,
	|	втСерииОснования.Характеристика КАК Характеристика,
	|	втСерииОснования.Номенклатура.Представление   КАК НоменклатураПредставление,
	|	втСерииОснования.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	втСерииОснования.Серия          КАК Серия
	|ИЗ
	|	втСерииОснования КАК втСерииОснования
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	втНомераКиЗ КАК втНомераКиЗ
	|ПО
	|	втНомераКиЗ.НомерКиЗ = втСерииОснования.НомерКиЗ
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки ВОЗР");
	
	Запрос.УстановитьПараметр("НомераКиЗ", НомераКиЗ.Выгрузить(, "НомерСтроки,НомерКиЗ"));
	Запрос.УстановитьПараметр("Основание", Основание);
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.РеализацияТоваров") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ДокументОснование#", "РеализацияТоваров");
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ДокументОснование#", "ВозвратТоваровПоставщику");
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ПоступлениеТоваров") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ДокументОснование#", "ПоступлениеТоваров");
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.СписаниеТоваров") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ДокументОснование#", "СписаниеТоваров");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого ТекСтрока Из НомераКиЗ Цикл
		
		Если Выборка.НайтиСледующий(ТекСтрока.НомерСтроки, "НомерСтроки") Тогда
			
			ТекСтрока.Номенклатура   = Выборка.Номенклатура;
			ТекСтрока.Характеристика = Выборка.Характеристика;
			ТекСтрока.Серия          = Выборка.Серия;
			ТекСтрока.НоменклатураПредставление = ОбщегоНазначенияРТ.ПолучитьПредставлениеНоменклатуры(
					Выборка.НоменклатураПредставление,
					Выборка.ХарактеристикаПредставление);
			
		Иначе
			
			ТекСтрока.Номенклатура   = Неопределено;
			ТекСтрока.Характеристика = Неопределено;
			ТекСтрока.Серия                     = Неопределено;
			ТекСтрока.НоменклатураПредставление = Неопределено;
			
		КонецЕсли;
		
		Выборка.Сбросить();
		
	КонецЦикла;
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьСлужебныеРеквизитыТабличнойЧасти
//
Процедура ЗаполнитьСлужебныеРеквизитыТабличнойЧастиЗаявкиНаВыпускКиЗ(Форма) Экспорт
	
	СтруктураДействий = Новый Структура;
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Номенклатура", "Номенклатура");
	СтруктураПараметров.Вставить("ХарактеристикиИспользуются", "ХарактеристикиИспользуются");
	
	СтруктураДействий.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", СтруктураПараметров);
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	
	СтруктураТЧ = Новый Структура;
	СтруктураТЧ.Вставить("СтрокиТЧ" , Форма.Объект.ЗаказанныеКиЗ);
	ОбработкаТабличнойЧастиТоварыСервер.ПриИзмененииРеквизитовВТЧСервер(СтруктураТЧ, СтруктураДействий, Неопределено);
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ЗаполнитьСлужебныеРеквизитыТабличнойЧастиУведомлениеОбИмпорте
//
Процедура ЗаполнитьСлужебныеРеквизитыТабличнойЧастиУведомлениеОбИмпорте(Товары) Экспорт
	
	МаркировкаТоваровГИСМРТ.ЗаполнитьСлужебныеРеквизитыТабличнойЧасти(Товары);
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.УведомлениеОбИмпортеОбработкаПроверкиЗаполнения
//
Процедура УведомлениеОбИмпортеОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	Перем МассивНепроверяемыхРеквизитов;
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// Проверка характеристик в табличной части Товары и в шапке документа.
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ИмяТЧ", "Товары");
	ПараметрыПроверки.Вставить("СуффиксДопРеквизита", "КиЗ");
	
	ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(Объект, МассивНепроверяемыхРеквизитов, Отказ);
	ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(Объект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ОбработкаПодбораЗаявкиНаВыпускКиЗ
//
Процедура ОбработкаПодбораЗаявкиНаВыпускКиЗ(Форма, ВыбранноеЗначение) Экспорт
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресТоваровВХранилище);
	
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		ТекущаяСтрока = Форма.Объект.ЗаказанныеКиЗ.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара, "Номенклатура, Характеристика, Количество");
		
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Номенклатура", "Номенклатура");
	СтруктураПараметров.Вставить("ХарактеристикиИспользуются", "ХарактеристикиИспользуются");
	
	ПараметрыЗаполненияРеквизитов = Новый Структура;
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются",
	                                                  СтруктураПараметров);
	ПараметрыЗаполненияРеквизитов.Вставить("ЗаполнитьПризнакТипНоменклатуры",
	                                                  Новый Структура("Номенклатура", "ТипНоменклатуры"));
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ДополнитьУсловноеОформлениеЗаявкаНаВыпускКиЗ
//
Процедура ДополнитьУсловноеОформлениеЗаявкаНаВыпускКиЗ(Форма) Экспорт
	
	МаркировкаТоваровГИСМРТ.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(Форма, "ЗаказанныеКиЗХарактеристика", "Объект.ЗаказанныеКиЗ.ХарактеристикиИспользуются");
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ДополнитьУсловноеОформлениеУведомлениеОбИмпорте
//
Процедура ДополнитьУсловноеОформлениеУведомлениеОбИмпорте(Форма) Экспорт
	
	МаркировкаТоваровГИСМРТ.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(Форма);
	МаркировкаТоваровГИСМРТ.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(Форма, "ТоварыХарактеристикаКиЗ", "Объект.Товары.ХарактеристикиКизИспользуются");
	
КонецПроцедуры

// См. процедуру ИнтеграцияГИСМПереопределяемый.ПроверитьЗаполнениеХарактеристик
//
Процедура ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, ИмяТаблицы, Отказ) Экспорт
	
	ПараметрыПроверкиХарактеристик = Новый Структура;
	ПараметрыПроверкиХарактеристик.Вставить("ИмяТЧ", ИмяТаблицы);
	ПараметрыПроверкиХарактеристик.Вставить("СуффиксДопРеквизита", "");
	ОбработкаТабличнойЧастиТоварыСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ, ПараметрыПроверкиХарактеристик);
	
КонецПроцедуры

#КонецОбласти

#Область Панель1СМаркировка

// См. функцию ИнтеграцияГИСМПереопределяемый.ДоступенОтчетОРозничныхПродажах
//
Функция ДоступенОтчетОРозничныхПродажах() Экспорт
	
	Возврат ПравоДоступа("Чтение", Метаданные.Документы.ОтчетОРозничныхПродажах);
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ДоступенВозвратТоваровОтРозничногоКлиента
//
Функция ДоступенВозвратТоваровОтРозничногоКлиента() Экспорт
	
	Возврат ПравоДоступа("Чтение", Метаданные.Документы.ВозвратТоваровОтПокупателя);
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаОтчетыОРозничныхПродажах
//
Функция ТекстЗапросаОтчетыОРозничныхПродажах() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ОтчетОРозничныхПродажах.Ссылка
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие <> ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется)
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|	И (ОтчетОРозничныхПродажах.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (ОтчетОРозничныхПродажах.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаОтчетыОРозничныхПродажахОжидайте
//
Функция ТекстЗапросаОтчетыОРозничныхПродажахОжидайте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ОтчетОРозничныхПродажах.Ссылка
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие В 
	|		(ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПередачуДанныхРегламентнымЗаданием),
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПолучениеКвитанцииОФиксации))
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|	И (ОтчетОРозничныхПродажах.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (ОтчетОРозничныхПродажах.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаОтчетыОРозничныхПродажахОтработайте
//
Функция ТекстЗапросаОтчетыОРозничныхПродажахОтработайте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ОтчетОРозничныхПродажах.Ссылка
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие В 
	|		(ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные),
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ВыполнитеОбмен))
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|	И (ОтчетОРозничныхПродажах.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (ОтчетОРозничныхПродажах.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаВозвратыТоваровОтРозничныхКлиентов
//
Функция ТекстЗапросаВозвратыТоваровОтРозничныхКлиентов() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ВозвратТоваровОтПокупателя.Ссылка
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие <> ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется)
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|	И (ВозвратТоваровОтПокупателя.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (ВозвратТоваровОтПокупателя.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаУведомленияОСписанииКиЗГИСМ
//
Функция ТекстЗапросаУведомленияОСписанииКиЗГИСМ() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.УведомлениеОСписанииКиЗГИСМ КАК УведомлениеОСписанииКиЗГИСМ
	|ПО
	|	СтатусыИнформированияГИСМ.ТекущееУведомление = УведомлениеОСписанииКиЗГИСМ.Ссылка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.СписаниеТоваров КАК СписаниеТоваров
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = СписаниеТоваров.Ссылка
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.СписаниеТоваров
	|	И (СписаниеТоваров.Ссылка ЕСТЬ НЕ NULL
	|	И (СтатусыИнформированияГИСМ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка))
	|	И (СписаниеТоваров.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО))
	|	ИЛИ
	|	(УведомлениеОСписанииКиЗГИСМ.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие <> ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется)
	|	И (УведомлениеОСписанииКиЗГИСМ.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (УведомлениеОСписанииКиЗГИСМ.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)))
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции


// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаУведомленияОСписанииКиЗГИСМОформите
//
Функция ТекстЗапросаУведомленияОСписанииКиЗГИСМОформите() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.СписаниеТоваров КАК СписаниеТоваров
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = СписаниеТоваров.Ссылка
	|ГДЕ
	|	СписаниеТоваров.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка))
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.СписаниеТоваров
	|	И (СписаниеТоваров.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
		
КонецФункции


// См. процедуру ИнтеграцияГИСМПереопределяемый.ТекстЗапросаПоПроблемнымПоступлениям
//
Функция ТекстЗапросаПоПроблемнымПоступлениям(ИмяНабораДанных, ОтборОрганизация, ОтборКонтрагент) Экспорт
	
	Если ИмяНабораДанных = "КиЗ" Тогда
		ИмяТабличнойЧастиДокументаГИСМ = "Документ.ЗаявкаНаВыпускКиЗГИСМ.ВыпущенныеКиЗ";
		ТекстОтбораПоНоменклатуре        = "ДвиженияСерийТоваров.Номенклатура.КиЗГИСМ";
	ИначеЕсли ИмяНабораДанных = "МаркированныеТовары" Тогда
		ИмяТабличнойЧастиДокументаГИСМ = "Документ.УведомлениеОПоступленииМаркированныхТоваровГИСМ.НомераКиЗ";
		ТекстОтбораПоНоменклатуре        = "ДвиженияСерийТоваров.Номенклатура.ПродукцияМаркируемаяДляГИСМ";
	КонецЕсли;
	
	ТекстОтбораДокументыПоступления = "";
	ТекстОтбораДокументыГИСМ        = "";
	Если ЗначениеЗаполнено(ОтборОрганизация) Тогда
		ТекстОтбораДокументыПоступления = ТекстОтбораДокументыПоступления + " И ДвиженияСерийТоваров.Регистратор.Организация = &Организация ";
		ТекстОтбораДокументыГИСМ        = ТекстОтбораДокументыГИСМ + " И ТабличнаяЧастьДокументаГИСМ.Ссылка.Организация = &Организация ";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда
		ТекстОтбораДокументыПоступления = ТекстОтбораДокументыПоступления + " И ДвиженияСерийТоваров.Регистратор.Контрагент = &Контрагент ";
		ТекстОтбораДокументыГИСМ        = ТекстОтбораДокументыГИСМ + " И ТабличнаяЧастьДокументаГИСМ.Ссылка.Контрагент = &Контрагент ";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор.Организация КАК Организация,
	|	ДвиженияСерийТоваров.Регистратор.Контрагент КАК Контрагент,
	|	ДвиженияСерийТоваров.Серия.НомерКиЗГИСМ КАК НомерКИЗ,
	|	СУММА(1) КАК КоличествоВДокументахПоступления
	|ПОМЕСТИТЬ НеСопоставленныеПоступления
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИмяТабличнойЧастиДокументаГИСМ КАК ТабличнаяЧастьДокументаГИСМ
	|		ПО ДвиженияСерийТоваров.Регистратор.Организация = ТабличнаяЧастьДокументаГИСМ.Ссылка.Организация
	|			И ДвиженияСерийТоваров.Регистратор.Контрагент = ТабличнаяЧастьДокументаГИСМ.Ссылка.Контрагент
	|			И ДвиженияСерийТоваров.Серия.НомерКиЗГИСМ = ТабличнаяЧастьДокументаГИСМ.НомерКиЗ
	|			И ДвиженияСерийТоваров.Регистратор = ТабличнаяЧастьДокументаГИСМ.ДокументПоступления
	|			И (НЕ ТабличнаяЧастьДокументаГИСМ.Ссылка.ПометкаУдаления)
	|ГДЕ
	|	(ДвиженияСерийТоваров.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
	|	ИЛИ ДвиженияСерийТоваров.Регистратор ССЫЛКА Документ.ПриходныйОрдерНаТовары)
	|	И ТабличнаяЧастьДокументаГИСМ.Ссылка ЕСТЬ NULL 
	|	И &ТекстОтбораПоНоменклатуре
	|	И &ТекстОтбораДокументыПоступления
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияСерийТоваров.Серия.НомерКиЗГИСМ,
	|	ДвиженияСерийТоваров.Регистратор.Организация,
	|	ДвиженияСерийТоваров.Регистратор.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////1
	|ВЫБРАТЬ
	|	ТабличнаяЧастьДокументаГИСМ.НомерКиЗ           КАК НомерКиЗ,
	|	ТабличнаяЧастьДокументаГИСМ.Ссылка.Организация КАК Организация,
	|	ТабличнаяЧастьДокументаГИСМ.Ссылка.Контрагент  КАК Контрагент,
	|	СУММА(1) КАК КоличествоВДокументахГИСМ
	|ПОМЕСТИТЬ НеСопоставленныеДокументыГИСМ
	|ИЗ
	|	ИмяТабличнойЧастиДокументаГИСМ КАК ТабличнаяЧастьДокументаГИСМ
	|ГДЕ
	|	НЕ ТабличнаяЧастьДокументаГИСМ.Ссылка.ПометкаУдаления
	|	И ТабличнаяЧастьДокументаГИСМ.ДокументПоступления В (ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ВозвратТоваровОтПокупателя.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|	И &ТекстОтбораДокументыГИСМ
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабличнаяЧастьДокументаГИСМ.НомерКиЗ,
	|	ТабличнаяЧастьДокументаГИСМ.Ссылка.Организация,
	|	ТабличнаяЧастьДокументаГИСМ.Ссылка.Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////2
	|ВЫБРАТЬ
	|	НеСопоставленныеПоступления.НомерКИЗ,
	|	НеСопоставленныеПоступления.Организация,
	|	НеСопоставленныеПоступления.Контрагент,
	|	ЕСТЬNULL(НеСопоставленныеДокументыГИСМ.КоличествоВДокументахГИСМ, 0) КАК КоличествоВДокументахГИСМ,
	|	НеСопоставленныеПоступления.КоличествоВДокументахПоступления
	|ПОМЕСТИТЬ ПроблемныеНомераКиЗ
	|ИЗ
	|	НеСопоставленныеПоступления КАК НеСопоставленныеПоступления
	|		ЛЕВОЕ СОЕДИНЕНИЕ НеСопоставленныеДокументыГИСМ КАК НеСопоставленныеДокументыГИСМ
	|		ПО НеСопоставленныеПоступления.Организация = НеСопоставленныеДокументыГИСМ.Организация
	|			И НеСопоставленныеПоступления.Контрагент = НеСопоставленныеДокументыГИСМ.Контрагент
	|			И НеСопоставленныеПоступления.НомерКИЗ = НеСопоставленныеДокументыГИСМ.НомерКиЗ
	|ГДЕ
	|	НеСопоставленныеПоступления.КоличествоВДокументахПоступления > ЕСТЬNULL(НеСопоставленныеДокументыГИСМ.КоличествоВДокументахГИСМ, 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////3
	|ВЫБРАТЬ
	|	ПроблемныеНомераКиЗ.НомерКИЗ,
	|	ПроблемныеНомераКиЗ.Организация,
	|	ПроблемныеНомераКиЗ.Контрагент,
	|	ПроблемныеНомераКиЗ.КоличествоВДокументахГИСМ,
	|	ПроблемныеНомераКиЗ.КоличествоВДокументахПоступления
	|ИЗ
	|	ПроблемныеНомераКиЗ КАК ПроблемныеНомераКиЗ
	|ИТОГИ
	|  ПО Контрагент,
	|     Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////4
	|ВЫБРАТЬ
	|	ДвиженияСерийТоваров.Регистратор             КАК ДокументПоступления,
	|	ДвиженияСерийТоваров.Регистратор.Организация КАК Организация,
	|	ДвиженияСерийТоваров.Регистратор.Контрагент  КАК Контрагент,
	|	ДвиженияСерийТоваров.Серия.НомерКиЗГИСМ      КАК НомерКИЗ
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров КАК ДвиженияСерийТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИмяТабличнойЧастиДокументаГИСМ КАК ТабличнаяЧастьДокументаГИСМ
	|		ПО ДвиженияСерийТоваров.Регистратор.Организация = ТабличнаяЧастьДокументаГИСМ.Ссылка.Организация
	|			И ДвиженияСерийТоваров.Регистратор.Контрагент = ТабличнаяЧастьДокументаГИСМ.Ссылка.Контрагент
	|			И ДвиженияСерийТоваров.Серия.НомерКиЗГИСМ = ТабличнаяЧастьДокументаГИСМ.НомерКиЗ
	|			И ДвиженияСерийТоваров.Регистратор = ТабличнаяЧастьДокументаГИСМ.ДокументПоступления
	|			И (НЕ ТабличнаяЧастьДокументаГИСМ.Ссылка.ПометкаУдаления)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПроблемныеНомераКиЗ КАК ПроблемныеНомераКиЗ
	|		ПО ДвиженияСерийТоваров.Регистратор.Организация = ПроблемныеНомераКиЗ.Организация
	|			И ДвиженияСерийТоваров.Регистратор.Контрагент = ПроблемныеНомераКиЗ.Контрагент
	|			И ДвиженияСерийТоваров.Серия.НомерКиЗГИСМ = ПроблемныеНомераКиЗ.НомерКИЗ
	|ГДЕ
	|	(ДвиженияСерийТоваров.Регистратор ССЫЛКА Документ.ПоступлениеТоваров
	|	ИЛИ ДвиженияСерийТоваров.Регистратор ССЫЛКА Документ.ПриходныйОрдерНаТовары)
	|	И ТабличнаяЧастьДокументаГИСМ.Ссылка ЕСТЬ NULL
	|	И &ТекстОтбораПоНоменклатуре
	|	И &ТекстОтбораДокументыПоступления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////5
	|ВЫБРАТЬ
	|	ТабличнаяЧастьДокументаГИСМ.Ссылка             КАК ДокументГИСМ,
	|	ТабличнаяЧастьДокументаГИСМ.НомерКиЗ           КАК НомерКиЗ,
	|	ТабличнаяЧастьДокументаГИСМ.Ссылка.Организация КАК Организация,
	|	ТабличнаяЧастьДокументаГИСМ.Ссылка.Контрагент  КАК Контрагент
	|ИЗ
	|	ИмяТабличнойЧастиДокументаГИСМ КАК ТабличнаяЧастьДокументаГИСМ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПроблемныеНомераКиЗ КАК ПроблемныеНомераКиЗ
	|		ПО ТабличнаяЧастьДокументаГИСМ.Ссылка.Организация = ПроблемныеНомераКиЗ.Организация
	|			И ТабличнаяЧастьДокументаГИСМ.Ссылка.Контрагент = ПроблемныеНомераКиЗ.Контрагент
	|			И ТабличнаяЧастьДокументаГИСМ.НомерКиЗ = ПроблемныеНомераКиЗ.НомерКИЗ
	|ГДЕ
	|	НЕ ТабличнаяЧастьДокументаГИСМ.Ссылка.ПометкаУдаления
	|	И ТабличнаяЧастьДокументаГИСМ.ДокументПоступления В (ЗНАЧЕНИЕ(Документ.ПоступлениеТоваров.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ВозвратТоваровОтПокупателя.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|	И &ТекстОтбораДокументыГИСМ";
	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяТабличнойЧастиДокументаГИСМ",     ИмяТабличнойЧастиДокументаГИСМ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстОтбораПоНоменклатуре",         ТекстОтбораПоНоменклатуре);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТекстОтбораДокументыГИСМ",        ТекстОтбораДокументыГИСМ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТекстОтбораДокументыПоступления", ТекстОтбораДокументыПоступления);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаУведомленияОбОтгрузке
//
Функция ТекстЗапросаУведомленияОбОтгрузке() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ КАК УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ
	|ПО
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ТекущееУведомлениеОбОтгрузке = УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.Ссылка
	|%ЛевоеСоединение%
	|ГДЕ
	|	(%УсловиеСсылка%
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ОтклоненоКлиентом),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ПустаяСсылка))
	|	И (%УсловиеОрганизация%
	|		&Организация = НЕОПРЕДЕЛЕНО))
	|	ИЛИ
	|	(УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ДальнейшееДействие <>
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется)
	|	И (УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО))
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	ПравоДоступаРеализацияТоваров = ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваров);
	ПравоДоступаВозвратТоваровПоставщику = ПравоДоступа("Чтение", Метаданные.Документы.ВозвратТоваровПоставщику);
	
	ЛевоеСоединение = "";
	Если ПравоДоступаРеализацияТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.РеализацияТоваров КАК РеализацияТоваров
		|ПО
		|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ = РеализацияТоваров.Ссылка
		|";
	КонецЕсли;
	
	Если ПравоДоступаВозвратТоваровПоставщику Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|ПО
		|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ = ВозвратТоваровПоставщику.Ссылка
		|";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	УсловиеСсылка = "";
	УсловиеОрганизация = "";
	
	Если ПравоДоступаРеализацияТоваров
		И ПравоДоступаВозвратТоваровПоставщику Тогда

		УсловиеСсылка = "(РеализацияТоваров.Ссылка ЕСТЬ НЕ NULL ИЛИ ВозвратТоваровПоставщику.Ссылка ЕСТЬ НЕ NULL) И";
		УсловиеОрганизация = "РеализацияТоваров.Организация = &Организация ИЛИ ВозвратТоваровПоставщику.Организация = &Организация ИЛИ";
		
	ИначеЕсли ПравоДоступаРеализацияТоваров Тогда
		
		УсловиеСсылка = "РеализацияТоваров.Ссылка ЕСТЬ НЕ NULL И";
		УсловиеОрганизация = "РеализацияТоваров.Организация = &Организация ИЛИ";
		
	ИначеЕсли ПравоДоступаВозвратТоваровПоставщику Тогда
		
		УсловиеСсылка = "ВозвратТоваровПоставщику.Ссылка ЕСТЬ НЕ NULL И";
		УсловиеОрганизация = "ВозвратТоваровПоставщику.Организация = &Организация ИЛИ";
		
	КонецЕсли;
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", УсловиеСсылка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", УсловиеОрганизация);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаУведомленияОбОтгрузкеОформите
//
Функция ТекстЗапросаУведомленияОбОтгрузкеОформите() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ
	|%ЛевоеСоединение%
	|ГДЕ
	|	%УсловиеСсылка%
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ОтклоненоКлиентом),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ПустаяСсылка)
	|	)
	|	И (%УсловиеОрганизация% &Организация = НЕОПРЕДЕЛЕНО)

	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	
	ПравоДоступаРеализацияТоваров = ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваров);
	ПравоДоступаВозвратТоваровПоставщику = ПравоДоступа("Чтение", Метаданные.Документы.ВозвратТоваровПоставщику);
	
	ЛевоеСоединение = "";
	Если ПравоДоступаРеализацияТоваров Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.РеализацияТоваров КАК РеализацияТоваров
		|ПО
		|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ = РеализацияТоваров.Ссылка
		|";
	КонецЕсли;
	
	Если ПравоДоступаВозвратТоваровПоставщику Тогда
		ЛевоеСоединение = ЛевоеСоединение + "
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Документ.ВозвратТоваровПоставщику КАК ВозвратТоваровПоставщику
		|ПО
		|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ = ВозвратТоваровПоставщику.Ссылка
		|";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ЛевоеСоединение%", ЛевоеСоединение);
	
	УсловиеСсылка = "";
	УсловиеОрганизация = "";
	
	Если ПравоДоступаРеализацияТоваров
		И ПравоДоступаВозвратТоваровПоставщику Тогда

		УсловиеСсылка = "(РеализацияТоваров.Ссылка ЕСТЬ НЕ NULL ИЛИ ВозвратТоваровПоставщику.Ссылка ЕСТЬ НЕ NULL) И";
		УсловиеОрганизация = "РеализацияТоваров.Организация = &Организация ИЛИ ВозвратТоваровПоставщику.Организация = &Организация ИЛИ";
		
	ИначеЕсли ПравоДоступаРеализацияТоваров Тогда
		
		УсловиеСсылка = "РеализацияТоваров.Ссылка ЕСТЬ НЕ NULL И";
		УсловиеОрганизация = "РеализацияТоваров.Организация = &Организация ИЛИ";
		
	ИначеЕсли ПравоДоступаВозвратТоваровПоставщику Тогда
		
		УсловиеСсылка = "ВозвратТоваровПоставщику.Ссылка ЕСТЬ НЕ NULL И";
		УсловиеОрганизация = "ВозвратТоваровПоставщику.Организация = &Организация ИЛИ";
		
	КонецЕсли;
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеСсылка%", УсловиеСсылка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеОрганизация%", УсловиеОрганизация);
	
	Возврат ТекстЗапроса;
		
КонецФункции

Функция ТекстЗапросаЗаявкиНаВыпускКиЗГИСМ() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыЗаявокНаВыпускКиЗГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК СтатусыЗаявокНаВыпускКиЗГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ЗаявкаНаВыпускКиЗГИСМ КАК ЗаявкаНаВыпускКиЗГИСМ
	|ПО
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.ТекущаяЗаявкаНаВыпускКиЗ = ЗаявкаНаВыпускКиЗГИСМ.Ссылка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ПО
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.Документ = ЗаказПоставщику.Ссылка
	|ГДЕ
	|	(ЗаказПоставщику.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОтклоненаЭмитентом),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОтклоненаГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ПустаяСсылка))
	|	И (ЗаказПоставщику.Организация = &Организация

	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО))
	|	ИЛИ
	|	(ЗаявкаНаВыпускКиЗГИСМ.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыЗаявокНаВыпускКиЗГИСМ.ДальнейшееДействие <> ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется)
	|	И (ЗаявкаНаВыпускКиЗГИСМ.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (ЗаявкаНаВыпускКиЗГИСМ.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО))
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция ТекстЗапросаЗаявкиНаВыпускКиЗГИСМОформите() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыЗаявокНаВыпускКиЗГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК СтатусыЗаявокНаВыпускКиЗГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ПО
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.Документ = ЗаказПоставщику.Ссылка
	|ГДЕ
	|	НЕ СтатусыЗаявокНаВыпускКиЗГИСМ.Документ ССЫЛКА Документ.ЗаявкаНаВыпускКиЗГИСМ
	|	И ЗаказПоставщику.Ссылка ЕСТЬ НЕ NULL
	|	И (СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОтклоненаЭмитентом),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОтклоненаГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ПустаяСсылка)
	|	))
	|	И (ЗаказПоставщику.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаУведомленияОВвозеИзЕАЭС
//
Функция ТекстЗапросаУведомленияОВвозеИзЕАЭС() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ КАК УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ
	|ПО
	|	СтатусыИнформированияГИСМ.ТекущееУведомление = УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ.Ссылка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ПоступлениеТоваров.Ссылка
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка ЕСТЬ НЕ NULL
	|	И ПоступлениеТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ПоступлениеТоваров
	|	И ((СтатусыИнформированияГИСМ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка))
	|	И (ПоступлениеТоваров.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО))
	|	ИЛИ
	|	(УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие <> ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется)
	|	И (УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (УведомлениеОВвозеМаркированныхТоваровИзЕАЭСГИСМ.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)))
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаУведомленияОВвозеИзЕАЭСОформите
//
Функция ТекстЗапросаУведомленияОВвозеИзЕАЭСОформите() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ПоступлениеТоваров.Ссылка
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка))
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ПоступлениеТоваров
	|	И ПоступлениеТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
	|	И (СтатусыИнформированияГИСМ.Документ.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
		
КонецФункции

Функция ТекстЗапросаУведомленияОбИмпорте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.УведомлениеОбИмпортеМаркированныхТоваровГИСМ КАК УведомлениеОбИмпортеМаркированныхТоваровГИСМ
	|ПО
	|	СтатусыИнформированияГИСМ.ТекущееУведомление = УведомлениеОбИмпортеМаркированныхТоваровГИСМ.Ссылка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ПоступлениеТоваров.Ссылка
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ПоступлениеТоваров
	|	И ПоступлениеТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|	И ((СтатусыИнформированияГИСМ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка))
	|	И (ПоступлениеТоваров.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО))
	|	ИЛИ
	|	(УведомлениеОбИмпортеМаркированныхТоваровГИСМ.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие <> ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.НеТребуется)
	|	И (УведомлениеОбИмпортеМаркированныхТоваровГИСМ.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (УведомлениеОбИмпортеМаркированныхТоваровГИСМ.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)))
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаУведомленияОбИмпортеОформите
//
Функция ТекстЗапросаУведомленияОбИмпортеОформите() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ПоступлениеТоваров КАК ПоступлениеТоваров
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ПоступлениеТоваров.Ссылка
	|ГДЕ
	|	ПоступлениеТоваров.Ссылка ЕСТЬ НЕ NULL
	|	И ПоступлениеТоваров.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|	И СтатусыИнформированияГИСМ.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка))
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ПоступлениеТоваров
	|	И (ПоступлениеТоваров.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
		
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаКоличествоВозвратовОтРозничныхКлиентов
//
Функция ТекстЗапросаКоличествоВозвратовОтРозничныхКлиентовОжидающиеОтправки() Экспорт
	
	Возврат "ВЫБРАТЬ
			|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
			|ИЗ
			|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
			|ПО
			|	СтатусыИнформированияГИСМ.ТекущееУведомление = ВозвратТоваровОтПокупателя.Ссылка
			|ГДЕ
			|	ВозвратТоваровОтПокупателя.Ссылка ЕСТЬ НЕ NULL
			|	И (СтатусыИнформированияГИСМ.ДальнейшееДействие В (
			|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ВыполнитеОбмен),
			|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПередачуДанныхРегламентнымЗаданием)
			|	)
			|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
			|	И (ВозвратТоваровОтПокупателя.Организация = &Организация
			|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
			|	И (ВозвратТоваровОтПокупателя.Ответственный = &Ответственный
			|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО))
			|/////////////////////////////////////////////////////////////////////////////
			|ОБЪЕДИНИТЬ ВСЕ ";
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаКоличествоОтчетовОРозничныхПродажах
//
Функция ТекстЗапросаКоличествоОтчетовОРозничныхПродажахОжидающиеОтправки() Экспорт
	
	Возврат "ВЫБРАТЬ
			|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
			|ИЗ
			|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
			|ПО
			|	СтатусыИнформированияГИСМ.ТекущееУведомление = ОтчетОРозничныхПродажах.Ссылка
			|ГДЕ
			|	ОтчетОРозничныхПродажах.Ссылка ЕСТЬ НЕ NULL
			|	И (СтатусыИнформированияГИСМ.ДальнейшееДействие В (
			|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ВыполнитеОбмен),
			|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПередачуДанныхРегламентнымЗаданием)
			|	)
			|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ОтчетОРозничныхПродажах
			|	И (ОтчетОРозничныхПродажах.Организация = &Организация
			|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
			|	И (ОтчетОРозничныхПродажах.Ответственный = &Ответственный
			|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО))
			|/////////////////////////////////////////////////////////////////////////////
			|ОБЪЕДИНИТЬ ВСЕ ";
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаВозвратыТоваровОтРозничныхКлиентовОжидайте
//
Функция ТекстЗапросаВозвратыТоваровОтРозничныхКлиентовОжидайте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ВозвратТоваровОтПокупателя.Ссылка
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие В 
	|		(ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПередачуДанныхРегламентнымЗаданием),
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ОжидайтеПолучениеКвитанцииОФиксации))
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|	И (ВозвратТоваровОтПокупателя.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (ВозвратТоваровОтПокупателя.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаВозвратыТоваровОтРозничныхКлиентовОтработайте
//
Функция ТекстЗапросаВозвратыТоваровОтРозничныхКлиентовОтработайте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО (РАЗЛИЧНЫЕ СтатусыИнформированияГИСМ.Документ) КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
	|ПО
	|	СтатусыИнформированияГИСМ.Документ = ВозвратТоваровОтПокупателя.Ссылка
	|ГДЕ
	|	ВозвратТоваровОтПокупателя.Ссылка ЕСТЬ НЕ NULL
	|	И СтатусыИнформированияГИСМ.ДальнейшееДействие В 
	|		(ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ПередайтеДанные),
	|		ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюГИСМ.ВыполнитеОбмен))
	|	И СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ВозвратТоваровОтПокупателя
	|	И (ВозвратТоваровОтПокупателя.Организация = &Организация
	|		ИЛИ &Организация = НЕОПРЕДЕЛЕНО)
	|	И (ВозвратТоваровОтПокупателя.Ответственный = &Ответственный
	|		ИЛИ &Ответственный = НЕОПРЕДЕЛЕНО)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|";
	Возврат ТекстЗапроса;
	
КонецФункции


// Обновляет в табличной части документа "Заявкам на выпуск КиЗ" табличную часть "Заказанные КиЗ"
//
//  Объект - Объект.
//
Процедура ОбновитьКолонкиВыпущеноПолученоВТЧЗаказанныеКиЗ(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗаказанныхКиЗ.Номенклатура,
	|	ТаблицаЗаказанныхКиЗ.Характеристика,
	|	ТаблицаЗаказанныхКиЗ.Количество
	|ПОМЕСТИТЬ втЗаказанныеКиЗ
	|ИЗ
	|	&ТаблицаЗаказанныхКиЗ КАК ТаблицаЗаказанныхКиЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////1
	|ВЫБРАТЬ
	|	ТаблицаВыпущенныеКиЗ.ВидКиЗ,
	|	ТаблицаВыпущенныеКиЗ.GTIN,
	|	ТаблицаВыпущенныеКиЗ.СпособВыпускаВОборот,
	|	ТаблицаВыпущенныеКиЗ.РазмерКиЗ,
	|	ТаблицаВыпущенныеКиЗ.ДокументПоступления
	|ПОМЕСТИТЬ втВыпущенныеКиЗ
	|ИЗ
	|	&ТаблицаВыпущенныеКиЗ КАК ТаблицаВыпущенныеКиЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////2
	|ВЫБРАТЬ
	|	втЗаказанныеКиЗ.Номенклатура,
	|	втЗаказанныеКиЗ.Характеристика,
	|	СУММА(втЗаказанныеКиЗ.Количество) КАК Заказано
	|ПОМЕСТИТЬ втЗаказанныеКиЗСгруппированые
	|ИЗ
	|	втЗаказанныеКиЗ КАК втЗаказанныеКиЗ
	|
	|СГРУППИРОВАТЬ ПО
	|	втЗаказанныеКиЗ.Номенклатура,
	|	втЗаказанныеКиЗ.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////3
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	Номенклатура.КиЗГИСМВид КАК ВидКиЗ,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер КАК РазмерКиЗ,
	|	Номенклатура.КиЗГИСМGTIN КАК GTIN
	|ПОМЕСТИТЬ НоменклатураИХарактеристикиБД
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	НЕ Номенклатура.ЭтоГруппа
	|	И Номенклатура.ВидНоменклатуры.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.НеИспользовать)
	|	И (Номенклатура.КиЗГИСМВид,
	|	   Номенклатура.КиЗГИСМСпособВыпускаВОборот,
	|	   Номенклатура.КиЗГИСМРазмер,
	|	   Номенклатура.КиЗГИСМ,
	|	   Номенклатура.КиЗГИСМGTIN) В
	|		(ВЫБРАТЬ
	|			втВыпущенныеКиЗ.ВидКиЗ               КАК ВидКиЗ,
	|			втВыпущенныеКиЗ.СпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|			втВыпущенныеКиЗ.РазмерКиЗ            КАК РазмерКиЗ,
	|			ИСТИНА                               КАК КиЗГИСМ,
	|			втВыпущенныеКиЗ.GTIN                 КАК GTIN
	|		ИЗ втВыпущенныеКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	ХарактеристикиНоменклатуры.Ссылка,
	|	Номенклатура.КиЗГИСМВид КАК ВидКиЗ,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер КАК РазмерКиЗ,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.КиЗГИСМGTIN <> """" ТОГДА
	|			ХарактеристикиНоменклатуры.КиЗГИСМGTIN
	|		ИНАЧЕ
	|			Номенклатура.КиЗГИСМGTIN
	|	КОНЕЦ КАК GTIN
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО Номенклатура.Ссылка = ХарактеристикиНоменклатуры.Владелец
	|ГДЕ
	|	Номенклатура.ВидНоменклатуры.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ИндивидуальныеДляНоменклатуры)
	|	И (
	|	Номенклатура.КиЗГИСМВид,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер,
	|	Номенклатура.КиЗГИСМ,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.КиЗГИСМGTIN <> """" ТОГДА
	|			ХарактеристикиНоменклатуры.КиЗГИСМGTIN
	|		ИНАЧЕ
	|			Номенклатура.КиЗГИСМGTIN
	|	КОНЕЦ) В
	|		(ВЫБРАТЬ
	|			втВыпущенныеКиЗ.ВидКиЗ               КАК ВидКиЗ,
	|			втВыпущенныеКиЗ.СпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|			втВыпущенныеКиЗ.РазмерКиЗ            КАК РазмерКиЗ,
	|			ИСТИНА                               КАК КиЗГИСМ,
	|			втВыпущенныеКиЗ.GTIN                 КАК GTIN
	|		ИЗ втВыпущенныеКиЗ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка,
	|	ХарактеристикиНоменклатуры.Ссылка,
	|	Номенклатура.КиЗГИСМВид КАК ВидКиЗ,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер КАК РазмерКиЗ,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.КиЗГИСМGTIN <> """" ТОГДА
	|			ХарактеристикиНоменклатуры.КиЗГИСМGTIN
	|		ИНАЧЕ
	|			Номенклатура.КиЗГИСМGTIN
	|	КОНЕЦ КАК GTIN
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО Номенклатура.ВидНоменклатуры = ХарактеристикиНоменклатуры.Владелец
	|ГДЕ
	|	Номенклатура.ВидНоменклатуры.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыВеденияДополнительныхДанныхПоНоменклатуре.ОбщиеДляВидаНоменклатуры)
	|	И (
	|	Номенклатура.КиЗГИСМВид,
	|	Номенклатура.КиЗГИСМСпособВыпускаВОборот,
	|	Номенклатура.КиЗГИСМРазмер,
	|	Номенклатура.КиЗГИСМ,
	|	ВЫБОР
	|		КОГДА ХарактеристикиНоменклатуры.КиЗГИСМGTIN <> """" ТОГДА
	|			ХарактеристикиНоменклатуры.КиЗГИСМGTIN
	|		ИНАЧЕ
	|			Номенклатура.КиЗГИСМGTIN
	|	КОНЕЦ) В
	|		(ВЫБРАТЬ
	|			втВыпущенныеКиЗ.ВидКиЗ               КАК ВидКиЗ,
	|			втВыпущенныеКиЗ.СпособВыпускаВОборот КАК СпособВыпускаВОборот,
	|			втВыпущенныеКиЗ.РазмерКиЗ            КАК РазмерКиЗ,
	|			ИСТИНА                               КАК КиЗГИСМ,
	|			втВыпущенныеКиЗ.GTIN                 КАК GTIN
	|		ИЗ втВыпущенныеКиЗ)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////4
	|ВЫБРАТЬ
	|	Таблица.Номенклатура   КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	втВыпущенныеКиЗ.GTIN,
	|	втВыпущенныеКиЗ.ВидКиЗ,
	|	втВыпущенныеКиЗ.СпособВыпускаВОборот,
	|	втВыпущенныеКиЗ.РазмерКиЗ,
	|	СУММА(1) КАК Выпущено,
	|	СУММА(ВЫБОР
	|			КОГДА втВыпущенныеКиЗ.ДокументПоступления В (&ТипыДокументаПоступления) 
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Поступило
	|ПОМЕСТИТЬ втВыпущенныеКиЗСгруппированные
	|ИЗ
	|	втВыпущенныеКиЗ КАК втВыпущенныеКиЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураИХарактеристикиБД КАК Таблица
	|		ПО втВыпущенныеКиЗ.ВидКиЗ                  = Таблица.ВидКиЗ
	|			И втВыпущенныеКиЗ.СпособВыпускаВОборот = Таблица.СпособВыпускаВОборот
	|			И втВыпущенныеКиЗ.РазмерКиЗ            = Таблица.РазмерКиЗ
	|			И втВыпущенныеКиЗ.GTIN                 = Таблица.GTIN
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	втВыпущенныеКиЗ.ВидКиЗ,
	|	втВыпущенныеКиЗ.GTIN,
	|	втВыпущенныеКиЗ.СпособВыпускаВОборот,
	|	втВыпущенныеКиЗ.РазмерКиЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////5
	|ВЫБРАТЬ
	|	втЗаказанныеКиЗСгруппированые.Номенклатура,
	|	втЗаказанныеКиЗСгруппированые.Характеристика,
	|	втЗаказанныеКиЗСгруппированые.Заказано,
	|	ЕСТЬNULL(втВыпущенныеКиЗСгруппированные.Выпущено, 0) КАК Выпущено,
	|	ЕСТЬNULL(втВыпущенныеКиЗСгруппированные.Поступило, 0) КАК Поступило
	|ИЗ
	|	втЗаказанныеКиЗСгруппированые КАК втЗаказанныеКиЗСгруппированые
	|		ЛЕВОЕ СОЕДИНЕНИЕ втВыпущенныеКиЗСгруппированные КАК втВыпущенныеКиЗСгруппированные
	|		ПО втЗаказанныеКиЗСгруппированые.Номенклатура = втВыпущенныеКиЗСгруппированные.Номенклатура
	|		 И втЗаказанныеКиЗСгруппированые.Характеристика = втВыпущенныеКиЗСгруппированные.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////6
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втВыпущенныеКиЗСгруппированные.ВидКиЗ,
	|	втВыпущенныеКиЗСгруппированные.GTIN,
	|	втВыпущенныеКиЗСгруппированные.СпособВыпускаВОборот,
	|	втВыпущенныеКиЗСгруппированные.РазмерКиЗ
	|ИЗ
	|	втВыпущенныеКиЗСгруппированные КАК втВыпущенныеКиЗСгруппированные
	|ГДЕ
	|	втВыпущенныеКиЗСгруппированные.Номенклатура ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////7
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втВыпущенныеКиЗСгруппированные.Номенклатура,
	|	втВыпущенныеКиЗСгруппированные.ВидКиЗ,
	|	втВыпущенныеКиЗСгруппированные.СпособВыпускаВОборот,
	|	втВыпущенныеКиЗСгруппированные.РазмерКиЗ,
	|	втВыпущенныеКиЗСгруппированные.GTIN
	|ИЗ
	|	втВыпущенныеКиЗСгруппированные КАК втВыпущенныеКиЗСгруппированные
	|		ЛЕВОЕ СОЕДИНЕНИЕ втЗаказанныеКиЗСгруппированые КАК втЗаказанныеКиЗСгруппированые
	|		ПО втВыпущенныеКиЗСгруппированные.Номенклатура = втЗаказанныеКиЗСгруппированые.Номенклатура
	|ГДЕ
	|	втВыпущенныеКиЗСгруппированные.Номенклатура ЕСТЬ НЕ NULL
	|	И втЗаказанныеКиЗСгруппированые.Номенклатура ЕСТЬ NULL
	|";
	
	Запрос.УстановитьПараметр("ТаблицаВыпущенныеКиЗ", Объект.ВыпущенныеКиЗ.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаЗаказанныхКиЗ", Объект.ЗаказанныеКиЗ.Выгрузить());
	Запрос.УстановитьПараметр("ТипыДокументаПоступления", ИнтеграцияГИСМ.МассивПустыхЗначенийДокументовПоступленияМаркированнойПродукции());
	
	Результат = Запрос.ВыполнитьПакет();
	
	Если Не Результат[5].Пустой() Тогда
		
		Выборка = Результат[5].Выбрать();
		ИнтеграцияГИСМ.РаспределитьВыпущенныеПолученныеКиЗНаЗаказанные(Выборка, Объект);
		
	КонецЕсли;
	
	Если Не Результат[6].Пустой() Тогда
		
		Выборка = Результат[6].Выбрать();
		ИнтеграцияГИСМ.ОтразитьНеНайденнуюНоменклатуруВСтроке(Выборка, Объект);
		
	КонецЕсли;
	
	Если Не Результат[7].Пустой() Тогда
		
		Выборка = Результат[7].Выбрать();
		ИнтеграцияГИСМ.ОтразитьНеЗаказаннуюНоменклатуруВСтроке(Выборка, Объект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗапросыДинамическихСписковРаспоряжений

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаДинамическогоСпискаРаспоряженийУведомлениеОбИмпорте
//
Функция ТекстЗапросаДинамическогоСпискаРаспоряженийУведомлениеОбИмпорте() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтатусыИнформированияГИСМ.Документ КАК Документ,
	|	СтатусыИнформированияГИСМ.ТекущееУведомление КАК ТекущееУведомление,
	|	ВЫБОР
	|		КОГДА СтатусыИнформированияГИСМ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует)
	|		ИНАЧЕ СтатусыИнформированияГИСМ.Статус
	|	КОНЕЦ КАК СтатусГИСМУведомленияКОформлению,
	|	СтатусыИнформированияГИСМ.Документ.Дата КАК Дата,
	|	СтатусыИнформированияГИСМ.Документ.Номер КАК Номер,
	|	СтатусыИнформированияГИСМ.Документ.Организация КАК Организация,
	|	ИСТИНА КАК Картинка
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ПоступлениеТоваров
	|	И СтатусыИнформированияГИСМ.Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпорту)
	|	И СтатусыИнформированияГИСМ.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует), ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ))
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаДинамическогоСпискаРаспоряженийУведомлениеОВвозеИзЕАЭС
//
Функция ТекстЗапросаДинамическогоСпискаРаспоряженийУведомлениеОВвозеИзЕАЭС() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтатусыИнформированияГИСМ.Документ КАК Документ,
	|	СтатусыИнформированияГИСМ.ТекущееУведомление КАК ТекущееУведомление,
	|	ВЫБОР
	|		КОГДА СтатусыИнформированияГИСМ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует)
	|		ИНАЧЕ СтатусыИнформированияГИСМ.Статус
	|	КОНЕЦ КАК СтатусГИСМУведомленияКОформлению,
	|	СтатусыИнформированияГИСМ.Документ.Дата КАК Дата,
	|	СтатусыИнформированияГИСМ.Документ.Номер КАК Номер,
	|	СтатусыИнформированияГИСМ.Документ.Организация КАК Организация,
	|	СтатусыИнформированияГИСМ.Документ.Контрагент КАК Контрагент,
	|	ИСТИНА КАК Картинка
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.ПоступлениеТоваров
	|	И СтатусыИнформированияГИСМ.Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС)
	|	И СтатусыИнформированияГИСМ.Статус В
	|		(ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ))
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаДинамическогоСпискаРаспоряженийУведомлениеОСписании
//
Функция ТекстЗапросаДинамическогоСпискаРаспоряженийУведомлениеОСписании() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтатусыИнформированияГИСМ.Документ КАК Документ,
	|	СтатусыИнформированияГИСМ.ТекущееУведомление КАК ТекущееУведомление,
	|	ВЫБОР
	|		КОГДА СтатусыИнформированияГИСМ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует)
	|		ИНАЧЕ СтатусыИнформированияГИСМ.Статус
	|	КОНЕЦ КАК СтатусГИСМКОформлению,
	|	СтатусыИнформированияГИСМ.Документ.Дата КАК Дата,
	|	СтатусыИнформированияГИСМ.Документ.Номер КАК Номер,
	|	СтатусыИнформированияГИСМ.Документ.Организация КАК Организация,
	|	ИСТИНА КАК Картинка
	|ИЗ
	|	РегистрСведений.СтатусыИнформированияГИСМ КАК СтатусыИнформированияГИСМ
	|ГДЕ
	|	СтатусыИнформированияГИСМ.Документ ССЫЛКА Документ.СписаниеТоваров
	|	И СтатусыИнформированияГИСМ.Статус В
	|		(ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ))
	|	И НЕ СтатусыИнформированияГИСМ.Документ.Ссылка ЕСТЬ NULL 
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаДинамическогоСпискаРаспоряженийУведомлениеОбОтгрузке
//
Функция ТекстЗапросаДинамическогоСпискаРаспоряженийУведомлениеОбОтгрузке() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.Дата КАК ДатаУведомления,
	|	УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.НомерГИСМ КАК НомерГИСМ,
	|	УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.Контрагент КАК Контрагент,
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ КАК Документ,
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ.Организация КАК Организация,
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ТекущееУведомлениеОбОтгрузке КАК ТекущееУведомлениеОбОтгрузке,
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Статус КАК СтатусГИСМУведомленияКОформлению,
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ.Дата КАК Дата,
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ.Номер КАК Номер,
	|	ИСТИНА КАК Картинка
	|ИЗ
	|	РегистрСведений.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ КАК СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ КАК УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ
	|		ПО СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ТекущееУведомлениеОбОтгрузке = УведомлениеОбОтгрузкеМаркированныхТоваровГИСМ.Ссылка
	|ГДЕ
	|	СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Статус В 
	|		(ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ОтклоненоКлиентом),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Отсутствует),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ОтклоненоГИСМ),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.ПустаяСсылка))
	|	И НЕ СтатусыУведомленийОбОтгрузкеМаркированныхТоваровГИСМ.Документ.Ссылка ЕСТЬ NULL 
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ТекстЗапросаДинамическогоСпискаРаспоряженийЗаявкаНаВыпускКиЗ
//
Функция ТекстЗапросаДинамическогоСпискаРаспоряженийЗаявкаНаВыпускКиЗ() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.ТекущаяЗаявкаНаВыпускКиЗ,
	|	ВЫБОР
	|		КОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ПустаяСсылка)
	|			ТОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.Отсутствует)
	|	КОНЕЦ КАК СтатусГИСМКОформлению,
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.Документ.ПометкаУдаления КАК ПометкаУдаления,
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.Документ.Проведен КАК Проведен,
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.Документ.Организация КАК Организация,
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.Документ.Контрагент КАК Контрагент,
	|	СтатусыЗаявокНаВыпускКиЗГИСМ.Документ.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.Документ ССЫЛКА Документ.ЗаявкаНаВыпускКиЗГИСМ
	|			ТОГДА СтатусыЗаявокНаВыпускКиЗГИСМ.Документ.Ответственный
	|		ИНАЧЕ СтатусыЗаявокНаВыпускКиЗГИСМ.Документ.Ответственный
	|	КОНЕЦ КАК Ответственный,
	|	ИСТИНА КАК Картинка
	|ИЗ
	|	РегистрСведений.СтатусыЗаявокНаВыпускКиЗГИСМ КАК СтатусыЗаявокНаВыпускКиЗГИСМ
	|ГДЕ
	|	(СтатусыЗаявокНаВыпускКиЗГИСМ.ТекущаяЗаявкаНаВыпускКиЗ = ЗНАЧЕНИЕ(Документ.ЗаявкаНаВыпускКиЗГИСМ.ПустаяСсылка)
	|			ИЛИ НЕ СтатусыЗаявокНаВыпускКиЗГИСМ.Документ ССЫЛКА Документ.ЗаявкаНаВыпускКиЗГИСМ
	|				И СтатусыЗаявокНаВыпускКиЗГИСМ.СтатусЗаявкиНаВыпускКиЗ В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОтклоненаГИСМ), ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВыпускКиЗГИСМ.ОтклоненаЭмитентом)))
	|	И НЕ СтатусыЗаявокНаВыпускКиЗГИСМ.Документ.Ссылка ЕСТЬ NULL 
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Прочее

// См. функцию ИнтеграцияГИСМПереопределяемый.КонтрагентПоИННКПП
//
Функция КонтрагентПоИННКПП(ИНН, КПП) Экспорт
	
	РезультатПоиска = ОбщегоНазначенияРТ.ИННКППУжеИспользуетсяВИнформационнойБазе(ИНН, КПП);
	Если ЗначениеЗаполнено(РезультатПоиска) Тогда
		Возврат РезультатПоиска.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ИННКПППоКонтрагенту
//
Функция ИННКПППоКонтрагенту(Контрагент) Экспорт
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "ИНН, КПП");
	
КонецФункции

// См. функцию ИнтеграцияГИСМПереопределяемый.ИспользоватьНесколькоОрганизаций
//
Функция ИспользоватьНесколькоОрганизаций() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
КонецФункции

#КонецОбласти

#КонецОбласти