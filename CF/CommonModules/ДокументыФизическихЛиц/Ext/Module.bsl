////////////////////////////////////////////////////////////////////////////////
// ДокументыФизическихЛицКлиентСервер. Содержит вспомогательные процедуры 
//  обработки документов физических лиц.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает структуру документа удостоверяющего личность
Функция СтруктураДокументаУдостоверяющегоЛичность(Физлицо, Дата = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Физлицо",	Физлицо);
	Запрос.УстановитьПараметр("ДатаСреза",	Дата);
	Запрос.УстановитьПараметр("Паспорт",	ПредопределенноеЗначение("Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ"));
	
	РезультатСтруктура = Новый Структура;
	РезультатСтруктура.Вставить("Физлицо");
	РезультатСтруктура.Вставить("ВидДокумента");
	РезультатСтруктура.Вставить("Серия");
	РезультатСтруктура.Вставить("Номер");
	РезультатСтруктура.Вставить("ДатаВыдачи");
	РезультатСтруктура.Вставить("СрокДействия");
	РезультатСтруктура.Вставить("КемВыдан");
	РезультатСтруктура.Вставить("КодПодразделения");
	РезультатСтруктура.Вставить("Представление");
	
	Текст =
	"ВЫБРАТЬ
	|	ДокументыФизическихЛицСрезПоследних.Физлицо,
	|	ДокументыФизическихЛицСрезПоследних.ВидДокумента,
	|	ДокументыФизическихЛицСрезПоследних.Серия,
	|	ДокументыФизическихЛицСрезПоследних.Номер,
	|	ДокументыФизическихЛицСрезПоследних.ДатаВыдачи,
	|	ДокументыФизическихЛицСрезПоследних.СрокДействия,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан,
	|	ДокументыФизическихЛицСрезПоследних.КодПодразделения,
	|	ДокументыФизическихЛицСрезПоследних.Представление,
	|	ВЫБОР
	|		КОГДА ДокументыФизическихЛицСрезПоследних.ВидДокумента = &Паспорт
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПолеСортировки
	|ПОМЕСТИТЬ ТаблицаВЗапросе
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(&ДатаСреза, Физлицо = &Физлицо) КАК ДокументыФизическихЛицСрезПоследних
	|ГДЕ
	|	ДокументыФизическихЛицСрезПоследних.ЯвляетсяДокументомУдостоверяющимЛичность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаВЗапросе.Физлицо,
	|	ТаблицаВЗапросе.ВидДокумента,
	|	ТаблицаВЗапросе.Серия,
	|	ТаблицаВЗапросе.Номер,
	|	ТаблицаВЗапросе.ДатаВыдачи,
	|	ТаблицаВЗапросе.СрокДействия,
	|	ТаблицаВЗапросе.КемВыдан,
	|	ТаблицаВЗапросе.КодПодразделения,
	|	ТаблицаВЗапросе.Представление
	|ИЗ
	|	ТаблицаВЗапросе КАК ТаблицаВЗапросе
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаВЗапросе.ПолеСортировки";
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Текст = СтрЗаменить(Текст, "&ДатаСреза", "");
	КонецЕсли;
	
	Запрос.Текст = Текст;
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(РезультатСтруктура, Выборка);
		
	КонецЕсли;
	
	Возврат РезультатСтруктура;
	
КонецФункции

#КонецОбласти
