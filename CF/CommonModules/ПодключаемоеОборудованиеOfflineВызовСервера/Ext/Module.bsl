
#Область ПрограммныйИнтерфейс

// заполнение переопределяемых процедур модуля МенеджерОфлайнОборудованияПереопределяемый

Процедура ЗаполнитьНастройкиККМ(ОфлайнОборудование, НастройкиОборудования) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилоОбмена.Склад КАК Склад,
	|	ПравилоОбмена.Склад.Организация КАК Организация,
	|	ПравилоОбмена.Склад.Организация.Наименование КАК НаименованиеОрганизации,
	|	ПравилоОбмена.Склад.Магазин КАК Магазин,
	|	ПравилоОбмена.Склад.Магазин.Наименование КАК НаименованиеМагазина,
	|	ПравилоОбмена.Склад.Организация.ИНН КАК ИНН,
	|	ПравилоОбмена.Склад.Организация.КПП КАК КПП
	|ИЗ
	|	Справочник.ПравилаОбменаСПодключаемымОборудованием КАК ПравилоОбмена
	|ГДЕ
	|	ПравилоОбмена.Ссылка = &ПравилоОбмена";
	
	Запрос.УстановитьПараметр("ПравилоОбмена",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОфлайнОборудование, "ПравилоОбмена"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		НастройкиОборудования.НаименованиеОрганизации = Выборка.НаименованиеОрганизации;
		НастройкиОборудования.НаименованиеМагазина = Выборка.НаименованиеМагазина;
		НастройкиОборудования.ИНН = Выборка.ИНН;
		НастройкиОборудования.КПП = Выборка.КПП;
		
		//МестоТочкиПродажи
		НастройкиОборудования.АдресТочкиПродажи = ОбщегоНазначенияРТ.АдресМагазина(Выборка.Магазин);
		
		//АдресТочкиПродажи
		
		//СистемыНалогообложения
		ДатаСеанса = ТекущаяДатаСеанса();
		
		ВидНалога = ПодключаемоеОборудованиеРТ.ВидНалогаПоОрганизацииИСкладу(ДатаСеанса, Выборка.Организация, Выборка.Склад);
		
		СистемаНалогообложенияККТ = ПодключаемоеОборудованиеРТ.ПолучитьСистемуНалогообложенияККТ(
			Выборка.Организация,
			ВидНалога,
			ДатаСеанса
		);
		
		НастройкиОборудования.СистемыНалогообложения.Добавить(СистемаНалогообложенияККТ);
		
		//ЭлектроннаяПочтаОтправителяЧека
		СистемнаяУчетнаяЗапись = РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись();
		Если ЗначениеЗаполнено(СистемнаяУчетнаяЗапись) Тогда
			НастройкиОборудования.ЭлектроннаяПочтаОтправителяЧека = СистемнаяУчетнаяЗапись.АдресЭлектроннойПочты;
		КонецЕсли;
		
	КонецЕсли;
	
	
	
	
	// виды электронной оплаты
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КассыККМ.Ссылка КАК Ссылка,
	|	КассыККМ.СоответствиеВидовОплаты КАК СоответствиеВидовОплаты
	|ПОМЕСТИТЬ КассыККМ
	|ИЗ
	|	Справочник.КассыККМ КАК КассыККМ
	|ГДЕ
	|	КассыККМ.ПодключаемоеОборудование = &ОфлайнОборудование
	|	И НЕ КассыККМ.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствиеВидов.КодВидаОплатыККМOffline КАК КодВидаОплаты,
	|	СоответствиеВидов.ВидОплаты КАК ВидОплаты,
	|	СоответствиеВидов.ВидОплаты.ТипОплаты КАК ТипОплаты,
	|	СоответствиеВидов.ВидОплаты.Наименование КАК ВидОплатыНаименование
	|ИЗ
	|	Справочник.СоответствиеВидовОплатыСККМOffline.СоответствиеВидовОплаты КАК СоответствиеВидов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КассыККМ КАК КассыККМ
	|		ПО СоответствиеВидов.Ссылка = КассыККМ.СоответствиеВидовОплаты";
	
	Запрос.УстановитьПараметр("ОфлайнОборудование", ОфлайнОборудование);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.Наличные Тогда
			Продолжить;
		КонецЕсли;
		
		ВидОплаты = Неопределено;
		ТипОплаты = Неопределено;
		
		Если Выборка.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта Тогда
			
			ВидОплаты = Выборка.ВидОплаты;
			ТипЭлектроннойОплаты = МенеджерОфлайнОборудования.ТипЭлектроннойОплатыПлатежнаяКарта();
			
		ИначеЕсли Выборка.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.БанковскийКредит Тогда
			
			ВидОплаты = Выборка.ВидОплаты;
			ТипЭлектроннойОплаты = МенеджерОфлайнОборудования.ТипЭлектроннойОплатыБанковскийКредит();
			
		ИначеЕсли Выборка.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.ПодарочныйСертификат Тогда
			
			ВидОплаты = Выборка.ВидОплаты;
			ТипЭлектроннойОплаты = МенеджерОфлайнОборудования.ТипЭлектроннойОплатыПодарочныйСертификат();
			
		ИначеЕсли Выборка.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.Бонусы Тогда
			
			ВидОплаты = Выборка.ВидОплаты;
			ТипЭлектроннойОплаты = МенеджерОфлайнОборудования.ТипЭлектроннойОплатыБонусы();
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВидОплаты) Тогда
			
			ВидОплатыККМ = МенеджерОфлайнОборудования.ПолучитьЗаписьВидЭлектроннойОплаты();
			
			ВидОплатыККМ.Код 						= Выборка.КодВидаОплаты;
			ВидОплатыККМ.Наименование 				= Выборка.ВидОплатыНаименование;
			ВидОплатыККМ.ТипЭлектроннойОплаты 		= ТипЭлектроннойОплаты;
			ВидОплатыККМ.УникальныйИдентификатор 	= Выборка.ВидОплаты.УникальныйИдентификатор();
			
			НастройкиОборудования.ВидыЭлектроннойОплаты.Добавить(ВидОплатыККМ);
			
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПрайсЛистККМ(ОфлайнОборудование, ПрайсЛист, ПолнаяВыгрузка) Экспорт
	
	Параметры = ПолучитьПараметрыУстройства(ОфлайнОборудование);
	
	Если Параметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ПравилоОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ЧастичнаяВыгрузка", НЕ ПолнаяВыгрузка);
	
	Параметры.Вставить("КассаККМ",
		Справочники.КассыККМ.ПолучитьКассуККМПоЭкземпляруОборудования(ОфлайнОборудование)
	);
	
	ТоварыКВыгрузке = ПолучитьТоварыКВыгрузкеККМ(ОфлайнОборудование, Параметры);
	
	ЗаполнитьСтруктуруПрайсЛистаИзДанныхКВыгрузке(ПрайсЛист, ТоварыКВыгрузке);
	
КонецПроцедуры

Процедура ЗаполнитьПрайсЛистККМПоШтрихкоду(ОфлайнОборудование, ПрайсЛист, Штрихкод) Экспорт
	
	Параметры = ПолучитьПараметрыУстройства(ОфлайнОборудование);
	
	Если Параметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметры.ПравилоОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ЧастичнаяВыгрузка", Ложь);
	
	Параметры.Вставить("КассаККМ",
		Справочники.КассыККМ.ПолучитьКассуККМПоЭкземпляруОборудования(ОфлайнОборудование)
	);
	
	ШтрихкодАвтопроверки = ПроверитьТипШтрихкода(Штрихкод);
	
	Если ШтрихкодАвтопроверки Тогда
		Штрихкод = ПолучитьШтрихкодПоКодуАвтопроверки(Штрихкод);
	КонецЕсли;
	
	ТоварыКВыгрузке = ПолучитьТоварыКВыгрузкеККМ(ОфлайнОборудование, Параметры, Штрихкод);
	
	ЗаполнитьСтруктуруПрайсЛистаИзДанныхКВыгрузке(ПрайсЛист, ТоварыКВыгрузке);
	
КонецПроцедуры

Процедура ЗаполнитьЗаказыККМ(ОфлайнОборудование, ЗаказыККМ) Экспорт
	
	Параметры = ПолучитьПараметрыУстройства(ОфлайнОборудование);
	
	Если Параметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказПокупателяИзменения.Ссылка КАК Заказ,
	|	ВЫРАЗИТЬ(ЗаказПокупателяИзменения.Ссылка.ВладелецДисконтнойКарты КАК Справочник.ФизическиеЛица) КАК ФизицескоеЛицо,
	|	ВЫРАЗИТЬ(ЗаказПокупателяИзменения.Ссылка.ВладелецДисконтнойКарты КАК Справочник.Контрагенты) КАК Контрагент
	|ПОМЕСТИТЬ ЗарегистрированныеЗаказы
	|ИЗ
	|	Документ.ЗаказПокупателя.Изменения КАК ЗаказПокупателяИзменения
	|ГДЕ
	|	ЗаказПокупателяИзменения.Узел = &Узел
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заказ,
	|	ФизицескоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗарегистрированныеЗаказы.Заказ КАК Заказ,
	|	ЗарегистрированныеЗаказы.Заказ.КассаККМ КАК КассаККМ,
	|	ЗарегистрированныеЗаказы.Заказ.Номер КАК Номер,
	|	ЗарегистрированныеЗаказы.Заказ.Дата КАК Дата,
	|	ЗарегистрированныеЗаказы.Заказ.Комментарий КАК Комментарий,
	|	ЗарегистрированныеЗаказы.Заказ.ЖелаемаяДатаПродажи КАК ЖелаемаяДатаПродажи,
	|	ЗарегистрированныеЗаказы.Заказ.АдресДоставки КАК АдресДоставки,
	|	ЗарегистрированныеЗаказы.Заказ.НомерЗаказаНаСайте КАК НомерЗаказаНаСайте,
	|	ЗарегистрированныеЗаказы.Заказ.ДатаЗаказаНаСайте КАК ДатаЗаказаНаСайте,
	|	ЗарегистрированныеЗаказы.Заказ.Статус КАК Статус,
	|	ЗарегистрированныеЗаказы.Заказ.Отменено КАК Отменено,
	|	ЗарегистрированныеЗаказы.ФизицескоеЛицо КАК ФизицескоеЛицо,
	|	ВЫБОР
	|		КОГДА ЗарегистрированныеЗаказы.Контрагент ЕСТЬ NULL
	|			ТОГДА ЗарегистрированныеЗаказы.Заказ.Контрагент
	|		ИНАЧЕ ЗарегистрированныеЗаказы.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ЗарегистрированныеЗаказы.Контрагент ЕСТЬ NULL
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(ЗарегистрированныеЗаказы.Заказ.Контрагент)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(ЗарегистрированныеЗаказы.Контрагент)
	|	КОНЕЦ КАК КонтрагентПредставление,
	|	ЗарегистрированныеЗаказы.Заказ.ДисконтнаяКарта КАК ДисконтнаяКарта
	|ИЗ
	|	ЗарегистрированныеЗаказы КАК ЗарегистрированныеЗаказы
	|ГДЕ
	|	НЕ(ЗарегистрированныеЗаказы.Заказ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПокупателей.Закрыт)
	|				И НЕ ЗарегистрированныеЗаказы.Заказ.Отменено)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказПокупателяТовары.Ссылка КАК Заказ,
	|	ЗаказПокупателяТовары.НомерСтроки КАК НомерСтроки,
	|	ЗаказПокупателяТовары.Номенклатура КАК Номенклатура,
	|	ЗаказПокупателяТовары.Характеристика КАК Характеристика,
	|	ЗаказПокупателяТовары.Количество КАК Количество,
	|	ЗаказПокупателяТовары.Упаковка КАК Упаковка,
	|	ЗаказПокупателяТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ЗаказПокупателяТовары.Цена КАК Цена,
	|	ЗаказПокупателяТовары.Продавец КАК Продавец,
	|	ЗаказПокупателяТовары.ПроцентАвтоматическойСкидки КАК ПроцентАвтоматическойСкидки,
	|	ЗаказПокупателяТовары.ПроцентРучнойСкидки КАК ПроцентРучнойСкидки,
	|	ЗаказПокупателяТовары.Сумма КАК Сумма,
	|	ЗаказПокупателяТовары.СтавкаНДС КАК СтавкаНДС,
	|	ЗаказПокупателяТовары.СуммаНДС КАК СуммаНДС,
	|	ЗаказПокупателяТовары.СуммаАвтоматическойСкидки КАК СуммаАвтоматическойСкидки,
	|	ЗаказПокупателяТовары.СуммаРучнойСкидки КАК СуммаРучнойСкидки,
	|	ЗаказПокупателяТовары.КлючСвязи КАК КлючСвязи,
	|	ЗаказПокупателяТовары.КодСтроки КАК КодСтроки,
	|	ЗаказПокупателяТовары.Резервировать КАК Резервировать,
	|	ЗаказПокупателяТовары.Отменено КАК Отменено,
	|	ЗаказПокупателяТовары.ПричинаОтмены КАК ПричинаОтмены,
	|	ЗаказПокупателяТовары.ПродажаПодарка КАК ПродажаПодарка,
	|	КодыТоваровSKU.SKU КАК КодSKU
	|ИЗ
	|	ЗарегистрированныеЗаказы КАК ЗарегистрированныеЗаказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя.Товары КАК ЗаказПокупателяТовары
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|			ПО ЗаказПокупателяТовары.Номенклатура = КодыТоваровSKU.Номенклатура
	|				И ЗаказПокупателяТовары.Характеристика = КодыТоваровSKU.Характеристика
	|				И ЗаказПокупателяТовары.Упаковка = КодыТоваровSKU.Упаковка
	|		ПО ЗарегистрированныеЗаказы.Заказ = ЗаказПокупателяТовары.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Заказ,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФИОФизЛицСрезПоследних.ФизЛицо КАК ФизицескоеЛицо,
	|	ФИОФизЛицСрезПоследних.Фамилия КАК Фамилия,
	|	ФИОФизЛицСрезПоследних.Имя КАК Имя,
	|	ФИОФизЛицСрезПоследних.Отчество КАК Отчество
	|ИЗ
	|	РегистрСведений.ФИОФизЛиц.СрезПоследних(
	|			,
	|			ФизЛицо В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Т.ФизицескоеЛицо
	|				ИЗ
	|					ЗарегистрированныеЗаказы КАК Т)) КАК ФИОФизЛицСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОплатаКартой.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ОплатаКартой.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|			ТОГДА ОплатаКартой.СуммаДокумента
	|		КОГДА ОплатаКартой.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	|			ТОГДА -ОплатаКартой.СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаКарта,
	|	0 КАК СуммаНаличные
	|ПОМЕСТИТЬ Предоплаты
	|ИЗ
	|	ЗарегистрированныеЗаказы КАК ЗарегистрированныеЗаказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОплатаОтПокупателяПлатежнойКартой КАК ОплатаКартой
	|		ПО ЗарегистрированныеЗаказы.Заказ = ОплатаКартой.ЗаказПокупателя
	|ГДЕ
	|	ОплатаКартой.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходныйОрдер.ЗаказПокупателя,
	|	0,
	|	ПриходныйОрдер.СуммаДокумента
	|ИЗ
	|	ЗарегистрированныеЗаказы КАК ЗарегистрированныеЗаказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер КАК ПриходныйОрдер
	|		ПО ЗарегистрированныеЗаказы.Заказ = ПриходныйОрдер.ЗаказПокупателя
	|ГДЕ
	|	ПриходныйОрдер.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(РасходныйОрдерРасшифровка.ДокументРасчетовСКонтрагентом КАК Документ.ПриходныйКассовыйОрдер).ЗаказПокупателя,
	|	0,
	|	-РасходныйОрдерРасшифровка.Сумма
	|ИЗ
	|	ЗарегистрированныеЗаказы КАК ЗарегистрированныеЗаказы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйКассовыйОрдер.РасшифровкаПлатежа КАК РасходныйОрдерРасшифровка
	|		ПО (ЗарегистрированныеЗаказы.Заказ = ВЫРАЗИТЬ(РасходныйОрдерРасшифровка.ДокументРасчетовСКонтрагентом КАК Документ.ПриходныйКассовыйОрдер).ЗаказПокупателя)
	|ГДЕ
	|	РасходныйОрдерРасшифровка.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПриходныйКассовыйОрдер
	|	И РасходныйОрдерРасшифровка.Ссылка.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Предоплаты.ЗаказПокупателя КАК Заказ,
	|	СУММА(Предоплаты.СуммаКарта) КАК СуммаКарта,
	|	СУММА(Предоплаты.СуммаНаличные) КАК СуммаНаличные
	|ИЗ
	|	Предоплаты КАК Предоплаты
	|
	|СГРУППИРОВАТЬ ПО
	|	Предоплаты.ЗаказПокупателя";
	
	Запрос.УстановитьПараметр("Узел", Параметры.УзелИнформационнойБазы);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	// Заказы
	РезультатЗаказы = РезультатЗапроса[1];
	ВыборкаЗаказы = РезультатЗаказы.Выбрать();
	
	// Товары заказов
	РезультатТовары = РезультатЗапроса[2];
	ТаблицаТовары = РезультатТовары.Выгрузить();
	
	// ФИО клиентов
	РезультатФИО = РезультатЗапроса[3];
	ТаблицаФИО = РезультатФИО.Выгрузить();
	
	// Предоплаты
	РезультатПредоплаты = РезультатЗапроса[5];
	ТаблицаПредоплаты = РезультатПредоплаты.Выгрузить();
	
	// структуры для поиска
	ПараметрыОтбораФИО = Новый Структура;
	ПараметрыОтбораФИО.Вставить("ФизицескоеЛицо");
	
	ПараметрыОтбораТоваров = Новый Структура;
	ПараметрыОтбораТоваров.Вставить("Заказ");
	
	ПараметрыОтбораПредоплаты = Новый Структура;
	ПараметрыОтбораПредоплаты.Вставить("Заказ");
	
	Пока ВыборкаЗаказы.Следующий() Цикл
		
		ЗаказККМ = МенеджерОфлайнОборудования.ПолучитьЗаписьЗаказа();
		
		// УникальныйИдентификатор
		ЗаказККМ.УникальныйИдентификатор = ВыборкаЗаказы.Заказ.УникальныйИдентификатор();
		
		// Комментарий
		ЗаказККМ.Комментарий = ВыборкаЗаказы.Комментарий;
		
		// НомерЗаказа
		ЗаказККМ.НомерЗаказа = ?(
			ЗначениеЗаполнено(ВыборкаЗаказы.НомерЗаказаНаСайте),
			ВыборкаЗаказы.НомерЗаказаНаСайте,
			ВыборкаЗаказы.Номер
			);
			
		// ДатаЗаказа
		ЗаказККМ.ДатаЗаказа = ?(
			ЗначениеЗаполнено(ВыборкаЗаказы.ДатаЗаказаНаСайте),
			ВыборкаЗаказы.ДатаЗаказаНаСайте,
			ВыборкаЗаказы.Дата
			);
			
		// ДатаДоставки
		ЗаказККМ.ДатаДоставки  = ВыборкаЗаказы.ЖелаемаяДатаПродажи;
		
		// Статус
		Если ВыборкаЗаказы.Статус = Перечисления.СтатусыЗаказовПокупателей.Закрыт Тогда
			
			ЗаказККМ.СтатусЗаказа = "Отменен"; // выполненные заказы не должны выгружаться, условие в запросе
			
		ИначеЕсли ВыборкаЗаказы.Статус = Перечисления.СтатусыЗаказовПокупателей.НеСогласован Тогда
			
			ЗаказККМ.СтатусЗаказа  = "НеСогласован";
			
		ИначеЕсли ВыборкаЗаказы.Статус = Перечисления.СтатусыЗаказовПокупателей.Согласован Тогда
			
			ЗаказККМ.СтатусЗаказа  = "Согласован";
			
		КонецЕсли;
		
		// ФИО
		Если ЗначениеЗаполнено(ВыборкаЗаказы.ФизицескоеЛицо) Тогда
			
			ПараметрыОтбораФИО.ФизицескоеЛицо = ВыборкаЗаказы.ФизицескоеЛицо;
			НайденныеФИО = ТаблицаФИО.НайтиСтроки(ПараметрыОтбораФИО);
			
			Если НЕ НайденныеФИО.Количество() = 0 Тогда
				
				ЗаказККМ.ФамилияКлиента = НайденныеФИО[0].Фамилия;
				ЗаказККМ.ИмяКлиента = НайденныеФИО[0].Имя;
				
			КонецЕсли;
			
		Иначе
			
			ЗаказККМ.ФамилияКлиента = ВыборкаЗаказы.КонтрагентПредставление;
		КонецЕсли;
		
		
		// НомерТелефонаКлиента
		Если ЗначениеЗаполнено(ВыборкаЗаказы.ДисконтнаяКарта) Тогда
			ЗаказККМ.НомерТелефонаКлиента = ПолучитьТелефонИзКонтактнойИнформации(ВыборкаЗаказы.ДисконтнаяКарта);
		КонецЕсли;
			
		Если ЗначениеЗаполнено(ВыборкаЗаказы.ФизицескоеЛицо) И НЕ ЗначениеЗаполнено(ЗаказККМ.НомерТелефонаКлиента) Тогда
			ЗаказККМ.НомерТелефонаКлиента = ПолучитьТелефонИзКонтактнойИнформации(ВыборкаЗаказы.ФизицескоеЛицо);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаЗаказы.Контрагент) И НЕ ЗначениеЗаполнено(ЗаказККМ.НомерТелефонаКлиента) Тогда
			ЗаказККМ.НомерТелефонаКлиента = ПолучитьТелефонИзКонтактнойИнформации(ВыборкаЗаказы.Контрагент);
		КонецЕсли;
		
		
		// АдресЭлектроннойПочты
		Если ЗначениеЗаполнено(ВыборкаЗаказы.ДисконтнаяКарта) Тогда
			ЗаказККМ.EmailКлиента = ПолучитьАдресЭлектроннойПочтыИзКонтактнойИнформации(ВыборкаЗаказы.ДисконтнаяКарта);
		КонецЕсли;
			
		Если ЗначениеЗаполнено(ВыборкаЗаказы.ФизицескоеЛицо) И НЕ ЗначениеЗаполнено(ЗаказККМ.EmailКлиента) Тогда
			ЗаказККМ.EmailКлиента = ПолучитьАдресЭлектроннойПочтыИзКонтактнойИнформации(ВыборкаЗаказы.ФизицескоеЛицо);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаЗаказы.Контрагент) И НЕ ЗначениеЗаполнено(ЗаказККМ.EmailКлиента) Тогда
			ЗаказККМ.EmailКлиента = ПолучитьАдресЭлектроннойПочтыИзКонтактнойИнформации(ВыборкаЗаказы.Контрагент);
		КонецЕсли;
		
		
		//! Адрес доставки
		Если ЗначениеЗаполнено(ВыборкаЗаказы.АдресДоставки) Тогда
			ЗаказККМ.ГородДоставки = ВыборкаЗаказы.АдресДоставки;
		КонецЕсли;
		
		// Товары
		
		ПараметрыОтбораТоваров.Заказ = ВыборкаЗаказы.Заказ;
		НайденныеТовары = ТаблицаТовары.НайтиСтроки(ПараметрыОтбораТоваров);
		
		Для Каждого ЗаписьТовары Из НайденныеТовары Цикл
			
			СтруктураЗаписиТовары = МенеджерОфлайнОборудования.ПолучитьЗаписьТовараЗаказа();
			
			СтруктураЗаписиТовары.Код 			= Формат(ЗаписьТовары.КодSKU, "ЧГ=0");
			СтруктураЗаписиТовары.Количество 	= ЗаписьТовары.КоличествоУпаковок;
			СтруктураЗаписиТовары.Цена 			= ЗаписьТовары.Цена;
			СтруктураЗаписиТовары.Сумма 		= ЗаписьТовары.Сумма;
			
			СтруктураЗаписиТовары.УникальныйИдентификаторТовара = ЗаписьТовары.Номенклатура.УникальныйИдентификатор();
			
			Если ЗначениеЗаполнено(ЗаписьТовары.Характеристика) Тогда
				СтруктураЗаписиТовары.УникальныйИдентификаторХарактеристики = ЗаписьТовары.Характеристика.УникальныйИдентификатор();
			КонецЕсли;
				
			Если ЗначениеЗаполнено(ЗаписьТовары.Упаковка) Тогда
				СтруктураЗаписиТовары.УникальныйИдентификаторУпаковки = ЗаписьТовары.Упаковка.УникальныйИдентификатор();
			КонецЕсли;
			
			ЗаказККМ.Товары.Добавить(СтруктураЗаписиТовары);
		КонецЦикла;
		
		// Оплаты
		ПараметрыОтбораПредоплаты.Заказ = ВыборкаЗаказы.Заказ;
		НайденныеПредоплаты = ТаблицаПредоплаты.НайтиСтроки(ПараметрыОтбораПредоплаты);
		
		Для Каждого ЗаписьПредоплата Из НайденныеПредоплаты Цикл
			
			СтруктураЗаписиОплаты = МенеджерОфлайнОборудования.ПолучитьЗаписьОплатыЗаказа();
			
			СтруктураЗаписиОплаты.СуммаНаличнойОплаты    = ?(ЗаписьПредоплата.СуммаНаличные > 0, ЗаписьПредоплата.СуммаНаличные, 0);
			СтруктураЗаписиОплаты.СуммаЭлектроннойОплаты = ?(ЗаписьПредоплата.СуммаКарта > 0, ЗаписьПредоплата.СуммаКарта, 0);
			
			Если СтруктураЗаписиОплаты.СуммаНаличнойОплаты > 0 ИЛИ СтруктураЗаписиОплаты.СуммаЭлектроннойОплаты > 0 Тогда
				ЗаказККМ.Оплаты.Добавить(СтруктураЗаписиОплаты);
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаказыККМ.Добавить(ЗаказККМ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьОтчетыОПродажахИзККМ(ОфлайнОборудование, ДанныеОПродажах, Отказ, СообщениеОбОшибке) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивСозданныхДокументов = Новый Массив;
	
	Параметры = ПолучитьПараметрыУстройства(ОфлайнОборудование);
	Параметры.Вставить("КассаККМ",
		Справочники.КассыККМ.ПолучитьКассуККМПоЭкземпляруОборудования(ОфлайнОборудование)
	);
	
	Параметры.Вставить("Организация", Параметры.КассаККМ.Владелец);
	
	Для Каждого ОтчетОПродажах Из ДанныеОПродажах Цикл
		
		Если НЕ ОтчетОПродажах.СтатусСмены = Перечисления.СтатусыКассовойСмены.Закрыта Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаТоваров 				= ПустаяТаблицаТоваров();
		ТаблицаВозвратов 			= ПустаяТаблицаТоваров();
		ТаблицаШтрихкодовАкцизов 	= ПустаяТаблицаШтрихкодовАкцизов();
		ТаблицаОплат 				= ПустаяТаблицаОплат();
		ТаблицаРасчетыСКлиентами 	= ПустаяТаблицаРасчетыСКлиентами();
		
		Для Каждого ЧекККМ Из ОтчетОПродажах.Чеки Цикл
			
			Если ЧекККМ.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств Тогда
				ЭтоЧекНаВозврат = Ложь;
				
			ИначеЕсли ЧекККМ.ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств Тогда
				ЭтоЧекНаВозврат = Истина;
				
			Иначе
				Продолжить;
			КонецЕсли;
			
			ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
			РасчетСКлиентом = Неопределено;
			
			Если ВРег(ЧекККМ.ТипСвязанногоДокументаККМ) = ВРег("Заказ")
				И ЗначениеЗаполнено(ЧекККМ.УникальныйИдентификаторСвязанногоДокументаККМ) Тогда
				
				СсылкаНаЗаказ = Документы.ЗаказПокупателя.ПолучитьСсылку(ЧекККМ.УникальныйИдентификаторСвязанногоДокументаККМ);
				Если НЕ СсылкаНаЗаказ.ПолучитьОбъект() = Неопределено Тогда
					ЗаказПокупателя = СсылкаНаЗаказ;
					
					РасчетСКлиентом = ТаблицаРасчетыСКлиентами.Добавить();
					РасчетСКлиентом.ЗаказПокупателя = ЗаказПокупателя;
				КонецЕсли;
			КонецЕсли;
			
			СуммаТоваров = 0;
			
			Для Каждого СтрокаТЧ Из ЧекККМ.Товары Цикл
				
				Если НЕ (СтрокаТЧ.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаБезОплаты
					ИЛИ СтрокаТЧ.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой
					ИЛИ СтрокаТЧ.ПризнакСпособаРасчета = Перечисления.ПризнакиСпособаРасчета.ПередачаСЧастичнойОплатой) Тогда
					
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаТоваров.Добавить();
				
				НоваяСтрока.Код 		= СтрокаТЧ.Код;
				НоваяСтрока.Количество 	= ?(ЭтоЧекНаВозврат, -СтрокаТЧ.Количество, СтрокаТЧ.Количество);
				НоваяСтрока.Сумма 		= ?(ЭтоЧекНаВозврат, -СтрокаТЧ.Сумма, СтрокаТЧ.Сумма);
				НоваяСтрока.Цена 		= СтрокаТЧ.Цена;
				НоваяСтрока.СтавкаНДС 	= ПолучитьСтавкуНДС(СтрокаТЧ.СтавкаНДС);
				НоваяСтрока.ШтрихкодАлкогольнойМарки = СтрокаТЧ.ШтрихкодАлкогольнойПродукции;
				НоваяСтрока.Заказ = ЗаказПокупателя;
				НоваяСтрока.УникальныйИдентификатор = ОтчетОПродажах.УникальныйИдентификатор;
				
				СуммаТоваров = СуммаТоваров + СтрокаТЧ.Сумма;
				
				Если НЕ ЭтоЧекНаВозврат Тогда
					Для Каждого ШтрихкодАлкогольнойМарки Из СтрокаТЧ.ШтрихкодАлкогольнойПродукции Цикл
						
						НоваяСтрокаШтрихкод = ТаблицаШтрихкодовАкцизов.Добавить();
						НоваяСтрокаШтрихкод.УникальныйИдентификатор = НоваяСтрока.УникальныйИдентификатор;
						НоваяСтрокаШтрихкод.Код = НоваяСтрока.Код;
						НоваяСтрокаШтрихкод.ШтрихкодАлкогольнойМарки = ШтрихкодАлкогольнойМарки;
						
					КонецЦикла;
				КонецЕсли;
				
				Если ЭтоЧекНаВозврат Тогда
					
					НоваяСтрока = ТаблицаВозвратов.Добавить();
					
					НоваяСтрока.Код 		= СтрокаТЧ.Код;
					НоваяСтрока.Количество 	= СтрокаТЧ.Количество;
					НоваяСтрока.Сумма 		= СтрокаТЧ.Сумма;
					НоваяСтрока.Цена 		= СтрокаТЧ.Цена;
					НоваяСтрока.СтавкаНДС 	= ПолучитьСтавкуНДС(СтрокаТЧ.СтавкаНДС);
					НоваяСтрока.ШтрихкодАлкогольнойМарки = СтрокаТЧ.ШтрихкодАлкогольнойПродукции;
					НоваяСтрока.Заказ = ЗаказПокупателя;
					
				КонецЕсли;
				
			КонецЦикла;
			
			СуммаОплаты = 0;
			
			Для Каждого СтрокаТЧ Из ЧекККМ.Оплаты Цикл
				
				Если ЗначениеЗаполнено(СтрокаТЧ.СуммаНаличнойОплаты) Тогда
					
					НоваяСтрока = ТаблицаОплат.Добавить();
					НоваяСтрока.Сумма = ?(ЭтоЧекНаВозврат, -СтрокаТЧ.СуммаНаличнойОплаты, СтрокаТЧ.СуммаНаличнойОплаты);
					НоваяСтрока.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.Наличные;
					
					СуммаОплаты = СуммаОплаты + НоваяСтрока.Сумма;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаТЧ.СуммаЭлектроннойОплаты) Тогда
					
					НоваяСтрока = ТаблицаОплат.Добавить();
					
					НоваяСтрока.Сумма = ?(ЭтоЧекНаВозврат, -СтрокаТЧ.СуммаЭлектроннойОплаты, СтрокаТЧ.СуммаЭлектроннойОплаты);
					НоваяСтрока.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта;
					
					Если ЗначениеЗаполнено(СтрокаТЧ.КодВидаЭлектроннойОплаты) Тогда
						НоваяСтрока.КодВидаОплаты = СтрокаТЧ.КодВидаЭлектроннойОплаты;
					КонецЕсли;
					
					СуммаОплаты = СуммаОплаты + НоваяСтрока.Сумма;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если НЕ РасчетСКлиентом = Неопределено Тогда
				РасчетСКлиентом.Отгружено = СуммаТоваров;
				РасчетСКлиентом.Оплачено  = СуммаОплаты;
			КонецЕсли;
			
		КонецЦикла;
		
		ТаблицаТоваров.Свернуть("Код, Цена, Заказ, СтавкаНДС", "Количество, Сумма, Скидка");
		ТаблицаВозвратов.Свернуть("Код, Цена, Заказ, СтавкаНДС", "Количество, Сумма, Скидка");
		ТаблицаОплат.Свернуть("КодВидаОплаты, ТипОплаты", "Сумма");
		
		РеквизитыОтчета = Новый Структура;
		РеквизитыОтчета.Вставить("УникальныйИдентификатор", ОтчетОПродажах.УникальныйИдентификатор);
		РеквизитыОтчета.Вставить("Дата", ОтчетОПродажах.ДатаЗакрытияСмены);
		РеквизитыОтчета.Вставить("НомерСмены", ОтчетОПродажах.НомерСмены);
		
		ТаблицыДанных = Новый Структура("Товары, Оплаты, ВозвратыТоваров, Реквизиты, ТаблицаШтрихкодовАкцизов, ТаблицаРасчетыСКлиентами");
		ТаблицыДанных.Товары = ТаблицаТоваров;
		ТаблицыДанных.Оплаты = ТаблицаОплат;
		ТаблицыДанных.ВозвратыТоваров = ТаблицаВозвратов;
		ТаблицыДанных.Реквизиты = РеквизитыОтчета;
		ТаблицыДанных.ТаблицаШтрихкодовАкцизов = ТаблицаШтрихкодовАкцизов;
		ТаблицыДанных.ТаблицаРасчетыСКлиентами = ТаблицаРасчетыСКлиентами;
		
		Комментарий = СформироватьКомментарий(ОфлайнОборудование);
		
		СоздатьИЗаполнитьОтчетОПродажах(МассивСозданныхДокументов, Параметры, ТаблицыДанных, Комментарий);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗагрузитьОтчетыОПроверкеЦенников(ОфлайнОборудование, ДанныеОПроверкахЦенников, Отказ, СообщениеОбОшибке) Экспорт
	
	Товары = Новый ТаблицаЗначений;
	Товары.Колонки.Добавить("Код", Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(9),));
	Товары.Колонки.Добавить("Штрихкод", Новый ОписаниеТипов("Строка"));
	Товары.Колонки.Добавить("ШтрихАвтопроверки", Новый ОписаниеТипов("Строка"));
	Товары.Колонки.Добавить("КоличествоЦенников", Новый ОписаниеТипов("Число"));
		
	Для Каждого Документ ИЗ ДанныеОПроверкахЦенников Цикл
		
		Для Каждого Строка Из Документ.Товары Цикл
			СтрокаТовар = Товары.Добавить();
			ЭтоШтрихАвтопроверки = ПроверитьТипШтрихкода(Строка.Штрихкод);
			Если ЭтоШтрихАвтопроверки Тогда
				СтрокаТовар.ШтрихАвтопроверки = Строка.Штрихкод;
			Иначе
				СтрокаТовар.Штрихкод = Строка.Штрихкод;
			КонецЕсли;
			СтрокаТовар.Код = Строка.Код;
			СтрокаТовар.КоличествоЦенников = 1;
		КонецЦикла;
			
	КонецЦикла;
		
	Если ОфлайнОборудование.СохранятьЦенникиВКонфигурации Тогда
		
		ВыполненоУспешно = Ложь;
		
		Товары.Свернуть("Код, ШтрихАвтопроверки", "КоличествоЦенников");
		
		ЗагрузитьЦенники(ОфлайнОборудование, Товары, ВыполненоУспешно);
		
		Если ВыполненоУспешно = Ложь Тогда
			Отказ = Истина;
			СообщениеОбОшибке = НСтр("ru = 'Ошибка загрузки. Принимающая сторона не смогла обработать принятый отчет.'");
		КонецЕсли;
		
	Иначе
		
		Товары.Свернуть("Код, Штрихкод, ШтрихАвтопроверки", "КоличествоЦенников");
		
		ДанныеОТоварах = ПолучитьДанныеОТоварах(Товары, ОфлайнОборудование.СохранятьЦенникиВКонфигурации);
		
		Результат = УправлениеПечатьюРТ.ПечатьЦенников(ДанныеОТоварах, ОфлайнОборудование);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьРегистрациюПрайсЛистаПослеВыгрузки(ОфлайнОборудование) Экспорт
	
	Параметры = ПолучитьПараметрыУстройства(ОфлайнОборудование);
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		ПланыОбмена.УдалитьРегистрациюИзменений(Параметры.УзелИнформационнойБазы, Метаданные.РегистрыСведений.КодыТоваровPLUНаОборудовании);
		ПланыОбмена.УдалитьРегистрациюИзменений(Параметры.УзелИнформационнойБазы, Метаданные.РегистрыСведений.КодыТоваровSKU);
		ПланыОбмена.УдалитьРегистрациюИзменений(Параметры.УзелИнформационнойБазы, Метаданные.РегистрыНакопления.ТоварыНаСкладах);
		
		УзелОбъект = Параметры.УзелИнформационнойБазы.ПолучитьОбъект();
		УзелОбъект.ДатаВыгрузки      = ТекущаяДатаСеанса();
		УзелОбъект.ВыгрузкаВыполнена = Истина;
		УзелОбъект.Записать();
		
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура УдалитьРегистрациюЗаказовПослеВыгрузки(ОфлайнОборудование) Экспорт
	
	Параметры = ПолучитьПараметрыУстройства(ОфлайнОборудование);
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		ПланыОбмена.УдалитьРегистрациюИзменений(Параметры.УзелИнформационнойБазы, Метаданные.Документы.ЗаказПокупателя);
		
		УзелОбъект = Параметры.УзелИнформационнойБазы.ПолучитьОбъект();
		УзелОбъект.ДатаВыгрузки      = ТекущаяДатаСеанса();
		УзелОбъект.ВыгрузкаВыполнена = Истина;
		УзелОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПолучитьТоварыКВыгрузкеККМ(Устройство, Параметры, Штрихкод = "")
	
	ПравилоОбмена = Параметры.ПравилоОбмена;
	
	РегистрироватьКОбменуПриИзмененииОстатка = Истина;
	
	ТаблицаТоваровПоПравилу = Справочники.ПравилаОбменаСПодключаемымОборудованием.СписокТоваровПоПравилу(ПравилоОбмена);
	
	АвтоматическиГенерироватьSKU = АвтоматическиГенерироватьSKU();
	
	РегистрыСведений.КодыТоваровSKU.ОбновитьКоды_SKU_PLU(ТаблицаТоваровПоПравилу, АвтоматическиГенерироватьSKU, ПравилоОбмена);
	
	ВерхняяГраница = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("ВерхняяГраницаДиапазонаSKUВесовогоТовара");
	НижняяГраница  = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("НижняяГраницаДиапазонаSKUВесовогоТовара");
	
	ВыгружатьГруппыТоваров = ?(Параметры.Свойство("ВыгружатьГруппыТоваров"), Параметры.ВыгружатьГруппыТоваров, Истина);
	
	УстановитьПривилегированныйРежим(Истина);
	
	#Область Запрос
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыТоваровPLUНаОборудовании.КодТовараSKU КАК SKUГруппы,
	|	КодыТоваровSKU.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ КодыГрупп
	|ИЗ
	|	РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровPLUНаОборудовании КАК КодыТоваровPLUНаОборудовании
	|		ПО (КодыТоваровPLUНаОборудовании.КодТовараSKU = КодыТоваровSKU.SKU)
	|ГДЕ
	|	КодыТоваровPLUНаОборудовании.ПравилоОбмена = &ПравилоОбмена
	|	И КодыТоваровSKU.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И КодыТоваровSKU.Номенклатура.ЭтоГруппа
	|	И &ВыгружатьГруппыТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахИзменения.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ РегистраторыТоварыНаСкладах
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Изменения КАК ТоварыНаСкладахИзменения
	|ГДЕ
	|	ТоварыНаСкладахИзменения.Узел = &Узел
	|	И &РегистрироватьПриИзмененииОстатка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладах.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТоварыСИзменившемсяОстатком
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыТоварыНаСкладах КАК РегистраторыТоварыНаСкладах
	|		ПО ТоварыНаСкладах.Регистратор = РегистраторыТоварыНаСкладах.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодыТоваровSKU.SKU КАК SKU
	|ПОМЕСТИТЬ SKU_КОбмену
	|ИЗ
	|	РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыСИзменившемсяОстатком КАК ТоварыСИзменившемсяОстатком
	|		ПО КодыТоваровSKU.Номенклатура = ТоварыСИзменившемсяОстатком.Номенклатура
	|			И КодыТоваровSKU.Характеристика = ТоварыСИзменившемсяОстатком.Характеристика
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	КодыТоваровPLUНаОборудовании.КодТовараSKU
	|ИЗ
	|	РегистрСведений.КодыТоваровPLUНаОборудовании КАК КодыТоваровPLUНаОборудовании
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			КодыТоваровPLUНаОборудованииИзменения.КодТовараPLU КАК КодТовараPLU,
	|			КодыТоваровPLUНаОборудованииИзменения.ПравилоОбмена КАК ПравилоОбмена
	|		ИЗ
	|			РегистрСведений.КодыТоваровPLUНаОборудовании.Изменения КАК КодыТоваровPLUНаОборудованииИзменения
	|		ГДЕ
	|			КодыТоваровPLUНаОборудованииИзменения.Узел = &Узел
	|			И КодыТоваровPLUНаОборудованииИзменения.ПравилоОбмена = &ПравилоОбмена) КАК PLUКОбмену
	|		ПО КодыТоваровPLUНаОборудовании.КодТовараPLU = PLUКОбмену.КодТовараPLU
	|			И КодыТоваровPLUНаОборудовании.ПравилоОбмена = PLUКОбмену.ПравилоОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодыТоваровSKU.SKU КАК SKU,
	|	КодыТоваровSKU.Номенклатура КАК Номенклатура,
	|	КодыТоваровSKU.Характеристика КАК Характеристика,
	|	КодыТоваровSKU.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА SKU_КОбмену.SKU ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьИзменения
	|ПОМЕСТИТЬ КодыТоваровSKU
	|ИЗ
	|	РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ЛЕВОЕ СОЕДИНЕНИЕ SKU_КОбмену КАК SKU_КОбмену
	|		ПО КодыТоваровSKU.SKU = SKU_КОбмену.SKU
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоПравилу.PLU КАК PLU,
	|	ТаблицаТоваровПоПравилу.SKU КАК SKU,
	|	ТаблицаТоваровПоПравилу.Весовой КАК Весовой,
	|	ТаблицаТоваровПоПравилу.Группа КАК Группа,
	|	ТаблицаТоваровПоПравилу.КоличествоОстаток КАК КоличествоОстаток,
	|	ТаблицаТоваровПоПравилу.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровПоПравилу.Упаковка КАК Упаковка,
	|	ТаблицаТоваровПоПравилу.Характеристика КАК Характеристика,
	|	ТаблицаТоваровПоПравилу.Цена КАК Цена,
	|	ТаблицаТоваровПоПравилу.ЭтоГруппа КАК ЭтоГруппа,
	|	ТаблицаТоваровПоПравилу.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента
	|ПОМЕСТИТЬ ТаблицаТоваровПоПравилу
	|ИЗ
	|	&ТаблицаТоваровПоПравилу КАК ТаблицаТоваровПоПравилу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоПравилу.PLU КАК PLU,
	|	ТаблицаТоваровПоПравилу.SKU КАК SKU,
	|	ТаблицаТоваровПоПравилу.Весовой КАК Весовой,
	|	ТаблицаТоваровПоПравилу.Группа КАК Группа,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаТоваровПоПравилу.КоличествоОстаток, 0) КАК ЧИСЛО(15, 3)) КАК КоличествоОстаток,
	|	ТаблицаТоваровПоПравилу.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровПоПравилу.Упаковка КАК Упаковка,
	|	ТаблицаТоваровПоПравилу.Характеристика КАК Характеристика,
	|	ТаблицаТоваровПоПравилу.Цена КАК Цена,
	|	ТаблицаТоваровПоПравилу.ЭтоГруппа КАК ЭтоГруппа,
	|	СпрНоменклатура.Наименование КАК НоменклатураНаименование,
	|	СпрНоменклатура.НаименованиеПолное КАК НоменклатураНаименованиеПолное,
	|	СпрНоменклатура.Артикул КАК Артикул,
	|	СпрНоменклатура.Описание КАК Описание,
	|	СпрНоменклатура.СтавкаНДС КАК СтавкаНДС,
	|	СпрНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СпрНоменклатура.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СпрНоменклатура.ВидАлкогольнойПродукцииЕГАИС КАК ВидАлкогольнойПродукцииЕГАИС,
	|	СпрНоменклатура.ОбъемДАЛ КАК ОбъемДАЛ,
	|	СпрНоменклатура.Крепость КАК Крепость,
	|	СпрНоменклатура.ПроизводительИмпортерАлкогольнойПродукции КАК ПроизводительИмпортерАлкогольнойПродукции,
	|	ЕСТЬNULL(УпаковкиНоменклатуры.Коэффициент, 1) КАК КоэффициентУпаковки,
	|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Наименование, &ПустаяСтрока) КАК ХарактеристикаНаименование,
	|	СпрНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(УпаковкиНоменклатуры.Наименование, &ПустаяСтрока) КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА SKU_КОбмену.SKU ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьИзменения,
	|	СпрНоменклатура.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаТоваровПоПравилу.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	СпрНоменклатура.ВидНоменклатуры.ПродаетсяВРозлив КАК ПродаетсяВРозлив,
	|	СпрНоменклатура.ВидНоменклатуры.ПризнакПредметаРасчета.ТипПризнакаПредметаРасчета КАК ПризнакПредметаРасчета
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ТаблицаТоваровПоПравилу КАК ТаблицаТоваровПоПравилу
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ТаблицаТоваровПоПравилу.Номенклатура = СпрНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|		ПО ТаблицаТоваровПоПравилу.Упаковка = УпаковкиНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ТаблицаТоваровПоПравилу.Характеристика = ХарактеристикиНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ SKU_КОбмену КАК SKU_КОбмену
	|		ПО ТаблицаТоваровПоПравилу.SKU = SKU_КОбмену.SKU
	|ГДЕ
	|	НЕ ТаблицаТоваровПоПравилу.SKU ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.PLU КАК КодТовараPLU,
	|	ТаблицаТоваров.SKU КАК КодТовараSKU,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.ЭтоГруппа КАК ЭтоГруппа,
	|	ТаблицаТоваров.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТоваров.Весовой КАК Весовой,
	|	ТаблицаТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаТоваров.КоэффициентУпаковки КАК КоэффициентУпаковки,
	|	ТаблицаТоваров.НоменклатураНаименование КАК НоменклатураНаименование,
	|	ТаблицаТоваров.НоменклатураНаименование КАК НоменклатураНаименованиеПолное,
	|	ТаблицаТоваров.Артикул КАК Артикул,
	|	ТаблицаТоваров.Описание КАК Описание,
	|	ТаблицаТоваров.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.УпаковкаНаименование КАК УпаковкаНаименование,
	|	ЕСТЬNULL(Штрихкоды.Штрихкод, &ПустаяСтрока) КАК Штрихкод,
	|	ТаблицаТоваров.КоличествоОстаток / ВЫБОР
	|		КОГДА ТаблицаТоваров.КоэффициентУпаковки = 0
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваров.КоэффициентУпаковки, 1)
	|	КОНЕЦ КАК КоличествоОстаток,
	|	ТаблицаТоваров.АлкогольнаяПродукция КАК Алкоголь,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ВидАлкогольнойПродукцииЕГАИС.Маркируемый
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Маркируемый,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ВидАлкогольнойПродукцииЕГАИС.Код
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КодВидаАлкогольнойПродукции,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ОбъемДАЛ * 10
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ЕмкостьТары,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.Крепость
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Крепость,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ПроизводительИмпортерАлкогольнойПродукции.ИНН
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИННПроизводителя,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ПроизводительИмпортерАлкогольнойПродукции.КПП
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КПППроизводителя,
	|	КодыГрупп.SKUГруппы КАК SKUГруппы,
	|	ТаблицаТоваров.Цена КАК Цена,
	|	ТаблицаТоваров.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	ТаблицаТоваров.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
	|	ТаблицаТоваров.ПродаетсяВРозлив КАК ПродаетсяВРозлив,
	|	ТаблицаТоваров.Группа КАК Группа
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК Штрихкоды
	|		ПО ТаблицаТоваров.Номенклатура = Штрихкоды.Владелец
	|			И ТаблицаТоваров.Характеристика = Штрихкоды.Характеристика
	|			И ТаблицаТоваров.Упаковка = Штрихкоды.Упаковка
	|		ЛЕВОЕ СОЕДИНЕНИЕ КодыГрупп КАК КодыГрупп
	|		ПО ТаблицаТоваров.Группа = КодыГрупп.Номенклатура
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА &ТолькоИзмененные = ИСТИНА
	|				ТОГДА ТаблицаТоваров.ЕстьИзменения = ИСТИНА
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ВыгружатьГруппыТоваров = ЛОЖЬ
	|				ТОГДА НЕ ТаблицаТоваров.ЭтоГруппа
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ОтборПоШтрихкоду = ИСТИНА
	|				ТОГДА ЕСТЬNULL(Штрихкоды.Штрихкод, &ПустаяСтрока) = &Штрихкод
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|ИТОГИ
	|	МАКСИМУМ(Штрихкод)
	|ПО
	|	КодТовараPLU";
	
	Запрос.УстановитьПараметр("ТолькоИзмененные",       Параметры.ЧастичнаяВыгрузка); 
	Запрос.УстановитьПараметр("ПравилоОбмена",          Параметры.ПравилоОбмена);
	Запрос.УстановитьПараметр("Узел",                   Параметры.УзелИнформационнойБазы);
	Запрос.УстановитьПараметр("Склад",                  Параметры.ПравилоОбмена.Склад);
	Запрос.УстановитьПараметр("ВыгружатьГруппыТоваров", ВыгружатьГруппыТоваров);
	Запрос.УстановитьПараметр("РегистрироватьПриИзмененииОстатка", РегистрироватьКОбменуПриИзмененииОстатка);
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.УстановитьПараметр("ТаблицаТоваровПоПравилу", ТаблицаТоваровПоПравилу);
	Запрос.УстановитьПараметр("Устройство",              Устройство);
	Запрос.УстановитьПараметр("ОтборПоШтрихкоду",        НЕ ПустаяСтрока(Штрихкод));
	Запрос.УстановитьПараметр("Штрихкод",                Штрихкод);
	
	#КонецОбласти
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("PLU",								Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("SKU",								Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("SKUГруппы",						Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Номенклатура",						Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТоваров.Колонки.Добавить("Группа",							Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТоваров.Колонки.Добавить("Характеристика",					Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Упаковка",							Новый ОписаниеТипов("СправочникСсылка.УпаковкиНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС",						Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	ТаблицаТоваров.Колонки.Добавить("ТипНоменклатуры",					Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Артикул",							Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Описание",							Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмерения",					Новый ОписаниеТипов("СправочникСсылка.БазовыеЕдиницыИзмерения"));
	ТаблицаТоваров.Колонки.Добавить("Наименование",						Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("НаименованиеПолное",				Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Штрихкод",							Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("МассивШтрихкодов",					Новый ОписаниеТипов("Массив"));
	ТаблицаТоваров.Колонки.Добавить("Цена",								Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Весовой",							Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("ЭтоГруппа",						Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("Остаток",							Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("КоэффициентУпаковки",				Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("ЕстьОшибки",						Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("ИндексКартинкиЕстьИзменения",		Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("НоменклатураНаименование",			Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("УпаковкаНаименование",				Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ХарактеристикаНаименование",		Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Алкоголь",							Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("Маркируемый",						Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("КодВидаАлкогольнойПродукции",		Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ЕмкостьТары",						Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Крепость",							Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("ИННПроизводителя",					Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("КПППроизводителя",					Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ДоговорПлатежногоАгента",			Новый ОписаниеТипов("СправочникСсылка.ДоговорыПлатежныхАгентов"));
	ТаблицаТоваров.Колонки.Добавить("НомерСекции",						Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("ПризнакПредметаРасчета",			Новый ОписаниеТипов("ПеречислениеСсылка.ПризнакиПредметаРасчета"));
	ТаблицаТоваров.Колонки.Добавить("ПродаетсяВРозлив",					Новый ОписаниеТипов("Булево"));
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// определение номера секции товара
	ТаблицаТовары = РезультатЗапроса.Выгрузить();
	ТаблицаТовары.Свернуть("Номенклатура");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Ссылка
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&ТаблицаТовары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Ссылка КАК Номенклатура,
	|	Товары.Ссылка КАК НомерСтроки
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка ЕСТЬ НЕ NULL
	|	И НЕ Товары.Ссылка.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаТовары);
	
	ТаблицаТоварыДляСоответсвия = Запрос.Выполнить().Выгрузить();
	
	ТаблицаСоответствияТоваровСекциям = Новый Соответствие();
	ПодключаемоеОборудованиеРТВызовСервера.ЗаполнитьСоответствиеСекцийДляТабличнойЧастиПоКассеККМ(
		Параметры.КассаККМ,
		ТаблицаТоварыДляСоответсвия,
		ТаблицаСоответствияТоваровСекциям
	);
	
	ВыборкаПоКодам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоКодам.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		
		Выборка = ВыборкаПоКодам.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Штрихкод = СокрЛП(Выборка.Штрихкод);
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.PLU) Тогда
				
				НоваяСтрока.PLU								= Выборка.КодТовараPLU;
				НоваяСтрока.SKU								= Выборка.КодТовараSKU;
				НоваяСтрока.SKUГруппы						= ?(ВыгружатьГруппыТоваров, Выборка.SKUГруппы, 0);
				НоваяСтрока.Наименование					= ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(Выборка.НоменклатураНаименование, Выборка.ХарактеристикаНаименование) + ?(ЗначениеЗаполнено(Выборка.УпаковкаНаименование),", " + Выборка.УпаковкаНаименование,"");
				НоваяСтрока.НаименованиеПолное				= ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(Выборка.НоменклатураНаименованиеПолное, Выборка.ХарактеристикаНаименование) + ?(ЗначениеЗаполнено(Выборка.УпаковкаНаименование),", " + Выборка.УпаковкаНаименование,"");
				НоваяСтрока.Остаток							= Выборка.КоличествоОстаток;
				НоваяСтрока.ИндексКартинкиЕстьИзменения		= 0;
				НоваяСтрока.НомерСекции 					= ТаблицаСоответствияТоваровСекциям.Получить(Выборка.Номенклатура);
				
				НоваяСтрока.Штрихкод						= Штрихкод;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,
					"Номенклатура,
					|Характеристика,
					|ЕдиницаИзмерения,
					|Группа,
					|ХарактеристикаНаименование,
					|Упаковка,
					|Весовой,
					|ЭтоГруппа,
					|Артикул,
					|Описание,
					|СтавкаНДС,
					|КоэффициентУпаковки,
					|ТипНоменклатуры,
					|УпаковкаНаименование,
					|НоменклатураНаименование,
					|Алкоголь,
					|Маркируемый,
					|КодВидаАлкогольнойПродукции,
					|ЕмкостьТары,
					|Крепость,
					|ИННПроизводителя,
					|КПППроизводителя,
					|Цена,
					|ДоговорПлатежногоАгента,
					|ПризнакПредметаРасчета,
					|ПродаетсяВРозлив
					|");
				
			Иначе
				НоваяСтрока.Штрихкод = НоваяСтрока.Штрихкод + " " + Штрихкод;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Штрихкод) Тогда
				НоваяСтрока.МассивШтрихкодов.Добавить(Штрихкод);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТаблицаТоваров;
	
КонецФункции

Процедура ЗаполнитьСтруктуруПрайсЛистаИзДанныхКВыгрузке(ПрайсЛистККМ, ТоварыКВыгрузке)
	
	#Область Запрос

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыКВыгруке.SKU КАК SKU,
		|	ТоварыКВыгруке.SKUГруппы КАК SKUГруппы,
		|	ТоварыКВыгруке.Артикул КАК Артикул,
		|	ТоварыКВыгруке.Весовой КАК Весовой,
		|	ТоварыКВыгруке.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТоварыКВыгруке.КоэффициентУпаковки КАК КоэффициентУпаковки,
		|	ТоварыКВыгруке.Номенклатура КАК Номенклатура,
		|	ТоварыКВыгруке.Группа КАК Группа,
		|	ТоварыКВыгруке.НоменклатураНаименование КАК НоменклатураНаименование,
		|	ТоварыКВыгруке.СтавкаНДС КАК СтавкаНДС,
		|	ТоварыКВыгруке.Остаток КАК Остаток,
		|	ТоварыКВыгруке.Алкоголь КАК Алкоголь,
		|	ТоварыКВыгруке.Маркируемый КАК Маркируемый,
		|	ТоварыКВыгруке.КодВидаАлкогольнойПродукции КАК КодВидаАлкогольнойПродукции,
		|	ТоварыКВыгруке.ЕмкостьТары КАК ЕмкостьТары,
		|	ТоварыКВыгруке.Крепость КАК Крепость,
		|	ТоварыКВыгруке.ИННПроизводителя КАК ИННПроизводителя,
		|	ТоварыКВыгруке.КПППроизводителя КАК КПППроизводителя,
		|	ТоварыКВыгруке.Упаковка КАК Упаковка,
		|	ТоварыКВыгруке.УпаковкаНаименование КАК УпаковкаНаименование,
		|	ТоварыКВыгруке.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ТоварыКВыгруке.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьХарактеристики,
		|	ВЫБОР
		|		КОГДА ТоварыКВыгруке.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьУпаковки,
		|	ТоварыКВыгруке.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
		|	ТоварыКВыгруке.Цена КАК Цена,
		|	ТоварыКВыгруке.Штрихкод КАК Штрихкод,
		|	ТоварыКВыгруке.ЭтоГруппа КАК ЭтоГруппа,
		|	ТоварыКВыгруке.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ТоварыКВыгруке.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
		|	ТоварыКВыгруке.ПродаетсяВРозлив КАК ПродаетсяВРозлив,
		|	ТоварыКВыгруке.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
		|	ТоварыКВыгруке.Описание КАК Описание,
		|	ТоварыКВыгруке.НомерСекции КАК НомерСекции
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	&ТоварыКВыгруке КАК ТоварыКВыгруке
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.НоменклатураНаименование КАК НоменклатураНаименование,
		|	Товары.ЭтоГруппа КАК ЭтоГруппа,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
		|	Товары.Упаковка КАК Упаковка,
		|	Товары.УпаковкаНаименование КАК УпаковкаНаименование,
		|	Товары.Группа КАК Группа,
		|	Товары.SKUГруппы КАК SKUГруппы,
		|	Товары.Артикул КАК Артикул,
		|	Товары.Весовой КАК Весовой,
		|	Товары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.SKU КАК SKU,
		|	Товары.Остаток КАК Остаток,
		|	Товары.Цена КАК Цена,
		|	Товары.Штрихкод КАК Штрихкод,
		|	Товары.Алкоголь КАК Алкоголь,
		|	Товары.Маркируемый КАК Маркируемый,
		|	Товары.КодВидаАлкогольнойПродукции КАК КодВидаАлкогольнойПродукции,
		|	Товары.ЕмкостьТары КАК ЕмкостьТары,
		|	Товары.Крепость КАК Крепость,
		|	Товары.ИННПроизводителя КАК ИННПроизводителя,
		|	Товары.КПППроизводителя КАК КПППроизводителя,
		|	Товары.КоэффициентУпаковки КАК КоэффициентУпаковки,
		|	Товары.ТипНоменклатуры КАК ТипНоменклатуры,
		|	Товары.ЕстьХарактеристики КАК ЕстьХарактеристики,
		|	Товары.ЕстьУпаковки КАК ЕстьУпаковки,
		|	Товары.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
		|	Товары.ПродаетсяВРозлив КАК ПродаетсяВРозлив,
		|	Товары.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
		|	Товары.Описание КАК Описание,
		|	Товары.НомерСекции КАК НомерСекции
		|ИЗ
		|	Товары КАК Товары
		|ИТОГИ
		|	МАКСИМУМ(НоменклатураНаименование),
		|	МАКСИМУМ(ХарактеристикаНаименование),
		|	МАКСИМУМ(ЭтоГруппа),
		|	МАКСИМУМ(Группа),
		|	МАКСИМУМ(СтавкаНДС),
		|	МАКСИМУМ(SKUГруппы),
		|	МАКСИМУМ(Артикул),
		|	МАКСИМУМ(Весовой),
		|	МАКСИМУМ(ПризнакПредметаРасчета),
		|	МАКСИМУМ(ПродаетсяВРозлив),
		|	МАКСИМУМ(ДоговорПлатежногоАгента),
		|	МАКСИМУМ(ЕдиницаИзмерения),
		|	МАКСИМУМ(SKU),
		|	МАКСИМУМ(ТипНоменклатуры),
		|	МАКСИМУМ(ЕстьХарактеристики),
		|	МАКСИМУМ(ЕстьУпаковки),
		|	МАКСИМУМ(Штрихкод),
		|	МАКСИМУМ(Алкоголь),
		|	МАКСИМУМ(Маркируемый),
		|	МАКСИМУМ(КодВидаАлкогольнойПродукции),
		|	МАКСИМУМ(ЕмкостьТары),
		|	МАКСИМУМ(Крепость),
		|	МАКСИМУМ(ИННПроизводителя),
		|	МАКСИМУМ(КПППроизводителя),
		|	МАКСИМУМ(Цена),
		|	МАКСИМУМ(Остаток),
		|	МАКСИМУМ(Описание),
		|	МАКСИМУМ(НомерСекции)
		|ПО
		|	Номенклатура,
		|	Характеристика";
		
	Запрос.УстановитьПараметр("ТоварыКВыгруке", ТоварыКВыгрузке);
	РезультатЗапроса = Запрос.Выполнить();
	
	#КонецОбласти
	
	ТипНоменклатурыУслуга = Перечисления.ТипыНоменклатуры.Услуга;
	
	ВыборкаПоНоменклатуре = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КэшЕИ = ПолучитьКэшТаблицуЕИ();
	КэшПлатежныеАгенты = ПолучитьКэшТаблицуПлатежныеАгенты();
	
	Пока ВыборкаПоНоменклатуре.Следующий() Цикл
		
		Если ВыборкаПоНоменклатуре.ЭтоГруппа Тогда
			
			ЗаписьГруппа = МенеджерОфлайнОборудования.ПолучитьЗаписьГруппыТоваров();
			
			ЗаписьГруппа.Код = Строка(Формат(ВыборкаПоНоменклатуре.SKU, "ЧГ=0"));
			
			Если ЗначениеЗаполнено(ВыборкаПоНоменклатуре.SKUГруппы) Тогда
				ЗаписьГруппа.КодГруппы = Строка(Формат(ВыборкаПоНоменклатуре.SKUГруппы, "ЧГ=0"));
				ЗаписьГруппа.УникальныйИдентификаторГруппы = ВыборкаПоНоменклатуре.Группа.УникальныйИдентификатор();
			КонецЕсли;
			
			ЗаписьГруппа.Наименование = ВыборкаПоНоменклатуре.НоменклатураНаименование;
			ЗаписьГруппа.УникальныйИдентификатор = ВыборкаПоНоменклатуре.Номенклатура.УникальныйИдентификатор();
			ПрайсЛистККМ.ГруппыТоваров.Добавить(ЗаписьГруппа);
			
		Иначе
			
			ЗаписьТовар = МенеджерОфлайнОборудования.ПолучитьЗаписьТовара();
			
			Если ЗначениеЗаполнено(ВыборкаПоНоменклатуре.ПризнакПредметаРасчета) Тогда
				ЗаписьТовар.ПризнакПредметаРасчета = ВыборкаПоНоменклатуре.ПризнакПредметаРасчета;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаПоНоменклатуре.SKUГруппы) Тогда
				ЗаписьТовар.КодГруппы = Строка(Формат(ВыборкаПоНоменклатуре.SKUГруппы, "ЧГ=0"));
				ЗаписьТовар.УникальныйИдентификаторГруппы = ВыборкаПоНоменклатуре.Группа.УникальныйИдентификатор();
			КонецЕсли;
			
			ЗаписьТовар.Наименование	= ВыборкаПоНоменклатуре.НоменклатураНаименование;
			ЗаписьТовар.Артикул			= ВыборкаПоНоменклатуре.Артикул;
			ЗаписьТовар.ЭтоВесовойТовар = ВыборкаПоНоменклатуре.Весовой;
			
			ЗаписьТовар.СтавкаНДС = ПолучитьСтавкуНДСККМ(ВыборкаПоНоменклатуре.СтавкаНДС);
			ЗаписьТовар.Описание = ВыборкаПоНоменклатуре.Описание;
			
			Если ЗначениеЗаполнено(ВыборкаПоНоменклатуре.НомерСекции) Тогда
				ЗаписьТовар.НомерСекции = ВыборкаПоНоменклатуре.НомерСекции;
			КонецЕсли;
			
			ЗаписьТовар.ЭтоАлкоголь = ВыборкаПоНоменклатуре.Алкоголь;
			
			Если ЗаписьТовар.ЭтоАлкоголь Тогда
				ЗаписьТовар.АлкогольныеРеквизиты.Маркируемый 		= ВыборкаПоНоменклатуре.Маркируемый;
				ЗаписьТовар.АлкогольныеРеквизиты.ЕмкостьТары		= ВыборкаПоНоменклатуре.ЕмкостьТары;
				ЗаписьТовар.АлкогольныеРеквизиты.Крепость			= ВыборкаПоНоменклатуре.Крепость;
				ЗаписьТовар.АлкогольныеРеквизиты.ИННПроизводителя	= ВыборкаПоНоменклатуре.ИННПроизводителя;
				ЗаписьТовар.АлкогольныеРеквизиты.КПППроизводителя	= ВыборкаПоНоменклатуре.КПППроизводителя;
				ЗаписьТовар.АлкогольныеРеквизиты.ВРозлив 			= ВыборкаПоНоменклатуре.ПродаетсяВРозлив;
				ЗаписьТовар.АлкогольныеРеквизиты.КодВидаАлкогольнойПродукции = ВыборкаПоНоменклатуре.КодВидаАлкогольнойПродукции;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаПоНоменклатуре.ЕдиницаИзмерения) Тогда
				ЗаписатьЕИ(ПрайсЛистККМ, ЗаписьТовар, ВыборкаПоНоменклатуре.ЕдиницаИзмерения, КэшЕИ);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаПоНоменклатуре.ДоговорПлатежногоАгента) Тогда
				ЗаписатьДоговорПлатежногоАгента(ПрайсЛистККМ, ЗаписьТовар, ВыборкаПоНоменклатуре.ДоговорПлатежногоАгента, КэшПлатежныеАгенты);
			КонецЕсли;
			
			ЗаписьТовар.УникальныйИдентификатор = ВыборкаПоНоменклатуре.Номенклатура.УникальныйИдентификатор();
			
			Если ВыборкаПоНоменклатуре.ЕстьХарактеристики Тогда
				
				ЗаписьТовар.ИмеетХарактеристики = Истина;
				ЗаписьТовар.ИмеетУпаковки       = Ложь;
				
				ВыборкаПоХарактеристикам = ВыборкаПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаПоХарактеристикам.Следующий() Цикл
					
					ЗаписьХарактеристика = МенеджерОфлайнОборудования.ПолучитьЗаписьХарактеристики();
					ЗаписьХарактеристика.Наименование = ВыборкаПоХарактеристикам.ХарактеристикаНаименование;
					
					Если ВыборкаПоХарактеристикам.ЕстьУпаковки Тогда
						
						ЗаписьХарактеристика.ИмеетУпаковки       = Истина;
						
						ВыборкаПоУпаковкам = ВыборкаПоХарактеристикам.Выбрать();
						
						Пока ВыборкаПоУпаковкам.Следующий() Цикл
							
							Если ЗначениеЗаполнено(ВыборкаПоУпаковкам.Упаковка) Тогда
								
								ЗаписатьУпаковку(ВыборкаПоУпаковкам, ЗаписьХарактеристика);
								
							Иначе
								
								ЗаписьХарактеристика.Код      = Строка(Формат(ВыборкаПоУпаковкам.SKU, "ЧГ=0"));
								ЗаписьХарактеристика.Цена     = ВыборкаПоУпаковкам.Цена;
								ЗаписьХарактеристика.Остаток  = ВыборкаПоУпаковкам.Остаток;
								
								ЗаписатьШтрихкод(ВыборкаПоУпаковкам, ЗаписьХарактеристика);
								
							КонецЕсли;
							
						КонецЦикла;
						
					Иначе
						
						ЗаписьХарактеристика.ИмеетУпаковки = Ложь;
						
						ЗаписатьШтрихкод(ВыборкаПоХарактеристикам, ЗаписьХарактеристика);
						
						ЗаписьХарактеристика.Код      = Строка(Формат(ВыборкаПоХарактеристикам.SKU, "ЧГ=0"));
						ЗаписьХарактеристика.Цена     = ВыборкаПоХарактеристикам.Цена;
						ЗаписьХарактеристика.Остаток  = ВыборкаПоХарактеристикам.Остаток;
						
					КонецЕсли;
					
					ЗаписьХарактеристика.УникальныйИдентификатор = ВыборкаПоХарактеристикам.Характеристика.УникальныйИдентификатор();
					
					ЗаписьТовар.Характеристики.Добавить(ЗаписьХарактеристика);
					
				КонецЦикла;
				
			ИначеЕсли ВыборкаПоНоменклатуре.ЕстьУпаковки И НЕ ВыборкаПоНоменклатуре.ЕстьХарактеристики Тогда
				
				ВыборкаПоПустойХарактеристике = ВыборкаПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				ВыборкаПоПустойХарактеристике.Следующий();
				
				ВыборкаПоУпаковкам = ВыборкаПоПустойХарактеристике.Выбрать();
				
				ЗаписьТовар.ИмеетУпаковки       = Истина;
				ЗаписьТовар.ИмеетХарактеристики = Ложь;
				
				Пока ВыборкаПоУпаковкам.Следующий() Цикл
					
					Если ЗначениеЗаполнено(ВыборкаПоУпаковкам.Упаковка) Тогда
						ЗаписатьУпаковку(ВыборкаПоУпаковкам, ЗаписьТовар);
					Иначе
						
						ЗаписьТовар.Код      = Строка(Формат(ВыборкаПоУпаковкам.SKU, "ЧГ=0"));
						ЗаписьТовар.Цена     = ВыборкаПоУпаковкам.Цена;
						ЗаписьТовар.Остаток  = ВыборкаПоУпаковкам.Остаток;
						
						ЗаписатьШтрихкод(ВыборкаПоУпаковкам, ЗаписьТовар);
						
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				
				ЗаписатьШтрихкод(ВыборкаПоНоменклатуре, ЗаписьТовар);
				
				ЗаписьТовар.Код      = Строка(Формат(ВыборкаПоНоменклатуре.SKU, "ЧГ=0"));
				ЗаписьТовар.Цена     = ВыборкаПоНоменклатуре.Цена;
				ЗаписьТовар.Остаток  = ВыборкаПоНоменклатуре.Остаток;
				
				ЗаписьТовар.ИмеетУпаковки       = Ложь;
				ЗаписьТовар.ИмеетХарактеристики = Ложь;
				
			КонецЕсли;
			
			ПрайсЛистККМ.Товары.Добавить(ЗаписьТовар);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСтавкуНДС(СтавкаНДСККМ)
	
	Если СтавкаНДСККМ = МенеджерОфлайнОборудования.ПолучитьСтавкуБезНДС() Тогда
		
		Возврат Перечисления.СтавкиНДС.БезНДС;
		
	ИначеЕсли СтавкаНДСККМ = МенеджерОфлайнОборудования.ПолучитьСтавкуНДС0() Тогда
		
		Возврат Перечисления.СтавкиНДС.НДС0;
		
	ИначеЕсли СтавкаНДСККМ = МенеджерОфлайнОборудования.ПолучитьСтавкуНДС10() Тогда
		
		Возврат Перечисления.СтавкиНДС.НДС10;
		
	ИначеЕсли СтавкаНДСККМ = МенеджерОфлайнОборудования.ПолучитьСтавкуНДС18() Тогда
		
		Возврат Перечисления.СтавкиНДС.НДС18;
		
	ИначеЕсли СтавкаНДСККМ = МенеджерОфлайнОборудования.ПолучитьСтавкуНДС20() Тогда
		
		Возврат Перечисления.СтавкиНДС.НДС20;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСтавкуНДСККМ(СтавкаНДС)
	
	Если СтавкаНДС = Перечисления.СтавкиНДС.БезНДС Тогда
		
		Возврат МенеджерОфлайнОборудования.ПолучитьСтавкуБезНДС();
		
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС0 Тогда
		
		Возврат МенеджерОфлайнОборудования.ПолучитьСтавкуНДС0();
		
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Тогда
		
		Возврат МенеджерОфлайнОборудования.ПолучитьСтавкуНДС10();
		
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Тогда
		
		Возврат МенеджерОфлайнОборудования.ПолучитьСтавкуНДС18();
		
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Тогда
		
		Возврат МенеджерОфлайнОборудования.ПолучитьСтавкуНДС20();
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьКэшТаблицуЕИ()
	
	ТаблицаКэш = Новый ТаблицаЗначений;
	ТаблицаКэш.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.БазовыеЕдиницыИзмерения"));
	ТаблицаКэш.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка"));
	ТаблицаКэш.Колонки.Добавить("УИ", Новый ОписаниеТипов("УникальныйИдентификатор"));
	
	Возврат ТаблицаКэш;
	
КонецФункции

Функция ПолучитьКэшТаблицуПлатежныеАгенты()
	
	ТаблицаКэш = Новый ТаблицаЗначений;
	ТаблицаКэш.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.ДоговорыПлатежныхАгентов"));
	ТаблицаКэш.Колонки.Добавить("КодПоставщика", Новый ОписаниеТипов("Строка"));
	ТаблицаКэш.Колонки.Добавить("КодАгента", Новый ОписаниеТипов("Строка"));
	ТаблицаКэш.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка"));
	ТаблицаКэш.Колонки.Добавить("УИ", Новый ОписаниеТипов("УникальныйИдентификатор"));
	
	Возврат ТаблицаКэш;
	
КонецФункции

Процедура ЗаписатьЕИ(ПрайсЛистККМ, ЗаписьТовар, ЕдиницаИзмерения, КэшТаблица)
	
	Кэш = КэшТаблица.Найти(ЕдиницаИзмерения, "Ссылка");
	
	Если Кэш = Неопределено Тогда
		
		СтруктураРеквизитов = Новый Структура;
		СтруктураРеквизитов.Вставить("Код", "Код");
		СтруктураРеквизитов.Вставить("Наименование", "Наименование");
		
		РеквизитыЕИ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЕдиницаИзмерения, СтруктураРеквизитов);
		
		ЕдиницаИзмеренияККМ = МенеджерОфлайнОборудования.ПолучитьЗаписьЕдиницыИзмерения();
		ЕдиницаИзмеренияККМ.Код = РеквизитыЕИ.Код;
		ЕдиницаИзмеренияККМ.Наименование = РеквизитыЕИ.Наименование;
		ЕдиницаИзмеренияККМ.УникальныйИдентификатор = ЕдиницаИзмерения.УникальныйИдентификатор();
		
		ПрайсЛистККМ.ЕдиницыИзмерения.Добавить(ЕдиницаИзмеренияККМ);
		
		Кэш = КэшТаблица.Добавить();
		Кэш.Ссылка = ЕдиницаИзмерения;
		Кэш.Код = РеквизитыЕИ.Код;
		Кэш.УИ = ЕдиницаИзмеренияККМ.УникальныйИдентификатор;
		
	КонецЕсли;
	
	ЗаписьТовар.КодЕдиницыИзмерения = Кэш.Код;
	ЗаписьТовар.УникальныйИдентификаторЕдиницыИзмерения = Кэш.УИ;
	
КонецПроцедуры

Процедура ЗаписатьДоговорПлатежногоАгента(ПрайсЛистККМ, ЗаписьТовар, ДоговорПлатежногоАгента, КэшТаблица)
	
	Кэш = КэшТаблица.Найти(ДоговорПлатежногоАгента, "Ссылка");
	
	Если Кэш = Неопределено Тогда
		
		СтруктураРеквизитов = Новый Структура;
		
		// Поставщик
		СтруктураРеквизитов.Вставить("ТелефонПоставщика",  "ТелефонПоставщика");
		СтруктураРеквизитов.Вставить("ИННПоставщикаУслуг", "ИННПоставщикаУслуг");
		СтруктураРеквизитов.Вставить("Агент", "Агент");
		
		// Агент
		СтруктураРеквизитов.Вставить("ПризнакАгента", 						"ПризнакАгента");
		СтруктураРеквизитов.Вставить("ОперацияПлатежногоАгента", 			"ОперацияПлатежногоАгента");
		СтруктураРеквизитов.Вставить("ТелефонПлатежногоАгента", 			"ТелефонПлатежногоАгента");
		СтруктураРеквизитов.Вставить("ТелефонОператораПеревода", 			"ТелефонОператораПеревода");
		СтруктураРеквизитов.Вставить("НаименованиеОператораПеревода", 		"НаименованиеОператораПеревода");
		СтруктураРеквизитов.Вставить("АдресОператораПеревода", 				"АдресОператораПеревода");
		СтруктураРеквизитов.Вставить("ИННОператораПеревода", 				"ИННОператораПеревода");
		СтруктураРеквизитов.Вставить("ТелефонОператораПоПриемуПлатежей", 	"ТелефонОператораПоПриемуПлатежей");
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорПлатежногоАгента, СтруктураРеквизитов);
		
		Код = Неопределено;
		УУИД = ДоговорПлатежногоАгента.УникальныйИдентификатор();
		
		Если КэшТаблица.Количество() = 0 Тогда
			Код = "1";
		Иначе
			ПоследнийКэш = КэшТаблица.Получить(КэшТаблица.Количество()-1);
			Код = Строка(Число(ПоследнийКэш.Код) + 1);
		КонецЕсли;
		
		КодПоставщика = Неопределено;
		Если ЗначениеЗаполнено(РеквизитыДоговора.ТелефонПоставщика)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.ИННПоставщикаУслуг)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.Агент) Тогда
			
			ДанныеПоставщикаККМ = МенеджерОфлайнОборудования.ПолучитьЗаписьДанныхПоставщика();
			ДанныеПоставщикаККМ.Код = Код;
			ДанныеПоставщикаККМ.УникальныйИдентификатор = УУИД;
			ДанныеПоставщикаККМ.ТелефонПоставщика = РеквизитыДоговора.ТелефонПоставщика;
			ДанныеПоставщикаККМ.НаименованиеПоставщика = Строка(РеквизитыДоговора.Агент);
			ДанныеПоставщикаККМ.ИННПоставщика = РеквизитыДоговора.ИННПоставщикаУслуг;
			
			КодПоставщика = Код;
			
			ПрайсЛистККМ.ДанныеПоставщиков.Добавить(ДанныеПоставщикаККМ);
		КонецЕсли;
		
		КодАгента = Неопределено;
		Если ЗначениеЗаполнено(РеквизитыДоговора.ПризнакАгента)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.ОперацияПлатежногоАгента)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.ТелефонПлатежногоАгента)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.ТелефонОператораПеревода)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.НаименованиеОператораПеревода)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.АдресОператораПеревода)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.ИННОператораПеревода)
			ИЛИ ЗначениеЗаполнено(РеквизитыДоговора.ТелефонОператораПоПриемуПлатежей) Тогда
			
			ДанныеАгентаККМ = МенеджерОфлайнОборудования.ПолучитьЗаписьДанныхАгента();
			ДанныеАгентаККМ.Код = Код;
			ДанныеАгентаККМ.УникальныйИдентификатор = УУИД;
			
			ДанныеАгентаККМ.ПризнакАгента 						= РеквизитыДоговора.ПризнакАгента;
			ДанныеАгентаККМ.ОперацияПлатежногоАгента 			= РеквизитыДоговора.ОперацияПлатежногоАгента;
			ДанныеАгентаККМ.ТелефонПлатежногоАгента 			= РеквизитыДоговора.ТелефонПлатежногоАгента;
			ДанныеАгентаККМ.ТелефонОператораПеревода 			= РеквизитыДоговора.ТелефонОператораПеревода;
			ДанныеАгентаККМ.НаименованиеОператораПеревода 		= РеквизитыДоговора.НаименованиеОператораПеревода;
			ДанныеАгентаККМ.АдресОператораПеревода 				= РеквизитыДоговора.АдресОператораПеревода;
			ДанныеАгентаККМ.ИННОператораПеревода 				= РеквизитыДоговора.ИННОператораПеревода;
			ДанныеАгентаККМ.ТелефонОператораПоПриемуПлатежей 	= РеквизитыДоговора.ТелефонОператораПоПриемуПлатежей;
			
			КодАгента = Код;
			
			ПрайсЛистККМ.ДанныеАгентов.Добавить(ДанныеАгентаККМ);
		КонецЕсли;
		
		Если НЕ КодПоставщика = Неопределено ИЛИ НЕ КодАгента = Неопределено Тогда
			
			Кэш = КэшТаблица.Добавить();
			Кэш.Ссылка = ДоговорПлатежногоАгента;
			Кэш.УИ = УУИД;
			Кэш.КодПоставщика = КодПоставщика;
			Кэш.КодАгента = КодАгента;
			Кэш.Код = Код;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Кэш.КодПоставщика) Тогда
		ЗаписьТовар.КодДанныхПоставщика = Кэш.КодПоставщика;
		ЗаписьТовар.УникальныйИдентификаторДанныхПоставщика = Кэш.УИ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Кэш.КодАгента) Тогда
		ЗаписьТовар.КодДанныхАгента = Кэш.КодПоставщика;
		ЗаписьТовар.УникальныйИдентификаторДанныхАгента = Кэш.УИ;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьУпаковку(Выборка, Запись)
	
	ЗаписьУпаковка = МенеджерОфлайнОборудования.ПолучитьЗаписьУпаковки();
	
	ЗаписатьШтрихкод(Выборка, ЗаписьУпаковка);
	
	ЗаписьУпаковка.Код      = Строка(Формат(Выборка.SKU, "ЧГ=0"));
	ЗаписьУпаковка.Цена     = Выборка.Цена;
	ЗаписьУпаковка.Остаток  = Выборка.Остаток;
	
	ЗаписьУпаковка.УникальныйИдентификатор = Выборка.Упаковка.УникальныйИдентификатор();
	ЗаписьУпаковка.Коэффициент = Выборка.КоэффициентУпаковки;
	ЗаписьУпаковка.Наименование  = Выборка.УпаковкаНаименование;
	
	Запись.Упаковки.Добавить(ЗаписьУпаковка);
	
КонецПроцедуры

Процедура ЗаписатьШтрихкод(Выборка, Запись)
	
	ШтрихкодыМассив = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Выборка.Штрихкод, " ", Истина);
	
	Для Каждого Штрихкод ИЗ ШтрихкодыМассив Цикл
		ДанныеШтрихкодаККМ = МенеджерОфлайнОборудования.ПолучитьЗаписьШтрихкода();
		ДанныеШтрихкодаККМ.Штрихкод = Штрихкод;
		
		Запись.Штрихкоды.Добавить(ДанныеШтрихкодаККМ);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьТелефонИзКонтактнойИнформации(Объект)

	Если ЗначениеЗаполнено(Объект) Тогда
	
		Если ТипЗнч(Объект) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			Окончание = "ФизическогоЛица";
			ВладелецКонтактнойИнформации = Объект;
			
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Контрагенты") Тогда
			Окончание = "Контрагента";
			ВладелецКонтактнойИнформации = Объект;
			
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ИнформационныеКарты") Тогда
			Окончание = "ИнформационнойКарты";
			ВладелецКонтактнойИнформации = Объект;
		КонецЕсли;

		ВидТелефона = Справочники.ВидыКонтактнойИнформации["Телефон" + Окончание].Ссылка;

		ТабЗн           = ВладелецКонтактнойИнформации.КонтактнаяИнформация.Выгрузить();
		НайденныеСтроки = ТабЗн.НайтиСтроки(Новый Структура("Тип,Вид", Перечисления.ТипыКонтактнойИнформации.Телефон, ВидТелефона));
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Возврат НайденныеСтроки[0].Представление;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция ПолучитьАдресЭлектроннойПочтыИзКонтактнойИнформации(Объект)

	Если ЗначениеЗаполнено(Объект) Тогда
	
		Если ТипЗнч(Объект) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			Окончание = "ФизическогоЛица";
			ВладелецКонтактнойИнформации = Объект;
			
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.Контрагенты") Тогда
			Окончание = "Контрагента";
			ВладелецКонтактнойИнформации = Объект;
			
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникСсылка.ИнформационныеКарты") Тогда
			Окончание = "ИнформационнойКарты";
			ВладелецКонтактнойИнформации = Объект;
		КонецЕсли;
		
		ВидEmail = Справочники.ВидыКонтактнойИнформации["Email" + Окончание].Ссылка;
	
		ТабЗн           = ВладелецКонтактнойИнформации.КонтактнаяИнформация.Выгрузить();
		НайденныеСтроки = ТабЗн.НайтиСтроки(Новый Структура("Тип,Вид", Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты, ВидEmail));
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			Возврат НайденныеСтроки[0].Представление;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Функция получает параметры устройства.
//
Функция ПолучитьПараметрыУстройства(Устройство) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПодключаемоеОборудование.ПравилоОбмена КАК ПравилоОбмена,
	|	ПодключаемоеОборудование.УзелИнформационнойБазы КАК УзелИнформационнойБазы,
	|	ЕСТЬNULL(ПодключаемоеОборудование.ПравилоОбмена.Склад.Магазин.ПравилоЦенообразования.ВидЦен, ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)) КАК ВидЦены,
	|	ЕСТЬNULL(ПодключаемоеОборудование.ПравилоОбмена.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад,
	|	ЕСТЬNULL(ПодключаемоеОборудование.ПравилоОбмена.МаксимальныйКодPLU, 0) КАК МаксимальныйКодPLU,
	|	ЕСТЬNULL(ПодключаемоеОборудование.ПравилоОбмена.СвояНумерацияPLUНаОборудовании, Ложь) КАК СвояНумерацияPLUНаОборудовании,
	|	ЕСТЬNULL(ПодключаемоеОборудование.ПравилоОбмена.ВыгружатьГруппыТоваров, Ложь) КАК ВыгружатьГруппыТоваров,
	|	ПодключаемоеОборудование.ТипОборудования КАК ТипОборудования
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|	ПодключаемоеОборудование.Ссылка = &Устройство";
	
	Запрос.УстановитьПараметр("Устройство", Устройство);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ПравилоОбмена",                  Выборка.ПравилоОбмена);
	ВозвращаемоеЗначение.Вставить("УзелИнформационнойБазы",         Выборка.УзелИнформационнойБазы);
	ВозвращаемоеЗначение.Вставить("Склад",                          Выборка.Склад);
	ВозвращаемоеЗначение.Вставить("ВидЦены",                        Выборка.ВидЦены);
	ВозвращаемоеЗначение.Вставить("ТипОборудования",                Выборка.ТипОборудования);
	ВозвращаемоеЗначение.Вставить("МаксимальныйКодPLU",             Выборка.МаксимальныйКодPLU);
	ВозвращаемоеЗначение.Вставить("СвояНумерацияPLUНаОборудовании", Строка(Выборка.СвояНумерацияPLUНаОборудовании));
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Функция удаляет регистрация изменений для устройства.
//
// Параметры:
//  Устройство - <СправочникСсылка.ПодключаемоеОборудование>
//
// Возвращаемое значение:
//  Нет
//
Процедура УдалитьРегистрациюИзменений(Устройство) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПодключаемоеОборудование.УзелИнформационнойБазы КАК УзелИнформационнойБазы
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|	ПодключаемоеОборудование.Ссылка = &Устройство");
	
	Запрос.УстановитьПараметр("Устройство", Устройство);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НачатьТранзакцию();
	
	Попытка
		
		Пока Выборка.Следующий() Цикл
			ПланыОбмена.УдалитьРегистрациюИзменений(Выборка.УзелИнформационнойБазы);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Функция регистрирует изменения для устройства.
//
// Параметры:
//  Устройство - <СправочникСсылка.ПодключаемоеОборудование>
//
// Возвращаемое значение:
//  Нет
//
Процедура ЗарегистрироватьИзменения(Устройство) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПодключаемоеОборудование.УзелИнформационнойБазы КАК УзелИнформационнойБазы,
	|	КодыТоваровPLUНаОборудовании.КодТовараPLU КАК КодТовараPLU,
	|	КодыТоваровPLUНаОборудовании.ПравилоОбмена КАК ПравилоОбмена
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровPLUНаОборудовании КАК КодыТоваровPLUНаОборудовании
	|		ПО ПодключаемоеОборудование.ПравилоОбмена = КодыТоваровPLUНаОборудовании.ПравилоОбмена
	|ГДЕ
	|	ПодключаемоеОборудование.Ссылка = &Устройство");
	
	Запрос.УстановитьПараметр("Устройство", Устройство);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НачатьТранзакцию();
	
	Попытка
		
		Набор = РегистрыСведений.КодыТоваровPLUНаОборудовании.СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.ПравилоОбмена) Тогда
				
				Набор.Отбор.ПравилоОбмена.Значение = Выборка.ПравилоОбмена;
				Набор.Отбор.ПравилоОбмена.Использование = Истина;
				
				Набор.Отбор.КодТовараPLU.Значение = Выборка.КодТовараPLU;
				Набор.Отбор.КодТовараPLU.Использование = Истина;
				
				ПланыОбмена.ЗарегистрироватьИзменения(Выборка.УзелИнформационнойБазы, Набор);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

// Функция регистрирует изменения для правила обмена.
//
Процедура ЗарегистрироватьИзмененияДляПравилаОбмена(ПравилоОбмена) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПодключаемоеОборудование.УзелИнформационнойБазы КАК УзелИнформационнойБазы
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|	ПодключаемоеОборудование.ПравилоОбмена = &ПравилоОбмена");
	
	Запрос.УстановитьПараметр("ПравилоОбмена", ПравилоОбмена);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();

	НачатьТранзакцию();
	
	Попытка
		
		Пока Выборка.Следующий() Цикл
			ПланыОбмена.ЗарегистрироватьИзменения(Выборка.УзелИнформационнойБазы, Метаданные.РегистрыСведений.КодыТоваровPLUНаОборудовании);
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

// Процедура вызывается при очистке товаров в устройстве.
// Выполняет запись информации в узел плана обмена.
//
// Параметры:
//  Устройство       - <СправочникСсылка.ПодключаемоеОборудование>
//  ВыполненоУспешно - <Булево> Признак успешного выполнения операции.
//
// Возвращаемое значение:
//  Нет
//
Процедура ПриОчисткеТоваровВУстройстве(Устройство, ВыполненоУспешно = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПодключаемоеОборудование.УзелИнформационнойБазы КАК УзелИнформационнойБазы
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|	ПодключаемоеОборудование.Ссылка = &Устройство");
	
	Запрос.УстановитьПараметр("Устройство", Устройство);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НачатьТранзакцию();
	
	Попытка
		
		Пока Выборка.Следующий() Цикл
			УзелОбъект = Выборка.УзелИнформационнойБазы.ПолучитьОбъект();
			УзелОбъект.ДатаВыгрузки      = ТекущаяДатаСеанса();
			УзелОбъект.ВыгрузкаВыполнена = ВыполненоУспешно;
			УзелОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Процедура вызывается при выгрузке товаров в устройство.
// Выполняет запись информации в узел плана обмена.
//
// Параметры:
//  Устройство       - <СправочникСсылка.ПодключаемоеОборудование>
//  ВыполненоУспешно - <Булево> Признак успешного выполнения операции.
//
// Возвращаемое значение:
//  Нет
//
Процедура ПриВыгрузкеТоваровВУстройство(Устройство, СтруктураДанные, ВыгружатьИзменения, ВыполненоУспешно = Истина, РасширеннаяВыгрузка = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПодключаемоеОборудование.УзелИнформационнойБазы  КАК УзелИнформационнойБазы,
	|	ПодключаемоеОборудование.ПравилоОбмена           КАК ПравилоОбмена,
	|	ПодключаемоеОборудование.ТипОборудования         КАК ТипОборудования
	|ИЗ
	|	Справочник.ПодключаемоеОборудование КАК ПодключаемоеОборудование
	|ГДЕ
	|	ПодключаемоеОборудование.Ссылка = &Устройство");
	
	Запрос.УстановитьПараметр("Устройство", Устройство);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	НачатьТранзакцию();
	
	Попытка
		
		Пока Выборка.Следующий() Цикл
			
			Если ВыполненоУспешно И СтруктураДанные <> Неопределено Тогда
					
					ПланыОбмена.УдалитьРегистрациюИзменений(Выборка.УзелИнформационнойБазы);
					
			КонецЕсли;
			
			УзелОбъект = Выборка.УзелИнформационнойБазы.ПолучитьОбъект();
			УзелОбъект.ДатаВыгрузки      = ТекущаяДатаСеанса();
			УзелОбъект.ВыгрузкаВыполнена = ВыполненоУспешно;
			УзелОбъект.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Процедура вызывается при загрузке отчета о проверке ценников с устройства.
// Выполняет запись информации в узел плана обмена. Создает отчет о розничных продажах.
//
// Параметры:
//  Устройство       - <СправочникСсылка.ПодключаемоеОборудование>
//  ВыполненоУспешно - <Булево> Признак успешного выполнения операции.
//
// Возвращаемое значение:
//  Нет
//
Процедура ЗагрузитьЦенники(Устройство, МассивДанных, ВыполненоУспешно = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивСозданныхДокументов = Новый Массив;
	НаборЗаписей = РегистрыСведений.ЦенникиКПечати.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	ДатаПриема = ТекущаяДата();
	
	Товары = ПолучитьДанныеОТоварах(МассивДанных, Устройство.СохранятьЦенникиВКонфигурации);
	
	Для Каждого Товар Из Товары Цикл
		
		Запись = НаборЗаписей.Добавить();
		Запись.Номенклатура             = Товар.Номенклатура;
		Запись.Упаковка                 = Товар.Упаковка;
		Запись.Характеристика           = Товар.Характеристика;
		Запись.ДатаПриема               = ДатаПриема;
		Запись.ИДЦенника                = Новый УникальныйИдентификатор;
		Запись.ПодключаемоеОборудование = Устройство;
		Запись.ШтрихАвтопроверки        = Товар.ШтрихАвтопроверки;
		
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	ВыполненоУспешно = Истина;
	
КонецПроцедуры

Процедура ЗарегистрироватьИзмененияРегистраНакопленияДляОбменаСОборудованиемOfflineПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	ОбменДаннымиСобытия.МеханизмРегистрацииОбъектовПередЗаписьюРегистра("ОбменСПодключаемымОборудованием", Источник, Отказ, Замещение);
	
КонецПроцедуры

Функция ДоступностьРаботыСКодамиТоваровSKU() Экспорт
	
	Доступность = НЕ ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("SKUУстанавливаетсяВГлавномУзлеРИБ")
		ИЛИ (ПланыОбмена.ГлавныйУзел() = Неопределено);
		
	Возврат Доступность;
	
КонецФункции

Процедура СохранитьПараметрыПрайсЧекера(Идентификатор, ПринтерПечати, ШаблонЦенника, СохранятьЦенники) Экспорт
	
	Объект = Идентификатор.ПолучитьОбъект();
	Объект.ПринтерПечати = ПринтерПечати;
	Объект.ШаблонЦенника = ШаблонЦенника;
	Объект.СохранятьЦенникиВКонфигурации = СохранятьЦенники;
	Объект.Записать();
	
КонецПроцедуры

// Функция возвращает параметры выбора для поля ввода ПравилоОбмена.
//
Функция ПолучитьПараметрыВыбораПравилаОбмена(ПодключаемоеОборудованиеОбъект) Экспорт
	
	Если ПодключаемоеОборудованиеОбъект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипПодключаемогоОборудования", Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток);
	ИначеЕсли ПодключаемоеОборудованиеОбъект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККМОфлайн Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипПодключаемогоОборудования", Перечисления.ТипыПодключаемогоОборудования.ККМОфлайн);
	ИначеЕсли ПодключаемоеОборудованиеОбъект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.УдалитьWebСервисОборудование Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипПодключаемогоОборудования", Перечисления.ТипыПодключаемогоОборудования.УдалитьWebСервисОборудование);
	КонецЕсли;
	
	ПараметрыВыбораЭлемента = Новый Массив;
	ПараметрыВыбораЭлемента.Добавить(НовыйПараметр);
	Возврат Новый ФиксированныйМассив(ПараметрыВыбораЭлемента);
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейсВыгрузкаТоваров

// Обновить цены для списка товаров.
//
Процедура ОбновитьЦены(ТаблицаТоваров, Магазин) Экспорт
	
	Если ТаблицаТоваров <> Неопределено Тогда
		
		ТаблицаТоваров.Колонки.Добавить("НомерСтроки" , Новый ОписаниеТипов("Число"));
		
		Для Индекс = 0 По ТаблицаТоваров.Количество() - 1 Цикл
			ТаблицаТоваров[Индекс].НомерСтроки = Индекс;
		КонецЦикла;
		
		Запрос = Новый Запрос;
		ЗапасыСервер.СформироватьЗапросВременнаяТаблицаТовары(Запрос.Текст);
		ЗапасыСервер.СформироватьЗапросЦеныПоРозничнымЦенам(Запрос.Текст, Магазин);
		
		СтруктураРеквизитов = Новый Структура;
		СтруктураРеквизитов.Вставить("ФорматМагазина");
		СтруктураРеквизитов.Вставить("ПравилоЦенообразования");
		СтруктураРеквизитов.Вставить("ИспользоватьПрименениеЦен");
		СтруктураРеквизитов.Вставить("ВидМинимальныхЦенПродажи");
		РеквизитыМагазина = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Магазин, СтруктураРеквизитов);
		
		Запрос.УстановитьПараметр("Товары", ТаблицаТоваров); 
		Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("Магазин", Магазин);
		Запрос.УстановитьПараметр("ФорматОбъектаЦенообразования", РеквизитыМагазина.ФорматМагазина);
		Запрос.УстановитьПараметр("ПравилоЦенообразования", РеквизитыМагазина.ПравилоЦенообразования);
		Запрос.УстановитьПараметр("ИспользоватьПрименениеЦен", РеквизитыМагазина.ИспользоватьПрименениеЦен);
		Запрос.УстановитьПараметр("ПриводитьКМинимальнойЦене", ЗначениеЗаполнено(РеквизитыМагазина.ВидМинимальныхЦенПродажи));
		Запрос.УстановитьПараметр("ВидМинимальныхЦенПродажи", РеквизитыМагазина.ВидМинимальныхЦенПродажи);
		
		ТаблицаЦен = Запрос.Выполнить().Выгрузить();
		ТаблицаЦен.Индексы.Добавить("НомерСтроки");
		
		Если ТаблицаЦен.Количество() > 0 Тогда
			Для Каждого ТекСтрока Из ТаблицаТоваров Цикл
				
				ЦенаНайдена = Ложь;
			
				НайденнаяСтрока = ТаблицаЦен.Найти(ТекСтрока.НомерСтроки, "НомерСтроки");
				ЦенаНайдена = (НайденнаяСтрока <> Неопределено);
			
				Если НЕ ЦенаНайдена Тогда
					Продолжить;
				КонецЕсли;
					
				ТекСтрока.Цена = НайденнаяСтрока.Цена;
			
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Функция возвращает структуру с данными в формате, необходимом для выгрузки списка товаров в весы с печатью этикеток.
//
// Параметры:
//  Устройство - <СправочникСсылка.ПодключаемоеОборудование> - Устройство для которого необходимо получить данные.
//  ТолькоИзмененные - <Булево> - Флаг получения только измененных данных.
//
// Возвращаемое значение:
//  <Структура> с массивом структур для выгрузки и количеством не выгруженных строк.
//
Функция ПолучитьДанныеДляВесов(Устройство, ТолькоИзмененные = Истина) Экспорт
	
	Параметры = ПолучитьПараметрыУстройства(Устройство);
	Если Параметры = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.ПравилоОбмена) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Параметры.Вставить("ЧастичнаяВыгрузка"     , ТолькоИзмененные);
	Параметры.Вставить("ВыгружатьГруппыТоваров", Ложь);
	
	ВозвращаемоеЗначение = Новый Структура("Данные, КоличествоНеВыгруженныхСтрокСОшибками, ЧастичнаяВыгрузка, Параметры");
	ВозвращаемоеЗначение.Данные = Новый Массив();
	ВозвращаемоеЗначение.КоличествоНеВыгруженныхСтрокСОшибками = 0;
	ВозвращаемоеЗначение.ЧастичнаяВыгрузка = ТолькоИзмененные;
	ВозвращаемоеЗначение.Параметры = Параметры;
	
	Таблица = ПолучитьТоварыКВыгрузке(Устройство, Параметры);
	
	Для Каждого СтрокаТЧ Из Таблица Цикл
		
		Если СтрокаТЧ.ЕстьОшибки Тогда
			ВозвращаемоеЗначение.КоличествоНеВыгруженныхСтрокСОшибками = ВозвращаемоеЗначение.КоличествоНеВыгруженныхСтрокСОшибками + 1;
			Продолжить;
		КонецЕсли;
		
		ЭлементМассива = Новый Структура;
		ЭлементМассива.Вставить("PLU", СтрокаТЧ.PLU);
		ЭлементМассива.Вставить("Код", СтрокаТЧ.SKU);
		ЭлементМассива.Вставить("Штрихкод", "");
		ЭлементМассива.Вставить("Наименование", Строка(СтрокаТЧ.НаименованиеПолное));
		ЭлементМассива.Вставить("Цена", СтрокаТЧ.Цена);
		ЭлементМассива.Вставить("ОписаниеТовара", СтрокаТЧ.Описание);
		
		ВозвращаемоеЗначение.Данные.Добавить(ЭлементМассива);
		
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;

КонецФункции

// Функция возвращает таблицу товаров с данными о товарам для правила выгрузки с ценами.
//
// Параметры:
//  ПравилоОбмена - <СправочникСсылка.ПравилаОбменаСПодключаемымОборудованием>
//  ВидЦены - <СправочникСсылка.ВидыЦен>
//
// Возвращаемое значение:
//  <ТаблицаЗначений>
//
Функция ПолучитьТоварыДляПравила(ПравилоОбмена) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВерхняяГраница = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("ВерхняяГраницаДиапазонаSKUВесовогоТовара");
	НижняяГраница  = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("НижняяГраницаДиапазонаSKUВесовогоТовара");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КодыТоваровPLUНаОборудовании.КодТовараPLU КАК КодТовараPLU,
	|	КодыТоваровPLUНаОборудовании.КодТовараSKU КАК КодТовараSKU,
	|	КодыТоваровSKU.Номенклатура КАК Номенклатура,
	|	
	|	ЕСТЬNULL(КодыТоваровSKU.Номенклатура.Наименование,"""")       КАК НоменклатураНаименование,
	|	ЕСТЬNULL(КодыТоваровSKU.Номенклатура.НаименованиеПолное,"""") КАК НоменклатураНаименованиеПолное,
	|	
	|	КодыТоваровSKU.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(КодыТоваровSKU.Характеристика.Наименование, """")       КАК ХарактеристикаНаименование,
	|	ЕСТЬNULL(КодыТоваровSKU.Номенклатура.ЕдиницаИзмерения.Наименование, """") КАК ЕдиницаИзмеренияНаименование,
	|	
	|	КодыТоваровSKU.Упаковка КАК Упаковка,
	|	ЕСТЬNULL(КодыТоваровSKU.Упаковка.Наименование, """") КАК УпаковкаНаименование,
	|	ЕСТЬNULL(Штрихкоды.Штрихкод, """") КАК Штрихкод,
	|	
	|	КодыТоваровSKU.Номенклатура.Весовой КАК Весовой
	|	
	|ИЗ
	|	РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровPLUНаОборудовании КАК КодыТоваровPLUНаОборудовании
	|		ПО (КодыТоваровPLUНаОборудовании.КодТовараSKU = КодыТоваровSKU.SKU)
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК Штрихкоды
	|		ПО КодыТоваровSKU.Номенклатура = Штрихкоды.Владелец
	|		И КодыТоваровSKU.Характеристика = Штрихкоды.Характеристика
	|		И КодыТоваровSKU.Упаковка = Штрихкоды.Упаковка
	|ГДЕ
	|	КодыТоваровPLUНаОборудовании.ПравилоОбмена = &ПравилоОбмена
	|	И КодыТоваровSKU.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|ИТОГИ
	|	МАКСИМУМ(Штрихкод)
	|ПО
	|	КодТовараPLU");
	
	Запрос.УстановитьПараметр("ПравилоОбмена", ПравилоОбмена);
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("PLU",                Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("SKU",                Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Номенклатура",       Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТоваров.Колонки.Добавить("Характеристика",     Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Упаковка",           Новый ОписаниеТипов("СправочникСсылка.УпаковкиНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Наименование",       Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("НаименованиеПолное", Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Штрихкод",           Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Весовой",            Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("ЕстьОшибки",         Новый ОписаниеТипов("Булево"));
	
	ВыборкаПоКодам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоКодам.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		
		Выборка = ВыборкаПоКодам.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Штрихкод = СокрЛП(Выборка.Штрихкод);
			
			Если Не ЗначениеЗаполнено(НоваяСтрока.PLU) Тогда
				НоваяСтрока.PLU                = Выборка.КодТовараPLU;
				НоваяСтрока.SKU                = Выборка.КодТовараSKU;
				НоваяСтрока.Номенклатура       = Выборка.Номенклатура;
				НоваяСтрока.Характеристика     = Выборка.Характеристика;
				НоваяСтрока.Упаковка           = Выборка.Упаковка;
				НоваяСтрока.Наименование       = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(Выборка.НоменклатураНаименование, Выборка.ХарактеристикаНаименование) + ?(ЗначениеЗаполнено(Выборка.УпаковкаНаименование),", "+Выборка.УпаковкаНаименование, "");
				НоваяСтрока.НаименованиеПолное = ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(Выборка.НоменклатураНаименованиеПолное, Выборка.ХарактеристикаНаименование) + ?(ЗначениеЗаполнено(Выборка.УпаковкаНаименование),", "+Выборка.УпаковкаНаименование, "");
				НоваяСтрока.Весовой            = Выборка.Весовой;
				НоваяСтрока.Штрихкод           = Штрихкод;
			Иначе
				НоваяСтрока.Штрихкод = НоваяСтрока.Штрихкод + ", " + Штрихкод;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПравилоОбмена.ТипПодключаемогоОборудования = Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток Тогда
			Если (ПравилоОбмена.СвояНумерацияPLUНаОборудовании И НоваяСтрока.PLU > ПравилоОбмена.МаксимальныйКодPLU)
		     ИЛИ (НоваяСтрока.SKU > ВерхняяГраница) ИЛИ (НоваяСтрока.SKU < НижняяГраница) Тогда
				НоваяСтрока.ЕстьОшибки = Истина;
			КонецЕсли
		КонецЕсли;

	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТаблицаТоваров;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает таблицу товаров с данными к выгрузке в устройство.
//
// Параметры:
//  Устройство - <СправочникСсылка.ПодключаемоеОборудование> - Устройство для которого необходимо получить данные.
//  ТолькоИзмененные - <Булево> - Флаг получения только измененных данных.
//  ОбновитьКодыТоваров - <Булево> - Флаг обновления кодов товаров перед получением данных.
//
// Возвращаемое значение:
//  <ТаблицаЗначений> товаров к выгрузке.
//
Функция ПолучитьТоварыКВыгрузке(Устройство, Параметры)
	
	ПравилоОбмена = Параметры.ПравилоОбмена;
	
	РегистрироватьКОбменуПриИзмененииОстатка = Истина;
	
	ТаблицаТоваровПоПравилу = Справочники.ПравилаОбменаСПодключаемымОборудованием.СписокТоваровПоПравилу(ПравилоОбмена);
	
	АвтоматическиГенерироватьSKU = АвтоматическиГенерироватьSKU();
	
	РегистрыСведений.КодыТоваровSKU.ОбновитьКоды_SKU_PLU(ТаблицаТоваровПоПравилу, АвтоматическиГенерироватьSKU, ПравилоОбмена);
	
	ВерхняяГраница = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("ВерхняяГраницаДиапазонаSKUВесовогоТовара");
	НижняяГраница  = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("НижняяГраницаДиапазонаSKUВесовогоТовара");
	
	ВыгружатьГруппыТоваров = ?(Параметры.Свойство("ВыгружатьГруппыТоваров"), Параметры.ВыгружатьГруппыТоваров, Истина);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КодыТоваровPLUНаОборудовании.КодТовараSKU КАК SKUГруппы,
	|	КодыТоваровSKU.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ КодыГрупп
	|ИЗ
	|	РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровPLUНаОборудовании КАК КодыТоваровPLUНаОборудовании
	|		ПО (КодыТоваровPLUНаОборудовании.КодТовараSKU = КодыТоваровSKU.SKU)
	|ГДЕ
	|	КодыТоваровPLUНаОборудовании.ПравилоОбмена = &ПравилоОбмена
	|	И КодыТоваровSKU.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И КодыТоваровSKU.Номенклатура.ЭтоГруппа
	|	И &ВыгружатьГруппыТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахИзменения.Регистратор
	|ПОМЕСТИТЬ РегистраторыТоварыНаСкладах
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Изменения КАК ТоварыНаСкладахИзменения
	|ГДЕ
	|	ТоварыНаСкладахИзменения.Узел = &Узел
	|	И &РегистрироватьПриИзмененииОстатка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладах.Номенклатура,
	|	ТоварыНаСкладах.Характеристика
	|ПОМЕСТИТЬ ТоварыСИзменившемсяОстатком
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистраторыТоварыНаСкладах КАК РегистраторыТоварыНаСкладах
	|		ПО ТоварыНаСкладах.Регистратор = РегистраторыТоварыНаСкладах.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодыТоваровSKU.SKU
	|ПОМЕСТИТЬ SKU_КОбмену
	|ИЗ
	|	РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыСИзменившемсяОстатком КАК ТоварыСИзменившемсяОстатком
	|		ПО КодыТоваровSKU.Номенклатура = ТоварыСИзменившемсяОстатком.Номенклатура
	|			И КодыТоваровSKU.Характеристика = ТоварыСИзменившемсяОстатком.Характеристика
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	КодыТоваровPLUНаОборудовании.КодТовараSKU
	|ИЗ
	|	РегистрСведений.КодыТоваровPLUНаОборудовании КАК КодыТоваровPLUНаОборудовании
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			КодыТоваровPLUНаОборудованииИзменения.КодТовараPLU КАК КодТовараPLU,
	|			КодыТоваровPLUНаОборудованииИзменения.ПравилоОбмена КАК ПравилоОбмена
	|		ИЗ
	|			РегистрСведений.КодыТоваровPLUНаОборудовании.Изменения КАК КодыТоваровPLUНаОборудованииИзменения
	|		ГДЕ
	|			КодыТоваровPLUНаОборудованииИзменения.Узел = &Узел
	|			И КодыТоваровPLUНаОборудованииИзменения.ПравилоОбмена = &ПравилоОбмена) КАК PLUКОбмену
	|		ПО КодыТоваровPLUНаОборудовании.КодТовараPLU = PLUКОбмену.КодТовараPLU
	|			И КодыТоваровPLUНаОборудовании.ПравилоОбмена = PLUКОбмену.ПравилоОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КодыТоваровSKU.SKU,
	|	КодыТоваровSKU.Номенклатура,
	|	КодыТоваровSKU.Характеристика,
	|	КодыТоваровSKU.Упаковка,
	|	ВЫБОР
	|		КОГДА SKU_КОбмену.SKU ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьИзменения
	|ПОМЕСТИТЬ КодыТоваровSKU
	|ИЗ
	|	РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ЛЕВОЕ СОЕДИНЕНИЕ SKU_КОбмену КАК SKU_КОбмену
	|		ПО КодыТоваровSKU.SKU = SKU_КОбмену.SKU
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоПравилу.PLU,
	|	ТаблицаТоваровПоПравилу.SKU,
	|	ТаблицаТоваровПоПравилу.Весовой,
	|	ТаблицаТоваровПоПравилу.Группа,
	|	ТаблицаТоваровПоПравилу.КоличествоОстаток,
	|	ТаблицаТоваровПоПравилу.Номенклатура,
	|	ТаблицаТоваровПоПравилу.Упаковка,
	|	ТаблицаТоваровПоПравилу.Характеристика,
	|	ТаблицаТоваровПоПравилу.Цена,
	|	ТаблицаТоваровПоПравилу.ЭтоГруппа
	|ПОМЕСТИТЬ ТаблицаТоваровПоПравилу
	|ИЗ
	|	&ТаблицаТоваровПоПравилу КАК ТаблицаТоваровПоПравилу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровПоПравилу.PLU,
	|	ТаблицаТоваровПоПравилу.SKU,
	|	ТаблицаТоваровПоПравилу.Весовой,
	|	ТаблицаТоваровПоПравилу.Группа,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(ТаблицаТоваровПоПравилу.КоличествоОстаток, 0) КАК ЧИСЛО(15,3)) КАК КоличествоОстаток,
	|	ТаблицаТоваровПоПравилу.Номенклатура,
	|	ТаблицаТоваровПоПравилу.Упаковка,
	|	ТаблицаТоваровПоПравилу.Характеристика,
	|	ТаблицаТоваровПоПравилу.Цена,
	|	ТаблицаТоваровПоПравилу.ЭтоГруппа,
	|	СпрНоменклатура.Наименование КАК НоменклатураНаименование,
	|	СпрНоменклатура.НаименованиеПолное КАК НоменклатураНаименованиеПолное,
	|	СпрНоменклатура.Артикул КАК Артикул,
	|	СпрНоменклатура.Описание КАК Описание,
	|	СпрНоменклатура.СтавкаНДС КАК СтавкаНДС,
	|	СпрНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СпрНоменклатура.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СпрНоменклатура.ВидАлкогольнойПродукцииЕГАИС КАК ВидАлкогольнойПродукцииЕГАИС,
	|	СпрНоменклатура.ОбъемДАЛ КАК ОбъемДАЛ,
	|	СпрНоменклатура.Крепость КАК Крепость,
	|	СпрНоменклатура.ПроизводительИмпортерАлкогольнойПродукции КАК ПроизводительИмпортерАлкогольнойПродукции,
	|	ЕСТЬNULL(УпаковкиНоменклатуры.Коэффициент, 1) КАК КоэффициентУпаковки,
	|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Наименование, &ПустаяСтрока) КАК ХарактеристикаНаименование,
	|	ЕдиницыИзмерения.Наименование КАК ЕдиницаИзмеренияНаименование,
	|	ЕСТЬNULL(УпаковкиНоменклатуры.Наименование, &ПустаяСтрока) КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА SKU_КОбмену.SKU ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьИзменения
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ТаблицаТоваровПоПравилу КАК ТаблицаТоваровПоПравилу
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ТаблицаТоваровПоПравилу.Номенклатура = СпрНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|		ПО ТаблицаТоваровПоПравилу.Упаковка = УпаковкиНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ТаблицаТоваровПоПравилу.Характеристика = ХарактеристикиНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БазовыеЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО (СпрНоменклатура.Ссылка.ЕдиницаИзмерения = ЕдиницыИзмерения.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ SKU_КОбмену КАК SKU_КОбмену
	|		ПО ТаблицаТоваровПоПравилу.SKU = SKU_КОбмену.SKU
	|ГДЕ 
	|	НЕ ТаблицаТоваровПоПравилу.SKU ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.PLU КАК КодТовараPLU,
	|	ТаблицаТоваров.SKU КАК КодТовараSKU,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаТоваров.ЭтоГруппа КАК ЭтоГруппа,
	|	ТаблицаТоваров.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаТоваров.Весовой КАК Весовой,
	|	ТаблицаТоваров.КоэффициентУпаковки КАК КоэффициентУпаковки,
	|	ТаблицаТоваров.НоменклатураНаименование КАК НоменклатураНаименование,
	|	ТаблицаТоваров.НоменклатураНаименование КАК НоменклатураНаименованиеПолное,
	|	ТаблицаТоваров.Артикул КАК Артикул,
	|	ТаблицаТоваров.Описание КАК Описание,
	|	ТаблицаТоваров.ХарактеристикаНаименование КАК ХарактеристикаНаименование,
	|	ЕСТЬNULL(ТаблицаТоваров.ЕдиницаИзмеренияНаименование, &ПустаяСтрока) КАК ЕдиницаИзмеренияНаименование,
	|	ТаблицаТоваров.УпаковкаНаименование КАК УпаковкаНаименование,
	|	ЕСТЬNULL(Штрихкоды.Штрихкод, &ПустаяСтрока) КАК Штрихкод,
	|	ТаблицаТоваров.КоличествоОстаток / ВЫБОР
	|		КОГДА ТаблицаТоваров.КоэффициентУпаковки = 0
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаТоваров.КоэффициентУпаковки, 1)
	|	КОНЕЦ КАК КоличествоОстаток,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|			ТОГДА 1
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаТоваров.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|					ТОГДА 2
	|				ИНАЧЕ 3
	|			КОНЕЦ
	|	КОНЕЦ КАК КодНалога,
	|	ТаблицаТоваров.АлкогольнаяПродукция КАК Алкоголь,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ВидАлкогольнойПродукцииЕГАИС.Маркируемый
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Маркируемый,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ВидАлкогольнойПродукцииЕГАИС.Код
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КодВидаАлкогольнойПродукции,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ОбъемДАЛ * 10
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ЕмкостьТары,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.Крепость
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Крепость,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ПроизводительИмпортерАлкогольнойПродукции.ИНН
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ИННПроизводителя,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.АлкогольнаяПродукция
	|			ТОГДА ТаблицаТоваров.ПроизводительИмпортерАлкогольнойПродукции.КПП
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК КПППроизводителя,
	|	КодыГрупп.SKUГруппы КАК SKUГруппы,
	|	ТаблицаТоваров.Цена КАК Цена
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК Штрихкоды
	|		ПО ТаблицаТоваров.Номенклатура = Штрихкоды.Владелец
	|			И ТаблицаТоваров.Характеристика = Штрихкоды.Характеристика
	|			И ТаблицаТоваров.Упаковка = Штрихкоды.Упаковка
	|		ЛЕВОЕ СОЕДИНЕНИЕ КодыГрупп КАК КодыГрупп
	|		ПО ТаблицаТоваров.Группа = КодыГрупп.Номенклатура
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА &ТолькоИзмененные = ИСТИНА
	|				ТОГДА ТаблицаТоваров.ЕстьИзменения = ИСТИНА
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ВыгружатьГруппыТоваров = ЛОЖЬ
	|				ТОГДА НЕ ТаблицаТоваров.ЭтоГруппа
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|ИТОГИ
	|	МАКСИМУМ(Штрихкод)
	|ПО
	|	КодТовараPLU";
	
	Запрос.УстановитьПараметр("ТолькоИзмененные",       Параметры.ЧастичнаяВыгрузка); 
	Запрос.УстановитьПараметр("ПравилоОбмена",          Параметры.ПравилоОбмена);
	Запрос.УстановитьПараметр("Узел",                   Параметры.УзелИнформационнойБазы);
	Запрос.УстановитьПараметр("Склад",                  Параметры.ПравилоОбмена.Склад);
	Запрос.УстановитьПараметр("ВыгружатьГруппыТоваров", ВыгружатьГруппыТоваров);
	Запрос.УстановитьПараметр("РегистрироватьПриИзмененииОстатка", РегистрироватьКОбменуПриИзмененииОстатка);
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.УстановитьПараметр("ТаблицаТоваровПоПравилу", ТаблицаТоваровПоПравилу);
	Запрос.УстановитьПараметр("ВыгружатьГруппыТоваров",  ВыгружатьГруппыТоваров);
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("PLU",							Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("SKU",							Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("SKUГруппы",					Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Номенклатура",					Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТоваров.Колонки.Добавить("Характеристика",				Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Упаковка",						Новый ОписаниеТипов("СправочникСсылка.УпаковкиНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС",					Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	ТаблицаТоваров.Колонки.Добавить("ТипНоменклатуры",				Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Артикул",						Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Описание",						Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмерения",				Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ЕдиницаИзмеренияНаименование", Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Наименование",					Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("НаименованиеПолное",			Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Штрихкод",						Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("МассивШтрихкодов",				Новый ОписаниеТипов("Массив"));
	ТаблицаТоваров.Колонки.Добавить("Цена",							Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Весовой",						Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("ЭтоГруппа",					Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("Остаток",						Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("КоэффициентУпаковки",			Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("ЕстьОшибки",					Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("ИндексКартинкиЕстьИзменения",	Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("НоменклатураНаименование",		Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("УпаковкаНаименование",			Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ХарактеристикаНаименование",	Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("КодНалога",					Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Алкоголь",						Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("Маркируемый",					Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("КодВидаАлкогольнойПродукции",	Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("ЕмкостьТары",					Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Крепость",						Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("ИННПроизводителя",				Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("КПППроизводителя",				Новый ОписаниеТипов("Строка"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПоКодам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоКодам.Следующий() Цикл
		
		НоваяСтрока = ТаблицаТоваров.Добавить();
		
		Выборка = ВыборкаПоКодам.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Штрихкод = СокрЛП(Выборка.Штрихкод);
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.PLU) Тогда
				
				НоваяСтрока.PLU								= Выборка.КодТовараPLU;
				НоваяСтрока.SKU								= Выборка.КодТовараSKU;
				НоваяСтрока.SKUГруппы						= ?(ВыгружатьГруппыТоваров, Выборка.SKUГруппы, 0);
				НоваяСтрока.Наименование					= ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(Выборка.НоменклатураНаименование, Выборка.ХарактеристикаНаименование) + ?(ЗначениеЗаполнено(Выборка.УпаковкаНаименование),", " + Выборка.УпаковкаНаименование,"");
				НоваяСтрока.НаименованиеПолное				= ФормированиеПечатныхФормСервер.ПолучитьПредставлениеНоменклатурыДляПечати(Выборка.НоменклатураНаименованиеПолное, Выборка.ХарактеристикаНаименование) + ?(ЗначениеЗаполнено(Выборка.УпаковкаНаименование),", " + Выборка.УпаковкаНаименование,"");
				НоваяСтрока.Остаток							= Выборка.КоличествоОстаток;
				НоваяСтрока.ИндексКартинкиЕстьИзменения		= 0;
				
				НоваяСтрока.Штрихкод						= Штрихкод;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка,
					"Номенклатура,
					|Характеристика,
					|ХарактеристикаНаименование,
					|Упаковка,
					|Весовой,
					|ЭтоГруппа,
					|Артикул,
					|Описание,
					|ЕдиницаИзмеренияНаименование,
					|СтавкаНДС,
					|КоэффициентУпаковки,
					|ТипНоменклатуры,
					|УпаковкаНаименование,
					|НоменклатураНаименование,
					|КодНалога,
					|Алкоголь,
					|Маркируемый,
					|КодВидаАлкогольнойПродукции,
					|ЕмкостьТары,
					|Крепость,
					|ИННПроизводителя,
					|КПППроизводителя,
					|Цена");
				
			Иначе
				НоваяСтрока.Штрихкод = НоваяСтрока.Штрихкод + " " + Штрихкод;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Штрихкод) Тогда
				НоваяСтрока.МассивШтрихкодов.Добавить(Штрихкод);
			КонецЕсли;
			
		КонецЦикла;
		
		Если Параметры.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ВесыСПечатьюЭтикеток Тогда
			Если (Параметры.СвояНумерацияPLUНаОборудовании И НоваяСтрока.PLU > Параметры.МаксимальныйКодPLU)
				ИЛИ (НоваяСтрока.SKU > ВерхняяГраница) ИЛИ (НоваяСтрока.SKU < НижняяГраница) Тогда
				НоваяСтрока.ЕстьОшибки = Истина;
			КонецЕсли
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТаблицаТоваров;
	
КонецФункции

Процедура ДобавитьГруппыНоменклатуры(Товары, МассивНоменклатур, ПравилоОбмена)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СпрНоменклатура.Родитель КАК Номенклатура,
	|	МАКСИМУМ(КодыТоваровSKU.SKU) КАК SKU,
	|	СпрНоменклатура.Родитель.Родитель КАК Группа
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ПО СпрНоменклатура.Родитель = КодыТоваровSKU.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKUГруппы
	|		ПО СпрНоменклатура.Родитель.Родитель = КодыТоваровSKUГруппы.Номенклатура
	|ГДЕ
	|	СпрНоменклатура.Ссылка В(&МассивНоменклатур)
	|	И НЕ СпрНоменклатура.Родитель = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СпрНоменклатура.Родитель,
	|	КодыТоваровSKUГруппы.SKU,
	|	СпрНоменклатура.Родитель.Родитель";
	
	Запрос.УстановитьПараметр("ПравилоОбмена", ПравилоОбмена);
	Запрос.УстановитьПараметр("МассивНоменклатур", МассивНоменклатур);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = Товары.Вставить(0);
			НоваяСтрока.Номенклатура   = Выборка.Номенклатура;
			НоваяСтрока.SKU            = Выборка.SKU;
			НоваяСтрока.Группа         = Выборка.Группа;
			НоваяСтрока.ЭтоГруппа      = Истина;
			НоваяСтрока.Весовой        = Ложь;
			
		КонецЦикла;
		
		МассивНоменклатур = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Номенклатура");
		
		ДобавитьГруппыНоменклатуры(Товары, МассивНоменклатур, ПравилоОбмена);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьИЗаполнитьОтчетОПродажах(МассивСозданныхДокументов, РеквизитыККМ, ТаблицыДанных, Комментарий = "")
	
	АналитикаОперацииВозвратаОтПокупателя = ПолучитьАналитикуОперацииВозвратаОтПокупателя();
	
	ОтчетОРозничныхПродажахОбъект = Неопределено;
	
	Если ЗначениеЗаполнено(ТаблицыДанных.Реквизиты.УникальныйИдентификатор) Тогда
		
		УИ = ТаблицыДанных.Реквизиты.УникальныйИдентификатор;
		
		ОтчетСсылка = Документы.ОтчетОРозничныхПродажах.ПолучитьСсылку(УИ);
		
		ОтчетОРозничныхПродажахОбъект = ОтчетСсылка.ПолучитьОбъект();
		
		Если ОтчетОРозничныхПродажахОбъект = Неопределено Тогда
			ОтчетОРозничныхПродажахОбъект = Документы.ОтчетОРозничныхПродажах.СоздатьДокумент();
			ОтчетОРозничныхПродажахОбъект.УстановитьСсылкуНового(ОтчетСсылка);
		КонецЕсли;
		
	Иначе
		
		ОтчетОРозничныхПродажахОбъект = Документы.ОтчетОРозничныхПродажах.СоздатьДокумент();
		
	КонецЕсли;
	
	ОтчетОРозничныхПродажахОбъект.Товары.Очистить();
	ОтчетОРозничныхПродажахОбъект.ОплатаПлатежнымиКартами.Очистить();
	ОтчетОРозничныхПродажахОбъект.ВозвращенныеТовары.Очистить();
	ОтчетОРозничныхПродажахОбъект.ОплатаБанковскимиКредитами.Очистить();
	ОтчетОРозничныхПродажахОбъект.РасчетыСКлиентами.Очистить();
	ОтчетОРозничныхПродажахОбъект.СуммаОплатыНаличных = 0;
	
	ОтчетОРозничныхПродажахОбъект.Дата               = ТаблицыДанных.Реквизиты.Дата;
	ОтчетОРозничныхПродажахОбъект.Магазин            = РеквизитыККМ.Склад.Магазин;
	ОтчетОРозничныхПродажахОбъект.КассаККМ           = РеквизитыККМ.КассаККМ;
	ОтчетОРозничныхПродажахОбъект.Комментарий        = Комментарий;
	ОтчетОРозничныхПродажахОбъект.Организация        = РеквизитыККМ.Организация;
	ОтчетОРозничныхПродажахОбъект.Ответственный      = Пользователи.ТекущийПользователь();
	ОтчетОРозничныхПродажахОбъект.ЦенаВключаетНДС    = Истина;
	ОтчетОРозничныхПродажахОбъект.УчитыватьНДС       = Истина;
	
	ВыборкаПоТоварам = ПолучитьВыборкуНоменклатурыПоSKU(РеквизитыККМ.ПравилоОбмена, ТаблицыДанных.Товары);
	
	ОрганизацияЕГАИС = Справочники.КлассификаторОрганизацийЕГАИС.ОрганизацияЕГАИСПоОрганизацииИТорговомуОбъекту(
		ОтчетОРозничныхПродажахОбъект.Организация,
		ОтчетОРозничныхПродажахОбъект.Магазин
	);
	
	ТаблицаАкцизныеМарки = ПолучитьТаблицуАкцизныхМарок(ТаблицыДанных.ТаблицаШтрихкодовАкцизов, ОрганизацияЕГАИС);
	
	Пока ВыборкаПоТоварам.Следующий() Цикл
		
		Если ВыборкаПоТоварам.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоТоварам, "Номенклатура, Характеристика, Упаковка, Количество, Сумма, Цена, СтавкаНДС");
		
		НоваяСтрока.КоличествоУпаковок   = ВыборкаПоТоварам.Количество;
		НоваяСтрока.ПроцентСкидкиНаценки = ВыборкаПоТоварам.ПроцентРучнойСкидки;
		НоваяСтрока.Склад                = РеквизитыККМ.Склад;
		
		Если ЗначениеЗаполнено(ВыборкаПоТоварам.Заказ) Тогда
			
			ТаблицаТоваровЗаказа = ВыборкаПоТоварам.Заказ.Товары;
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Номенклатура", 	ВыборкаПоТоварам.Номенклатура);
			ПараметрыОтбора.Вставить("Характеристика", 	ВыборкаПоТоварам.Характеристика);
			ПараметрыОтбора.Вставить("Упаковка", 		ВыборкаПоТоварам.Упаковка);
			
			НайденныеСтроки = ТаблицаТоваровЗаказа.НайтиСтроки(ПараметрыОтбора);
			
			Если НЕ НайденныеСтроки = Неопределено И НайденныеСтроки.Количество() > 0 Тогда
				
				НоваяСтрока.ЗаказПокупателя = ВыборкаПоТоварам.Заказ;
				НоваяСтрока.КодСтроки 		= НайденныеСтроки[0].КодСтроки;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаАкциз Из ТаблицаАкцизныеМарки Цикл
		
		ШтрихкодАлкогольнойМарки = СокрЛП(СтрокаАкциз.ШтрихкодАлкогольнойМарки);
		
		Если НЕ ЗначениеЗаполнено(ШтрихкодАлкогольнойМарки) Тогда
			Продолжить;
		КонецЕсли;
		
		АкцизнаяМарка = СтрокаАкциз.АкцизнаяМарка;
		
		Если НЕ ЗначениеЗаполнено(АкцизнаяМарка) Тогда
			АкцизнаяМарка = Справочники.ШтрихкодыУпаковокТоваров.СгенерироватьАкцизнуюМарку(
			СтрокаАкциз.ШтрихкодАлкогольнойМарки,
			СтрокаАкциз.Номенклатура,
			СтрокаАкциз.Характеристика);
		КонецЕсли;
		
		НоваяСтрока = ОтчетОРозничныхПродажахОбъект.АкцизныеМарки.Добавить();
		НоваяСтрока.АкцизнаяМарка = АкцизнаяМарка;
		НоваяСтрока.Справка2 = СтрокаАкциз.Справка2;
		
	КонецЦикла;
	
	Если НЕ ТаблицыДанных.ВозвратыТоваров = Неопределено Тогда
		
		ВыборкаПоВозвратам = ПолучитьВыборкуНоменклатурыПоSKU(РеквизитыККМ.ПравилоОбмена, ТаблицыДанных.ВозвратыТоваров);
		
		Пока ВыборкаПоВозвратам.Следующий() Цикл
			
			Если ВыборкаПоВозвратам.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ОтчетОРозничныхПродажахОбъект.ВозвращенныеТовары.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоВозвратам, "Номенклатура, Характеристика, Упаковка, Количество, Сумма, Цена, СтавкаНДС");
			
			НоваяСтрока.КоличествоУпаковок   = ВыборкаПоВозвратам.Количество;
			НоваяСтрока.Склад                = РеквизитыККМ.Склад;
			НоваяСтрока.АналитикаХозяйственнойОперации = АналитикаОперацииВозвратаОтПокупателя;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ТаблицыДанных.Оплаты = Неопределено Тогда
		
		СоответствиеВидовОплаты = РеквизитыККМ.КассаККМ.СоответствиеВидовОплаты;
		
		ВидЭлектроннойОплатыПоУмолчанию = Неопределено;
		
		Если ЗначениеЗаполнено(СоответствиеВидовОплаты) Тогда
			ВидЭлектроннойОплатыПоУмолчанию = СоответствиеВидовОплаты.ВидЭлектроннойОплатыПоУмолчанию;
		КонецЕсли;
		
		Для Каждого Оплата ИЗ ТаблицыДанных.Оплаты Цикл
			
			ВидОплаты = Неопределено;
			
			Если ЗначениеЗаполнено(СоответствиеВидовОплаты) И ЗначениеЗаполнено(Оплата.КодВидаОплаты) Тогда
				ВидОплаты = Справочники.СоответствиеВидовОплатыСККМOffline.ПолучитьВидОплатыПоКодуВидаОплатыККМ(
					СоответствиеВидовОплаты, Оплата.КодВидаОплаты
				);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ВидОплаты) И Оплата.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта Тогда
				ВидОплаты = ВидЭлектроннойОплатыПоУмолчанию;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ВидОплаты) Тогда
				ВидОплаты = Справочники.ВидыОплатЧекаККМ.Наличные;
			КонецЕсли;
			
			Если ВидОплаты.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.Наличные Тогда
				
				ОтчетОРозничныхПродажахОбъект.СуммаОплатыНаличных = ОтчетОРозничныхПродажахОбъект.СуммаОплатыНаличных + Оплата.Сумма;
				
			ИначеЕсли ВидОплаты.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта Тогда
				
				ОплатаПлатежнойКартой = ОтчетОРозничныхПродажахОбъект.ОплатаПлатежнымиКартами.Добавить();
				ОплатаПлатежнойКартой.Сумма = Оплата.Сумма;
				ОплатаПлатежнойКартой.ВидОплаты = ВидОплаты;
				
			ИначеЕсли ВидОплаты.ТипОплаты = Перечисления.ТипыОплатЧекаККМ.БанковскийКредит Тогда
				
				ОплатаКредитом = ОтчетОРозничныхПродажахОбъект.ОплатаБанковскимиКредитами.Добавить();
				ОплатаКредитом.Сумма = Оплата.Сумма;
				ОплатаКредитом.ВидОплаты = ВидОплаты;
				ЭквайрингВызовСервера.ЗаполнитьБанкиПроцентКомиссии(ВидОплаты, ОплатаКредитом.БанкКредитор, ОплатаКредитом.ПроцентБанковскойКомиссии);
				ОплатаКредитом.СуммаБанковскойКомиссии = ОплатаКредитом.Сумма * ОплатаКредитом.ПроцентБанковскойКомиссии / 100;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ТаблицыДанных.ТаблицаРасчетыСКлиентами = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаРасчетыСКлиентами.ЗаказПокупателя КАК Заказ,
		|	ТаблицаРасчетыСКлиентами.Отгружено КАК Отгружено,
		|	ТаблицаРасчетыСКлиентами.Оплачено КАК Оплачено
		|ПОМЕСТИТЬ РасчетыСКлиентами
		|ИЗ
		|	&ТаблицаРасчетыСКлиентами КАК ТаблицаРасчетыСКлиентами
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТаблицаРасчетыСКлиентами.ЗаказПокупателя
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетыСКлиентамиОстатки.Магазин КАК Магазин,
		|	РасчетыСКлиентамиОстатки.Организация КАК Организация,
		|	РасчетыСКлиентамиОстатки.Контрагент КАК Контрагент,
		|	РасчетыСКлиентамиОстатки.ДокументРасчета КАК ДокументРасчета,
		|	РасчетыСКлиентамиОстатки.ЗаказПокупателя КАК Заказ,
		|	РасчетыСКлиентамиОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами.Остатки(
		|			,
		|			ЗаказПокупателя В
		|				(ВЫБРАТЬ
		|					Т.Заказ
		|				ИЗ
		|					РасчетыСКлиентами КАК Т)) КАК РасчетыСКлиентамиОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетыСКлиентами.Заказ КАК Заказ,
		|	ЗаказПокупателя.Контрагент КАК Контрагент,
		|	РасчетыСКлиентами.Отгружено КАК Отгружено,
		|	РасчетыСКлиентами.Оплачено КАК Оплачено
		|ИЗ
		|	РасчетыСКлиентами КАК РасчетыСКлиентами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПокупателя КАК ЗаказПокупателя
		|		ПО РасчетыСКлиентами.Заказ = ЗаказПокупателя.Ссылка";
		
		Запрос.УстановитьПараметр("ТаблицаРасчетыСКлиентами", ТаблицыДанных.ТаблицаРасчетыСКлиентами);
		
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		РасчетыСКлиентамиОстатки = РезультатЗапроса[1].Выгрузить();
		
		ТаблицаРасчетыСКлиентами = РезультатЗапроса[2].Выгрузить();
		
		ПараметрыОтбора = Новый Структура("Заказ");
		
		Для Каждого РасчетСКлиентом Из ТаблицаРасчетыСКлиентами Цикл
			
			ПараметрыОтбора.Заказ = РасчетСКлиентом.Заказ;
			НайденныеСтроки = РасчетыСКлиентамиОстатки.НайтиСтроки(ПараметрыОтбора);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				
				НоваяСтрока = ОтчетОРозничныхПродажахОбъект.РасчетыСКлиентами.Добавить();
				
				НоваяСтрока.ЗаказПокупателя = РасчетСКлиентом.Заказ;
				НоваяСтрока.Контрагент = РасчетСКлиентом.Контрагент;
				НоваяСтрока.Оплачено   = РасчетСКлиентом.Оплачено;
				НоваяСтрока.Отгружено  = РасчетСКлиентом.Отгружено;
				
			ИначеЕсли НайденныеСтроки.Количество() = 1 Тогда
				
				НоваяСтрока = ОтчетОРозничныхПродажахОбъект.РасчетыСКлиентами.Добавить();
				
				НоваяСтрока.ЗаказПокупателя = РасчетСКлиентом.Заказ;
				НоваяСтрока.ДокументРасчета = НайденныеСтроки[0].ДокументРасчета;
				НоваяСтрока.Контрагент = РасчетСКлиентом.Контрагент;
				НоваяСтрока.Оплачено   = РасчетСКлиентом.Оплачено;
				НоваяСтрока.Отгружено  = РасчетСКлиентом.Отгружено;
				
			ИначеЕсли НайденныеСтроки.Количество() > 1 Тогда
				
				СуммаТоваров = РасчетСКлиентом.Отгружено;
				СуммаОплаты = РасчетСКлиентом.Оплачено;
				
				НоваяСтрока = Неопределено;
				Для Каждого РасчетОстаток Из НайденныеСтроки Цикл
					
					Если СуммаТоваров > 0 Тогда
						
						НоваяСтрока = ОтчетОРозничныхПродажахОбъект.РасчетыСКлиентами.Добавить();
						
						НоваяСтрока.ЗаказПокупателя = РасчетСКлиентом.Заказ;
						НоваяСтрока.ДокументРасчета = РасчетОстаток.ДокументРасчета;
						НоваяСтрока.Контрагент = РасчетСКлиентом.Контрагент;
						НоваяСтрока.Оплачено   = 0;
						НоваяСтрока.Отгружено  = Мин(РасчетОстаток.СуммаОстаток, СуммаТоваров);
						
						СуммаТоваров = СуммаТоваров - НоваяСтрока.Отгружено;
					Иначе
						
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
				Если СуммаТоваров > 0 И НЕ НоваяСтрока = Неопределено Тогда
					НоваяСтрока.Отгружено = НоваяСтрока.Отгружено + СуммаТоваров;
				КонецЕсли;
				
				Если СуммаТоваров > 0 И НЕ НоваяСтрока = Неопределено Тогда
					НоваяСтрока.Оплачено = СуммаОплаты;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(Документы.ОтчетОРозничныхПродажах.ПараметрыУказанияСерий(ОтчетОРозничныхПродажахОбъект));
	ОбработкаТабличнойЧастиТоварыСервер.ЗаполнитьСтатусыУказанияСерий(ОтчетОРозничныхПродажахОбъект,ПараметрыУказанияСерий);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС",
		ОбработкаТабличнойЧастиТоварыКлиентСервер.ПолучитьСтруктуруПересчетаСуммыНДСВТЧ(ОтчетОРозничныхПродажахОбъект)
	);
	
	СтруктураТЧ = Новый Структура;
	СтруктураТЧ.Вставить("СтрокиТЧ" , ОтчетОРозничныхПродажахОбъект.Товары);
	
	ОбработкаТабличнойЧастиТоварыСервер.ПриИзмененииРеквизитовВТЧСервер(СтруктураТЧ, СтруктураДействий, Неопределено);
	
	Попытка
		Если ОтчетОРозничныхПродажахОбъект.ПроверитьЗаполнение() Тогда
			ОтчетОРозничныхПродажахОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Иначе
			ОтчетОРозничныхПродажахОбъект.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;
	Исключение
		ОтчетОРозничныхПродажахОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецПопытки;
	
	СуммаВыемки = ПолучитьСуммуВыемкиОтчета(ОтчетОРозничныхПродажахОбъект);
	
	Если НЕ СуммаВыемки = 0 Тогда
		
		Попытка
			
			ВыемкиДС = Документы.ВыемкаДенежныхСредствИзКассыККМ.ПолучитьВыемкиДСОтчетаОРозничныхПродажах(
				ОтчетОРозничныхПродажахОбъект.Ссылка
			);
			
			ДополнительныеПараметры = Новый Структура;
			
			Если НЕ ВыемкиДС.Количество() = 0 Тогда
				ДополнительныеПараметры.Вставить("ВыемкаСсылка", ВыемкиДС[0]);
			КонецЕсли;
			
			ДокументВыемкаДенежныхСредствСсылка = ДенежныеСредстваВызовСервера.СоздатьДокументВыемкаДенежныхСредствИзКассыККМ(
				СуммаВыемки,
				ОтчетОРозничныхПродажахОбъект.КассаККМ,
				ОтчетОРозничныхПродажахОбъект.Ссылка,
				ДополнительныеПараметры
			);
			
		Исключение
			
		КонецПопытки;
		
	КонецЕсли;
	
	УзелОбъект = РеквизитыККМ.УзелИнформационнойБазы.ПолучитьОбъект();
	УзелОбъект.ДатаЗагрузки = ТекущаяДатаСеанса();
	УзелОбъект.Записать();
	
	МассивСозданныхДокументов.Добавить(ОтчетОРозничныхПродажахОбъект.Ссылка);
	
КонецПроцедуры

Функция ПолучитьСуммуВыемкиОтчета(ОтчетОРозничныхПродажахОбъект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		&СуммаДокумента КАК Сумма
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Сумма
	|	ИЗ
	|		Документ.ОтчетОРозничныхПродажах.ОплатаПлатежнымиКартами КАК ОтчетОРозничныхПродажахОплатаПлатежнымиКартами
	|	ГДЕ
	|		ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-ОтчетОРозничныхПродажахОплатаБанковскимиКредитами.Сумма
	|	ИЗ
	|		Документ.ОтчетОРозничныхПродажах.ОплатаБанковскимиКредитами КАК ОтчетОРозничныхПродажахОплатаБанковскимиКредитами
	|	ГДЕ
	|		ОтчетОРозничныхПродажахОплатаБанковскимиКредитами.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-&ОплатаПодарочнымиСертификатами
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-ОплатаНаличнымиАгентскихПлатежей.Сумма
	|	ИЗ
	|		Документ.ОтчетОРозничныхПродажах.ОплатаНаличнымиАгентскихПлатежей КАК ОплатаНаличнымиАгентскихПлатежей
	|	ГДЕ
	|		ОплатаНаличнымиАгентскихПлатежей.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-ОтчетОРозничныхПродажахПрочаяВыручка.СуммаПоступления
	|	ИЗ
	|		Документ.ОтчетОРозничныхПродажах.ПрочаяВыручка КАК ОтчетОРозничныхПродажахПрочаяВыручка
	|	ГДЕ
	|		ОтчетОРозничныхПродажахПрочаяВыручка.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОтчетОРозничныхПродажахПрочаяВыручка.СуммаВозврата
	|	ИЗ
	|		Документ.ОтчетОРозничныхПродажах.ПрочаяВыручка КАК ОтчетОРозничныхПродажахПрочаяВыручка
	|	ГДЕ
	|		ОтчетОРозничныхПродажахПрочаяВыручка.Ссылка = &Ссылка) КАК ВложенныйЗапрос
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СУММА(ОплатаНаличнымиАгентскихПлатежей.Сумма)
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.ОплатаНаличнымиАгентскихПлатежей КАК ОплатаНаличнымиАгентскихПлатежей
	|ГДЕ
	|	ОплатаНаличнымиАгентскихПлатежей.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ОтчетОРозничныхПродажахОбъект.Ссылка);
	Запрос.УстановитьПараметр("СуммаДокумента", ОтчетОРозничныхПродажахОбъект.СуммаДокумента);
	Запрос.УстановитьПараметр("ОплатаПодарочнымиСертификатами", ОтчетОРозничныхПродажахОбъект.ОплатаПодарочнымиСертификатами);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СуммаВыемки = 0;
	Если Выборка.Следующий() Тогда
		СуммаВыемки = Выборка.Сумма;
	КонецЕсли;
	
	Возврат СуммаВыемки;
	
КонецФункции

Процедура ДополнитьМассивДочернимиЭлементами(МассивГруппТоваров, Код = Неопределено, Результат = Неопределено)
	
	// Если Код = Неопределено, то это корневой элемент иерархии.
	Если Код = Неопределено Тогда
		
		Результат = Новый Массив;
		
		Для Каждого ТекСтрока Из МассивГруппТоваров Цикл
			
			Если НЕ ЗначениеЗаполнено(ТекСтрока.КодГруппы) Тогда
				Результат.Добавить(ТекСтрока);
				ДополнитьМассивДочернимиЭлементами(МассивГруппТоваров, ТекСтрока.Код, Результат);
			КонецЕсли;
			
		КонецЦикла;
		
		МассивГруппТоваров = Результат;
		
	Иначе
		
		Для Каждого ТекСтрока Из МассивГруппТоваров Цикл
			
			Если ТекСтрока.КодГруппы = Код Тогда
				Результат.Добавить(ТекСтрока);
				ДополнитьМассивДочернимиЭлементами(МассивГруппТоваров, ТекСтрока.Код, Результат);
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьАналитикуОперацииВозвратаОтПокупателя()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	АналитикаХозяйственныхОпераций.Ссылка
	               |ИЗ
	               |	Справочник.АналитикаХозяйственныхОпераций КАК АналитикаХозяйственныхОпераций
	               |ГДЕ
	               |	АналитикаХозяйственныхОпераций.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПокупателя)
	               |	И НЕ АналитикаХозяйственныхОпераций.ПометкаУдаления";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция СформироватьКомментарий(Устройство)
	
	Комментарий = НСтр("ru = 'Загружено из ККМ Офлайн: %Устройство%'");
	Комментарий = СтрЗаменить(Комментарий, "%Устройство%", Устройство);
	
	Возврат Комментарий;
	
КонецФункции

Функция ПолучитьВыборкуНоменклатурыПоSKU(ПравилоОбмена, ТаблицаТоваров)
	
	Запрос = Новый Запрос(
	
	"ВЫБРАТЬ
	|	Товары.Код КАК Код,
	|	Товары.Цена КАК Цена,
	|	Товары.Количество КАК Количество,
	|	Товары.Скидка КАК Скидка,
	|	Товары.Сумма КАК Сумма,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.Заказ КАК Заказ
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&ТаблицаЗначений КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(КодыТоваровSKU.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЕСТЬNULL(КодыТоваровSKU.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
	|	ЕСТЬNULL(КодыТоваровSKU.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)) КАК Упаковка,
	|	Товары.Количество КАК КоличествоУпаковок,
	|	ЕСТЬNULL(КодыТоваровSKU.Упаковка.Коэффициент, 1) * Товары.Количество КАК Количество,
	|	Товары.Цена КАК Цена,
	|	Товары.Сумма КАК Сумма,
	|	Товары.Скидка КАК ПроцентРучнойСкидки,
	|	ВЫБОР
	|		КОГДА Товары.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
	|			ТОГДА КодыТоваровSKU.Номенклатура.СтавкаНДС
	|		ИНАЧЕ Товары.СтавкаНДС
	|	КОНЕЦ КАК СтавкаНДС,
	|	Товары.Заказ КАК Заказ
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ПО Товары.Код = КодыТоваровSKU.SKU");
	
	Запрос.УстановитьПараметр("ПравилоОбмена",   ПравилоОбмена);
	Запрос.УстановитьПараметр("ТаблицаЗначений", ТаблицаТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции

Функция ПолучитьТаблицуАкцизныхМарок(ТаблицаШтрихкодовАкцизов, ОрганизацияЕГАИС)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.Код КАК Код,
	|	ВЫРАЗИТЬ(Товары.ШтрихкодАлкогольнойМарки КАК СТРОКА(200)) КАК ШтрихкодАлкогольнойМарки,
	|	ВЫРАЗИТЬ(Товары.УникальныйИдентификатор КАК СТРОКА(200)) КАК УникальныйИдентификатор
	|ПОМЕСТИТЬ ВтТаблицаШтрихкодов
	|ИЗ
	|	&ТаблицаШтрихкодов КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Код КАК Код,
	|	ШтрихкодыУпаковокТоваров.Ссылка КАК АкцизнаяМарка,
	|	Товары.ШтрихкодАлкогольнойМарки КАК ШтрихкодАлкогольнойМарки,
	|	Товары.УникальныйИдентификатор КАК УникальныйИдентификатор
	|ПОМЕСТИТЬ ВтТоварыСАкцизнымиМарками
	|ИЗ
	|	ВтТаблицаШтрихкодов КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	|		ПО Товары.ШтрихкодАлкогольнойМарки = ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(КодыТоваровSKU.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЕСТЬNULL(КодыТоваровSKU.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
	|	Товары.АкцизнаяМарка КАК АкцизнаяМарка,
	|	АкцизныеМаркиЕГАИС.Справка2 КАК Справка2,
	|	Товары.ШтрихкодАлкогольнойМарки КАК ШтрихкодАлкогольнойМарки,
	|	Товары.УникальныйИдентификатор КАК УникальныйИдентификатор
	|ИЗ
	|	ВтТоварыСАкцизнымиМарками КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	|		ПО Товары.Код = КодыТоваровSKU.SKU
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АкцизныеМаркиЕГАИС КАК АкцизныеМаркиЕГАИС
	|		ПО Товары.АкцизнаяМарка = АкцизныеМаркиЕГАИС.АкцизнаяМарка
	|			И (АкцизныеМаркиЕГАИС.ОрганизацияЕГАИС = &ОрганизацияЕГАИС)";
	
	Запрос.УстановитьПараметр("ТаблицаШтрихкодов", ТаблицаШтрихкодовАкцизов);
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

Функция ПустаяТаблицаТоваров()
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Код",        Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Цена",       Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Скидка",     Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("Сумма",      Новый ОписаниеТипов("Число"));
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС",  Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	ТаблицаТоваров.Колонки.Добавить("ШтрихкодАлкогольнойМарки", Новый ОписаниеТипов("Строка"));
	ТаблицаТоваров.Колонки.Добавить("Заказ", Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя"));
	ТаблицаТоваров.Колонки.Добавить("УникальныйИдентификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаТоваров;
	
КонецФункции

Функция ПустаяТаблицаШтрихкодовАкцизов()
	
	ТаблицаШтрихкодовАкцизов = Новый ТаблицаЗначений;
	ТаблицаШтрихкодовАкцизов.Колонки.Добавить("Код",  Новый ОписаниеТипов("Число"));
	ТаблицаШтрихкодовАкцизов.Колонки.Добавить("ШтрихкодАлкогольнойМарки", Новый ОписаниеТипов("Строка"));
	ТаблицаШтрихкодовАкцизов.Колонки.Добавить("УникальныйИдентификатор", Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаШтрихкодовАкцизов;
	
КонецФункции

Функция ПустаяТаблицаОплат()
	
	ТаблицаОплат = Новый ТаблицаЗначений;
	ТаблицаОплат.Колонки.Добавить("КодВидаОплаты", Новый ОписаниеТипов("Строка"));
	ТаблицаОплат.Колонки.Добавить("Сумма",         Новый ОписаниеТипов("Число"));
	ТаблицаОплат.Колонки.Добавить("ТипОплаты",     Новый ОписаниеТипов("ПеречислениеСсылка.ТипыОплатЧекаККМ"));
	
	Возврат ТаблицаОплат;
	
КонецФункции

Функция ПустаяТаблицаРасчетыСКлиентами()
	
	ТаблицаРасчетыСКлиентами = Новый ТаблицаЗначений;
	
	ТаблицаРасчетыСКлиентами.Колонки.Добавить("ЗаказПокупателя", Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя"));
	ТаблицаРасчетыСКлиентами.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаРасчетыСКлиентами.Колонки.Добавить("Отгружено", Новый ОписаниеТипов("Число"));
	ТаблицаРасчетыСКлиентами.Колонки.Добавить("Оплачено", Новый ОписаниеТипов("Число"));
	
	Возврат ТаблицаРасчетыСКлиентами;
	
КонецФункции

Функция АвтоматическиГенерироватьSKU()
	
	Если ДоступностьРаботыСКодамиТоваровSKU()
		И ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("АвтоматическиГенерироватьSKU") Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьДанныеОТоварах(Товары, СохранятьЦенникиВКонфигурации)
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ТаблицаТоваров.Код КАК Код,";
	Если Не СохранятьЦенникиВКонфигурации Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	ТаблицаТоваров.Штрихкод КАК Штрихкод,
		|	ТаблицаТоваров.КоличествоЦенников КАК КоличествоЦенников,"
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	               |	ТаблицаТоваров.ШтрихАвтопроверки КАК ШтрихАвтопроверки
	               |ПОМЕСТИТЬ Товары
	               |ИЗ
	               |	&ТаблицаКодовТоваров КАК ТаблицаТоваров
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(КодыТоваровSKU.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	               |	ЕСТЬNULL(КодыТоваровSKU.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
	               |	ЕСТЬNULL(КодыТоваровSKU.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиНоменклатуры.ПустаяСсылка)) КАК Упаковка,
	               |	КодыТоваровSKU.SKU КАК SKU,
	               |	ЕСТЬNULL(КодыТоваровSKU.Номенклатура.ВидНоменклатуры.ШаблонЦенника, ЗНАЧЕНИЕ(Справочник.ХранилищеШаблонов.ПустаяСсылка)) КАК ШаблонЦенника,";
	Если Не СохранятьЦенникиВКонфигурации Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	Товары.Штрихкод КАК Штрихкод,
		|	Товары.КоличествоЦенников КАК КоличествоЦенников,";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	               |	Товары.ШтрихАвтопроверки КАК ШтрихАвтопроверки
	               |ИЗ
	               |	Товары КАК Товары
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	               |		ПО Товары.Код = КодыТоваровSKU.SKU";
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ТаблицаКодовТоваров", Товары);
	
	ДанныеОТоварах = Новый ТаблицаЗначений;
	ДанныеОТоварах.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ДанныеОТоварах.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ДанныеОТоварах.Колонки.Добавить("Упаковка", Новый ОписаниеТипов("СправочникСсылка.УпаковкиНоменклатуры"));
	ДанныеОТоварах.Колонки.Добавить("Штрихкод", Новый ОписаниеТипов("Строка"));
	ДанныеОТоварах.Колонки.Добавить("ШтрихАвтопроверки", Новый ОписаниеТипов("Строка"));
	ДанныеОТоварах.Колонки.Добавить("ШаблонЦенника", Новый ОписаниеТипов("СправочникСсылка.ХранилищеШаблонов"));
	ДанныеОТоварах.Колонки.Добавить("КоличествоЦенников", Новый ОписаниеТипов("Число"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаТовар = ДанныеОТоварах.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТовар, Выборка);
		
	КонецЦикла;
	
	Возврат ДанныеОТоварах;
	
КонецФункции

Функция ПроверитьТипШтрихкода(Штрихкод)
	
	ИспользуетсяШтрихАвтопроверки = Ложь;
	
	ТипШтрихкодаЦенника = "";
	ДлинаШтрихкода = СтрДлина(Штрихкод);
	НомерСимвола = 1;
	
	КодИдентификатора = Лев(Штрихкод, 2);
	
	Если КодИдентификатора = "99" Тогда
		НомерСимвола = НомерСимвола + 11;
		КодИдентификатора = Сред(Штрихкод, НомерСимвола, 4);
		Если КодИдентификатора = "392N" Тогда
			ИспользуетсяШтрихАвтопроверки = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИспользуетсяШтрихАвтопроверки;
	
КонецФункции

Функция ПолучитьШтрихкодПоКодуАвтопроверки(ШтрихкодАвтопроверки)
	
	SKU = Число(Сред(ШтрихкодАвтопроверки, 3, 9));
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	КодыТоваровSKU.Номенклатура КАК Номенклатура,
	               |	КодыТоваровSKU.Характеристика КАК Характеристика,
	               |	КодыТоваровSKU.Упаковка КАК Упаковка,
	               |	КодыТоваровSKU.SKU КАК SKU
	               |ПОМЕСТИТЬ НоменклатураПОSKU
	               |ИЗ
	               |	РегистрСведений.КодыТоваровSKU КАК КодыТоваровSKU
	               |ГДЕ
	               |	КодыТоваровSKU.SKU = &SKU
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Штрихкоды.Штрихкод КАК Штрихкод
	               |ИЗ
	               |	НоменклатураПОSKU КАК НоменклатураПОSKU
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Штрихкоды КАК Штрихкоды
	               |		ПО НоменклатураПОSKU.Номенклатура = Штрихкоды.Владелец
	               |			И НоменклатураПОSKU.Характеристика = Штрихкоды.Характеристика
	               |			И НоменклатураПОSKU.Упаковка = Штрихкоды.Упаковка";
	
	Запрос.УстановитьПараметр("SKU", SKU);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаЗапроса   = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		Штрихкоды = РезультатЗапроса.Выгрузить();
		Штрихкод  = Штрихкоды[0].Штрихкод;
		
	КонецЦикла;
	
	Возврат Штрихкод;
	
КонецФункции

#КонецОбласти
