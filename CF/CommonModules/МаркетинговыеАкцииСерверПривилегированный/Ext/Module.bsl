
#Область ПрограммныйИнтерфейс

// Общая процедура проверки реализации и погашения серийных номеров.
//
// Параметры:
//  ДокументОбъект - объект проводимого документа документа.
//  ИмяТабличнойЧасти - Имя табличной части Товары.
//  ИмяТабличнойЧастиСерийныеНомера - Имя табличной части Серийные номера.
//  РежимПроведения - режим проведения документа.
//  Отказ - Переменная отвечающая за прерывание проведения.
//  Заголовок - Заголовок при ошибке.
//  Дата - дата проверки
//
Процедура ПроверитьДвиженияСерийныхНомеров(
	СтруктураДокумента,
	Отказ,
	ТекстОшибки = "",
	Дата = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДокументОбъект = СтруктураДокумента.ДокументОбъект;
	ЭтоДокумент = СтруктураДокумента.ЭтоДокумент;
	ЭтоНовый = СтруктураДокумента.ЭтоНовый;
	ИмяТабличнойЧасти = СтруктураДокумента.ИмяТабличнойЧасти;
	ТаблицаЗначений_Товары = СтруктураДокумента.ТаблицаЗначений_Товары;
	ТаблицаЗначений_СерийныеНомера = СтруктураДокумента.ТаблицаЗначений_СерийныеНомера;
	Если ТаблицаЗначений_СерийныеНомера.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Товары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ ТабТоварыВСЕ
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура,
	|	ТабТовары.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров,
	|	ТабТовары.НомерСтроки
	|ПОМЕСТИТЬ ТабТовары
	|ИЗ
	|	ТабТоварыВСЕ КАК ТабТовары
	|ГДЕ
	|	ТабТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|	И ТабТовары.Номенклатура.ИспользоватьСерийныеНомера
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СерийныеНомера.СерийныйНомер КАК Справочник.СерийныеНомера) КАК СерийныйНомер,
	|	СерийныеНомера.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров
	|ПОМЕСТИТЬ ТабСерийныеНомераВсе
	|ИЗ
	|	&СерийныеНомера КАК СерийныеНомера
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|	СерийныеНомера.КлючСвязиСерийныхНомеров КАК КлючСвязиСерийныхНомеров
	|ПОМЕСТИТЬ ТабСерийныеНомера
	|ИЗ
	|	ТабСерийныеНомераВсе КАК СерийныеНомера
	|ГДЕ
	|	(НЕ СерийныеНомера.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДвиженияСерийныхНомеров.Номенклатура,
	|	ДвиженияСерийныхНомеров.СерийныйНомер КАК СерийныйНомер,
	|	ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации,
	|	ВЫБОР
	|		КОГДА ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов)
	|			ТОГДА ДвиженияСерийныхНомеров.Получатель.Магазин
	|		ИНАЧЕ ДвиженияСерийныхНомеров.Отправитель.Магазин
	|	КОНЕЦ КАК Магазин,
	|	ДвиженияСерийныхНомеров.Период КАК ДатаОперации,
	|	ДвиженияСерийныхНомеров.Регистратор
	|ПОМЕСТИТЬ ДвиженияСерийныхНомеров
	|ИЗ
	|	РегистрСведений.ДвиженияСерийныхНомеров КАК ДвиженияСерийныхНомеров
	|ГДЕ
	|	ДвиженияСерийныхНомеров.Период < &Дата И &УсловиеПоРегистратору
	|			И ДвиженияСерийныхНомеров.СерийныйНомер В
	|				(ВЫБРАТЬ
	|					ТабСерийныеНомера.СерийныйНомер
	|				ИЗ
	|					ТабСерийныеНомера КАК ТабСерийныеНомера)
	|			И (ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.РеализацияТоваров)
	|				ИЛИ ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов)
	|				ИЛИ ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаЗатраты)
	|				ИЛИ ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПоИнвентаризации))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура,
	|	ТабТовары.НомерСтроки,
	|	ТабСерийныеНомера.СерийныйНомер
	|ПОМЕСТИТЬ ТоварыСерийныеНомера
	|ИЗ
	|	ТабТовары КАК ТабТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТабСерийныеНомера КАК ТабСерийныеНомера
	|		ПО ТабТовары.КлючСвязиСерийныхНомеров = ТабСерийныеНомера.КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	ТоварыСерийныеНомера.Номенклатура.Наименование КАК Наименование,
	|	ТоварыСерийныеНомера.НомерСтроки КАК НомерСтроки,
	|	ТоварыСерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|	ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации КАК АналитикаХозяйственнойОперации,
	|	ДвиженияСерийныхНомеров.Магазин,
	|	ДвиженияСерийныхНомеров.ДатаОперации,
	|	ВЫБОР
	|		КОГДА ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.РеализацияТоваров)
	|			ТОГДА ""Реализован""
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов)
	|					ТОГДА ""Погашен""
	|				ИНАЧЕ ""Списан""
	|			КОНЕЦ
	|	КОНЕЦ КАК Действие
	|ИЗ
	|	ТоварыСерийныеНомера КАК ТоварыСерийныеНомера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияСерийныхНомеров КАК ДвиженияСерийныхНомеров
	|		ПО ТоварыСерийныеНомера.СерийныйНомер = ДвиженияСерийныхНомеров.СерийныйНомер
	|ГДЕ
	|	(НЕ ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации ЕСТЬ NULL )
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	СерийныйНомер,
	|	Наименование,
	|	АналитикаХозяйственнойОперации";
	
	Запрос.УстановитьПараметр("Товары", ТаблицаЗначений_Товары);
	Запрос.УстановитьПараметр("СерийныеНомера", ТаблицаЗначений_СерийныеНомера);
	
	Если ЭтоДокумент Тогда 
		Запрос.УстановитьПараметр("Регистратор", ДокументОбъект.Ссылка);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоРегистратору", "ДвиженияСерийныхНомеров.Регистратор <> &Регистратор");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоРегистратору", "ИСТИНА");
	КонецЕсли;
	Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗапроса = РезультатЗапроса.Выгрузить();
	ТаблицаОбщая = ТаблицаЗапроса.Скопировать();
	ТаблицаОбщая.Свернуть("НомерСтроки, СерийныйНомер, Наименование");
	
	Для Каждого СтрокаОбщая Из ТаблицаОбщая Цикл
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номер подарочного сертификата номенклатуры ""%1"" (строка №%2) %3'"),
			СтрокаОбщая.Наименование,
			СтрокаОбщая.НомерСтроки,
			СтрокаОбщая.СерийныйНомер);
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("НомерСтроки"  ,СтрокаОбщая.НомерСтроки);
		СтруктураПоиска.Вставить("СерийныйНомер",СтрокаОбщая.СерийныйНомер);
		СтруктураПоиска.Вставить("Наименование" ,СтрокаОбщая.Наименование);
		
		СтрокиТаблицы = ТаблицаЗапроса.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
			
			СтрокаСообщения = "    " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 в магазине %2 Дата %3'"),
				СтрокаТаблицы.Действие,
				СтрокаТаблицы.Магазин,
				Формат(СтрокаТаблицы.ДатаОперации, "ДЛФ=D"));
			
			Текст = Текст + Символы.ПС + СтрокаСообщения;
		КонецЦикла;
		
		Если ЭтоДокумент Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДокументОбъект,
				ИмяТабличнойЧасти+"[" + (СтрокаОбщая.НомерСтроки - 1) + "].Номенклатура" ,
				,
				Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
		ТекстОшибки = ТекстОшибки + Символы.ПС + Текст;
		
	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Общая процедура проверки реализации и погашения серийных номеров.
//
// Параметры:
//  ДокументОбъект - объект проводимого документа документа.
//  РежимПроведения - режим проведения документа.
//  Отказ - Переменная отвечающая за прерывание проведения.
//  Заголовок - Заголовок при ошибке.
//
Процедура ПроверитьДвиженияСерийныхНомеровДляПогашения(
	СтруктураДокумента,
	Отказ,
	ТекстОшибки = "",
	Дата = Неопределено) Экспорт
	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДокументОбъект = СтруктураДокумента.ДокументОбъект;
	ЭтоДокумент = СтруктураДокумента.ЭтоДокумент;
	ЭтоНовый = СтруктураДокумента.ЭтоНовый;
	ТабличнаяЧасть = СтруктураДокумента.ТабличнаяЧасть;
	ТабличнаяЧасть_ПогашениеПодарочныхСертификатов = СтруктураДокумента.ТабличнаяЧасть_ПогашениеПодарочныхСертификатов;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СерийныеНомера.СерийныйНомер КАК Справочник.СерийныеНомера) КАК СерийныйНомер,
	|	СерийныеНомера.НомерСтроки
	|ПОМЕСТИТЬ ТабСерийныеНомераВсе
	|ИЗ
	|	&СерийныеНомера КАК СерийныеНомера
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабСерийныеНомераВсе.СерийныйНомер КАК СерийныйНомер,
	|	ТабСерийныеНомераВсе.НомерСтроки
	|ПОМЕСТИТЬ ТабСерийныеНомера
	|ИЗ
	|	ТабСерийныеНомераВсе КАК ТабСерийныеНомераВсе
	|ГДЕ
	|	(НЕ ТабСерийныеНомераВсе.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации,
	|	ДвиженияСерийныхНомеров.Период КАК ДатаОперации,
	|	ДвиженияСерийныхНомеров.Получатель.Магазин КАК Магазин,
	|	ДвиженияСерийныхНомеров.Регистратор,
	|	ДвиженияСерийныхНомеров.СерийныйНомер
	|ПОМЕСТИТЬ ДвиженияСерийныхНомеров
	|ИЗ
	|	РегистрСведений.ДвиженияСерийныхНомеров КАК ДвиженияСерийныхНомеров
	|ГДЕ
	|	ДвиженияСерийныхНомеров.Период < &Дата
	|	И &УсловиеПоРегистратору
	|	И ДвиженияСерийныхНомеров.СерийныйНомер В
	|			(ВЫБРАТЬ
	|				ТабСерийныеНомера.СерийныйНомер
	|			ИЗ
	|				ТабСерийныеНомера КАК ТабСерийныеНомера)
	|	И ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации,
	|	ДвиженияСерийныхНомеров.ДатаОперации КАК ДатаОперации,
	|	ДвиженияСерийныхНомеров.Магазин КАК Магазин,
	|	ТабСерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|	ТабСерийныеНомера.СерийныйНомер.Владелец КАК Номенклатура,
	|	ТабСерийныеНомера.НомерСтроки
	|ИЗ
	|	ТабСерийныеНомера КАК ТабСерийныеНомера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияСерийныхНомеров КАК ДвиженияСерийныхНомеров
	|		ПО ТабСерийныеНомера.СерийныйНомер = ДвиженияСерийныхНомеров.СерийныйНомер";
	
	Запрос.УстановитьПараметр("СерийныеНомера", ТабличнаяЧасть);
	
	Если ЭтоДокумент Тогда 
		Запрос.УстановитьПараметр("Регистратор", ДокументОбъект.Ссылка);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоРегистратору", "ДвиженияСерийныхНомеров.Регистратор <> &Регистратор");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоРегистратору", "ИСТИНА");
	КонецЕсли;
	Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
	
	Результат = Запрос.Выполнить();
	ТаблицаЗапроса = Результат.Выгрузить();
	
	Для Каждого СтрокаТаблицыЗапроса Из ТаблицаЗапроса Цикл
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номер подарочного сертификата %1 уже погашен. В магазине %2 Дата %3'"),
			СтрокаТаблицыЗапроса.СерийныйНомер,
			СтрокаТаблицыЗапроса.Магазин,
			Формат(СтрокаТаблицыЗапроса.ДатаОперации, "ДЛФ=D"));
		
		Если ЭтоДокумент Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДокументОбъект,
				,
				,
				Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
		ТекстОшибки = ТекстОшибки + Символы.ПС + Текст;
		
	КонецЦикла;
	
	ПогашенныеСерийныеНомера = ТаблицаЗапроса.ВыгрузитьКолонку("СерийныйНомер");
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СерийныеНомера.СерийныйНомер КАК Справочник.СерийныеНомера) КАК СерийныйНомер,
	|	СерийныеНомера.НомерСтроки
	|ПОМЕСТИТЬ ТабСерийныеНомераВсе
	|ИЗ
	|	&СерийныеНомера КАК СерийныеНомера
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабСерийныеНомераВсе.СерийныйНомер КАК СерийныйНомер,
	|	ТабСерийныеНомераВсе.НомерСтроки
	|ПОМЕСТИТЬ ТабСерийныеНомера
	|ИЗ
	|	ТабСерийныеНомераВсе КАК ТабСерийныеНомераВсе
	|ГДЕ
	|	(НЕ ТабСерийныеНомераВсе.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка))
	|	И (НЕ ТабСерийныеНомераВсе.СерийныйНомер В (&ПогашенныеСерийныеНомера))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации,
	|	ДвиженияСерийныхНомеров.Период КАК ДатаОперации,
	|	ДвиженияСерийныхНомеров.Регистратор,
	|	ДвиженияСерийныхНомеров.Получатель.Магазин КАК Магазин,
	|	ДвиженияСерийныхНомеров.СерийныйНомер
	|ПОМЕСТИТЬ ДвиженияСерийныхНомеров
	|ИЗ
	|	РегистрСведений.ДвиженияСерийныхНомеров КАК ДвиженияСерийныхНомеров
	|ГДЕ
	|	ДвиженияСерийныхНомеров.Период < &Дата
	|	И &УсловиеПоРегистратору
	|	И ДвиженияСерийныхНомеров.СерийныйНомер В
	|			(ВЫБРАТЬ
	|				ТабСерийныеНомера.СерийныйНомер
	|			ИЗ
	|				ТабСерийныеНомера КАК ТабСерийныеНомера)
	|	И ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.РеализацияТоваров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ 
	|	ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации,
	|	ДвиженияСерийныхНомеров.ДатаОперации КАК ДатаОперации,
	|	ДвиженияСерийныхНомеров.Магазин КАК Магазин,
	|	ТабСерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|	ТабСерийныеНомера.СерийныйНомер.Владелец КАК Номенклатура,
	|	ТабСерийныеНомера.НомерСтроки
	|ИЗ
	|	ТабСерийныеНомера КАК ТабСерийныеНомера
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияСерийныхНомеров КАК ДвиженияСерийныхНомеров
	|		ПО ТабСерийныеНомера.СерийныйНомер = ДвиженияСерийныхНомеров.СерийныйНомер
	|ГДЕ
	|	ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации ЕСТЬ NULL ";
	
	Запрос.УстановитьПараметр("СерийныеНомера", ТабличнаяЧасть_ПогашениеПодарочныхСертификатов);
	Запрос.УстановитьПараметр("ПогашенныеСерийныеНомера", ПогашенныеСерийныеНомера);
	
	Если ЭтоДокумент Тогда 
		Запрос.УстановитьПараметр("Регистратор", ДокументОбъект.Ссылка);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоРегистратору", "ДвиженияСерийныхНомеров.Регистратор <> &Регистратор");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоРегистратору", "ИСТИНА");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
	
	Результат = Запрос.Выполнить();
	ТаблицаЗапроса = Результат.Выгрузить();
	
	Для каждого СтрокаТаблицыЗапроса Из ТаблицаЗапроса Цикл
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Номер подарочного сертификата %1 еще не был реализован.'"),
			СтрокаТаблицыЗапроса.СерийныйНомер);
		
		
		Если ЭтоДокумент Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДокументОбъект,
				,
				,
				Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
		ТекстОшибки = ТекстОшибки + Символы.ПС + Текст;
		
	КонецЦикла;

	НеРеализованныеСерийныеНомера = ТаблицаЗапроса.ВыгрузитьКолонку("СерийныйНомер");
	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СерийныеНомера.СерийныйНомер КАК Справочник.СерийныеНомера) КАК СерийныйНомер,
	|	СерийныеНомера.НомерСтроки,
	|	ВЫРАЗИТЬ(СерийныеНомера.ПодарочныйСертификат КАК Справочник.Номенклатура) КАК ПодарочныйСертификат
	|ПОМЕСТИТЬ ТабСерийныеНомераВсе
	|ИЗ
	|	&СерийныеНомера КАК СерийныеНомера
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабСерийныеНомераВсе.СерийныйНомер КАК СерийныйНомер,
	|	ТабСерийныеНомераВсе.НомерСтроки,
	|	ТабСерийныеНомераВсе.ПодарочныйСертификат
	|ПОМЕСТИТЬ ТабСерийныеНомера
	|ИЗ
	|	ТабСерийныеНомераВсе КАК ТабСерийныеНомераВсе
	|ГДЕ
	|	(НЕ ТабСерийныеНомераВсе.СерийныйНомер = ЗНАЧЕНИЕ(Справочник.СерийныеНомера.ПустаяСсылка))
	|	И (НЕ ТабСерийныеНомераВсе.СерийныйНомер В (&ПогашенныеСерийныеНомера))
	|	И (НЕ ТабСерийныеНомераВсе.СерийныйНомер В (&НеРеализованныеСерийныеНомера))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабСерийныеНомераВсе.НомерСтроки,
	|	ТабСерийныеНомераВсе.ПодарочныйСертификат
	|ПОМЕСТИТЬ ТабБезСерийных
	|ИЗ
	|	ТабСерийныеНомераВсе КАК ТабСерийныеНомераВсе
	|ГДЕ
	|	(НЕ ТабСерийныеНомераВсе.ПодарочныйСертификат.ИспользоватьСерийныеНомера)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТабСерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|	ТабСерийныеНомера.НомерСтроки,
	|	ТабСерийныеНомера.ПодарочныйСертификат.ДатаОкончанияДействия КАК ДатаОкончанияДействия,
	|	NULL КАК Магазин,
	|	NULL КАК ДатаОперации,
	|	ЛОЖЬ КАК СрокОтПродажи,
	|	ТабСерийныеНомера.ПодарочныйСертификат КАК Номенклатура
	|ПОМЕСТИТЬ НеПравильныеСерийныеНомера
	|ИЗ
	|	ТабСерийныеНомера КАК ТабСерийныеНомера
	|ГДЕ
	|	ТабСерийныеНомера.ПодарочныйСертификат.ТипСрокаДействия = ЗНАЧЕНИЕ(Перечисление.СрокДействияПодарочныхСертификатов.СОграничениемНаДату)
	|	И КОНЕЦПЕРИОДА(ТабСерийныеНомера.ПодарочныйСертификат.ДатаОкончанияДействия, ДЕНЬ) < &Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.СерийныйНомер,
	|	ВложенныйЗапрос.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, ГОД, ВложенныйЗапрос.КоличествоПериодовДействия)
	|		КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, ПОЛУГОДИЕ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|		КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, КВАРТАЛ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|		КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, МЕСЯЦ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|		КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, ДЕКАДА, ВложенныйЗапрос.КоличествоПериодовДействия)
	|		КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, НЕДЕЛЯ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, ДЕНЬ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|	КОНЕЦ,
	|	ДвиженияСерийныхНомеровСрезПоследних.Отправитель.Магазин,
	|	ДвиженияСерийныхНомеровСрезПоследних.Период,
	|	ИСТИНА,
	|	ВложенныйЗапрос.Номенклатура
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТабСерийныеНомера.СерийныйНомер КАК СерийныйНомер,
	|		ТабСерийныеНомера.НомерСтроки КАК НомерСтроки,
	|		ТабСерийныеНомера.ПодарочныйСертификат.Периодичность КАК Периодичность,
	|		ТабСерийныеНомера.ПодарочныйСертификат.КоличествоПериодовДействия КАК КоличествоПериодовДействия,
	|		ТабСерийныеНомера.ПодарочныйСертификат КАК Номенклатура
	|	ИЗ
	|		ТабСерийныеНомера КАК ТабСерийныеНомера
	|	ГДЕ
	|		ТабСерийныеНомера.СерийныйНомер.Владелец.ТипСрокаДействия = ЗНАЧЕНИЕ(Перечисление.СрокДействияПодарочныхСертификатов.ПериодПослеПродажи)) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДвиженияСерийныхНомеров.СрезПоследних(
	|				&ДатаДвижений,
	|				СерийныйНомер В
	|						(ВЫБРАТЬ
	|							ТабСерийныеНомера.СерийныйНомер
	|						ИЗ
	|							ТабСерийныеНомера КАК ТабСерийныеНомера
	|						ГДЕ
	|							ТабСерийныеНомера.СерийныйНомер.Владелец.ТипСрокаДействия = ЗНАЧЕНИЕ(Перечисление.СрокДействияПодарочныхСертификатов.ПериодПослеПродажи))
	|					И АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.РеализацияТоваров)) КАК ДвиженияСерийныхНомеровСрезПоследних
	|		ПО ВложенныйЗапрос.СерийныйНомер = ДвиженияСерийныхНомеровСрезПоследних.СерийныйНомер
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, ГОД, ВложенныйЗапрос.КоличествоПериодовДействия)
	|			КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, ПОЛУГОДИЕ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|			КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, КВАРТАЛ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|			КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, МЕСЯЦ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|			КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, ДЕКАДА, ВложенныйЗапрос.КоличествоПериодовДействия)
	|			КОГДА ВложенныйЗапрос.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|				ТОГДА ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, НЕДЕЛЯ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(ДвиженияСерийныхНомеровСрезПоследних.Период, ДЕНЬ, ВложенныйЗапрос.КоличествоПериодовДействия)
	|		КОНЕЦ < &Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	ТабБезСерийных.НомерСтроки,
	|	ТабБезСерийных.ПодарочныйСертификат.ДатаОкончанияДействия,
	|	NULL,
	|	NULL,
	|	ЛОЖЬ,
	|	ТабБезСерийных.ПодарочныйСертификат
	|ИЗ
	|	ТабБезСерийных КАК ТабБезСерийных
	|ГДЕ
	|	КОНЕЦПЕРИОДА(ТабБезСерийных.ПодарочныйСертификат.ДатаОкончанияДействия, ДЕНЬ) < &Дата
	|	И ТабБезСерийных.ПодарочныйСертификат.ТипСрокаДействия = ЗНАЧЕНИЕ(Перечисление.СрокДействияПодарочныхСертификатов.СОграничениемНаДату)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СерийныйНомер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НеПравильныеСерийныеНомера.СерийныйНомер,
	|	НеПравильныеСерийныеНомера.НомерСтроки,
	|	НеПравильныеСерийныеНомера.ДатаОкончанияДействия,
	|	НеПравильныеСерийныеНомера.Магазин,
	|	НеПравильныеСерийныеНомера.ДатаОперации,
	|	НеПравильныеСерийныеНомера.СрокОтПродажи,
	|	НеПравильныеСерийныеНомера.Номенклатура
	|ИЗ
	|	НеПравильныеСерийныеНомера КАК НеПравильныеСерийныеНомера";
	
	Запрос.УстановитьПараметр("Дата", НачалоДня(?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса())));
	Запрос.УстановитьПараметр("ДатаДвижений", КонецДня(?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса())));
	Запрос.УстановитьПараметр("ПогашенныеСерийныеНомера", ПогашенныеСерийныеНомера);
	Запрос.УстановитьПараметр("НеРеализованныеСерийныеНомера", НеРеализованныеСерийныеНомера);
	
	Результат = Запрос.Выполнить();
	ТаблицаЗапроса = Результат.Выгрузить();
	
	Для каждого СтрокаТаблицыЗапроса Из ТаблицаЗапроса Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицыЗапроса.СерийныйНомер) Тогда
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Номер подарочного сертификата: %1 закончился срок действия - %2'"),
				СтрокаТаблицыЗапроса.СерийныйНомер,
				Формат(СтрокаТаблицыЗапроса.ДатаОкончанияДействия, "ДЛФ=D"));
			
		Иначе
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Подарочный сертификат: %1 закончился срок действия - %2'"),
				СтрокаТаблицыЗапроса.Номенклатура,
				Формат(СтрокаТаблицыЗапроса.ДатаОкончанияДействия, "ДЛФ=D"));
			
		КонецЕсли;
		
		Если СтрокаТаблицыЗапроса.СрокОтПродажи Тогда
			
			Текст = Текст + Символы.ПС + " "
					+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'реализация была в магазине %1 Дата %2'"),
						СтрокаТаблицыЗапроса.Магазин,
						Формат(СтрокаТаблицыЗапроса.ДатаОперации, "ДЛФ=D"));
			
		КонецЕсли;
		
		Если ЭтоДокумент Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДокументОбъект,
				,
				,
				Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
		ТекстОшибки = ТекстОшибки + Символы.ПС + Текст;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Общая процедура проверки реализации и погашения серийных номеров.
//
// Параметры:
//  ДокументОбъект - объект проводимого документа документа.
//  ИмяТабличнойЧасти - Имя табличной части Товары.
//  ИмяТабличнойЧастиСерийныеНомера - Имя табличной части Серийные номера.
//  РежимПроведения - режим проведения документа.
//  Отказ - Переменная отвечающая за прерывание проведения.
//  Заголовок - Заголовок при ошибке.
//  Дата - дата проверки
//  Результат - при необходимости возвращает единую строку ошибки.
//
Процедура ПроверитьОкончаниеАбсолютныхСроковДействияСертификатов(
	СтруктураДокумента,
	Отказ,
	ТекстОшибки = "",
	Дата = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДокументОбъект = СтруктураДокумента.ДокументОбъект;
	ЭтоДокумент = СтруктураДокумента.ЭтоДокумент;
	ЭтоНовый = СтруктураДокумента.ЭтоНовый;
	ТабличнаяЧасть = СтруктураДокумента.ТаблицаЗначений_Товары;
	ИмяТабличнойЧасти = СтруктураДокумента.ИмяТабличнойЧасти;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ ТабТоварыВСЕ
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.Номенклатура.Наименование КАК Наименование,
	|	ТабТовары.НомерСтроки КАК НомерСтроки,
	|	ТабТовары.Номенклатура.ДатаОкончанияДействия КАК ДатаОкончанияДействия
	|ИЗ
	|	ТабТоварыВСЕ КАК ТабТовары
	|ГДЕ
	|	ТабТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.ПодарочныйСертификат)
	|	И ТабТовары.Номенклатура.ТипСрокаДействия = ЗНАЧЕНИЕ(Перечисление.СрокДействияПодарочныхСертификатов.СОграничениемНаДату)
	|	И КОНЕЦПЕРИОДА(ТабТовары.Номенклатура.ДатаОкончанияДействия, ДЕНЬ) < &Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Товары", ТабличнаяЧасть);
	
	Запрос.УстановитьПараметр("Дата", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗапроса = РезультатЗапроса.Выгрузить();
	
	Для каждого СтрокаТаблицы Из ТаблицаЗапроса Цикл
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Продаваемый подарочный сертификат ""%1"" (строка №%2)'"),
			СтрокаТаблицы.Наименование,
			СтрокаТаблицы.НомерСтроки);
		
		СтрокаСообщения = "    " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Окончен срок годности Дата %1'"),
			Формат(СтрокаТаблицы.ДатаОкончанияДействия, "ДЛФ=D"));
		
		Текст = Текст + Символы.ПС + СтрокаСообщения;
		
		Если ЭтоДокумент Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ДокументОбъект,
				ИмяТабличнойЧасти+"[" + (СтрокаТаблицы.НомерСтроки - 1) + "].Номенклатура" ,
				,
				Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
		ТекстОшибки = ТекстОшибки + Символы.ПС + Текст;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Общая процедура Остатков серийных номеров.
//
// Параметры:
//  ДокументОбъект - объект проводимого документа документа.
//  ИмяТабличнойЧасти - Имя табличной части Товары.
//  ИмяТабличнойЧастиСерийныеНомера - Имя табличной части Серийные номера.
//  РежимПроведения - режим проведения документа.
//  Отказ - Переменная отвечающая за прерывание проведения.
//  Заголовок - Заголовок при ошибке.
//  Дата - дата проверки
//
Процедура ПроверитьОстаткиСерийныхНомеров(
		ТаблицаЗначений_Товары, 
		ТаблицаЗначений_СерийныеНомера,
		Отказ,
		ТекстОшибки) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаТовары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(ТаблицаТовары.КлючСвязиСерийныхНомеров КАК ЧИСЛО(5, 0)) КАК КлючСвязиСерийныхНомеров,
	|	ВЫРАЗИТЬ(ТаблицаТовары.Склад КАК Справочник.Склады) КАК Склад
	|ПОМЕСТИТЬ ДокТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаСерийныхНомеров.СерийныйНомер КАК Справочник.СерийныеНомера) КАК СерийныйНомер,
	|	ВЫРАЗИТЬ(ТаблицаСерийныхНомеров.КлючСвязиСерийныхНомеров КАК ЧИСЛО(5, 0)) КАК КлючСвязиСерийныхНомеров
	|ПОМЕСТИТЬ ТаблицаСерийныхНомеров
	|ИЗ
	|	&ТаблицаСерийныхНомеров КАК ТаблицаСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерийныхНомеров.СерийныйНомер КАК СерийныйНомер,
	|	ДокТовары.Номенклатура КАК Номенклатура,
	|	ДокТовары.Склад КАК Склад
	|ПОМЕСТИТЬ ТаблицаСерийныхНомеровПолная
	|ИЗ
	|	ТаблицаСерийныхНомеров КАК ТаблицаСерийныхНомеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокТовары КАК ДокТовары
	|		ПО ТаблицаСерийныхНомеров.КлючСвязиСерийныхНомеров = ДокТовары.КлючСвязиСерийныхНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.СерийныйНомер КАК СерийныйНомер,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияСерийныхНомеров.Номенклатура КАК Номенклатура,
	|		ДвиженияСерийныхНомеров.СерийныйНомер КАК СерийныйНомер,
	|		ДвиженияСерийныхНомеров.Получатель КАК Склад,
	|		ДвиженияСерийныхНомеров.Количество КАК Количество
	|	ИЗ
	|		РегистрСведений.ДвиженияСерийныхНомеров КАК ДвиженияСерийныхНомеров
	|	ГДЕ
	|		(ДвиженияСерийныхНомеров.Номенклатура, ДвиженияСерийныхНомеров.СерийныйНомер, ДвиженияСерийныхНомеров.Получатель) В
	|				(ВЫБРАТЬ
	|					Таблица.Номенклатура,
	|					Таблица.СерийныйНомер,
	|					Таблица.Склад
	|				ИЗ
	|					ТаблицаСерийныхНомеровПолная КАК Таблица)
	|		И (НЕ ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов))
	|		И (НЕ ДвиженияСерийныхНомеров.Получатель = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияСерийныхНомеров.Номенклатура,
	|		ДвиженияСерийныхНомеров.СерийныйНомер,
	|		ДвиженияСерийныхНомеров.Отправитель,
	|		-ДвиженияСерийныхНомеров.Количество
	|	ИЗ
	|		РегистрСведений.ДвиженияСерийныхНомеров КАК ДвиженияСерийныхНомеров
	|	ГДЕ
	|		(ДвиженияСерийныхНомеров.Номенклатура, ДвиженияСерийныхНомеров.СерийныйНомер, ДвиженияСерийныхНомеров.Отправитель) В
	|				(ВЫБРАТЬ
	|					Таблица.Номенклатура,
	|					Таблица.СерийныйНомер,
	|					Таблица.Склад
	|				ИЗ
	|					ТаблицаСерийныхНомеровПолная КАК Таблица)
	|		И (НЕ ДвиженияСерийныхНомеров.АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов))
	|		И (НЕ ДвиженияСерийныхНомеров.Отправитель = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Склад,
	|	ВложенныйЗапрос.СерийныйНомер,
	|	ВложенныйЗапрос.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерийныхНомеровПолная.СерийныйНомер,
	|	ТаблицаСерийныхНомеровПолная.Номенклатура,
	|	ТаблицаСерийныхНомеровПолная.Склад
	|ИЗ
	|	ТаблицаСерийныхНомеровПолная КАК ТаблицаСерийныхНомеровПолная
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
	|		ПО ТаблицаСерийныхНомеровПолная.СерийныйНомер = ТаблицаОстатков.СерийныйНомер
	|			И ТаблицаСерийныхНомеровПолная.Номенклатура = ТаблицаОстатков.Номенклатура
	|			И ТаблицаСерийныхНомеровПолная.Склад = ТаблицаОстатков.Склад
	|ГДЕ
	|	ЕСТЬNULL(ТаблицаОстатков.Количество, 0) <= 0";

	Запрос.УстановитьПараметр("ТаблицаТовары" , ТаблицаЗначений_Товары);
	Запрос.УстановитьПараметр("ТаблицаСерийныхНомеров" , ТаблицаЗначений_СерийныеНомера);
	ТаблицаПоОтрицательнымОстаткам = Запрос.Выполнить().Выгрузить();
	
	ШаблонСообщения = НСтр("ru = 'Номер подарочного сертификата %3 (%1) 
		|Отсутствует на складе %2'");
	
	Для каждого СтрокаТаблицы Из ТаблицаПоОтрицательнымОстаткам Цикл
		Отказ = Истина;
		ТекстОшибки = ТекстОшибки + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			Строка(СтрокаТаблицы.Номенклатура),
			Строка(СтрокаТаблицы.Склад),
			Строка(СтрокаТаблицы.СерийныйНомер));
	КонецЦикла;
	
КонецПроцедуры // ПроверитьОстаткиСерийныхНомеров

// Функция подбирает таблицу серийных номеров.
//
// Параметры:
//  Текст   - введенный текст
//  ТекстАвтоПодбора - текст авто подбора.
//  МассивИспользуемыхСерийныхНомеров - Уже выбранные серийные номера.
//  Номенклатура - номенклатура поиска.
//  ТипСерийногоНомера - тип серийного номера номенклатуры.
//
// ВозвращаемоеЗначение
//  Массив
//
Функция ПодобратьМассивСерийныхНомеров(СтруктураПараметров, Текст) Экспорт
	Перем Номенклатура, ТипСерийногоНомера, СерийныеНомера, Дата, ЭтоРеализация;
	
	МассивРезультата = Новый Массив;
	
	Если Не ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		Возврат МассивРезультата;
	КонецЕсли;
	
	СтруктураПараметров.Свойство("ЭтоРеализация"     , ЭтоРеализация);
	СтруктураПараметров.Свойство("Номенклатура"      , Номенклатура);
	СтруктураПараметров.Свойство("ТипСерийногоНомера", ТипСерийногоНомера);
	СтруктураПараметров.Свойство("СерийныеНомера"    , СерийныеНомера);
	СтруктураПараметров.Свойство("Дата"              , Дата);
	
	МассивИспользуемыхСерийныхНомеров = СерийныеНомера.Выгрузить().ВыгрузитьКолонку("СерийныйНомер");
	
	Если СтруктураПараметров.Свойство("МассивИспользованныхСерийныхНомеров") Тогда
		
		Для каждого ЭлементМассива Из СтруктураПараметров.МассивИспользованныхСерийныхНомеров Цикл
			
			МассивИспользуемыхСерийныхНомеров.Добавить(ЭлементМассива);
			
		КонецЦикла;
		
	КонецЕсли;
	
	МассивСерийныхНомеров = МаркетинговыеАкцииСервер.ПодобратьСерийныеНомераПоТексту(Текст, МассивИспользуемыхСерийныхНомеров, Номенклатура, ТипСерийногоНомера);
	
	Если МассивСерийныхНомеров.Количество() > 0  Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СерийныеНомера.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ТабСерийныеНомера
		|ИЗ
		|	Справочник.СерийныеНомера КАК СерийныеНомера
		|ГДЕ
		|	СерийныеНомера.Ссылка В(&МассивСерийныхНомеров)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////";
		Если ЭтоРеализация Тогда
		
			Запрос.Текст = Запрос.Текст + Символы.ПС + "ВЫБРАТЬ
			|	ДвиженияСерийныхНомеровСрезПоследних.СерийныйНомер
			|ПОМЕСТИТЬ ТабНеПравильныхСерийныхНомеров
			|ИЗ
			|	РегистрСведений.ДвиженияСерийныхНомеров.СрезПоследних(
			|			&Дата,
			|			СерийныйНомер В
			|					(ВЫБРАТЬ
			|						ТабСерийныеНомера.Ссылка
			|					ИЗ
			|						ТабСерийныеНомера КАК ТабСерийныеНомера)
			|				И (АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.РеализацияТоваров)
			|					ИЛИ АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов)
			|					ИЛИ АналитикаХозяйственнойОперации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаЗатраты)
			|					ИЛИ АналитикаХозяйственнойОперации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПоИнвентаризации))) КАК ДвиженияСерийныхНомеровСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТабСерийныеНомера.Ссылка
			|ИЗ
			|	ТабСерийныеНомера КАК ТабСерийныеНомера
			|ГДЕ
			|	(НЕ ТабСерийныеНомера.Ссылка В
			|				(ВЫБРАТЬ
			|					ТабНеПравильныхСерийныхНомеров.СерийныйНомер
			|				ИЗ
			|					ТабНеПравильныхСерийныхНомеров КАК ТабНеПравильныхСерийныхНомеров))";
			
		Иначе
			Запрос.Текст = Запрос.Текст + Символы.ПС + "ВЫБРАТЬ
			|	ДвиженияСерийныхНомеровСрезПоследних.СерийныйНомер
			|ПОМЕСТИТЬ ТабРеализованных
			|ИЗ
			|	РегистрСведений.ДвиженияСерийныхНомеров.СрезПоследних(
			|			&Дата,
			|			СерийныйНомер В
			|					(ВЫБРАТЬ
			|						ТабСерийныеНомера.Ссылка
			|					ИЗ
			|						ТабСерийныеНомера КАК ТабСерийныеНомера)
			|				И (АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.РеализацияТоваров)
			|					ИЛИ АналитикаХозяйственнойОперации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаЗатраты)
			|					ИЛИ АналитикаХозяйственнойОперации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПоИнвентаризации))) КАК ДвиженияСерийныхНомеровСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДвиженияСерийныхНомеровСрезПоследних.СерийныйНомер
			|ПОМЕСТИТЬ ТабПогашенныхИСписанных
			|ИЗ
			|	РегистрСведений.ДвиженияСерийныхНомеров.СрезПоследних(
			|			&Дата,
			|			СерийныйНомер В
			|					(ВЫБРАТЬ
			|						ТабСерийныеНомера.Ссылка
			|					ИЗ
			|						ТабСерийныеНомера КАК ТабСерийныеНомера)
			|				И (АналитикаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.АналитикаХозяйственныхОпераций.ПогашениеПодарочныхСертификатов)
			|					ИЛИ АналитикаХозяйственнойОперации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаЗатраты)
			|					ИЛИ АналитикаХозяйственнойОперации.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПоИнвентаризации))) КАК ДвиженияСерийныхНомеровСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТабСерийныеНомера.Ссылка
			|ИЗ
			|	ТабСерийныеНомера КАК ТабСерийныеНомера
			|ГДЕ
			|	(НЕ ТабСерийныеНомера.Ссылка В
			|				(ВЫБРАТЬ
			|					ТабПогашенныхИСписанных.СерийныйНомер
			|				ИЗ
			|					ТабПогашенныхИСписанных КАК ТабПогашенныхИСписанных))
			|	И ТабСерийныеНомера.Ссылка В
			|			(ВЫБРАТЬ
			|				ТабРеализованных.СерийныйНомер
			|			ИЗ
			|				ТабРеализованных КАК ТабРеализованных)";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("МассивСерийныхНомеров", МассивСерийныхНомеров);
		Запрос.УстановитьПараметр("МассивИспользуемыхСерийныхНомеров", МассивИспользуемыхСерийныхНомеров);
		Запрос.УстановитьПараметр("Дата", Дата);
		
		Результат = Запрос.Выполнить();
		ТаблицаЗапроса = Результат.Выгрузить();
		МассивРезультата = ТаблицаЗапроса.ВыгрузитьКолонку("Ссылка")
	КонецЕсли;
	
	Возврат МассивРезультата;
	

КонецФункции // ПодобратьМассивСерийныхНомеров()

#КонецОбласти
