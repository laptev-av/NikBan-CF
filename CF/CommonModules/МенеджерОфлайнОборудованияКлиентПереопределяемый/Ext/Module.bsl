
#Область ПереопределяемыеПроцедурыФорм

// Переопределяемая процедура, вызываемая при создании формы настройки офлайн оборудования.
//
// Параметры:
//   Форма - УправляемаяФорма - форма обработки
//   ОфлайнОборудование - Справочник.ПодключаемоеОборудование - ссылка на экземпляр оборудования.
//
Процедура ФормаНастройкиОфлайнОборудованияПриОткрытии(Форма, ОфлайнОборудование) Экспорт
	
	РеквизитыОборудования = ОбщегоНазначенияРТВызовСервера.ПолучитьЗначенияРеквизитовОбъекта(ОфлайнОборудование,
		Новый Структура("ТипОборудования, ТипОфлайнОборудования, ПринтерПечати"));
	
	Если РеквизитыОборудования.ТипОборудования = ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ККМОфлайн")
		И РеквизитыОборудования.ТипОфлайнОборудования = ПредопределенноеЗначение("Перечисление.ТипыОфлайнОборудования.ПрайсЧекер") Тогда
		
		лкКомпьютер = ".";
		WMIАдрес  = "winmgmts:\\" + лкКомпьютер + "\root\CIMv2";
		
		Попытка
			WMIСервис = ПолучитьCOMОбъект(WMIАдрес);
			оЭлементы = WMIСервис.ExecQuery(
			"SELECT * FROM Win32_Printer");
			Для Каждого оЭлемент Из оЭлементы Цикл
				Если Форма.Элементы.ПринтерПечати.СписокВыбора.НайтиПоЗначению(оЭлемент.Name) = Неопределено Тогда
					Форма.Элементы.ПринтерПечати.СписокВыбора.Добавить(оЭлемент.Name, оЭлемент.Name);
				КонецЕсли;
			КонецЦикла;
		Исключение
			ПринтерПечати = РеквизитыОборудования.ПринтерПечати;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая при создании формы настройки офлайн оборудования.
//
// Параметры:
//   Форма - УправляемаяФорма - форма обработки
//   ОфлайнОборудование - Справочник.ПодключаемоеОборудование - ссылка на экземпляр оборудования.
//   СохраняемыеПараметры - Структура - сохраняемые параметры оборудования
Процедура ФормаНастройкиОфлайнОборудованияПриСохраненииПараметров(Форма, ОфлайнОборудование, СохраняемыеПараметры) Экспорт
	
	РеквизитыОборудования = ОбщегоНазначенияРТВызовСервера.ПолучитьЗначенияРеквизитовОбъекта(ОфлайнОборудование,
		Новый Структура("ТипОборудования, ТипОфлайнОборудования, ПринтерПечати"));
	
	Если РеквизитыОборудования.ТипОборудования = ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ККМОфлайн")
		И РеквизитыОборудования.ТипОфлайнОборудования = ПредопределенноеЗначение("Перечисление.ТипыОфлайнОборудования.ПрайсЧекер") Тогда
		
		ПодключаемоеОборудованиеOfflineВызовСервера.СохранитьПараметрыПрайсЧекера(ОфлайнОборудование,
			Форма.ПринтерПечати, Форма.ШаблонЦенника, Форма.СохранятьЦенники
		);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

