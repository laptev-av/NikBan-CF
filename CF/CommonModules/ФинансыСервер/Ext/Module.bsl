
#Область ПрограммныйИнтерфейс

// Функция определяет вхождение ПКО или РКО в кассовую книгу по ссылке.
//
Функция ОпределитьВхождениеВКассовуюКнигу(Ссылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ВходитВКассовуюКнигу = Ложь;
	ИспользоватьКассовуюКнигу = ПолучитьФункциональнуюОпцию("ИспользоватьКассовуюКнигу");
	
	Если ИспользоватьКассовуюКнигу Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		
		"ВЫБРАТЬ
		|	КассоваяКнигаДокументы.Ссылка
		|ИЗ
		|	Документ.КассоваяКнига.КассовыеОрдера КАК КассоваяКнигаДокументы
		|ГДЕ
		|	КассоваяКнигаДокументы.Ссылка.Проведен
		|	И КассоваяКнигаДокументы.КассовыйОрдер = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Результат = Запрос.Выполнить();
		
		ВходитВКассовуюКнигу = НЕ Результат.Пустой();
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВходитВКассовуюКнигу;
	
КонецФункции

// Процедура заполняет табличную часть видов выручки
// остатками денежных средств
// по выбранной кассе ККМ.
Процедура ЗаполнитьСписокВидовВыручки(СписокВидовВыручки, КассаККМ) Экспорт
	
	СписокВидовВыручки.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОстаткиДС.КассаККМ КАК КассаККМ,
	|	ВЫБОР
	|		КОГДА ОстаткиДС.ДоговорПлатежногоАгента = &ПустойДоговор
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Приоритет,
	|	ОстаткиДС.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|	ОстаткиДС.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваККМ.Остатки(&ТекущаяДата, КассаККМ = &КассаККМ) КАК ОстаткиДС
	|ГДЕ
	|	ОстаткиДС.СуммаОстаток > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	ОстаткиДС.ДоговорПлатежногоАгента.Наименование";
	Запрос.УстановитьПараметр("ПустойДоговор", Справочники.ДоговорыПлатежныхАгентов.ПустаяСсылка());
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("КассаККМ", КассаККМ);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СписокВидовВыручки.Добавить();
		НоваяСтрока.ДоговорПлатежногоАгента = Выборка.ДоговорПлатежногоАгента;
		НоваяСтрока.СуммаОстаток = Выборка.СуммаОстаток;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
