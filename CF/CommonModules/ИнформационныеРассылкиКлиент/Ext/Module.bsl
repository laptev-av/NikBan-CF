////////////////////////////////////////////////////////////////////////////////
// ИнформационныеРассылкиКлиент содержит процедуры и функции 
// для интерактивной работы с информационными рассылками.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура вызывает диалог выбора параметра для сообщения информационной рассылки.
Процедура ВставитьПараметр(ЭлементФормы, ОбработчикОповещения) Экспорт
	
	СписокВыбора = СписокПараметровТекстаИнформационнойРассылки(Истина);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЭлементФормы", ЭлементФормы);
	ДополнительныеПараметры.Вставить("ОбработчикОповещения", ОбработчикОповещения);
	
	ОбработчикОповещенияПараметра = Новый ОписаниеОповещения("ОбработкаВыбораПараметраРассылки", ЭтотОбъект, ДополнительныеПараметры);
	СписокВыбора.ПоказатьВыборЭлемента(ОбработчикОповещенияПараметра, НСтр("ru = 'Выберите параметр'"));
	
КонецПроцедуры

// Процедура обрабатывает закрытие диалога выбора параметра
// для сообщения информационной рассылки
// и подставляет параметр в тест сообщения.
// Входящие параметры:
//		ЭлементФормы - элемент формы - текстовое поле, в которое подставляется параметр.
//		ОбработчикОповещения - ОписаниеОповещения, для обработки формулы ВыборМЖ.
Процедура ОбработкаВыбораПараметраРассылки(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		ВыбранныйПараметр = ВыбранныйЭлемент.Значение;
		Если ВыбранныйПараметр = "[ВыборМЖ(;)]" Тогда
			ОткрытьФорму("Документ.ИнформационнаяРассылка.Форма.ФормулаМЖ", , , , , , ДополнительныеПараметры.ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			ОбработатьВставкуПараметра(ДополнительныеПараметры.ЭлементФормы, ВыбранныйПараметр);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура обрабатывает вставку формулы ВыборМЖ().
// В текстовое поле информационной рассылки.
// Входящие параметры:
//		ЭлементФормы - элемент формы - текстовое поле, в которое подставляется параметр.
//		ВыбранныйПараметр - Строка, параметр, который добавляется в текстовое поле.
Процедура ОбработатьВставкуПараметра(ЭлементФормы, ВыбранныйПараметр) Экспорт
	
	ДлинаПараметра = СтрДлина(ВыбранныйПараметр);
	
	СтрокаНачальная = 0;
	СтрокаКонечная = 0;
	КолонкаНачальная = 0;
	КолонкаКонечная = 0;
	
	ЭлементФормы.ПолучитьГраницыВыделения(СтрокаНачальная,
											КолонкаНачальная,
											СтрокаКонечная,
											КолонкаКонечная);
											
	ЭлементФормы.ВыделенныйТекст = ВыбранныйПараметр;
	
	ЭлементФормы.УстановитьГраницыВыделения(СтрокаНачальная,
											КолонкаНачальная + ДлинаПараметра,
											СтрокаНачальная,
											КолонкаНачальная + ДлинаПараметра);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция формирует список параметров,
// которые могут использоваться для подстановки в шаблон
// текста сообщения информационной рассылки.
// Входящие параметры:
//		ДобавлятьФормулы - Булево - определяет, нужно ли добавлять в список 
//									дополнительные формулы.
//									По умолчанию - Ложь.
// Возвращаемое значение: СписокПараметров - список значений.
Функция СписокПараметровТекстаИнформационнойРассылки(ДобавлятьФормулы = Ложь)
	
	СписокПараметров = Новый СписокЗначений;
	СписокПараметров.Добавить("[ПредставлениеКонтакта]");
	СписокПараметров.Добавить("[НакопленнаяСуммаПродаж]");
	СписокПараметров.Добавить("[ОстатокБаллов]");
	СписокПараметров.Добавить("[НачисленоБаллов]");
	Если ДобавлятьФормулы Тогда
		СписокПараметров.Добавить("[ВыборМЖ(;)]");
	КонецЕсли;
	
	Возврат СписокПараметров;
	
КонецФункции

#КонецОбласти





