
// Процедура завершает замер времени на сервере и записывает результат на сервере.
// Параметры:
//  КлючеваяОперация - СправочникСсылка.КлючевыеОперации - ключевая операция
//						либо Строка - название ключевой операции.
//  ВремяНачала - Число или Дата.
Процедура ЗафиксироватьЗамерВремени(КлючеваяОперация, ВремяНачала, ИзОбработкиОжидания = Ложь) Экспорт
	
	Если ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности() Тогда
		ДатаОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
		ВремяОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Если ИзОбработкиОжидания Тогда
			ВремяОкончания = ВремяОкончания - 0.1;
		КонецЕсли;
		Если ТипЗнч(ВремяНачала) = Тип("Число") Тогда
			Длительность = (ВремяОкончания - ВремяНачала) / 1000;
			ДатаНачала = ДатаОкончания - Длительность;
		Иначе
			Длительность = (ДатаОкончания - ВремяНачала) / 1000;
			ДатаНачала = ВремяНачала;
		КонецЕсли;
		
		ПараметрыЗамера = Новый Структура;
		ПараметрыЗамера.Вставить("КлючеваяОперация", КлючеваяОперация);
		ПараметрыЗамера.Вставить("Длительность", Длительность);
		ПараметрыЗамера.Вставить("ДатаНачалаКлючевойОперации", ДатаНачала);
		ПараметрыЗамера.Вставить("ДатаОкончанияКлючевойОперации", ДатаОкончания);
		ПараметрыЗамера.Вставить("ВесЗамера", 1);
		ПараметрыЗамера.Вставить("Комментарий", "");
		ПараметрыЗамера.Вставить("Технологический", Ложь);
		ПараметрыЗамера.Вставить("Длительная", Ложь);
		
		ОценкаПроизводительности.ЗафиксироватьДлительностьКлючевойОперации(ПараметрыЗамера);
		
	КонецЕсли;
	
КонецПроцедуры
