////////////////////////////////////////////////////////////////////////////////
// Модуль содержит процедуры и функции для обработки действий пользователя
// в процессе работы с документами продажи.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработка ответа на вопрос проведения при вводе счете фактуры.
//
Процедура ОповещениеВопросПроведениеПриВводеСчетФактуры(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ВводитьСчетФактуру = Истина;
	Форма = ДополнительныеПараметры.ДанныеСчетаФактуры.Форма;
	Источник = ДополнительныеПараметры.ДанныеСчетаФактуры.Источник;
	ДанныеСчетаФактуры = ДополнительныеПараметры.ДанныеСчетаФактуры;
	
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Попытка
			ВводитьСчетФактуру = Форма.Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
			ДанныеСчетаФактуры.ДокументОснование = Источник.Ссылка;
		Исключение
			ПоказатьПредупреждение(,НСтр("ru = 'Не удалось выполнить проведение документа'"));
			ВводитьСчетФактуру = Ложь;
		КонецПопытки;
	Иначе
		ВводитьСчетФактуру = Ложь;
	КонецЕсли;
	
	Если ВводитьСчетФактуру Тогда
		
		ДополнительныеПараметры.ДанныеСчетаФактуры.Удалить("Форма");
		
		ПараметрыФормы = Новый Структура("Основание, ДокументОснование, ВозвращатьПараметрыПредставления",
									ДанныеСчетаФактуры, 
									ДанныеСчетаФактуры.ДокументОснование,
									Истина);
		ОткрытьФорму("Документ.СчетФактураВыданный.ФормаОбъекта", ПараметрыФормы, Форма);
		
	КонецЕсли;
	
КонецПроцедуры

// Создает документ "Счет-фактура выданный" или открывает существующий.
//
// Параметры:
//	Форма - Форма - Форма, из которой вызвана команда. Устанавливается владельцем открываемой формы счета-фактуры;
//  Организация - СправочникСсылка.Организации - Организация, на имя которой оформляется счет-фактура;
//  ОткрыватьСуществующую - Булево - Признак необходимости поиска и открытия формы найденного счета-фактуры;
//  Исправление - Булево - Истина - Создается исправление счета-фактуры;
//
Процедура ВвестиСчетФактуру(Форма, Объект, ОткрыватьСуществующую = Ложь, Исправление = Ложь) Экспорт
	
	ВводитьСчетФактуру = Истина;
	
	Если ОткрыватьСуществующую Тогда
		СчетФактураДокумента = ПродажиВызовСервера.СчетФактураДокумента(Объект.Ссылка, Объект.Организация);
		Если ЗначениеЗаполнено(СчетФактураДокумента) Тогда
			ВводитьСчетФактуру = Ложь;
			
			ПараметрыФормы = Новый Структура("Ключ, ДокументОснование, ВозвращатьПараметрыПредставления",
				СчетФактураДокумента, Объект.Ссылка, Истина);
			ОткрытьФорму("Документ.СчетФактураВыданный.ФормаОбъекта", ПараметрыФормы, Форма);
		КонецЕсли;
	КонецЕсли; 
	
	ДанныеСчетаФактуры = Новый Структура;
	ДанныеСчетаФактуры.Вставить("ДокументОснование", Объект.Ссылка);
	ДанныеСчетаФактуры.Вставить("Организация"      , Объект.Организация);
	ДанныеСчетаФактуры.Вставить("Дата"             , ?(ОткрыватьСуществующую, Объект.Дата, '00010101'));
	ДанныеСчетаФактуры.Вставить("Исправление"      , Исправление);
	ДанныеСчетаФактуры.Вставить("Источник"         , Объект);
	
	Если Объект.Свойство("Контрагент") Тогда
		ДанныеСчетаФактуры.Вставить("Контрагент"       , Объект.Контрагент);
	КонецЕсли;
	
	Если ВводитьСчетФактуру
		И (НЕ ЗначениеЗаполнено(Форма.Объект.Ссылка)
		ИЛИ НЕ Форма.Объект.Проведен
		ИЛИ Форма.Модифицированность) Тогда
		
		ТекстВопроса = НСтр("ru = 'Ввод счета-фактуры возможен только после проведения документа, провести документ?'");
		
		ДанныеСчетаФактуры.Вставить("Форма", Форма);
		
		ДополнительныеПараметры = Новый Структура; 
		ДополнительныеПараметры.Вставить("ДанныеСчетаФактуры", ДанныеСчетаФактуры);
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеВопросПроведениеПриВводеСчетФактуры", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		ВводитьСчетФактуру = Ложь;
		
	КонецЕсли;
	
	Если ВводитьСчетФактуру Тогда
		
		ПараметрыФормы = Новый Структура("Основание, ДокументОснование, ВозвращатьПараметрыПредставления",
			ДанныеСчетаФактуры, ДанныеСчетаФактуры.ДокументОснование, Истина);
		ОткрытьФорму("Документ.СчетФактураВыданный.ФормаОбъекта", ПараметрыФормы, Форма);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти