////////////////////////////////////////////////////////////////////////////////
// Модуль "ЦенообразованиеКлиентСервер", содержит процедуры и функции для 
// обработки действий пользователя в процессе работы с ценами.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает разрядность "психологического округления", т.е. к какому разряду числа применяется
// округление.
//
// Параметры:
// ПсихологическоеОкругление - Число, значение "психологического округления".
//
// Возвращаемое значение:
// Число
// Разрядность "психологического округления".
//
Функция РазрядностьПсихологическогоОкругления(ПсихологическоеОкругление) Экспорт
	
	Разрядность = 0;
	
	Пока ПсихологическоеОкругление * Pow(10, Разрядность) > 1 Цикл
		Разрядность = Разрядность - 1;
	КонецЦикла;
	
	Пока ПсихологическоеОкругление * Pow(10, Разрядность + 1) < 1 Цикл
		Разрядность = Разрядность + 1;
	КонецЦикла;
	
	Возврат -1 * Разрядность;
	
КонецФункции // РазрядностьПсихологическогоОкругления()

// Применяет "психологическое округление" к числу.
//
// Параметры:
// Число                     - Число, к которому применяется округление.
// ПсихологическоеОкругление - Число, значение "психологического округления".
//
// Возвращаемое значение:
// Число
// Результат применения "психологического округления" к числу.
//
Функция ПрименитьПсихологическоеОкругление(Число, ПсихологическоеОкругление) Экспорт
	
	Если ПсихологическоеОкругление = 0 Тогда
		Возврат Число;
	Иначе
	
		Разрядность         = РазрядностьПсихологическогоОкругления(ПсихологическоеОкругление);
		РезультатОкругления = Окр(Число, -1 * Разрядность) - (Pow(10, Разрядность) - ПсихологическоеОкругление);
		Если РезультатОкругления <= 0 Тогда
			РезультатОкругления = Число;
		КонецЕсли;
		Возврат РезультатОкругления;
		
	КонецЕсли;
		
КонецФункции // ПрименитьПсихологическоеОкругление()

#КонецОбласти
