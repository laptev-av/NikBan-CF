
#Область ПрограммныйИнтерфейс

// В процедуре требуется реализовать открытие формы нового документа пересчета товаров.
//
// Параметры:
//  Форма - УправляемаяФорма - форма запущенной обработки корректировки остатков,
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентЕГАИС - организация предприятия,
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - магазин, в котором корректируются остатки.
//
Процедура СоздатьПриказНаПроведениеИнвентаризации(Форма, Организация, ТорговыйОбъект) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Магазин", ТорговыйОбъект);
	Отбор.Вставить("Организация", Организация);
	Отбор.Вставить("ПравилаОтбораТоваровИнвентаризации", ПредопределенноеЗначение("Справочник.ПравилаОтбораТоваров.ИнвентаризацияАлкогольнойПродукции"));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Отбор);
	
	ОткрытьФорму("Документ.ПриказНаПроведениеИнвентаризацииТоваров.ФормаОбъекта", ПараметрыФормы, Форма);
	
КонецПроцедуры

// В процедуре требуется реализовать открытие формы списка документов пересчета товаров.
//
// Параметры:
//  Форма - УправляемаяФорма - форма запущенной обработки корректировки остатков,
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентЕГАИС - организация предприятия,
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - магазин, в котором корректируются остатки.
//
Процедура ОткрытьСписокИнвентаризаций(Форма, Организация, ТорговыйОбъект) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Магазин", ТорговыйОбъект);
	Отбор.Вставить("Организация", Организация);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("Документ.ПриказНаПроведениеИнвентаризацииТоваров.ФормаСписка", ПараметрыФормы, Форма);
	
КонецПроцедуры

// Формирует отчет по излишкам/недостачам для переданного торгового объекта.
//
// Параметры:
//  Форма - УправляемаяФорма - форма запущенной обработки корректировки остатков,
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - магазин, в котором корректируются остатки.
//
Процедура СформироватьОтчетПоИзлишкамНедостачам(Форма, ТорговыйОбъект) Экспорт
	
	ПараметрыФормы = Новый Структура(
		"Отбор, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии",
		Новый Структура("Склад", ИнтеграцияЕГАИСРТВызовСервера.ПолучитьСкладыМагазина(ТорговыйОбъект)),
		"ОформлениеИзлишковНедостачКонтекст",
		"ОформлениеИзлишковНедостачКонтекст",
		Истина);
	
	ОткрытьФорму("Отчет.ОформлениеИзлишковНедостачТоваров.Форма",
				ПараметрыФормы,
				Форма,
				Форма.УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти
