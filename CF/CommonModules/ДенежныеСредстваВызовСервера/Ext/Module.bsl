
#Область ПрограммныйИнтерфейс

// Функция получает сумму документа.
//
Функция ПолучитьСуммуДокумента(ДокументСсылка) Экспорт

	Возврат ДокументСсылка.СуммаДокумента;

КонецФункции

Функция ПолучитьСуммаВознаграждения(ДокументСсылка) Экспорт

	Возврат ДокументСсылка.СуммаВознаграждения;

КонецФункции

// Функция создает и возвращает документ Внесение денежных средств в кассу ККМ.
// Параметры:
//           СуммаВнесения - Число - Сумма внесения в кассу ККМ.
//           РКО - ДокументСсылка.РасходныйКассовыйОрдер - РКО на основании которого будет создан документ Внесение ДС
//                                                         в кассу ККМ.
//           КассаККМ - Тип СправочникСсылка.КассыККМ - Касса ККМ в которую вносятся ДС.
//
Функция СоздатьДокументВнесениеДенежныхСредствВКассуККМ(СуммаВнесения, РКО, КассаККМ) Экспорт
	Реквизиты = Справочники.КассыККМ.РеквизитыКассыККМ(КассаККМ);
	
	СтруктураДанныеЗаполнения = Новый Структура();
	СтруктураДанныеЗаполнения.Вставить("ДокументОснование", РКО);
	СтруктураДанныеЗаполнения.Вставить("Организация", Реквизиты.Организация);
	СтруктураДанныеЗаполнения.Вставить("КассаККМ", КассаККМ);
	СтруктураДанныеЗаполнения.Вставить("СуммаДокумента", СуммаВнесения);
	СтруктураДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
	СтруктураДанныеЗаполнения.Вставить("Ответственный", Пользователи.ТекущийПользователь());
	
	ДокументВнесениеДенегВКассуККМ = Документы.ВнесениеДенежныхСредствВКассуККМ.СоздатьДокумент();
	ЗаполнитьЗначенияСвойств(ДокументВнесениеДенегВКассуККМ, СтруктураДанныеЗаполнения);
	ДокументВнесениеДенегВКассуККМ.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
	Возврат ДокументВнесениеДенегВКассуККМ.Ссылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция создает и возвращает документ Выемка денежных средств из кассы ККМ.
// Параметры:
//           ИзымаемаяСумма - Число - Изымаемая сумма из кассы ККМ.
//           КассаККМ - СправочникСсылка.КассыККМ - Касса ККМ в которую выполняют внесение ДС.
//			 ДополнительныеПараметры - Структура дополнительных параметров.
//
Функция СоздатьДокументВыемкаДенежныхСредствИзКассыККМ(
			ИзымаемаяСумма,
			КассаККМ,
			СсылкаНаОтчет,
			ДополнительныеПараметры = Неопределено) Экспорт
			
	
	Если НЕ ДополнительныеПараметры = Неопределено И ДополнительныеПараметры.Свойство("ВыемкаСсылка") Тогда
		ДокументВыемка = ДополнительныеПараметры.ВыемкаСсылка.ПолучитьОбъект();
	Иначе
		ДокументВыемка = Документы.ВыемкаДенежныхСредствИзКассыККМ.СоздатьДокумент();
	КонецЕсли;
	
	Реквизиты = Справочники.КассыККМ.РеквизитыКассыККМ(КассаККМ);
	
	Если ЗначениеЗаполнено(СсылкаНаОтчет) Тогда
		ДокументВыемка.Дата = СсылкаНаОтчет.Дата + 1;
	Иначе
		ДокументВыемка.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ДокументВыемка.Организация    = Реквизиты.Организация;
	ДокументВыемка.КассаККМ       = КассаККМ;
	ДокументВыемка.Магазин        = КассаККМ.Магазин;
	ДокументВыемка.СуммаДокумента = ИзымаемаяСумма;
	ДокументВыемка.Ответственный  = Пользователи.ТекущийПользователь();
	ДокументВыемка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствИзКассыККМ;
	
	ДокументВыемка.ОтчетОРозничныхПродажах = СсылкаНаОтчет;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Если ДополнительныеПараметры.Свойство("ДоговорПлатежногоАгента") Тогда
			ДокументВыемка.ДоговорПлатежногоАгента = ДополнительныеПараметры.ДоговорПлатежногоАгента;
		КонецЕсли;
	КонецЕсли;
	
	ДокументВыемка.Записать(РежимЗаписиДокумента.Проведение);
	Возврат ДокументВыемка.Ссылка;
	
КонецФункции

#КонецОбласти
