
////////////////////////////////////////////////////////////////////////////////
// Подсистема "Работа с номенклатурой".
// ОбщийМодуль.РаботаСНоменклатуройКлиентПереопределяемый.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура открывает форму элемента номенклатуры в случает интерактивного заполнения.
//
// Параметры:
//  ОповещениеОЗавершении - ОбработкаОповещения - обработчик закрытия формы.
//  АдресДанныхЗаполнения - Строка - адрес временного хранилища с данными номенклатуры.
//
Процедура СоздатьНоменклатуруИнтерактивно(ОповещениеОЗавершении, АдресДанныхЗаполнения) Экспорт
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", Новый Структура("АдресДанныхЗаполнения", АдресДанныхЗаполнения) ,,,,, ОповещениеОЗавершении);
	
КонецПроцедуры

// Процедура управляет возможностью поиска номенклатуры в сервисе 1С:Номенклатура по штрихкоду.
//
// Параметры:
//  ИспользоватьПоискПоШтрихкоду - Булево - флаг использования функционала сервиса.
//
Процедура ИспользоватьПоискПоШтрихкодуВСервисе(ИспользоватьПоискПоШтрихкоду) Экспорт
	
	ИспользоватьПоискПоШтрихкоду = Истина;
	
КонецПроцедуры

// Открытие формы значения дополнительного реквизита.
//
// Параметры:
//  РеквизитСсылка	     - Ссылка - ссылка на дополнительный реквизит.
//  Владелец			 - УправляемаяФорма - форма владелец.
//
Процедура ОткрытьФормуДополнительногоЗначения(РеквизитСсылка, Владелец) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Владелец", РеквизитСсылка));
	ОткрытьФорму("Справочник.ЗначенияСвойствОбъектов.Форма.ФормаЭлемента", ПараметрыФормы, Владелец);
	
КонецПроцедуры

// Открытие формы дополнительного реквизита.
//
// Параметры:
//  ПараметрыФормы	 - Структура - параметры формы.
//  Владелец			 - УправляемаяФорма - форма владелец.
//
Процедура ОткрытьФормуДополнительногоРеквизита(ПараметрыФормы, Владелец) Экспорт
	
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.ФормаЭлемента", ПараметрыФормы, Владелец);
	
КонецПроцедуры

// Открытие формы номенклатуры.
//
// Параметры:
//  НоменклатураСсылка	 - Ссылка - ссылка на номенклатуру.
//  Владелец			 - УправляемаяФорма - форма владелец.
//
Процедура ОткрытьФормуНоменклатуры(НоменклатураСсылка, Владелец) Экспорт
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", 
		Новый Структура("Ключ", НоменклатураСсылка), Владелец);
		
КонецПроцедуры

// Открытие формы вида номенклатуры.
//
// Параметры:
//  ВидНоменклатурыСсылка - Ссылка - ссылка на вид номенклатуры.
//  Владелец			 - УправляемаяФорма - форма владелец.
//
Процедура ОткрытьФормуВидаНоменклатуры(ВидНоменклатурыСсылка, Владелец) Экспорт
	
	ОткрытьФорму("Справочник.ВидыНоменклатуры.Форма.ФормаЭлемента", 
		Новый Структура("Ключ", ВидНоменклатурыСсылка), Владелец);
	
КонецПроцедуры

// Открытие формы списка вида номенклатуры.
//
// Параметры:
//  ВидНоменклатурыСсылка - Ссылка - ссылка на вид номенклатуры.
//  Владелец			 - УправляемаяФорма - форма владелец.
//
Процедура ОткрытьФормуСпискаВидаНоменклатуры(ВидНоменклатурыСсылка, Владелец) Экспорт
	
	ОткрытьФорму("Справочник.ВидыНоменклатуры.Форма.ФормаСписка",,Владелец);
	
КонецПроцедуры

// Открытие формы списка номенклатуры.
//
// Параметры:
//  НоменклатураСсылка	 - Ссылка - ссылка на номенклатуру.
//  Владелец			 - УправляемаяФорма - форма владелец.
//
Процедура ОткрытьФормуСпискаНоменклатуры(НоменклатураСсылка, Владелец) Экспорт
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаСписка",,Владелец);
	
КонецПроцедуры

// Обработка загрузки категорий в форме списка вида номенклатуры.
//
// Параметры:
//  СозданныеОбъекты - Массив - ссылки на созданные элементы.
//  Форма			 - УправляемаяФорма - форма списка вида номенклатуры.
//
Процедура ПослеЗагрузкиКатегорий(СозданныеОбъекты, Форма) Экспорт
	
	Если СозданныеОбъекты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Элементы.Список.Обновить();
	Форма.Элементы.Список.ТекущаяСтрока = СозданныеОбъекты[0];
	
КонецПроцедуры

//
Функция НайтиНоменклатуруПоШтрихкодуВСервисе(ШтрихКод) Экспорт
	
	ИдентификаторыНоменклатуры = РаботаСНоменклатуройСлужебныйВызовСервера.ПолучитьДанныеПоНоменклатуреПоШтрихкоду(ШтрихКод);
	
	Если ИдентификаторыНоменклатуры <> Неопределено Тогда
		Если ИдентификаторыНоменклатуры.Количество() = 0 Тогда
			ИдентификаторыНоменклатуры = Неопределено;
		Иначе
			ПараметрыФормы = Новый Структура;
			
			ПараметрыФормы.Вставить("ИдентификаторыНоменклатуры", ИдентификаторыНоменклатуры);
			ПараметрыФормы.Вставить("СтрокаШтрихкода", ШтрихКод);
			
			ОткрытьФорму("Обработка.РаботаСНоменклатурой.Форма.КарточкаНоменклатуры", ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИдентификаторыНоменклатуры;
	
КонецФункции

#КонецОбласти
