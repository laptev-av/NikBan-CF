
#Область ПрограммныйИнтерфейс

// Функция возвращает пустую структуру,
// которая в последующем заполняется значениями и используется 
// при проверке ассортимента в табличной части.
// Возвращаемое значение: 
// 		СтруктураПроверкиАссортимента - Структура
Функция ПараметрыПроверкиАссортимента(
	Объект,
	ЭтоПродажа = Ложь,
	ИмяРеквизитаЗаказ = "",
	ИмяТабличнойЧасти = "Товары",
	ИмяРеквизитаМагазин = "Магазин") Экспорт 
	
	СтруктураПроверкиАссортимента = Новый Структура;
	
	СтруктураПроверкиАссортимента.Вставить("Ссылка", Объект.Ссылка);
	СтруктураПроверкиАссортимента.Вставить("Магазин", Объект[ИмяРеквизитаМагазин]);
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		СтруктураПроверкиАссортимента.Вставить("Дата", ?(Объект.Ссылка.Пустая(), ТекущаяДатаСеанса(), Объект.Дата));
	#Иначе
		СтруктураПроверкиАссортимента.Вставить("Дата", ?(Объект.Ссылка.Пустая(), ТекущаяДата(), Объект.Дата));
	#КонецЕсли
	
	Если ЭтоПродажа Тогда
		СтруктураПроверкиАссортимента.Вставить("ТекстСообщения", НСтр("ru = 'Номенклатура ""%1"" не включена в ассортимент магазина или запрещена к продаже.'"));
		СтруктураПроверкиАссортимента.Вставить("ИмяРесурсаАссортимента",  "РазрешеныПродажи");
		СтруктураПроверкиАссортимента.Вставить("РазрешатьДобавление", 	  Ложь);
	Иначе
		СтруктураПроверкиАссортимента.Вставить("ТекстСообщения", НСтр("ru = 'Номенклатура ""%1"" не включена в ассортимент магазина или запрещена к закупке. Принимать ее не рекомендуется.'"));
		СтруктураПроверкиАссортимента.Вставить("ИмяРесурсаАссортимента",  "РазрешеныЗакупки");
		СтруктураПроверкиАссортимента.Вставить("РазрешатьДобавление", 	  Истина);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяРеквизитаЗаказ) Тогда
		СтруктураПроверкиАссортимента.Вставить("ПроверитьТоварВЗаказе", Объект[ИмяРеквизитаЗаказ]);
	Иначе
		СтруктураПроверкиАссортимента.Вставить("ПроверитьТоварВЗаказе", Неопределено);
	КонецЕсли;
	
	СтруктураПроверкиАссортимента.Вставить("ИмяТабличнойЧасти", ИмяТабличнойЧасти);
	
	Возврат СтруктураПроверкиАссортимента;
	
КонецФункции

// Функция возвращает пустую структуру,
// которая в последующем заполняется значениями и используется 
// при формировании условия в ряде отчетов по ассортименту, не использующих СКД.
// Возвращаемое значение: 
// 		СтруктураПараметровУсловия - Структура
Функция ПараметрыДляУсловия() Экспорт 
	
	СтруктураПараметровУсловия = Новый Структура;
	
	СтруктураПараметровУсловия.Вставить("СчетчикУсловий", 0);
	СтруктураПараметровУсловия.Вставить("СчетчикПараметров", 0);
	СтруктураПараметровУсловия.Вставить("ТекстУсловия", "");
	СтруктураПараметровУсловия.Вставить("ТекстНачальногоУсловия", "");
	СтруктураПараметровУсловия.Вставить("УсловиеГруппы", "");
	СтруктураПараметровУсловия.Вставить("СоответствиеПолейУсловия", Новый Соответствие);
	
	Возврат СтруктураПараметровУсловия;
	
КонецФункции

#КонецОбласти
