
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДоступныхМагазинов = Обработки.ИзменениеТекущегоМагазина.ДоступныеМагазины();
	Если ТаблицаДоступныхМагазинов.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Для пользователя нет доступных магазинов кроме текущего.'");
	Иначе
		Для Сч = 0 По ТаблицаДоступныхМагазинов.Количество() - 1 Цикл
			СтрокаСписка = Магазины.Добавить();
			СтрокаСписка.Значение = ТаблицаДоступныхМагазинов[Сч].Магазин;
			СтрокаСписка.Пометка  = ТаблицаДоступныхМагазинов[Сч].Пометка;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМагазины

&НаКлиенте
Процедура МагазиныВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Магазины.ТекущиеДанные <> Неопределено Тогда
		ТекущийМагазин = Элементы.Магазины.ТекущиеДанные.Значение;
	КонецЕсли;
	
	ИзменитьТекущийМагазин();
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СменитьМагазин(Команда)
	
	Если Элементы.Магазины.ТекущиеДанные <> Неопределено Тогда
		ТекущийМагазин = Элементы.Магазины.ТекущиеДанные.Значение;
	КонецЕсли;
	
	УстановитьТекущийМагазин();
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьТекущийМагазин()
	
	Если ЗначениеЗаполнено(ТекущийМагазин) Тогда
		
		ИзменитьТекущийМагазин();
		ОбщегоНазначенияРТКлиент.УстановитьЗаголовокПриложенияРТ();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьТекущийМагазин()
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыСеанса.ТекущийМагазин = ТекущийМагазин;
	ОбновитьПовторноИспользуемыеЗначения();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти