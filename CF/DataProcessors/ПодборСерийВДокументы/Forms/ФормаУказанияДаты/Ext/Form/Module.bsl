
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ДатаИзготовления.Формат 			   = Параметры.ФорматДаты;
	Элементы.ДатаИзготовления.ФорматРедактирования = Параметры.ФорматДаты;
	
	ГоденДо = Параметры.ГоденДо;
	
	Если Не ЗначениеЗаполнено(ГоденДо) Тогда
		ГоденДо = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	ЕдиницаИзмеренияСрокаГодности = НСтр("ru = 'г.'");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГоденДоВыбор(Элемент, ВыбраннаяДата)
	Закрыть(ГоденДо);
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзготовленияПриИзменении(Элемент)
	РассчитатьГоденДо();
КонецПроцедуры

&НаКлиенте
Процедура СрокГодностиПриИзменении(Элемент)
	РассчитатьГоденДо();
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияСрокаГодностиПриИзменении(Элемент)
	РассчитатьГоденДо();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(ГоденДо);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее
&НаКлиенте

Процедура РассчитатьГоденДо()
	Если Не ЗначениеЗаполнено(ДатаИзготовления)
		Или Не ЗначениеЗаполнено(СрокГодности) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕдиницаИзмеренияСрокаГодности = НСтр("ru = 'г.'") Тогда
		ГоденДо = ДатаИзготовления + СрокГодности * 31536000;
	ИначеЕсли ЕдиницаИзмеренияСрокаГодности = НСтр("ru = 'мес.'") Тогда
		ГоденДо = ДобавитьМесяц(ДатаИзготовления,СрокГодности) - 86400;
	ИначеЕсли ЕдиницаИзмеренияСрокаГодности = НСтр("ru = 'дн.'") Тогда
		ГоденДо = ДатаИзготовления + СрокГодности * 86400;
	ИначеЕсли ЕдиницаИзмеренияСрокаГодности = НСтр("ru = 'ч.'") Тогда
		ГоденДо = ДатаИзготовления + СрокГодности * 3600;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
