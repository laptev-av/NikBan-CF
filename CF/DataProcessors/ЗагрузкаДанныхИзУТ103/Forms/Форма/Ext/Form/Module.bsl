#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		Элементы.ИмяФайлаОбмена.Видимость = Ложь;
		Элементы.ПредупреждениеЗагрузка.Видимость = Истина;
	Иначе
		Элементы.ИмяФайлаОбмена.Видимость = Истина;
		Элементы.ПредупреждениеЗагрузка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	Попытка
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоместитьФайлЗавершение", ЭтотОбъект);
		Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		НачатьПомещениеФайла(ОписаниеОповещения, АдресВременногоХранилища, "*.xml",, УникальныйИдентификатор);
		Иначе
		НачатьПомещениеФайла(ОписаниеОповещения, АдресВременногоХранилища, ИмяФайлаОбмена , Ложь, УникальныйИдентификатор);
		КонецЕсли;
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Ошибка передачи файла на сервер'");
		Сообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьНаСервере(АдресВременногоХранилища)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	ДвоичныеДанные = Неопределено;
	
	УникальныйИдентификатор_        = Новый УникальныйИдентификатор();
	ИмяВременногоФайлаПротоколаОбмена = КаталогВременныхФайлов() + УникальныйИдентификатор_ + ".txt";
	
	УОД = Обработки.УниверсальныйОбменДаннымиXML.Создать();
	УОД.ИмяФайлаОбмена                        = ИмяВременногоФайла;
	УОД.РежимОбмена                           = "Загрузка";
	УОД.ЗапоминатьЗагруженныеОбъекты          = Ложь;
	УОД.ВыводВПротоколСообщенийОбОшибках      = Истина;
	УОД.ВыводВПротоколИнформационныхСообщений = Ложь;
	УОД.ИмяФайлаПротоколаОбмена               = ИмяВременногоФайлаПротоколаОбмена;
	
	УОД.ЗагружатьДанныеВРежимеОбмена               = Истина;
	УОД.ОбъектыПоСсылкеЗагружатьБезПометкиУдаления = Истина;
	УОД.ОптимизированнаяЗаписьОбъектов             = Истина;
	УОД.ЗапоминатьЗагруженныеОбъекты               = Истина;
	ДатаНачалаЗагрузки = ТекущаяДатаСеанса();

	УстановитьПривилегированныйРежим(Истина);
	УОД.ВыполнитьЗагрузку();
	ЗагрузкаВыполнена = НЕ УОД.ФлагОшибки;
	УстановитьПривилегированныйРежим(Ложь);
	ДатаОкончанияЗагрузки = ТекущаяДатаСеанса();
	
	ПротоколОбмена = Новый ТекстовыйДокумент;
	ПротоколОбмена.Прочитать(ИмяВременногоФайлаПротоколаОбмена);
	
	Попытка
		УдалитьФайлы(ИмяВременногоФайлаПротоколаОбмена);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Обмен данными'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Предупреждение,,, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось удалить временный файл
			|%1 по причине: %2'"), ИмяВременногоФайлаПротоколаОбмена, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
	КонецПопытки;
	
	Элементы.ПротоколОбмена.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПутьККаталогу()

	ПутьККаталогу = "";
	Если ЗначениеЗаполнено(ИмяФайлаОбмена) Тогда
		
		Файл = Новый Файл(ИмяФайлаОбмена);
		Если Файл.Существует() Тогда
			ПутьККаталогу = Файл.Путь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПутьККаталогу;

КонецФункции

&НаКлиенте
Процедура ИмяФайлаОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИмяФайлаОбмена = Элемент.ТекстРедактирования;
	
	Если ПодключитьРасширениеРаботыСФайлами() Тогда
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогОткрытияФайла.Фильтр             = "Файл выгрузки (*.xml)|*.xml";
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите путь к файлу выгрузки данных из УТ 10.3'");
		ДиалогОткрытияФайла.Каталог = ПолучитьПутьККаталогу();
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			ИмяФайлаОбмена = ДиалогОткрытияФайла.ПолноеИмяФайла;
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Для выбора каталога необходимо установить расширение для работы с файлами в Веб-клиенте.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьФайлЗавершение(ВыборВыполнен, АдресИлиРезультатВыбора, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Если ВыборВыполнен Тогда
		ОчиститьСообщения();
		ПротоколОбмена.Очистить();
		Состояние(НСтр("ru = 'Загрузка данных...'"),, НСтр("ru = 'Выполняется загрузка данных из УТ 10.3'"));
		ЗагрузитьНаСервере(АдресИлиРезультатВыбора);
		ОбновитьИнтерфейс();
		СтандартныеПодсистемыКлиент.УстановитьРасширенныйЗаголовокПриложения();
		ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка завершена'"),,, БиблиотекаКартинок.Информация32);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти





