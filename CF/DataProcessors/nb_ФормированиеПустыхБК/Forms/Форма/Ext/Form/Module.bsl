Функция ПолучитьПоследнийБКНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФизическиеЛица.nb_БК КАК nb_БК
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.ЭтоГруппа = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	nb_БК УБЫВ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ПоследнийБК = ВыборкаДетальныеЗаписи.nb_БК
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПоследнийБК) Тогда
		ПоследнийБК = 0;
	КонецЕсли;
	
	Возврат ПоследнийБК +1
КонецФункции //


&НаСервере
Процедура КомандаПолучитьПоследнийБКНаСервере()
	Объект.НачальныйНомер = ПолучитьПоследнийБКНаСервере();
	Объект.КонечныйНомер = Объект.НачальныйНомер +10;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолучитьПоследнийБК(Команда)
	КомандаПолучитьПоследнийБКНаСервере();
КонецПроцедуры


&НаСервере
Процедура КомандаСформироватьДиапазонБКНаСервере()
	Для НомерБК=Объект.НачальныйНомер По Объект.КонечныйНомер  Цикл
		СформироватьБКНаСервере(НомерБК);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СформироватьБКНаСервере(НомерБК, Штрихкод = Неопределено)
	
	Отказ = Ложь;
	НачатьТранзакцию();
	Попытка
		ФизЛицоСсылка = nb_ПрограммаЛояльностиДружбаСервер.ПолучитьФизЛицоПоБК(НомерБК);
		Если НЕ ФизЛицоСсылка.Пустая() Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Отказ добавления БК: Найдено Физ.лицо с номером БК %1",НомерБК)
		КонецЕсли;	
		СтрокаНомерБК = nb_НастройкиСервер.ПолучитьСтроковоеПредставлениеНомераБК(НомерБК);
		////////// ФИЗЛИЦО
		
		НовыйФизЛицо = Справочники.ФизическиеЛица.СоздатьЭлемент();
		НовыйФизЛицо.Родитель = nb_НастройкиСервер.ПолучитьГруппуУчастниковПрограммыДружба();
		НовыйФизЛицо.Наименование = СтрокаНомерБК;
		НовыйФизЛицо.nb_БК = НомерБК;
		
		НовыйФизЛицо.Записать();
		
		////////// ДИСКОНТНАЯ КАРТА
		
		ДКСсылка = nb_ПрограммаЛояльностиДружбаСервер.ПолучитьДисконтнуюКартуПоКоду(СтрокаНомерБК);
		Если НЕ ДКСсылка.Пустая() Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Отказ добавления БК: Найдена ДК с номером %1",СтрокаНомерБК);
		КонецЕсли;	
		
		НовыйДисконтнаяКарта = Справочники.ИнформационныеКарты.СоздатьЭлемент();
		НовыйДисконтнаяКарта.Родитель 			= nb_НастройкиСервер.ПолучитьГруппуДисконтнойКартыПрограммыДружба();
		НовыйДисконтнаяКарта.ТипКарты 			= Перечисления.ТипыИнформационныхКарт.Дисконтная;
		НовыйДисконтнаяКарта.Наименование 		= СтрокаНомерБК; 
		НовыйДисконтнаяКарта.КодКарты 			= СтрокаНомерБК; 
		НовыйДисконтнаяКарта.ВладелецКарты 		= НовыйФизЛицо.Ссылка;
		НовыйДисконтнаяКарта.ВидКарты 			= Перечисления.ВидыИнформационныхКарт.Смешанная;
		НовыйДисконтнаяКарта.ВидДисконтнойКарты = nb_НастройкиСервер.ПолучитьВидДисконтнойКартыПрограммыДружба();
		НовыйДисконтнаяКарта.БонуснаяПрограммаЛояльности = nb_НастройкиСервер.ПолучитьБонуснуюПрограммуДружба();
		НовыйДисконтнаяКарта.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();	
		Отказ = Истина;
	КонецПопытки;	
	
	////////// ШТРИХКОД
	Если НЕ Отказ Тогда
		Если Штрихкод = Неопределено Тогда
			ПрефиксШК = "340";
			
			Штрихкод = ПрефиксШК + Формат(НомерБК, "ЧЦ=8; ЧВН=; ЧГ=");
			Штрихкод = Штрихкод + ПодключаемоеОборудованиеРТВызовСервера.КонтрольныйСимволEAN(ШтрихКод, 13);
		КонецЕсли;	
		ПодключаемоеОборудованиеРТВызовСервера.УстановитьШтрихкод(Штрихкод, НовыйДисконтнаяКарта.Ссылка, ,,"Дисконтная карта: Ошибка записи штрикода");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСформироватьБК(Команда)
	КомандаСформироватьДиапазонБКНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандаЗагрузитьИзФайлаНаСервере(АдресДанных)
	ТЗДанные = ПолучитьТЗДанныеИзФайла(АдресДанных);
	
	Для Каждого СтрокаТЗ ИЗ ТЗДанные Цикл
		СформироватьБКНаСервере(СтрокаТЗ.НомерБК, СтрокаТЗ.ШтрихКод);
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьТЗДанныеИзФайла(АдресДанных)
	Данные = ПолучитьИзВременногоХранилища(АдресДанных);
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	Данные.Записать(ИмяВременногоФайла);
	
	ТекстовыйФайл = Новый ЧтениеТекста(ИмяВременногоФайла);
	// первая строка - данные
	Стр = ТекстовыйФайл.ПрочитатьСтроку();
	
	ТЗДанные = Новый ТаблицаЗначений;
	ТЗДанные.Колонки.Добавить("НомерБК");
	ТЗДанные.Колонки.Добавить("ШтрихКод");
	
	
	Пока Стр <> Неопределено Цикл // строки читаются до символа перевода строки
		
		СтрокаТЗ = ТЗДанные.Добавить();

		МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Стр,";");
		СтрокаТЗ.НомерБК = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(МассивСтрок[0]);
		СтрокаТЗ.ШтрихКод = СокрЛП(МассивСтрок[1]);
		
		Стр = ТекстовыйФайл.ПрочитатьСтроку();
	КонецЦикла;
	
	Возврат ТЗДанные
КонецФункции

&НаКлиенте
Процедура КомандаЗагрузитьИзФайла(Команда)
	Данные = Новый ДвоичныеДанные(ПутьКФайлу);	
	АдресДанных = ПоместитьВоВременноеХранилище(Данные, ЭтаФорма.УникальныйИдентификатор);
	
	КомандаЗагрузитьИзФайлаНаСервере(АдресДанных);
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка= Ложь;
    Если Не ВыбратьФайл(ПутьКФайлу, РежимДиалогаВыбораФайла.Открытие, "Выбор файла данных", "*.csv") тогда
        Сообщить("Не удалось выбрать файл!");
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ВыбратьФайл(ПолноеИмяФайла, Режим, Заголовок, Фильтр)

    Диалог = Новый ДиалогВыбораФайла(Режим);
    Диалог.Заголовок                 = Заголовок;
    Диалог.ПредварительныйПросмотр     = Ложь;
    Диалог.Фильтр                   = Фильтр;

    Если ЗначениеЗаполнено(ПолноеИмяФайла) Тогда
        Диалог.ПолноеИмяФайла= ПолноеИмяФайла;
    КонецЕсли;

    Если Диалог.Выбрать() Тогда
        ПолноеИмяФайла= Диалог.ПолноеИмяФайла;
        Возврат Истина;
    Иначе
        Возврат Ложь;
    КонецЕсли;

КонецФункции
