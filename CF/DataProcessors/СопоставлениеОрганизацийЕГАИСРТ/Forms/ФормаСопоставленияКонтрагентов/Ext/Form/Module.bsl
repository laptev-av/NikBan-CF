
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ОрганизацияЕГАИС, Контрагент");
	
	РеквизитыКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "ИНН, КПП");
	КонтрагентИНН = РеквизитыКонтрагента.ИНН;
	КонтрагентКПП = РеквизитыКонтрагента.КПП;
	ФактическийАдресКонтрагента = ФормированиеПечатныхФормСервер.СведенияОЮрФизЛице(Контрагент, ТекущаяДатаСеанса()).ФактическийАдрес;
	
	УстановитьВидимостьЭлементовФормы();
	УстановитьОформлениеЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьСоответствие(Команда)
	
	Если ЕстьПредупреждения() Тогда
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗаписатьСоответствие_Завершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеПриЗавершении, НСтр("ru = 'Между связываемыми элементами есть расхождения. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.Отмена);
	Иначе
		ЗаписатьСоответствие_Завершение(КодВозвратаДиалога.ОК, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСоответствие_Завершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьСоответствиеНаСервере();
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСоответствиеНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Спр = ОрганизацияЕГАИС.ПолучитьОбъект();
	Спр.СоответствуетОрганизации = ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации");
	Спр.Контрагент = Контрагент;
	Спр.ТорговыйОбъект = ?(Спр.СоответствуетОрганизации, ТорговыйОбъект, Неопределено);
	
	Спр.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.ПредупреждениеИНН.Видимость = НЕ ПустаяСтрока(КонтрагентИНН) И НЕ ПустаяСтрока(ОрганизацияЕГАИС.ИНН) И СокрЛП(КонтрагентИНН) <> СокрЛП(ОрганизацияЕГАИС.ИНН);
	Элементы.ПредупреждениеКПП.Видимость = НЕ ПустаяСтрока(КонтрагентКПП) И НЕ ПустаяСтрока(ОрганизацияЕГАИС.КПП) И СокрЛП(КонтрагентКПП) <> СокрЛП(ОрганизацияЕГАИС.КПП);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементовФормы()
	
	Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Организации") Тогда
		Элементы.ТорговыйОбъект.Видимость = Истина;
		Элементы.ГруппаКонтрагент.Заголовок = "Организация";
		Элементы.Контрагент.Заголовок = "Организация";
		ЭтаФорма.Заголовок = "СопоставлениеОрганизаций";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьПредупреждения()
	
	Возврат Элементы.ПредупреждениеИНН.Видимость ИЛИ Элементы.ПредупреждениеКПП.Видимость;
	
КонецФункции

#КонецОбласти