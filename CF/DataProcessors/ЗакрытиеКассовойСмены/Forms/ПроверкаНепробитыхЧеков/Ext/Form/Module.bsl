
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("КассаККМ", Объект.КассаККМ) Тогда
		Отказ = Истина;
	Иначе
		
		ОбновитьДанныеСервер();
		
		Если ОсновнаяТаблица.Количество() = 0 Тогда
			
			Элементы.ГруппаИнформацияКратко.Видимость   = Истина;
			Элементы.ГруппаИнформацияПодробно.Видимость = Ложь;
			Элементы.ГруппаТаблицы.Видимость            = Ложь;
			
			Элементы.ДекорацияДанныеСистемыПроданоКратко.Заголовок = Формат(СуммаПродаж, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
			Элементы.ДекорацияДанныеСистемыВозвратКратко.Заголовок = Формат(СуммаВозвратов,"ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИспользоватьПодключаемоеОборудование = ЗначениеНастроекВызовСервера.ИспользоватьПодключаемоеОборудование();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "Обработка.ЗакрытиеКассовойСмены.Форма.ПроверкаНепробитыхЧеков.Открытие");

	Если ЗначениеЗаполнено(Объект.КассаККМ) И НомерДокументаКассыККМ[Объект.КассаККМ] = Неопределено Тогда
		ОбщегоНазначенияРТКлиент.ЗаполнитьНомерДокументаКассыККМ(Объект.КассаККМ);
	КонецЕсли;

	ОбновитьИнформационныеНадписи();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДанныеФРПродажаПриИзменении(Элемент)
	
	ОбновитьИнформационныеНадписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеФРВозвратПриИзменении(Элемент)
	
	ОбновитьИнформационныеНадписи();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыОсновнаяТаблица

&НаКлиенте
Процедура ОсновнаяТаблицаПометкаПриИзменении(Элемент)
	
	ОбновитьИнформационныеНадписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсновнаяТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = ОсновнаяТаблица.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", СтрокаТаблицы.ДокументНаККМ);
	
	ВидДокумента = СтрокаТаблицы.ВидДокумента;
	ОткрытьФорму("Документ." + ВидДокумента + ".ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Аннулировать(Команда)
	
	ВыводитьСообщение = Ложь;
	АннулироватьНаСервере(ВыводитьСообщение);
	
	Если ВыводитьСообщение Тогда
		ТекстСообщения = НСтр("ru = 'Документы, оплаты которых переданы в банк не могут быть отменены / аннулированы.'"); 
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеИнвертироватьФлажки(Команда)
	
	Для Каждого ЭлементОсновнойТаблицы Из ОсновнаяТаблица Цикл
		Если ЭлементОсновнойТаблицы.ВозможностьИзменения Тогда
			ЭлементОсновнойТаблицы.Пометка = НЕ ЭлементОсновнойТаблицы.Пометка;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьИнформационныеНадписи();
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеСнятьФлажки(Команда)
	
	СменитьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеУстановитьФлажки(Команда)
	
	СменитьФлажки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеСмены(Команда)
	
	Закрыть(НСтр("ru = 'Закрытие смены'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	ОбновитьДанныеСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетБезГашения(Команда)
	
	ОписаниеОшибки = "";

	ПараметрыКассыККМ         = ЗначениеНастроекВызовСервера.ПолучитьПараметрыКассыККМ(Объект.КассаККМ);
	ИдентификаторУстройстваФР = ПараметрыКассыККМ.ИдентификаторУстройства;
	
	ИспользоватьКассуККМБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	
	Если НЕ ИспользоватьПодключаемоеОборудование ИЛИ ИспользоватьКассуККМБезПодключенияОборудования Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = МенеджерОборудованияКлиентСервер.ПараметрыОткрытияЗакрытияСмены();
	ФИОКассира = ОбщегоНазначенияРТВызовСервера.ФИОФизЛицаПользователяСУчетомИзмененныхПрав();
	Если Не ФИОКассира = "" Тогда
		ПараметрыОперации.Кассир = ФИОКассира;
	Иначе
		ПараметрыОперации.Кассир = НСтр("ru='Администратор'");
	КонецЕсли;
	
	ЭтаФорма.Доступность = Ложь;
	Оповещение = Новый ОписаниеОповещения("ОтчетБезГашенияЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьФормированиеОтчетаБезГашения(Оповещение, УникальныйИдентификатор, ПараметрыОперации, ИдентификаторУстройстваФР);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетБезГашенияЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтаФорма.Доступность = Истина;
	
	Если НЕ РезультатВыполнения.Результат Тогда 
		ТекстСообщения = НСтр("ru = 'При подключении устройства произошла ошибка.'") + Символы.ПС + РезультатВыполнения.ОписаниеОшибки;
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитиеЧековЗавершние(Результат, ПараметрыВыполнения) Экспорт
	
	Если Результат Тогда
		
		Если ПараметрыВыполнения.ТекущийЭлемент < ПараметрыВыполнения.КоличествоЭлементов Тогда
			
			// Следующая итерация рекурсии "оповещениями" пробитие документов.
			
			ЭлементОсновнойТаблицы = ОсновнаяТаблица.Получить(ПараметрыВыполнения.ТекущийЭлемент);
			ПараметрыВыполнения.ТекущийЭлемент = ПараметрыВыполнения.ТекущийЭлемент + 1;
			
			Если ЭлементОсновнойТаблицы.Пометка И ЭлементОсновнойТаблицы.ВозможностьИзменения Тогда
				
				ОповещениеПриЗавершении = Новый ОписаниеОповещения("ПробитиеЧековЗавершние", ЭтотОбъект, ПараметрыВыполнения);
				ДокументНаККМ = ЭлементОсновнойТаблицы.ДокументНаККМ;
				
				Если ЭлементОсновнойТаблицы.ВидДокумента = "ЧекККМ" Тогда
					
					ПробитьЧекВыполнить(ОповещениеПриЗавершении, ДокументНаККМ);
					
				ИначеЕсли ЭлементОсновнойТаблицы.ВидДокумента = "ПриходныйКассовыйОрдер" Тогда
					
					ПараметрыЧека = ПараметрыЧека(ДокументНаККМ);
					
					Если ПараметрыЧека.ПробитЧек Тогда
						СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																		НСтр("ru = 'Чек уже пробит %1'"), 
																		ДокументНаККМ);
						ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, СтрокаСообщения);
						ЭтаФорма.Доступность = Истина;
					Иначе
						Контекст = Новый Структура();
						Контекст.Вставить("ИдентификаторУстройства"      , ПараметрыЧека.ИдентификаторУстройства);
						Контекст.Вставить("РаспределениеВыручкиПоСекциям", ПараметрыЧека.РаспределениеВыручкиПоСекциям);
						Контекст.Вставить("ДокументНаККМ"                , ДокументНаККМ);
						Контекст.Вставить("ОповещениеПриЗавершенииПробитияЧека", ОповещениеПриЗавершении);
						Контекст.Вставить("СуммаДокумента"               , ПараметрыЧека.СуммаДокумента);
						
						ОбщиеПараметры = Новый Структура();
						НомерЧека = НомерДокументаКассыККМ[Объект.КассаККМ] + 1;
						НапечататьЧекСерверИнкассацияЗавершение(ДокументНаККМ, 
																ПараметрыЧека.РаспределениеВыручкиПоСекциям,
																ОбщиеПараметры,
																НомерЧека);
						
						Оповещение = Новый ОписаниеОповещения("НапечататьЧекКлиентЗавершениеПКО", ЭтотОбъект, Контекст);
						
						МенеджерОборудованияКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(Оповещение, 
														УникальныйИдентификатор, 
														ОбщиеПараметры, 
														ПараметрыЧека.ИдентификаторУстройства);
						
					КонецЕсли;
				ИначеЕсли ЭлементОсновнойТаблицы.ВидДокумента = "РасходныйКассовыйОрдер" Тогда
					
					ПараметрыЧека = ПараметрыЧека(ДокументНаККМ);
					
					Если ПараметрыЧека.ПробитЧек Тогда
						СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																		НСтр("ru = 'Чек уже пробит %1'"), 
																		ДокументНаККМ);
						ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, СтрокаСообщения);
						ЭтаФорма.Доступность = Истина;
					Иначе
						Контекст = Новый Структура();
						Контекст.Вставить("ИдентификаторУстройства"      , ПараметрыЧека.ИдентификаторУстройства);
						Контекст.Вставить("РаспределениеВыручкиПоСекциям", ПараметрыЧека.РаспределениеВыручкиПоСекциям);
						Контекст.Вставить("ДокументНаККМ"                , ДокументНаККМ);
						Контекст.Вставить("ОповещениеПриЗавершенииПробитияЧека", ОповещениеПриЗавершении);
						
						ПараметрыОперации = Новый Структура;
						ПараметрыОперации.Вставить("ТипИнкассации", 1);
						ПараметрыОперации.Вставить("Сумма"        , ПараметрыЧека.СуммаДокумента);
						ПараметрыОперации.Вставить("ДокументНаККМ", ДокументНаККМ);
						ПараметрыОперации.Вставить("РаспределениеВыручкиПоСекциям", ПараметрыЧека.РаспределениеВыручкиПоСекциям);
						
						Оповещение = Новый ОписаниеОповещения("НапечататьЧекКлиентИнкассацияЗавершениеРКО", ЭтотОбъект, Контекст);
						
						МенеджерОборудованияКлиент.НачатьИнкассациюНаФискальномУстройстве(Оповещение, 
																						  УникальныйИдентификатор, 
																						  ПараметрыОперации, 
																						  ПараметрыЧека.ИдентификаторУстройства); 
					КонецЕсли;
				ИначеЕсли ЭлементОсновнойТаблицы.ВидДокумента = "ОплатаОтПокупателяПлатежнойКартой" Тогда
					
					Если НЕ ЭлементОсновнойТаблицы.ЕстьОперацииПереданныеВБанк Тогда
						ТекстСообщения = НСтр("ru = 'Документы %1 без оплаты картой не могут быть пробиты.
													|Или аннулируйте документы или совершите операцию по платежной карте.'");
						ТекстСообщения = СтрЗаменить(ТекстСообщения, "%1", НСтр("ru = 'Эквайринговая операция'"));
						ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, ТекстСообщения);
						
						ОбновитьДанныеСервер();
						
						ЭтаФорма.Доступность = Истина;
					Иначе
						
						ПараметрыДляЧека = Новый Структура;
						ПараметрыДляЧека.Вставить("ТипЧека",								ЭлементОсновнойТаблицы.ТипЧека);
						ПараметрыДляЧека.Вставить("ТипОплаты",								1);
						ПараметрыДляЧека.Вставить("ИспользоватьПодключаемоеОборудование",	ИспользоватьПодключаемоеОборудование);
						ПараметрыДляЧека.Вставить("НаличнаяОперация",						Ложь);
						ПараметрыДляЧека.Вставить("АгентскийДоговорВШапке",					Ложь);
						ПараметрыДляЧека.Вставить("ДокументНаККМ",							ДокументНаККМ);
						ПараметрыДляЧека.Вставить("КассаККМ",								Объект.КассаККМ);
						
						
						ПодключаемоеОборудованиеРТКлиент.НачатьПробитиеЧекаКОВыполнить(ОповещениеПриЗавершении, Объект, ПараметрыДляЧека, УникальныйИдентификатор);
							
					КонецЕсли;
						
				КонецЕсли;
				
			Иначе
				ПробитиеЧековЗавершние(Истина, ПараметрыВыполнения);
			КонецЕсли;
			
		Иначе
			// Выход из рекурсии "оповещениями" пробитие документов.
			ОбновитьДанныеСервер();
			ЭтаФорма.Доступность = Истина;
		КонецЕсли;
		
	Иначе
		// Выход из рекурсии "оповещениями" пробитие документов при ошибке.
		ОбновитьДанныеСервер();
		ЭтаФорма.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Пробить(Команда)
	
	ПараметрыВыполнения = Новый Структура();
	ПараметрыВыполнения.Вставить("ОсновнаяТаблица"    , ОсновнаяТаблица);
	ПараметрыВыполнения.Вставить("КоличествоЭлементов", ОсновнаяТаблица.Количество());
	ПараметрыВыполнения.Вставить("ТекущийЭлемент"     , 0);
	ПараметрыВыполнения.Вставить("ВыводитьСообщение"  , Ложь);
	
	ЭтаФорма.Доступность = Ложь;
	
	// Запускаем в рекурсии "оповещениями" пробитие документов.
	ПробитиеЧековЗавершние(Истина, ПараметрыВыполнения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура АннулироватьНаСервере(ВыводитьСообщение)
	
	ВыводитьСообщение = Ложь;
	Для Каждого ЭлементОсновнойТаблицы Из ОсновнаяТаблица Цикл
		Если ЭлементОсновнойТаблицы.Пометка
			И ЭлементОсновнойТаблицы.ВозможностьИзменения Тогда
			Если ЭлементОсновнойТаблицы.ЕстьОперацииПереданныеВБанк Тогда
				ВыводитьСообщение = Истина;
				Продолжить;
			КонецЕсли;
			ДокументНаККМ = ЭлементОсновнойТаблицы.ДокументНаККМ;
			Если ЭлементОсновнойТаблицы.ВидДокумента = "ЧекККМ" Тогда
				АннулироватьЧекККМ(ДокументНаККМ);
			Иначе
				АннулироватьПКО_РКО_ЭО(ДокументНаККМ);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьДанныеСервер();
	
КонецПроцедуры

&НаСервере
Процедура АннулироватьЧекККМ(ЧекККМ)

	ЧекККМОбъект = ЧекККМ.ПолучитьОбъект();
	ЧекККМОбъект.СтатусЧекаККМ = Перечисления.СтатусыЧековККМ.Аннулированный;
	Если ЧекККМОбъект.Проведен Тогда
		ЧекККМОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	Иначе
		ЧекККМОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура АннулироватьПКО_РКО_ЭО(ДокументНаККМ)
	ДокументНаККМОбъект = ДокументНаККМ.ПолучитьОбъект();
	Если ДокументНаККМОбъект.Проведен Тогда
		ДокументНаККМОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеСервер()
	
	ТаблицаНеПробитыхЧеков();
	ПолучитьСуммуПродаж();
	ОбновитьИнформационныеНадписи();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформационныеНадписи()
	
	Элементы.ДекорацияДанныеФРПродано.Заголовок = Формат(ДанныеФРПродажа, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	Элементы.ДекорацияДанныеФРВозврат.Заголовок = Формат(ДанныеФРВозврат, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	
	Элементы.ДекорацияДанныеСистемыПродано.Заголовок = Формат(СуммаПродаж, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	Элементы.ДекорацияДанныеСистемыВозврат.Заголовок = Формат(СуммаВозвратов,"ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	
	РасхожденияПродано = СуммаПродаж - ДанныеФРПродажа;
	РасхожденияВозврат = СуммаВозвратов - ДанныеФРВозврат;
	
	Элементы.ДекорацияРасхожденияПродано.Заголовок = Формат(РасхожденияПродано, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	Элементы.ДекорацияРасхожденияВозврат.Заголовок = Формат(РасхожденияВозврат, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	
	ПодобраноПродано = 0;
	ПодобраноВозврат = 0;
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Пометка", Истина);
	
	МассивСтрок = ОсновнаяТаблица.НайтиСтроки(СтруктураПоиска);
	Для каждого СтрокаТаблицы Из МассивСтрок Цикл
		ПодобраноПродано = ПодобраноПродано + СтрокаТаблицы.Сумма;
		ПодобраноВозврат = ПодобраноВозврат + СтрокаТаблицы.Возврат;
	КонецЦикла;
	
	Элементы.ДекорацияПодобраноПродано.Заголовок = Формат(ПодобраноПродано, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	Элементы.ДекорацияПодобраноВозврат.Заголовок = Формат(ПодобраноВозврат, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	
	ОстатокПродано = РасхожденияПродано - ПодобраноПродано;
	ОстатокВозврат = РасхожденияВозврат - ПодобраноВозврат;
	
	Элементы.ДекорацияОстатокПродано.Заголовок = Формат(ОстатокПродано, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	Элементы.ДекорацияОстатокВозврат.Заголовок = Формат(ОстатокВозврат, "ЧДЦ=2; ЧРД=.; ЧРГ=' '; ЧН=; ЧГ=3,0")+ " руб.";
	
	Если ОстатокПродано = 0 И ОстатокВозврат = 0 Тогда
		ЦветОстатка = WebЦвета.ТемноЗеленый;
	Иначе 
		ЦветОстатка = WebЦвета.Черный;
	КонецЕсли;
	
	Элементы.ДекорацияОстаток.ЦветТекста        = ЦветОстатка;
	Элементы.ДекорацияОстатокПродано.ЦветТекста = ЦветОстатка;
	Элементы.ДекорацияОстатокВозврат.ЦветТекста = ЦветОстатка;
	
	Элементы.ФормаЗакрытьСмену.Доступность = ОсновнаяТаблица.Количество() = 0;
	
КонецПроцедуры

// Получает сумму продаж по кассе и помещает ее в переменные.
//
// Параметры:
//  Нет
//
//
&НаСервере
Процедура ПолучитьСуммуПродаж()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.Возврат) КАК Возврат
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|				ТОГДА ЧекККМ.СуммаДокумента
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Сумма,
	|		ВЫБОР
	|			КОГДА ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|				ТОГДА ЧекККМ.СуммаДокумента
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Возврат
	|	ИЗ
	|		Документ.ЧекККМ КАК ЧекККМ
	|	ГДЕ
	|		ЧекККМ.Проведен
	|		И ЧекККМ.КассаККМ = &КассаККМ
	|		И НЕ ЧекККМ.СтатусЧекаККМ = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Архивный)
	|		И НЕ ЧекККМ.СтатусЧекаККМ = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Аннулированный)
	|		И НЕ ЧекККМ.СтатусЧекаККМ = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Отложенный)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПриходныйКассовыйОрдер.СуммаДокумента,
	|		0
	|	ИЗ
	|		Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|	ГДЕ
	|		ПриходныйКассовыйОрдер.КассаККМ = &КассаККМ
	|		И ПриходныйКассовыйОрдер.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента)
	|		И ПриходныйКассовыйОрдер.ПробиватьЧекиПоКассеККМ
	|		И ПриходныйКассовыйОрдер.Проведен
	|		И НЕ ПриходныйКассовыйОрдер.СменаЗакрыта
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		РасходныйКассовыйОрдер.СуммаДокумента
	|	ИЗ
	|		Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|	ГДЕ
	|		РасходныйКассовыйОрдер.КассаККМ = &КассаККМ
	|		И РасходныйКассовыйОрдер.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	|		И РасходныйКассовыйОрдер.ПробиватьЧекиПоКассеККМ
	|		И РасходныйКассовыйОрдер.Проведен
	|		И НЕ РасходныйКассовыйОрдер.СменаЗакрыта
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ОплатаОтПокупателяПлатежнойКартой.ХозяйственнаяОперация = &ХозяйственнаяОперацияПКО
	|				ТОГДА ОплатаОтПокупателяПлатежнойКартой.СуммаДокумента
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ОплатаОтПокупателяПлатежнойКартой.ХозяйственнаяОперация = &ХозяйственнаяОперацияРКО
	|				ТОГДА ОплатаОтПокупателяПлатежнойКартой.СуммаДокумента
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ОплатаОтПокупателяПлатежнойКартой КАК ОплатаОтПокупателяПлатежнойКартой
	|	ГДЕ
	|		ОплатаОтПокупателяПлатежнойКартой.КассаККМ = &КассаККМ
	|		И ОплатаОтПокупателяПлатежнойКартой.ПробиватьЧекиПоКассеККМ
	|		И ОплатаОтПокупателяПлатежнойКартой.Проведен
	|		И НЕ ОплатаОтПокупателяПлатежнойКартой.СменаЗакрыта) КАК ВложенныйЗапрос";
	
	Запрос.УстановитьПараметр("КассаККМ", Объект.КассаККМ);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияРКО"  , Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияПКО"  , Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		СуммаПродаж    = Выборка.Сумма;
		СуммаВозвратов = Выборка.Возврат;
		
		Если НЕ ЗначениеЗаполнено(СуммаПродаж) Тогда
			СуммаПродаж = 0;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СуммаВозвратов) Тогда
			СуммаВозвратов = 0;
		КонецЕсли;
		
	Иначе
		СуммаПродаж    = 0;
		СуммаВозвратов = 0;
	КонецЕсли;
	
КонецПроцедуры

// Получает таблицу не пробитых чеков.
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//   Таблица значений
//
&НаСервере
Функция ТаблицаНеПробитыхЧеков()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЧекККМ.Ссылка КАК ДокументНаККМ,
	|	СУММА(ВЫБОР
	|			КОГДА ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|				ТОГДА ЧекККМ.СуммаДокумента
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.Сумма, 0)) КАК ОплатаКартой,
	|	СУММА(ВЫБОР
	|			КОГДА ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|				ТОГДА ЧекККМ.СуммаДокумента
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Возврат,
	|	&ВозможностьИзмененияЧек КАК Пометка,
	|	ЧекККМ.Дата,
	|	&ВозможностьИзмененияЧек КАК ВозможностьИзменения,
	|	&ВидДокументаЧек КАК ВидДокумента,
	|	ЕСТЬNULL(ВложенныйЗапрос.ДанныеПереданыВБанк, ЛОЖЬ) КАК ЕстьОперацииПереданныеВБанк,
	|	ВЫБОР
	|		КОГДА ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ТипЧека,
	|	0 КАК ТипОплаты
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЧекККМОплата.Ссылка КАК Ссылка,
	|			СУММА(ВЫБОР
	|					КОГДА ЧекККМОплата.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|						ТОГДА ЧекККМОплата.Сумма
	|					ИНАЧЕ -ЧекККМОплата.Сумма
	|				КОНЕЦ) КАК Сумма,
	|			МАКСИМУМ(ЧекККМОплата.ДанныеПереданыВБанк) КАК ДанныеПереданыВБанк
	|		ИЗ
	|			Документ.ЧекККМ.Оплата КАК ЧекККМОплата
	|		ГДЕ
	|			ЧекККМОплата.Ссылка.Проведен
	|			И ЧекККМОплата.Ссылка.СтатусЧекаККМ = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.ПустаяСсылка)
	|			И ЧекККМОплата.ВидОплаты.ТипОплаты = ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.ПлатежнаяКарта)
	|			И ЧекККМОплата.Ссылка.КассаККМ = &КассаККМ
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ЧекККМОплата.Ссылка) КАК ВложенныйЗапрос
	|		ПО ЧекККМ.Ссылка = ВложенныйЗапрос.Ссылка
	|ГДЕ
	|	ЧекККМ.Проведен
	|	И ЧекККМ.СтатусЧекаККМ = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.ПустаяСсылка)
	|	И ЧекККМ.КассаККМ = &КассаККМ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЧекККМ.Ссылка,
	|	ЧекККМ.Дата,
	|	ЕСТЬNULL(ВложенныйЗапрос.ДанныеПереданыВБанк, ЛОЖЬ),
	|	ВЫБОР
	|		КОГДА ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйКассовыйОрдер.Ссылка,
	|	0,
	|	0,
	|	РасходныйКассовыйОрдер.СуммаДокумента,
	|	&ВозможностьИзмененияРКО,
	|	РасходныйКассовыйОрдер.Дата,
	|	&ВозможностьИзмененияРКО,
	|	&ВидДокументаРКО,
	|	ЛОЖЬ,
	|	1,
	|	0
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК РасходныйКассовыйОрдер
	|ГДЕ
	|	РасходныйКассовыйОрдер.КассаККМ = &КассаККМ
	|	И РасходныйКассовыйОрдер.ХозяйственнаяОперация = &ХозяйственнаяОперацияРКО
	|	И РасходныйКассовыйОрдер.ПробиватьЧекиПоКассеККМ
	|	И РасходныйКассовыйОрдер.Проведен
	|	И НЕ РасходныйКассовыйОрдер.ПробитЧек
	|	И НЕ РасходныйКассовыйОрдер.СменаЗакрыта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	ПриходныйКассовыйОрдер.СуммаДокумента,
	|	0,
	|	0,
	|	&ВозможностьИзмененияПКО,
	|	ПриходныйКассовыйОрдер.Дата,
	|	&ВозможностьИзмененияПКО,
	|	&ВидДокументаПКО,
	|	ЛОЖЬ,
	|	0,
	|	0
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.КассаККМ = &КассаККМ
	|	И ПриходныйКассовыйОрдер.ХозяйственнаяОперация = &ХозяйственнаяОперацияПКО
	|	И ПриходныйКассовыйОрдер.ПробиватьЧекиПоКассеККМ
	|	И ПриходныйКассовыйОрдер.Проведен
	|	И НЕ ПриходныйКассовыйОрдер.ПробитЧек
	|	И НЕ ПриходныйКассовыйОрдер.СменаЗакрыта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОплатаОтПокупателяПлатежнойКартой.Ссылка,
	|	ВЫБОР
	|		КОГДА ОплатаОтПокупателяПлатежнойКартой.ХозяйственнаяОперация = &ХозяйственнаяОперацияПКО
	|			ТОГДА ОплатаОтПокупателяПлатежнойКартой.СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ОплатаОтПокупателяПлатежнойКартой.СуммаДокумента,
	|	ВЫБОР
	|		КОГДА ОплатаОтПокупателяПлатежнойКартой.ХозяйственнаяОперация = &ХозяйственнаяОперацияРКО
	|			ТОГДА ОплатаОтПокупателяПлатежнойКартой.СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	&ВозможностьИзмененияЭО,
	|	ОплатаОтПокупателяПлатежнойКартой.Дата,
	|	&ВозможностьИзмененияЭО,
	|	&ВидДокументаЭО,
	|	ОплатаОтПокупателяПлатежнойКартой.ОплатаВыполнена,
	|	ВЫБОР
	|		КОГДА ОплатаОтПокупателяПлатежнойКартой.ХозяйственнаяОперация = &ХозяйственнаяОперацияПКО
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	1
	|ИЗ
	|	Документ.ОплатаОтПокупателяПлатежнойКартой КАК ОплатаОтПокупателяПлатежнойКартой
	|ГДЕ
	|	ОплатаОтПокупателяПлатежнойКартой.КассаККМ = &КассаККМ
	|	И ОплатаОтПокупателяПлатежнойКартой.ПробиватьЧекиПоКассеККМ
	|	И ОплатаОтПокупателяПлатежнойКартой.Проведен
	|	И НЕ ОплатаОтПокупателяПлатежнойКартой.ПробитЧек
	|	И НЕ ОплатаОтПокупателяПлатежнойКартой.СменаЗакрыта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.ДокументНаККМ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.ОплатаКартой,
	|	ТаблицаДокумента.Возврат,
	|	ТаблицаДокумента.Пометка,
	|	ТаблицаДокумента.ВозможностьИзменения,
	|	ТаблицаДокумента.ВидДокумента,
	|	ТаблицаДокумента.ЕстьОперацииПереданныеВБанк,
	|	ТаблицаДокумента.ТипЧека,
	|	ТаблицаДокумента.ТипОплаты
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокумента.Дата";
	
	Запрос.УстановитьПараметр("КассаККМ"                  , Объект.КассаККМ);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияРКО"  , Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияПКО"  , Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);
	
	Запрос.УстановитьПараметр("ВидДокументаЧек", "ЧекККМ");
	Запрос.УстановитьПараметр("ВидДокументаПКО", "ПриходныйКассовыйОрдер");
	Запрос.УстановитьПараметр("ВидДокументаРКО", "РасходныйКассовыйОрдер");
	Запрос.УстановитьПараметр("ВидДокументаЭО" , "ОплатаОтПокупателяПлатежнойКартой");
	
	ВозможностьИзмененияЧек = РольДоступна("ДобавлениеИзменениеЧековККМ")
							ИЛИ РольДоступна("ПолныеПрава");
	
	ВозможностьИзмененияПКО = РольДоступна("ДобавлениеИзменениеКассовыхОрдеров")
							ИЛИ РольДоступна("ПолныеПрава");
	
	ВозможностьИзмененияРКО = РольДоступна("ДобавлениеИзменениеКассовыхОрдеров")
							ИЛИ РольДоступна("ПолныеПрава");
	
	ВозможностьИзмененияЭО  = РольДоступна("ДобавлениеИзменениеОплатОтПокупателяПлатежнойКартой")
							ИЛИ РольДоступна("ПолныеПрава");
	
	Запрос.УстановитьПараметр("ВозможностьИзмененияЧек", ВозможностьИзмененияЧек);
	Запрос.УстановитьПараметр("ВозможностьИзмененияПКО", ВозможностьИзмененияПКО);
	Запрос.УстановитьПараметр("ВозможностьИзмененияРКО", ВозможностьИзмененияРКО);
	Запрос.УстановитьПараметр("ВозможностьИзмененияЭО" , ВозможностьИзмененияЭО);
	
	Результат = Запрос.Выполнить();
	ТаблицаЗапроса = Результат.Выгрузить();
	
	ИтогСуммаПродаж    = ТаблицаЗапроса.Итог("Сумма");
	ИтогСуммаВозвратов = ТаблицаЗапроса.Итог("Возврат");
	ИтогОплатаКартой   = ТаблицаЗапроса.Итог("ОплатаКартой");
	
	ОсновнаяТаблица.Загрузить(ТаблицаЗапроса);
	
КонецФункции

&НаСервере
Функция ПровестиПробитыйЧек(ЧекККМ, НомерСменыККМ, НомерЧекаККМ, Адрес, Подпись, СтруктураОшибкиПробитияЧека)
	
	Попытка
		ЧекККМОбъект = ЧекККМ.ПолучитьОбъект();
		
		ЧекККМОбъект.НомерСменыККМ = НомерСменыККМ;
		ЧекККМОбъект.НомерЧекаККМ  = НомерЧекаККМ;
		
		ЧекККМОбъект.АдресЧекаЕГАИС = Адрес;
		ЧекККМОбъект.ПодписьЧекаЕГАИС = Подпись;
		
		ЧекККМОбъект.СтатусЧекаККМ = Перечисления.СтатусыЧековККМ.Пробитый;
		ЧекККМОбъект.Записать(РежимЗаписиДокумента.Запись);
		НомерЧекаККМ = ЧекККМ.НомерЧекаККМ;
		Возврат Истина;
	Исключение
		ТекстОшибки = ОписаниеОшибки();
		СтруктураОшибкиПробитияЧека.Вставить("ТекстОшибки", ТекстОшибки);
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура СменитьФлажки(ФлагПометки)
	Для каждого ЭлементОсновнойТаблицы Из ОсновнаяТаблица Цикл
		Если ЭлементОсновнойТаблицы.ВозможностьИзменения Тогда
			ЭлементОсновнойТаблицы.Пометка = ФлагПометки;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьИнформационныеНадписи();
	
КонецПроцедуры

#Область ПечатьНаОборудовании

&НаСервере
Функция ПодготовитьДанныеДляПробитияЧека(ЧекККМСсылка, ДанныеЕГАИСДостаточны, НомерЧека, ТранспортныйМодуль = Неопределено, ЕстьАлкогольнаяПродукцияЕГАИС = Неопределено)
	
	ЧекККМОбъект = ЧекККМСсылка.ПолучитьОбъект();
	
	ОбщиеПараметры  = Документы.ЧекККМ.ПодготовитьДанныеДляПробитияЧека(ЧекККМОбъект, ЧекККМСсылка, ТранспортныйМодуль, ДанныеЕГАИСДостаточны, ЕстьАлкогольнаяПродукцияЕГАИС, НомерЧека);
	
	Возврат ОбщиеПараметры;
	
КонецФункции

&НаСервере
Функция ПараметрыЧека(Документ)
	
	ПараметрыКассыККМ = ЗначениеНастроекВызовСервера.ПолучитьПараметрыКассыККМ(Объект.КассаККМ);
	ПараметрыКассыККМ.Вставить("ПробитЧек", Документ.ПробитЧек);
	ПараметрыКассыККМ.Вставить("СуммаДокумента", Документ.СуммаДокумента);
	
	Возврат ПараметрыКассыККМ;
	
КонецФункции // ПараметрыЧека()

&НаКлиенте
Процедура ПробитьЧекВыполнитьЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		ЧекПробит = Ложь;
		АдресЧекаЕГАИС = "";
		ПодписьЧекаЕГАИС = "";
		// Установить полученное значение номера чека реквизиту документа.
		Если РезультатВыполнения.Свойство("ДополнительныеПараметры") Тогда
			Если РезультатВыполнения.ДополнительныеПараметры.Свойство("АдресЧека") Тогда
				АдресЧекаЕГАИС = РезультатВыполнения.ДополнительныеПараметры.АдресЧека;
			КонецЕсли;
			Если РезультатВыполнения.ДополнительныеПараметры.Свойство("ПодписьЧека") Тогда
				ПодписьЧекаЕГАИС = РезультатВыполнения.ДополнительныеПараметры.ПодписьЧека;
			КонецЕсли;
		КонецЕсли;
		СтруктураОшибкиПробитияЧека = Новый Структура;
		Если ЗначениеЗаполнено(АдресЧекаЕГАИС) ИЛИ ЗначениеЗаполнено(ПодписьЧекаЕГАИС) Тогда
			РезультатПроведения = ПровестиПробитыйЧек(Параметры.ЧекККМСсылка, 
													  РезультатВыполнения.ВыходныеПараметры[0], 
													  РезультатВыполнения.ВыходныеПараметры[1], 
													  АдресЧекаЕГАИС, 
													  ПодписьЧекаЕГАИС,
													  СтруктураОшибкиПробитияЧека);
		Иначе
			РезультатПроведения = ПровестиПробитыйЧек(Параметры.ЧекККМСсылка, 
													  РезультатВыполнения.ВыходныеПараметры[0], 
													  РезультатВыполнения.ВыходныеПараметры[1], 
													  "", 
													  "",
													  СтруктураОшибкиПробитияЧека);
		КонецЕсли;
		Если РезультатПроведения = Истина Тогда
			НомерДокументаКассыККМ[Объект.КассаККМ] = РезультатВыполнения.ВыходныеПараметры[1] + 1;
			ПорядковыйНомерПродажи = ПорядковыйНомерПродажи + 1;
			ЧекПробит = Истина;
		Иначе
			Если СтруктураОшибкиПробитияЧека.Свойство("ТекстОшибки") Тогда
				ЗаголовокИнформации = НСтр("ru = 'При печати чека произошла ошибка'");
				ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(ЗаголовокИнформации, СтруктураОшибкиПробитияЧека.ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриЗавершении, ЧекПробит);
	Иначе
		ТекстСообщения = НСтр("ru = 'При печати чека произошла ошибка.
									|Чек не напечатан на устройстве для печати чеков.
									|Дополнительное описание: %ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ЗаголовокИнформации = НСтр("ru = 'При печати чека произошла ошибка'");
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(ЗаголовокИнформации, ТекстСообщения);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПриЗавершении, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧека_ПослеОткрытияЧека(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ШапкаЧека = ПараметрыВыполнения.ВходныеПараметры;
	
	НомерСмены = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ПараметрыВыполнения.НомерСмены);
	
	Если НомерСмены <> 0 Тогда
		ШапкаЧека.НомерСмены = НомерСмены;
	КонецЕсли;
	
	НомерЧека = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ПараметрыВыполнения.НомерЧека);
	
	Если НомерЧека <> 0 Тогда
		ШапкаЧека.НомерЧека = НомерЧека;
	КонецЕсли;
	
	Если ПараметрыВыполнения.Свойство("ЗаводскойНомерФН") И ЗначениеЗаполнено(ПараметрыВыполнения.ЗаводскойНомерФН) Тогда
		ШапкаЧека.СерийныйНомер = ПараметрыВыполнения.ЗаводскойНомерФН;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЕстьАлкогольнаяПродукцияЕГАИС Тогда
	
		ПараметрыОперации = Новый Структура;
		ПараметрыОперации.Вставить("НомерСмены", ШапкаЧека.НомерСмены);
		ПараметрыОперации.Вставить("НомерЧека",  ШапкаЧека.НомерЧека);
		ПараметрыОперации.Вставить("СерийныйНомер",  ШапкаЧека.СерийныйНомер);
		
		ИнтеграцияЕГАИСКлиент.ПередатьНемедленно(
			ДополнительныеПараметры.ЧекККМСсылка,
			ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ПередайтеДанные"),
			ПараметрыОперации,
			Новый ОписаниеОповещения("ПослеПередачиЧекаЕГАИС", ЭтотОбъект, ПараметрыВыполнения));
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ПараметрыВыполнения.ОповещениеПродолжения, ПараметрыВыполнения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЧека_ПослеОшибкиПечатиЧека(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ШапкаЧека = ПараметрыВыполнения.ВходныеПараметры;
	ШапкаЧека.НомерЧека = ШапкаЧека.НомерЧека + 1000000;
	
	Если ШапкаЧека.ТипРасчета = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств") Тогда
		ШапкаЧека.ТипРасчета = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств");
	Иначе
		ШапкаЧека.ТипРасчета = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств");
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЕстьАлкогольнаяПродукцияЕГАИС Тогда
	
		ПараметрыОперации = Новый Структура;
		ПараметрыОперации.Вставить("НомерСмены", ШапкаЧека.НомерСмены);
		ПараметрыОперации.Вставить("НомерЧека",  ШапкаЧека.НомерЧека);
		
		ИнтеграцияЕГАИСКлиент.ПередатьНемедленно(
			ДополнительныеПараметры.ЧекККМСсылка,
			ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюЕГАИС.ОтменитеОперацию"),
			ПараметрыОперации,
			Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура печати чека на фискальном регистраторе.
//
&НаКлиенте
Процедура ПробитьЧекВыполнить(ОповещениеПриЗавершении, ЧекККМ)
	
	ЧекПробит = Ложь;
	
	ПараметрыКассыККМ = ЗначениеНастроекВызовСервера.ПолучитьПараметрыКассыККМ(Объект.КассаККМ);
	ИдентификаторУстройстваФР = ПараметрыКассыККМ.ИдентификаторУстройства;
	
	ИспользоватьКассуККМБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	
	Если НЕ ИспользоватьПодключаемоеОборудование ИЛИ ИспользоватьКассуККМБезПодключенияОборудования Тогда
		
		Если НомерДокументаКассыККМ[Объект.КассаККМ] <> Неопределено Тогда
			НомерЧекаККМ  = НомерДокументаКассыККМ[Объект.КассаККМ];
		Иначе
			НомерЧекаККМ  = ПорядковыйНомерПродажи;
		КонецЕсли;
		
		СтруктураОшибкиПробитияЧека = Новый Структура;
		РезультатПроведения = ПровестиПробитыйЧек(ЧекККМ, 0, НомерЧекаККМ, "", "", СтруктураОшибкиПробитияЧека);
		Если РезультатПроведения = Истина Тогда
			НомерДокументаКассыККМ[Объект.КассаККМ] = НомерЧекаККМ + 1;
			ПорядковыйНомерПродажи = ПорядковыйНомерПродажи + 1;
			ЧекПробит = Истина;
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, ЧекПробит);
		Возврат;
		
	КонецЕсли;
	
	Если ИдентификаторУстройстваФР <> Неопределено Тогда
		
		Контекст = Новый Структура;
		Контекст.Вставить("ЧекККМСсылка"       , ЧекККМ);
		Контекст.Вставить("КассаККМ"           , Объект.КассаККМ);
		Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		
		ЕстьАлкогольнаяПродукцияЕГАИС = Ложь;
		ТранспортныйМодуль = Неопределено;
		ДанныеЕГАИСДостаточны = Истина;
		ТекстПолногоСообщения = "";
		
		НомерЧека = НомерДокументаКассыККМ[Объект.КассаККМ] + 1;
		ОбщиеПараметры = ПодготовитьДанныеДляПробитияЧека(ЧекККМ, ДанныеЕГАИСДостаточны, НомерЧека, ТранспортныйМодуль, ЕстьАлкогольнаяПродукцияЕГАИС);
		
		Контекст.Вставить("ЕстьАлкогольнаяПродукцияЕГАИС", ЕстьАлкогольнаяПродукцияЕГАИС);
		
		Если НЕ ДанныеЕГАИСДостаточны Тогда
			ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Ложь);
			Возврат;
		КонецЕсли;
		
		Если ЕстьАлкогольнаяПродукцияЕГАИС Тогда
			
			Контекст.Вставить("ТранспортныйМодуль", ТранспортныйМодуль);
			
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения("ПробитьЧекВыполнитьЗавершение", ЭтотОбъект, Контекст);
		ПослеОткрытияЧека     = Новый ОписаниеОповещения("ПечатьЧека_ПослеОткрытияЧека", ЭтотОбъект, Контекст);
		ПослеОшибкиПечатиЧека = Новый ОписаниеОповещения("ПечатьЧека_ПослеОшибкиПечатиЧека", ЭтотОбъект, Контекст);
		
		МенеджерОборудованияКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(Оповещение, 
										УникальныйИдентификатор, 
										ОбщиеПараметры, 
										ИдентификаторУстройстваФР,
										,
										ПослеОткрытияЧека,
										ПослеОшибкиПечатиЧека);
		
	Иначе
		ТекстСообщения = НСтр("ru = 'Не выбрано устройство для печати чеков.'");
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, ТекстСообщения);
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьЧекКлиентИнкассацияЗавершениеРКО(РезультатВыполнения, ПараметрыОперации) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		ОбщиеПараметры = Новый Структура();
		НомерЧека = НомерДокументаКассыККМ[Объект.КассаККМ] + 1;
		НапечататьЧекСерверИнкассацияЗавершение(ПараметрыОперации.ДокументНаККМ, 
												ПараметрыОперации.РаспределениеВыручкиПоСекциям,
												ОбщиеПараметры,
												НомерЧека);
		
		Оповещение = Новый ОписаниеОповещения("НапечататьЧекКлиентЗавершениеРКО", ЭтотОбъект, ПараметрыОперации);
		
		МенеджерОборудованияКлиент.НачатьФискализациюЧекаНаФискальномУстройстве(Оповещение, 
										УникальныйИдентификатор, 
										ОбщиеПараметры, 
										ПараметрыОперации.ИдентификаторУстройства);
		
	Иначе
		ЭтаФорма.Доступность = Истина;
		ТекстСообщения = НСтр("ru = 'При операции внесения произошла ошибка.
									|Дополнительное описание: %ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьЧекКлиентИнкассацияЗавершениеПКО(РезультатВыполнения, ПараметрыОперации) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		
		Отказ = Ложь;
		
		НапечататьЧекСерверЗавершение(ПараметрыОперации.НомерЧекаККМ, ПараметрыОперации.ДокументНаККМ, Отказ);
		
		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОповещениеПриЗавершенииПробитияЧека, Истина);
		
	Иначе
		ЭтаФорма.Доступность = Истина;
		ТекстСообщения = НСтр("ru = 'При операции выемки произошла ошибка.
									|Дополнительное описание: %ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура НапечататьЧекСерверИнкассацияЗавершение(ДокументНаККМ, РаспределениеВыручкиПоСекциям, ОбщиеПараметры, НомерЧека)
	
	// Готовим данные
	ОбщиеПараметры  = ВходящиеДанныеДляФРСервер(ДокументНаККМ, РаспределениеВыручкиПоСекциям, НомерЧека);
	
КонецПроцедуры

&НаСервере
Функция ВходящиеДанныеДляФРСервер(ДокументНаККМ, РаспределениеВыручкиПоСекциям, НомерЧека)
	
	Если ТипЗнч(ДокументНаККМ) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		Возврат Документы.ПриходныйКассовыйОрдер.ПодготовитьДанныеДляПробитияЧека(ДокументНаККМ, РаспределениеВыручкиПоСекциям, НомерЧека);
	Иначе
		Возврат Документы.РасходныйКассовыйОрдер.ПодготовитьДанныеДляПробитияЧека(ДокументНаККМ, РаспределениеВыручкиПоСекциям, НомерЧека);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура НапечататьЧекКлиентЗавершениеРКО(РезультатВыполнения, ПараметрыОперации) Экспорт
	
	ЭтаФорма.Доступность = Истина;
	
	Если РезультатВыполнения.Результат Тогда
		
		Отказ = Ложь;
		
		НапечататьЧекСерверЗавершение(РезультатВыполнения.ВыходныеПараметры[1], ПараметрыОперации.ДокументНаККМ, Отказ);
		
		ПараметрыДокумента = ОбщегоНазначенияРТВызовСервера.ПолучитьЗначенияРеквизитовОбъекта(ПараметрыОперации.ДокументНаККМ, Новый Структура("КассаККМ, НомерЧекаККМ"));
		НомерДокументаКассыККМ[ПараметрыДокумента.КассаККМ] = ПараметрыДокумента.НомерЧекаККМ + 1;

		ПорядковыйНомерПродажи = ПорядковыйНомерПродажи + 1;
		
		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОповещениеПриЗавершенииПробитияЧека, Истина);
	Иначе
		ТекстСообщения = НСтр("ru = 'При печати чека произошла ошибка.
									|Чек не напечатан на фискальном устройстве.
									|Дополнительное описание: %ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьЧекКлиентЗавершениеПКО(РезультатВыполнения, ПараметрыОперации) Экспорт
	
	ЭтаФорма.Доступность = Истина;
	
	Если РезультатВыполнения.Результат Тогда
		
		Отказ = Ложь;
		
		ПараметрыОперации.Вставить("НомерЧекаККМ", РезультатВыполнения.ВыходныеПараметры[1]);
		
		ПараметрыДокумента = ОбщегоНазначенияРТВызовСервера.ПолучитьЗначенияРеквизитовОбъекта(ПараметрыОперации.ДокументНаККМ, Новый Структура("КассаККМ"));
		
		НомерДокументаКассыККМ[ПараметрыДокумента.КассаККМ] = ПараметрыОперации.НомерЧекаККМ + 1;

		ПорядковыйНомерПродажи = ПорядковыйНомерПродажи + 1;
		
		ПараметрыОперации.Вставить("ТипИнкассации", 0);
		ПараметрыОперации.Вставить("Сумма"        , ПараметрыОперации.СуммаДокумента);
		
		Оповещение = Новый ОписаниеОповещения("НапечататьЧекКлиентИнкассацияЗавершениеПКО", ЭтотОбъект, ПараметрыОперации);
		
		МенеджерОборудованияКлиент.НачатьИнкассациюНаФискальномУстройстве(Оповещение, 
																		  УникальныйИдентификатор, 
																		  ПараметрыОперации, 
																		  ПараметрыОперации.ИдентификаторУстройства); 
	Иначе
		ТекстСообщения = НСтр("ru = 'При печати чека произошла ошибка.
									|Чек не напечатан на фискальном устройстве.
									|Дополнительное описание: %ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура НапечататьЧекСерверЗавершение(НомерЧекаККМ, ДокументНаККМ, Отказ)
	
	ДокументНаККМОбъект = ДокументНаККМ.ПолучитьОбъект();
	
	// Установить полученное значение номера чека реквизиту документа.
	ДокументНаККМОбъект.НомерЧекаККМ = НомерЧекаККМ;
	ДокументНаККМОбъект.ПробитЧек    = Истина;
	
	Попытка
		ДокументНаККМОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Отказ = Истина;
		Возврат
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПередачиЧекаЕГАИС(Изменения, ПараметрыВыполнения) Экспорт
	
	ЕстьОшибки = (Изменения.Количество() <> 1);
	
	Ошибки = Новый Массив;
	Для Каждого ЭлементДанных Из Изменения Цикл
		Если ЗначениеЗаполнено(ЭлементДанных.ТекстОшибки) Тогда
			ЕстьОшибки = Истина;
			Ошибки.Добавить(ЭлементДанных.ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	Если Ошибки.Количество() > 0 Тогда
		Ошибки.Вставить(0, НСтр("В процессе передачи данных в ЕГАИС возникли ошибки:"));
	КонецЕсли;
	ОписаниеОшибки = СтрСоединить(Ошибки, Символы.ПС);
	
	ПараметрыВыполнения.ПродолжитьПечать = Не ЕстьОшибки;
	ПараметрыВыполнения.ОписаниеОшибки   = ОписаниеОшибки;
	
	Если НЕ ЕстьОшибки Тогда
		ПараметрыВыполнения.Вставить("АдресЧека",   Изменения[0].ИдентификаторЗапроса);
		ПараметрыВыполнения.Вставить("ПодписьЧека", Изменения[0].Подпись);
		
		ИННОрганизации = "";
		КППОрганизации = "";
		НаименованиеОрганизации = "";
		
		Если ЗначениеЗаполнено(Изменения[0].ОрганизацияЕГАИС) Тогда
			РеквизитыОрганизацииЕГАИС = ИнтеграцияЕГАИСРТВызовСервера.ЗначенияРеквизитовОбъекта(Изменения[0].ОрганизацияЕГАИС, "ИНН, КПП, Наименование");
			
			ИННОрганизации = РеквизитыОрганизацииЕГАИС.ИНН;
			КППОрганизации = РеквизитыОрганизацииЕГАИС.КПП;
			
			НаименованиеОрганизации = СокрЛП(РеквизитыОрганизацииЕГАИС.Наименование);
			Если ПустаяСтрока(НаименованиеОрганизации) Тогда
				НаименованиеОрганизации = ПараметрыВыполнения.ВходныеПараметры.ОрганизацияНазвание;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыВыполнения.Вставить("ИНН", ИННОрганизации);
		ПараметрыВыполнения.Вставить("КПП", КППОрганизации);
		ПараметрыВыполнения.Вставить("НаименованиеОрганизации", НаименованиеОрганизации);
	КонецЕсли;

	ВыполнитьОбработкуОповещения(ПараметрыВыполнения.ОповещениеПродолжения, ПараметрыВыполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти