
&НаКлиенте
Перем СтараяУпаковка;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Цена           = Параметры.Цена;
	Упаковка       = Параметры.Упаковка;
	Номенклатура   = Параметры.Номенклатура;
	Характеристика = Параметры.Характеристика;
	
	СтараяУпаковка = Упаковка;
	
	Элементы.Цена.ТолькоПросмотр        = НЕ Параметры.РедактироватьЦену;
	Элементы.Цена.ПропускатьПриВводе    = НЕ Параметры.РедактироватьЦену;
	
	НаименованиеТовара = "" + Параметры.Номенклатура + ?(ЗначениеЗаполнено(Параметры.Характеристика), " (" + Параметры.Характеристика + ")","");
	
	Если Параметры.СкрытьЦену Тогда
		Элементы.Цена.Видимость        = Ложь;
		ЭтаФорма.АвтоЗаголовок         = Ложь;
		ЭтаФорма.Заголовок             = НСтр("ru = 'Ввод количества'");
		ЭтаФорма.ПоведениеКлавишиEnter = ТипПоведенияКлавишиEnter.КнопкаПоУмолчанию;
	КонецЕсли;
		
	Элементы.Упаковка.Видимость         = ЗначениеЗаполнено(Номенклатура.НаборУпаковок);
	Элементы.ЕдиницаИзмерения.Видимость = Не ЗначениеЗаполнено(Номенклатура.НаборУпаковок);
	
	КоличествоУпаковок = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Элементы.Цена.Видимость Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОжиданиеВводаЦены", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УпаковкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтараяУпаковка = Упаковка;
	
	ТекущаяСтрока = Новый Структура;
	ТекущаяСтрока.Вставить("Номенклатура", Номенклатура);
	ТекущаяСтрока.Вставить("Характеристика", Характеристика);
	ОбработкаТабличнойЧастиТоварыКлиент.ВыбратьУпаковкуНоменклатуры(ДанныеВыбора, СтандартнаяОбработка, ТекущаяСтрока, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПриИзменении(Элемент)
	
	УпаковкаПриИзмененииНаСервере(СтараяУпаковка);
	СтараяУпаковка = Упаковка;
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаОчистка(Элемент, СтандартнаяОбработка)
	
	УпаковкаПриИзмененииНаСервере(СтараяУпаковка);
	СтараяУпаковка = Упаковка;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	ОчиститьСообщения();
	
	Если КоличествоУпаковок = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнено количество'"),,"КоличествоУпаковок",,Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивСтруктур = Новый Массив;
	
	НоваяСтрока = Новый Структура;
	НоваяСтрока.Вставить("Номенклатура",       Номенклатура);
	НоваяСтрока.Вставить("Характеристика",     Характеристика);
	НоваяСтрока.Вставить("Упаковка",           Упаковка);
	НоваяСтрока.Вставить("Цена",               Цена);
	НоваяСтрока.Вставить("КоличествоУпаковок", КоличествоУпаковок);
	МассивСтруктур.Добавить(НоваяСтрока);
	
	Закрыть(МассивСтруктур);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УпаковкаПриИзмененииНаСервере(СтараяУпаковка)
	
	Если СтараяУпаковка = Неопределено Тогда
		КоэффициентСтаройУпаковки = 1;
	Иначе
		Если Не ЗначениеЗаполнено(СтараяУпаковка.Коэффициент) Тогда
			КоэффициентСтаройУпаковки = 1;
		Иначе
			КоэффициентСтаройУпаковки = СтараяУпаковка.Коэффициент;
		КонецЕсли;
	КонецЕсли;
	
	Цена = Цена * ?(ЗначениеЗаполнено(Упаковка.Коэффициент), Упаковка.Коэффициент, 1) / КоэффициентСтаройУпаковки;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОжиданиеВводаЦены()
	
	Если ЭтаФорма.ТекущийЭлемент = Элементы.Цена Тогда
		ЭтаФорма.ПоведениеКлавишиEnter = ТипПоведенияКлавишиEnter.КнопкаПоУмолчанию;
	Иначе
		ПодключитьОбработчикОжидания("Подключаемый_ОжиданиеВводаЦены", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти