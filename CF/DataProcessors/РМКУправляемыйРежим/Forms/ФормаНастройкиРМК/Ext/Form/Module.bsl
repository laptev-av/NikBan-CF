
#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ОповещениеОткрытьФормуВыбораНастройкиРМК(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(РезультатОткрытияФормы) И НЕ РезультатОткрытияФормы = НастройкаРМК Тогда
		
		НастройкаРМК = РезультатОткрытияФормы;
		ЗаписатьТекущуюНастройкуРМК();
		ОбновитьЗаголовкиФормыИЭлементовКлиент();
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОткрытьФормуВопросаВыбратьНастройкуРМК(РезультатОткрытияФормы, ДополнительныеПараметры) Экспорт
	Если НЕ РезультатОткрытияФормы = Неопределено Тогда
		Если ВРЕГ(РезультатОткрытияФормы) = "ДА" Тогда
			ВыборНастройкиРМК();
			ЗавершитьИзменениеНастройки()
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если Не ВебКлиент Тогда
	мИмяКомпьютера = ИмяКомпьютера();
	#КонецЕсли  
	
	ПолучитьТекущуюНастройкуРМКСервер();
	
	ОбновитьЗаголовкиФормыИЭлементовКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаИзменениеНастройкиРМК(Команда)
	Если Не ЗначениеЗаполнено(НастройкаРМК) Тогда
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуВопросаВыбратьНастройкуРМК", ЭтотОбъект);
		ОбщегоНазначенияРТКлиент.ВывестиВопросДляРМКУправляемой(НСтр("ru = 'На данном компьютере РМК не настроено! Выбрать настройку РМК?'"),,,ОбработчикОповещения);
		
	КонецЕсли;
	
	ЗавершитьИзменениеНастройки()
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыборНастройкиРМК(Команда)
	
	ВыборНастройкиРМК();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаТорговоеОборудование(Команда)
	
	ПараметрыФормы = Новый Структура();
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.ФормаСписка", ПараметрыФормы, ЭтотОбъект,,,,, Режим);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПраваПользователей(Команда)
	
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОткрытьФорму("РегистрСведений.ЗначенияДополнительныхПравПользователя.Форма.ФормаРедактирования",,,,,,, Режим);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьТекущуюНастройкуРМКСервер()
	
	НастройкаРМК = ПродажиСервер.ТекущаяНастройкаРМКСервер(мИмяКомпьютера);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборНастройкиРМК()
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.НастройкиРМК.Форма.ФормаВыбора.Открытие");

	ОбработчикОповещения = Новый ОписаниеОповещения("ОповещениеОткрытьФормуВыбораНастройкиРМК", ЭтотОбъект);
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОткрытьФорму("Справочник.НастройкиРМК.ФормаВыбора",,ЭтотОбъект,,,, ОбработчикОповещения, Режим);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьТекущуюНастройкуРМК()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерРегистраНастройкаРМК = РегистрыСведений.НастройкаРМКНаКомпьютере.СоздатьМенеджерЗаписи();
	МенеджерРегистраНастройкаРМК.Компьютер    = мИмяКомпьютера;
	МенеджерРегистраНастройкаРМК.НастройкаРМК = НастройкаРМК;
	МенеджерРегистраНастройкаРМК.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовкиФормыИЭлементовКлиент()
	
	ЭтотОбъект.Заголовок = "Настройки " + мИмяКомпьютера;
	
	Если ЗначениеЗаполнено(НастройкаРМК) Тогда
		Элементы.КомандаИзменениеНастройкиРМК.Заголовок = НСтр("ru = 'Изменение настройки РМК'") + " " + НастройкаРМК;
	Иначе
		Элементы.КомандаИзменениеНастройкиРМК.Заголовок = НСтр("ru = 'Изменение настройки РМК (Не выбрана)'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьИзменениеНастройки()
	
	Если Не ЗначениеЗаполнено(НастройкаРМК) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", НастройкаРМК);
	
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОткрытьФорму("Справочник.НастройкиРМК.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект,,,,, Режим);
	
КонецПроцедуры

#КонецОбласти