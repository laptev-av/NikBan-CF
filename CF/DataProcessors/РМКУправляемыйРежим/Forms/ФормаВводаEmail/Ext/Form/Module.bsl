#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ОповещениеПоискаПоШтрихкоду(Штрихкод, ДополнительныеПараметры) Экспорт
	
	ОбработатьПолученныеДанные(Штрихкод, ДополнительныеПараметры);
	
КонецПроцедуры


&НаКлиенте
Процедура ОповещениеПоискаПоМагнитномуКоду(ТекКод, ДополнительныеПараметры) Экспорт
	
	ОбработатьПолученныеДанные(ТекКод, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Email") Тогда
		Email = Параметры.Email;
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
	Если Параметры.Свойство("ОтказКлиентаОтСохраненияEmail") Тогда
		ОтказКлиентаОтСохраненияEmail = Параметры.ОтказКлиентаОтСохраненияEmail;
	КонецЕсли;
	
	Если Параметры.Свойство("ДисконтнаяКарта") Тогда
		ДисконтнаяКарта = Параметры.ДисконтнаяКарта;
	КонецЕсли;
		
	ОбработатьДисконтнуюКарту();
		
	ТекущийЭлемент = Элементы.ВведенныйEmail;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	ПоддерживаемыеТипыОборудования = "СканерШтрихкода, СчитывательМагнитныхКарт";
	
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтаФорма, ПоддерживаемыеТипыОборудования);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если ВводДоступен() Тогда
		
		ПодключаемоеОборудованиеРТКлиент.ВнешнееСобытиеОборудованияРМК(ЭтотОбъект, Источник, Событие, Данные);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСтереть(Команда)
	
	Email = "";
	
	ТекущийЭлемент = Элементы.ВведенныйEmail;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПередатьДанные(Команда)
	
	ЗакрытьСПередачейРезультата();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСохранениеЗначения(Команда)
	
	ОтказКлиентаОтСохраненияEmail = НЕ ОтказКлиентаОтСохраненияEmail;
	ВывестиКартинкуСохраненияEmailКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедуры
&НаКлиенте
Процедура ЗакрытьСПередачейРезультата()
	
	Отказ = НЕ ДанныеКорректны(Email);
	
	Если НЕ Отказ Тогда
		СтруктураОтвета = Новый Структура;
		СтруктураОтвета.Вставить("Email", Email);
		СтруктураОтвета.Вставить("ОтказКлиентаОтСохраненияEmail", ОтказКлиентаОтСохраненияEmail);
		
		Закрыть(СтруктураОтвета)
	Иначе
		ЗаголовокИнформации = НСтр("ru = 'Email введен не правильно'");
		ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(ЗаголовокИнформации, Email);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьДисконтнуюКарту()
	
	Если ЗначениеЗаполнено(ДисконтнаяКарта) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	0 КАК ПолеСортировки,
		|	ИнформационныеКартыКонтактнаяИнформация.АдресЭП,
		|	ИнформационныеКартыКонтактнаяИнформация.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Справочник.ИнформационныеКарты.КонтактнаяИнформация КАК ИнформационныеКартыКонтактнаяИнформация
		|ГДЕ
		|	ИнформационныеКартыКонтактнаяИнформация.Тип = &Тип
		|	И ИнформационныеКартыКонтактнаяИнформация.Ссылка = &ДисконтнаяКарта
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	1,
		|	ФизическиеЛицаКонтактнаяИнформация.АдресЭП,
		|	ФизическиеЛицаКонтактнаяИнформация.НомерСтроки
		|ИЗ
		|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
		|ГДЕ
		|	ФизическиеЛицаКонтактнаяИнформация.Ссылка = &ВладелецКарты
		|	И ФизическиеЛицаКонтактнаяИнформация.Тип = &Тип
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПолеСортировки,
		|	НомерСтроки";
		
		Запрос.УстановитьПараметр("Тип"            , Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты);
		Запрос.УстановитьПараметр("ДисконтнаяКарта", ДисконтнаяКарта);
		Запрос.УстановитьПараметр("ВладелецКарты"  , ДисконтнаяКарта.ВладелецКарты);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		ЕстьEmail = Ложь;
		Пока Выборка.Следующий() Цикл
			Если НЕ ЗначениеЗаполнено(Email) Тогда
				Email = Выборка.АдресЭП;
			КонецЕсли;
			
			Элементы.ВведенныйEmail.СписокВыбора.Добавить(Выборка.АдресЭП);
			ЕстьEmail = Истина;
		КонецЦикла;
		
		Если ЕстьEmail Тогда
			Элементы.ВведенныйEmail.КнопкаВыпадающегоСписка = Истина;
			Элементы.ВведенныйEmail.ОтображениеКнопкиВыбора = ОтображениеКнопкиВыбора.ОтображатьВВыпадающемСписке;
		КонецЕсли;
		ВывестиКартинкуСохраненияEmailСервер();
	Иначе
		Элементы.КомандаСохранениеЗначения.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиКартинкуСохраненияEmailКлиент()
	
	Если ОтказКлиентаОтСохраненияEmail Тогда
		Элементы.КомандаСохранениеЗначения.Картинка = БиблиотекаКартинок.НеСохранять32;
	Иначе
		Элементы.КомандаСохранениеЗначения.Картинка = БиблиотекаКартинок.Сохранить32;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиКартинкуСохраненияEmailСервер()
	
	Если ОтказКлиентаОтСохраненияEmail Тогда
		Элементы.КомандаСохранениеЗначения.Картинка = БиблиотекаКартинок.НеСохранять32;
	Иначе
		Элементы.КомандаСохранениеЗначения.Картинка = БиблиотекаКартинок.Сохранить32;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПодготовитьДанные(Знач Данные)
	Данные = СтрЗаменить(Данные, " ", "");
	Данные = СтрЗаменить(Данные, ";", "@");
	
	Возврат Данные;
	
КонецФункции

&НаКлиенте
Функция ДанныеКорректны(Данные)
	
	Если Не ЗначениеЗаполнено(Данные) Тогда
		Возврат Ложь
	КонецЕсли;
	
	Отказ = Ложь;
	Если ЗначениеЗаполнено(Данные) Тогда
		НомерСимволаПочты = Найти(Данные, "@");
		Отказ = НомерСимволаПочты < 2 ИЛИ СтрДлина(Данные) = 2;
		Если НЕ Отказ Тогда
			ОстатокEmail = Сред(Данные, НомерСимволаПочты + 1);
			НомерСимволаТочки = Найти(ОстатокEmail, ".");
			Отказ = НомерСимволаТочки < 2 ИЛИ СтрДлина(ОстатокEmail) = 2;
			Если НЕ Отказ Тогда
				ОстатокEmail = Сред(ОстатокEmail, НомерСимволаТочки + 1);
				Отказ = НЕ ЗначениеЗаполнено(ОстатокEmail);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат НЕ Отказ;
КонецФункции

&НаКлиенте
Процедура ОбработатьПолученныеДанные(Данные, ДополнительныеПараметры)
	Если НЕ ПустаяСтрока(Данные) Тогда
		
		// Подготовка штрихкода
		Данные = ПодготовитьДанные(Данные);
		
		// Проверка считанных данных
		Если НЕ ДанныеКорректны(Данные) Тогда
			ЗаголовокИнформации = НСтр("ru = 'Считан некорректный E-mail'");
			ОбщегоНазначенияРТКлиент.ВывестиИнформациюДляРМКУправляемой(ЗаголовокИнформации, Email);
			Возврат;
		КонецЕсли;
		
		Email = Данные;
		
	КонецЕсли;
КонецПроцедуры
 
#КонецОбласти
