
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Период.Вариант = ВариантСтандартногоПериода.Сегодня;
	
	Элементы.ТекущаяДата.Заголовок = Формат(ТекущаяДатаСеанса(), "ДЛФ=DD");
	Элементы.ДеньНедели.Заголовок = "(" + Формат(ТекущаяДатаСеанса(), "ДФ=дддд") + ")";
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ЗаполненыДанныеИзНастроек = Истина;
	
	ЗаполнитьДеревоРаспоряжений();
	
	Если ЗначениеЗаполнено(Период) Тогда
		ПриказыНаИнвентаризацию.Параметры.УстановитьЗначениеПараметра("ДатаНачала", Период.ДатаНачала);
		ПриказыНаИнвентаризацию.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", Период.ДатаОкончания);
	Иначе
		ПриказыНаИнвентаризацию.Параметры.УстановитьЗначениеПараметра("ДатаНачала", Дата("00010101"));
		ПриказыНаИнвентаризацию.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", Дата("39990101"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьСклада();
	
	Если НЕ ЗаполненыДанныеИзНастроек Тогда
		
		ЗаполнитьДеревоРаспоряжений();
		УстановитьПараметрыСпискаПриказов();
		
	КонецЕсли;
	
	УстановитьВсеОтборыДинамическогоСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МагазинПриИзменении(Элемент)
	
	УстановитьДоступностьСклада();
	
	ЗаполнитьДеревоРаспоряжений();
	РазвернутьСтрокиДерева();
	
	УстановитьВсеОтборыДинамическогоСписка();
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	ЗаполнитьДеревоРаспоряжений();
	РазвернутьСтрокиДерева();
	
	УстановитьОтборДинамическогоСписка("Склад");
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовТабличнойЧастиТовары

&НаКлиенте
Процедура РаспоряженияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Распоряжения.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные = Неопределено Тогда
		
		Если НЕ ТипЗнч(ТекущиеДанные.Распоряжение) = Тип("Строка") Тогда
			ПоказатьЗначение(,ТекущиеДанные.Распоряжение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьДеревоРаспоряжений();
	
	РазвернутьСтрокиДерева();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПриказы(Команда)
	
	Элементы.ПриказыНаИнвентаризацию.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетТоварыКПриемке(Команда)
	
	ОткрытьФорму("Отчет.ТоварыКПриемке.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетТоварыКОтгрузке(Команда)
	
	ОткрытьФорму("Отчет.ТоварыКОтгрузке.Форма");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетОформлениеИзлишковНедостач(Команда)
	
	ОткрытьФорму("Отчет.ОформлениеИзлишковНедостачТоваров.Форма");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьСклада()

	Элементы.Склад.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Магазин);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоРаспоряжений()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Склады.Ссылка КАК Склад
		|ПОМЕСТИТЬ СкладыМагазина
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.Магазин = &Магазин
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТоварыКПоступлениюОбороты.ДокументОснование КАК ДокументРаспоряжение,
		|	ТоварыКПоступлениюОбороты.Период КАК Дата
		|ПОМЕСТИТЬ РаспоряженияПоРегистру
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Регистратор,
		|			ВЫБОР
		|					КОГДА &ОтборПоСкладу
		|						ТОГДА Склад = &Склад
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ
		|				И ВЫБОР
		|					КОГДА &ОтборПоМагазину
		|						ТОГДА Склад В
		|								(ВЫБРАТЬ
		|									СкладыМагазина.Склад
		|								ИЗ
		|									СкладыМагазина КАК СкладыМагазина)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ) КАК ТоварыКПоступлениюОбороты
		|ГДЕ
		|	НЕ ТоварыКПоступлениюОбороты.Регистратор ССЫЛКА Документ.ПриходныйОрдерНаТовары
		|	И ВЫБОР
		|			КОГДА &ОтборПоМагазину
		|				ТОГДА ЕСТЬNULL(ТоварыКПоступлениюОбороты.ДокументОснование.Магазин, ТоварыКПоступлениюОбороты.ДокументОснование.МагазинПолучатель) = &Магазин
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументРаспоряжение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ЗаказПоставщику.Ссылка,
		|	ЗаказПоставщику.ДатаПоступления
		|ПОМЕСТИТЬ РаспоряженияПоЗаказам
		|ИЗ
		|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
		|ГДЕ
		|	ЗаказПоставщику.Проведен
		|	И НЕ ЗаказПоставщику.Ссылка В
		|				(ВЫБРАТЬ
		|					РаспоряженияПоРегистру.ДокументРаспоряжение
		|				ИЗ
		|					РаспоряженияПоРегистру КАК РаспоряженияПоРегистру)
		|	И (ЗаказПоставщику.Бессрочный
		|			ИЛИ НЕ ЗаказПоставщику.ДнейПросрочкиПоставки = 0)
		|	И ВЫБОР
		|			КОГДА НЕ ЗаказПоставщику.Бессрочный
		|				ТОГДА ЗаказПоставщику.ДатаПоступления МЕЖДУ &НачалоПериода И &КонецПериода
		|						ИЛИ ДОБАВИТЬКДАТЕ(ЗаказПоставщику.ДатаПоступления, ДЕНЬ, ЗаказПоставщику.ДнейПросрочкиПоставки) МЕЖДУ &НачалоПериода И &КонецПериода
		|						ИЛИ ЗаказПоставщику.ДатаПоступления <= &НачалоПериода
		|							И ДОБАВИТЬКДАТЕ(ЗаказПоставщику.ДатаПоступления, ДЕНЬ, ЗаказПоставщику.ДнейПросрочкиПоставки) >= &КонецПериода
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &ОтборПоСкладу
		|				ТОГДА ЗаказПоставщику.Склад = &Склад
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &ОтборПоМагазину
		|				ТОГДА ЗаказПоставщику.Магазин = &Магазин
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспоряженияПоРегистру.ДокументРаспоряжение,
		|	РаспоряженияПоРегистру.Дата
		|ПОМЕСТИТЬ Распоряжения
		|ИЗ
		|	РаспоряженияПоРегистру КАК РаспоряженияПоРегистру
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РаспоряженияПоЗаказам.Ссылка,
		|	РаспоряженияПоЗаказам.ДатаПоступления
		|ИЗ
		|	РаспоряженияПоЗаказам КАК РаспоряженияПоЗаказам
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	РаспоряженияПоРегистру.ДокументРаспоряжение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Распоряжения.Дата КАК Дата,
		|	ТоварыКПоступлениюОстаткиИОбороты.ДокументОснование КАК Распоряжение,
		|	ЕСТЬNULL(ТоварыКПоступлениюОстаткиИОбороты.ДокументОснование.Контрагент, ТоварыКПоступлениюОстаткиИОбороты.ДокументОснование.МагазинОтправитель) КАК Поставщик,
		|	ТоварыКПоступлениюОстаткиИОбороты.Номенклатура КАК Номенклатура,
		|	ТоварыКПоступлениюОстаткиИОбороты.Характеристика КАК Характеристика,
		|	ТоварыКПоступлениюОстаткиИОбороты.КоличествоКонечныйОстаток КАК КоличествоКонечныйОстаток,
		|	ТоварыКПоступлениюОстаткиИОбороты.КоличествоПриход КАК КоличествоПриход
		|ПОМЕСТИТЬ Результат
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.ОстаткиИОбороты(
		|			,
		|			,
		|			,
		|			,
		|			ДокументОснование В
		|				(ВЫБРАТЬ
		|					Распоряжения.ДокументРаспоряжение
		|				ИЗ
		|					Распоряжения КАК Распоряжения)) КАК ТоварыКПоступлениюОстаткиИОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Распоряжения КАК Распоряжения
		|		ПО ТоварыКПоступлениюОстаткиИОбороты.ДокументОснование = Распоряжения.ДокументРаспоряжение
		|ГДЕ
		|	ТоварыКПоступлениюОстаткиИОбороты.КоличествоКонечныйОстаток > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Поставщик,
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТоварыКОтгрузкеОбороты.ДокументОснование КАК ДокументРаспоряжение,
		|	ТоварыКОтгрузкеОбороты.Период КАК Дата
		|ПОМЕСТИТЬ РаспоряженияОтгрузка
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке.Обороты(
		|			&НачалоПериода,
		|			&КонецПериода,
		|			Регистратор,
		|			ВЫБОР
		|					КОГДА &ОтборПоСкладу
		|						ТОГДА Склад = &Склад
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ
		|				И ВЫБОР
		|					КОГДА &ОтборПоМагазину
		|						ТОГДА Склад В
		|								(ВЫБРАТЬ
		|									СкладыМагазина.Склад
		|								ИЗ
		|									СкладыМагазина КАК СкладыМагазина)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ) КАК ТоварыКОтгрузкеОбороты
		|ГДЕ
		|	НЕ ТоварыКОтгрузкеОбороты.Регистратор ССЫЛКА Документ.РасходныйОрдерНаТовары
		|	И ВЫБОР
		|			КОГДА &ОтборПоМагазину
		|				ТОГДА ЕСТЬNULL(ТоварыКОтгрузкеОбороты.ДокументОснование.Магазин, ТоварыКОтгрузкеОбороты.ДокументОснование.МагазинОтправитель) = &Магазин
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументРаспоряжение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РаспоряженияОтгрузка.Дата КАК Дата,
		|	ТоварыКОтгрузкеОстаткиИОбороты.ДокументОснование КАК Распоряжение,
		|	ЕСТЬNULL(ТоварыКОтгрузкеОстаткиИОбороты.ДокументОснование.Контрагент, ТоварыКОтгрузкеОстаткиИОбороты.ДокументОснование.МагазинПолучатель) КАК Получатель,
		|	ТоварыКОтгрузкеОстаткиИОбороты.Номенклатура КАК Номенклатура,
		|	ТоварыКОтгрузкеОстаткиИОбороты.Характеристика КАК Характеристика,
		|	ТоварыКОтгрузкеОстаткиИОбороты.КоличествоКонечныйОстаток КАК КоличествоКонечныйОстаток,
		|	ТоварыКОтгрузкеОстаткиИОбороты.КоличествоПриход КАК КоличествоПриход
		|ПОМЕСТИТЬ Результат1
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке.ОстаткиИОбороты(
		|			,
		|			,
		|			,
		|			,
		|			ДокументОснование В
		|				(ВЫБРАТЬ
		|					РаспоряженияОтгрузка.ДокументРаспоряжение
		|				ИЗ
		|					РаспоряженияОтгрузка КАК РаспоряженияОтгрузка)) КАК ТоварыКОтгрузкеОстаткиИОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспоряженияОтгрузка КАК РаспоряженияОтгрузка
		|		ПО ТоварыКОтгрузкеОстаткиИОбороты.ДокументОснование = РаспоряженияОтгрузка.ДокументРаспоряжение
		|ГДЕ
		|	ТоварыКОтгрузкеОстаткиИОбороты.КоличествоКонечныйОстаток > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Получатель,
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Результат.Распоряжение.Дата КАК Дата,
		|	Результат.Распоряжение КАК Распоряжение,
		|	Результат.Поставщик КАК Поставщик
		|ИЗ
		|	Результат КАК Результат
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Результат1.Распоряжение.Дата КАК Дата,
		|	Результат1.Распоряжение КАК Распоряжение,
		|	Результат1.Получатель КАК Получатель
		|ИЗ
		|	Результат1 КАК Результат1
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата";
	
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода",  Период.ДатаОкончания);

	Запрос.УстановитьПараметр("ОтборПоМагазину", ЗначениеЗаполнено(Магазин));
	Запрос.УстановитьПараметр("Магазин", Магазин);
	
	Запрос.УстановитьПараметр("ОтборПоСкладу", ЗначениеЗаполнено(Склад));
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Результат = Запрос.ВыполнитьПакет();
	ВыборкаПриемка = Результат[7].Выбрать();
	ВыборкаОтгрузка = Результат[8].Выбрать();
	
	Дерево = РеквизитФормыВЗначение("Распоряжения");
	Дерево.Строки.Очистить();
	
	СтрокаПриемка = Дерево.Строки.Добавить();
	СтрокаПриемка.Распоряжение = НСтр("ru = 'Приемка ('") + Строка(ВыборкаПриемка.Количество()) + ")";
	
	Если ВыборкаПриемка.Количество() = 0 Тогда
		Строка = СтрокаПриемка.Строки.Добавить();
		Строка.Распоряжение = НСтр("ru = '<нет распоряжений>'");
	Иначе
		Пока ВыборкаПриемка.Следующий() Цикл
			Строка = СтрокаПриемка.Строки.Добавить();
			Строка.Распоряжение = ВыборкаПриемка.Распоряжение;
		КонецЦикла;
	КонецЕсли;
	
	СтрокаОтгрузка = Дерево.Строки.Добавить();
	СтрокаОтгрузка.Распоряжение = НСтр("ru = 'Отгрузка ('") + Строка(ВыборкаОтгрузка.Количество()) + ")";
	
	Если ВыборкаОтгрузка.Количество() = 0 Тогда
		Строка = СтрокаОтгрузка.Строки.Добавить();
		Строка.Распоряжение = НСтр("ru = '<нет распоряжений>'");;
	Иначе
		Пока ВыборкаОтгрузка.Следующий() Цикл
			Строка = СтрокаОтгрузка.Строки.Добавить();
			Строка.Распоряжение = ВыборкаОтгрузка.Распоряжение;
		КонецЦикла;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Дерево, "Распоряжения");
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьСтрокиДерева()
	
	Строки = Распоряжения.ПолучитьЭлементы();
	
	Для Каждого Строка Из Строки Цикл
		ИдентификаторСтроки=Строка.ПолучитьИдентификатор();
		Элементы.Распоряжения.Развернуть(ИдентификаторСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВсеОтборыДинамическогоСписка()
	
	УстановитьОтборДинамическогоСписка("Магазин");
	УстановитьОтборДинамическогоСписка("Склад");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДинамическогоСписка(ИмяРеквизита)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		ПриказыНаИнвентаризацию,
		ИмяРеквизита,
		ЭтаФорма[ИмяРеквизита],
		ЗначениеЗаполнено(ЭтаФорма[ИмяРеквизита]));
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыСпискаПриказов()
	
	Если ЗначениеЗаполнено(Период) Тогда
		ПриказыНаИнвентаризацию.Параметры.УстановитьЗначениеПараметра("ДатаНачала", Период.ДатаНачала);
		ПриказыНаИнвентаризацию.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", Период.ДатаОкончания);
	Иначе
		ПриказыНаИнвентаризацию.Параметры.УстановитьЗначениеПараметра("ДатаНачала", Дата("00010101"));
		ПриказыНаИнвентаризацию.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", Дата("39990101"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
