
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОсновнойСклад") Тогда
		ОсновнойСклад = Параметры.ОсновнойСклад;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("СписокСкладов") Тогда
		Для Каждого ЭлементСписка Из Параметры.СписокСкладов Цикл
			НоваяСтрокаРаспределения = СкладыРаспределения.Добавить();
			НоваяСтрокаРаспределения.Склад = ЭлементСписка.Значение;
			Если НоваяСтрокаРаспределения.Склад = ОсновнойСклад Тогда
				НоваяСтрокаРаспределения.Видимость = Истина;
			Иначе
				НоваяСтрокаРаспределения.Видимость = ЭлементСписка.Пометка;
			КонецЕсли;
		КонецЦикла;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("хзСкладыПеремещения") Тогда
		СкладыПеремещения = Параметры.хзСкладыПеремещения.Получить();
		Для Каждого СкладПеремещения Из СкладыПеремещения Цикл
			СтрокиРаспределения = СкладыРаспределения.НайтиСтроки(Новый Структура("Склад", СкладПеремещения.Склад));
			Если СкладПеремещения.КоличествоПозиций = 0 Тогда
				СтрокиРаспределения[0].КоличествоПозицийРаспределено = "";
			Иначе
				СтрокиРаспределения[0].КоличествоПозицийРаспределено = "(" + Строка(СкладПеремещения.КоличествоПозиций) + ")";
			КонецЕсли;
		КонецЦикла;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	
	СписокРезультат = Новый СписокЗначений();
	
	ВидимыеСклады = СкладыРаспределения.НайтиСтроки(Новый Структура("Видимость", Истина));
	
	Для Каждого СкладРаспределения Из СкладыРаспределения Цикл
		Если СкладРаспределения.Склад = ОсновнойСклад Тогда
			ВидимостьТекущегоСклада = ВидимыеСклады.Количество() > 1;
		Иначе
			ВидимостьТекущегоСклада = СкладРаспределения.Видимость;
		КонецЕсли;
		НовыйЭлементСписка = СписокРезультат.Добавить(СкладРаспределения.Склад,, ВидимостьТекущегоСклада);
	КонецЦикла;
	
	Закрыть(СписокРезультат);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для Каждого СтрокаСклада Из СкладыРаспределения Цикл
		Если не СтрокаСклада.Склад = ОсновнойСклад и не ЗначениеЗаполнено(СтрокаСклада.КоличествоПозицийРаспределено) Тогда
			СтрокаСклада.Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсе(Команда)
	
	Для Каждого СтрокаСклада Из СкладыРаспределения Цикл
		Если не СтрокаСклада.Склад = ОсновнойСклад и не ЗначениеЗаполнено(СтрокаСклада.КоличествоПозицийРаспределено) Тогда
			СтрокаСклада.Видимость = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
