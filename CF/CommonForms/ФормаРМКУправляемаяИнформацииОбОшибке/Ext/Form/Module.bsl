#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЗаголовокИнформации") Тогда
		ДекорацияЗаголовокИнформации = Параметры.ЗаголовокИнформации;
	КонецЕсли;
	
	Если Параметры.Свойство("ТекстИнформации") Тогда
		ДекорацияТекстИнформации = Параметры.ТекстИнформации;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокИнформации") И НЕ ПустаяСтрока(Параметры.ЗаголовокИнформации)
		И Параметры.Свойство("ТекстИнформации") И НЕ ПустаяСтрока(Параметры.ТекстИнформации) Тогда
		ОтображатьЗаголовок = Ложь;
		Заголовок = "";
	КонецЕсли;
	
	Если Параметры.Свойство("ТипОтвета") Тогда
		Если Параметры.ТипОтвета = "Вопрос" Тогда
			Элементы.СтраницыКнопок.ТекущаяСтраница = Элементы.СтраницаВопрос;
			Если Параметры.Свойство("ОтветПоУмолчанию") 
				И ВРЕГ(Параметры.ОтветПоУмолчанию) = "ДА" Тогда
				Элементы.КомандаОтветДа.КнопкаПоУмолчанию = Истина;
				ТекущийЭлемент = Элементы.КомандаОтветДа;
			Иначе
				Элементы.КомандаОтветНет.КнопкаПоУмолчанию = Истина;
				ТекущийЭлемент = Элементы.КомандаОтветНет;
			КонецЕсли;
		Иначе
			Элементы.СтраницыКнопок.ТекущаяСтраница = Элементы.СтраницаИнформации;
			Элементы.КнопкаЗакрыть.КнопкаПоУмолчанию = Истина;
			ТекущийЭлемент = Элементы.КнопкаЗакрыть;
		КонецЕсли;
	Иначе
		ТекущийЭлемент = Элементы.КнопкаЗакрыть;
	КонецЕсли;
	
	Если Параметры.Свойство("Подсказка") Тогда
		
		Элементы.ДекорацияТекстИнформации.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
		Элементы.ДекорацияТекстИнформации.Подсказка            = Параметры.Подсказка;
		Элементы.ДекорацияТекстИнформации.Ширина = Элементы.ДекорацияТекстИнформации.Ширина - 3;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Элементы.КомандаОтветНет.КнопкаПоУмолчанию Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.КомандаОтветНет;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОтветДа(Команда)
	Закрыть("Да")
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтветНет(Команда)
	Закрыть("Нет")
КонецПроцедуры

#КонецОбласти
