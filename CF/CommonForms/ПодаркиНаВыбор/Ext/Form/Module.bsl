
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("АдресВоВременномХранилище", АдресВоВременномХранилище) Тогда
		
		ТаблицаПодарков = ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище);
		СформироватьДеревоПодарков(ТаблицаПодарков);
		
	Иначе
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоПодарков

&НаКлиенте
Процедура ДеревоПодарковПередНачаломИзменения(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные = Неопределено
		ИЛИ НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Номенклатура) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодарковПриИзменении(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено
		ИЛИ НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	Если Элемент.ТекущиеДанные.Выбран Тогда
		ТекущаяСтрокаДерева = ДеревоПодарков.НайтиПоИдентификатору(Элемент.ТекущаяСтрока);
		Если ТекущаяСтрокаДерева <> Неопределено Тогда
			ТекущийРодитель = ТекущаяСтрокаДерева.ПолучитьРодителя();
			ПодчиненныеСтроки = ТекущийРодитель.ПолучитьЭлементы();
			Для Каждого ПодчиненнаяСтрока Из ПодчиненныеСтроки Цикл
				Если ПодчиненнаяСтрока.Выбран
					И ПодчиненнаяСтрока <> ТекущаяСтрокаДерева Тогда
					ПодчиненнаяСтрока.Выбран = Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыдатьПодарки(Команда)
	
	АдресПодарков = ПоместитьПодаркиВоВременнойХранилище();
	Закрыть(АдресПодарков);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоместитьПодаркиВоВременнойХранилище()
	
	ТаблицаПодарков = СкидкиНаценкиСервер.ПустаяТаблицаПодарков();
	ЭлементыДерева = ДеревоПодарков.ПолучитьЭлементы();
	Для Каждого СтрокаДерева Из ЭлементыДерева Цикл
		ПодчиненныеСтроки = СтрокаДерева.ПолучитьЭлементы();
		Для Каждого ПодчиненнаяСтрока Из ПодчиненныеСтроки Цикл
			Если ПодчиненнаяСтрока.Выбран Тогда
				НоваяСтрока = ТаблицаПодарков.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПодчиненнаяСтрока);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Адрес = ПоместитьВоВременноеХранилище(ТаблицаПодарков, Новый УникальныйИдентификатор);
	
	Возврат Адрес;
	
КонецФункции

&НаСервере
Процедура СформироватьДеревоПодарков(ТаблицаПодарков)
	
	ЭлементыДерева = ДеревоПодарков.ПолучитьЭлементы();
	Для Каждого СтрокаПодарка Из ТаблицаПодарков Цикл
		СтрокаНайдена = Ложь;
		Для Каждого СтрокаДерева Из ЭлементыДерева Цикл
			Если СтрокаДерева.СкидкаНаценка = СтрокаПодарка.СкидкаНаценка Тогда
				СтрокаНайдена = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ СтрокаНайдена Тогда
			СтрокаДерева = ЭлементыДерева.Добавить();
			СтрокаДерева.СкидкаНаценка = СтрокаПодарка.СкидкаНаценка;
		КонецЕсли;
		ПодчиненныеСтроки = СтрокаДерева.ПолучитьЭлементы();
		НоваяСтрока = ПодчиненныеСтроки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПодарка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

