
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыАнализаПересортицы = Константы.ПараметрыАнализаПересортицы.Получить();
	
	МассивПараметров = СкладскиеОперацииКлиентСервер.ПолучитьМассивПараметровАнализаПересортицы(ПараметрыАнализаПересортицы);
	
	МассивПараметровПоУмолчанию = СкладскиеОперацииКлиентСерверПовтИсп.ПолучитьМассивПараметровАнализаПересортицыПоУмолчанию();
	
	СписокПараметров.ЗагрузитьЗначения(МассивПараметров);
	СписокПараметров.ЗаполнитьПометки(Истина);
	
	Для каждого СтрокаПараметра Из МассивПараметровПоУмолчанию Цикл
		
		Если МассивПараметров.Найти(СтрокаПараметра) = Неопределено Тогда
			
			СписокПараметров.Добавить(СтрокаПараметра);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область ПроцедурыОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПараметровПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ОчиститьСообщения();
	Отказ = Ложь;
	
	ЗаписатьСервер(Отказ);

	Если НЕ Отказ Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьСервер(Отказ)
	
	ПараметрыАнализаПересортицы = "";
	ПервыйЭлемент = Истина;
	Для каждого ЭлементСписка Из СписокПараметров Цикл
		Если ЭлементСписка.Пометка Тогда
			Если НЕ ПервыйЭлемент Тогда
				ПараметрыАнализаПересортицы = ПараметрыАнализаПересортицы + Символы.ПС;
			КонецЕсли;
			ПараметрыАнализаПересортицы = ПараметрыАнализаПересортицы + ЭлементСписка.Значение;
			ПервыйЭлемент = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если ПустаяСтрока(ПараметрыАнализаПересортицы) Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо выбрать хотя бы один параметр'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"СписокПараметров");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Константы.ПараметрыАнализаПересортицы.Установить(ПараметрыАнализаПересортицы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
