
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СуммаБезСкидок                   = Параметры.СуммаБезСкидок;
	СуммаАвтоматическойСкидкиНаценки = Параметры.СуммаАвтоматическойСкидкиНаценки;
	СуммаРучнойСкидкиНаценки         = Параметры.СуммаРучнойСкидкиНаценки;
	Валюта                           = Параметры.Валюта;
	
	СуммаДокумента = СуммаБезСкидок;
	СуммаДокументаСУчетомСкидкиАвтоматическойСкидки = СуммаБезСкидок - СуммаАвтоматическойСкидкиНаценки;
	Если СуммаДокументаСУчетомСкидкиАвтоматическойСкидки >= СуммаРучнойСкидкиНаценки Тогда
		СуммаДокументаСУчетомСкидки = СуммаДокументаСУчетомСкидкиАвтоматическойСкидки - СуммаРучнойСкидкиНаценки;
	Иначе
		СуммаРучнойСкидкиНаценки = СуммаДокументаСУчетомСкидкиАвтоматическойСкидки;
		СуммаДокументаСУчетомСкидки = 0;
	КонецЕсли;
	
	Если СуммаРучнойСкидкиНаценки < 0 Тогда
		СуммаРучнойСкидкиНаценки = -СуммаРучнойСкидкиНаценки;
		ВариантПредоставления = 2;
		Элементы.ВариантыПредоставления.ТекущаяСтраница = Элементы.ВариантыПредоставления.ПодчиненныеЭлементы.Наценка;
	Иначе
		СуммаРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки;
		ВариантПредоставления = 1;
		Элементы.ВариантыПредоставления.ТекущаяСтраница = Элементы.ВариантыПредоставления.ПодчиненныеЭлементы.Скидка;
	КонецЕсли;
	
	Если СуммаДокумента > 0 Тогда
		ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
	КонецЕсли;
	
	МаксимальнаяСуммаСкидки  = МаксимальныйПроцентСкидки * СуммаДокумента / 100;
	Если МаксимальнаяСуммаСкидки = 0 ИЛИ МаксимальнаяСуммаСкидки > (СуммаБезСкидок - СуммаАвтоматическойСкидкиНаценки) Тогда
		МаксимальнаяСуммаСкидки            = (СуммаБезСкидок - СуммаАвтоматическойСкидкиНаценки);
	КонецЕсли;
	МаксимальнаяСуммаСкидкиДляКонтроля = (СуммаБезСкидок - СуммаАвтоматическойСкидкиНаценки);
	МаксимальнаяСуммаСкидки = Мин(МаксимальнаяСуммаСкидки, МаксимальнаяСуммаСкидкиДляКонтроля);
	
	Если СуммаБезСкидок <> 0 Тогда
		Если МаксимальныйПроцентСкидки = 0 ИЛИ МаксимальныйПроцентСкидки > (МаксимальнаяСуммаСкидки/СуммаБезСкидок)*100 Тогда
			МаксимальныйПроцентСкидки = (МаксимальнаяСуммаСкидки/СуммаБезСкидок)*100;
		КонецЕсли;
	КонецЕсли;
	
	Если МаксимальнаяСуммаСкидки > 0 Тогда
		Элементы.СуммаСкидки.КнопкаВыпадающегоСписка = Истина;
		Элементы.СуммаСкидки.СписокВыбора.Добавить(МаксимальнаяСуммаСкидки, Формат(МаксимальнаяСуммаСкидки, "ЧЦ=15; ЧДЦ=2"));
	КонецЕсли;
	
	Если МаксимальныйПроцентСкидки > 0 Тогда
		Элементы.ПроцентСкидки.КнопкаВыпадающегоСписка = Истина;
		Элементы.ПроцентСкидки.СписокВыбора.Добавить(МаксимальныйПроцентСкидки, Формат(МаксимальныйПроцентСкидки, "ЧЦ=15; ЧДЦ=2"));
	КонецЕсли;
	
	Если МаксимальнаяСуммаНаценки > 0 Тогда
		Элементы.СуммаНаценки.КнопкаВыпадающегоСписка = Истина;
		Элементы.СуммаНаценки.СписокВыбора.Добавить(МаксимальнаяСуммаНаценки, Формат(МаксимальнаяСуммаНаценки, "ЧЦ=15; ЧДЦ=2"));
	КонецЕсли;
	
	Если МаксимальныйПроцентНаценки > 0 Тогда
		Элементы.ПроцентНаценки.КнопкаВыпадающегоСписка = Истина;
		Элементы.ПроцентНаценки.СписокВыбора.Добавить(МаксимальныйПроцентНаценки, Формат(МаксимальныйПроцентНаценки, "ЧЦ=15; ЧДЦ=2"));
	КонецЕсли;
	
	Элементы.ВариантПредоставления.СписокВыбора.Добавить(1, НСтр("ru = 'Скидка'"));
	Элементы.ВариантПредоставления.СписокВыбора.Добавить(2, НСтр("ru = 'Наценка'"));
	
	Элементы.СуммаСкидки.МаксимальноеЗначение = СуммаДокументаСУчетомСкидки;
	Элементы.СуммаСкидки.МинимальноеЗначение = 0;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если СуммаРучнойСкидкиНаценки > МаксимальнаяСуммаСкидкиДляКонтроля Тогда
		
		Отказ = Истина;
		
		ТекстСообщения =НСтр("ru = 'Сумма скидки превышает максимальное возможное значение: %МаксимальнаяСуммаСкидкиДляКонтроля% %Валюта%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%МаксимальнаяСуммаСкидкиДляКонтроля%", МаксимальнаяСуммаСкидкиДляКонтроля);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Валюта%", Валюта);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СуммаРучнойСкидкиНаценки");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантПредоставленияПриИзменении(Элемент)
	
	Если ВариантПредоставления = 1 Тогда
		Элементы.ВариантыПредоставления.ТекущаяСтраница = Элементы.ВариантыПредоставления.ПодчиненныеЭлементы.Скидка;
		СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - СуммаРучнойСкидкиНаценки;
	Иначе
		Элементы.ВариантыПредоставления.ТекущаяСтраница = Элементы.ВариантыПредоставления.ПодчиненныеЭлементы.Наценка;
		СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки + СуммаРучнойСкидкиНаценки;
	КонецЕсли;
	
КонецПроцедуры // ВариантПредоставленияПриИзменении()

&НаКлиенте
Процедура СуммаСкидкиПриИзменении(Элемент)
	
	Если СуммаДокумента > 0 Тогда
		ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
	КонецЕсли;
	СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - СуммаРучнойСкидкиНаценки;
	
КонецПроцедуры // СуммаСкидкиПриИзменении()

&НаКлиенте
Процедура ПроцентСкидкиПриИзменении(Элемент)
	
	СуммаРучнойСкидкиНаценки          = ПроцентРучнойСкидкиНаценки * СуммаДокумента / 100;
	СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - СуммаРучнойСкидкиНаценки;
	
КонецПроцедуры // ПроцентСкидкиПриИзменении()

&НаКлиенте
Процедура СуммаДокументаСУчетомСкидкиПриИзменении(Элемент)
	
	Если СуммаДокументаСУчетомСкидки  > СуммаДокумента Тогда
		СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки;
		СуммаРучнойСкидкиНаценки = 0;
		ПроцентРучнойСкидкиНаценки = 0;
	Иначе
		СуммаРучнойСкидкиНаценки   = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки - СуммаДокументаСУчетомСкидки;
		Если СуммаДокумента > 0 Тогда
			ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры // СуммаДокументаСУчетомСкидкиПриИзменении()

&НаКлиенте
Процедура СуммаНаценкиПриИзменении(Элемент)

	Если СуммаДокумента > 0 Тогда
		ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
	КонецЕсли;
	СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки + СуммаРучнойСкидкиНаценки;
	
КонецПроцедуры // СуммаНаценкиПриИзменении()

&НаКлиенте
Процедура ПроцентНаценкиПриИзменении(Элемент)
	
	СуммаРучнойСкидкиНаценки          = ПроцентРучнойСкидкиНаценки * СуммаДокумента / 100;
	СуммаДокументаСУчетомСкидки = СуммаДокумента + СуммаРучнойСкидкиНаценки - СуммаАвтоматическойСкидкиНаценки;
	
КонецПроцедуры // ПроцентНаценкиПриИзменении()

&НаКлиенте
Процедура СуммаДокументаСУчетомНаценкиПриИзменении(Элемент)
	
	Если СуммаДокументаСУчетомСкидки < СуммаДокумента Тогда
		СуммаДокументаСУчетомСкидки = СуммаДокумента - СуммаАвтоматическойСкидкиНаценки;
		СуммаРучнойСкидкиНаценки = 0;
		ПроцентРучнойСкидкиНаценки = 0;
	Иначе
		СуммаРучнойСкидкиНаценки   = СуммаДокументаСУчетомСкидки - (СуммаДокумента - СуммаАвтоматическойСкидкиНаценки);
		Если СуммаДокумента > 0 Тогда
			ПроцентРучнойСкидкиНаценки = СуммаРучнойСкидкиНаценки * 100 / СуммаДокумента;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СуммаДокументаСУчетомНаценкиПриИзменении()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ВариантПредоставления = 1 Тогда
		ОчиститьСообщения();
		Если ПроверитьЗаполнение() Тогда
			Закрыть(СуммаРучнойСкидкиНаценки);
		КонецЕсли;
	Иначе
		Закрыть(-СуммаРучнойСкидкиНаценки);
	КонецЕсли;
	
КонецПроцедуры // ОК()

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры // Отмена()

#КонецОбласти
