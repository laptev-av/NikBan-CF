#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеКолонкиНоменклатуры = ЗначениеНастроекПовтИсп.ПолучитьЗначениеКонстанты("ДополнительнаяКолонкаПриОтображенииНоменклатуры");
	
	ТаблицаТоваров.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресТоваровВХранилище));
	СкрытьКолонкуУпаковка = Параметры.СкрытьКолонкуУпаковка;
	
	ЭтотОбъект.ТекущийЭлемент = Элементы.ТаблицаТоваров;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Закрыть(ПодготовитьСтруктуруВыбора());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТаблицаТоваровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(ПодготовитьСтруктуруВыбора());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПодготовитьСтруктуруВыбора()
	
	ТекущиеДанные = Элементы.ТаблицаТоваров.ТекущиеДанные;
	СтруктураВыбора = Новый Структура;
	СтруктураВыбора.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
	СтруктураВыбора.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	Если НЕ СкрытьКолонкуУпаковка Тогда
		СтруктураВыбора.Вставить("Упаковка", ТекущиеДанные.Упаковка);
	КонецЕсли;
	
	Возврат СтруктураВыбора;
	
КонецФункции

#КонецОбласти