
#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТребуетсяКонтроль = НЕ УправлениеПользователями.ПолучитьБулевоЗначениеПраваПользователя(
							ПланыВидовХарактеристик.ПраваПользователей.РазрешитьПревышениеСвободногоОстаткаДС,
							Ложь);

	Если НЕ ТребуетсяКонтроль Тогда
		ДополнительныеСвойства.РассчитыватьИзменения = Ложь;
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДенежныеСредстваНаличные",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый", ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.Организация,
	|	Таблица.Магазин,
	|	Таблица.Касса,
	|	Таблица.ДоговорПлатежногоАгента,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Сумма
	|		ИНАЧЕ -Таблица.Сумма
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваНаличныеПередЗаписью
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваНаличные КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Магазин,
	|	ТаблицаИзменений.Касса,
	|	ТаблицаИзменений.ДоговорПлатежногоАгента,
	|	СУММА(ТаблицаИзменений.Сумма) КАК СуммаИзменение
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваНаличныеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Организация КАК Организация,
	|		ТаблицаПередЗаписью.Магазин КАК Магазин,
	|		ТаблицаПередЗаписью.Касса КАК Касса,
	|		ТаблицаПередЗаписью.ДоговорПлатежногоАгента КАК ДоговорПлатежногоАгента,
	|		ТаблицаПередЗаписью.Сумма КАК Сумма
	|	ИЗ
	|		ДвиженияДенежныеСредстваНаличныеПередЗаписью КАК ТаблицаПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.Магазин,
	|		ТаблицаПриЗаписи.Касса,
	|		ТаблицаПриЗаписи.ДоговорПлатежногоАгента,
	|		ВЫБОР
	|			КОГДА ТаблицаПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаПриЗаписи.Сумма
	|			ИНАЧЕ ТаблицаПриЗаписи.Сумма
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваНаличные КАК ТаблицаПриЗаписи
	|	ГДЕ
	|		ТаблицаПриЗаписи.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Магазин,
	|	ТаблицаИзменений.Касса,
	|	ТаблицаИзменений.ДоговорПлатежногоАгента
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.Сумма) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияДенежныеСредстваНаличныеПередЗаписью";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатЗапроса[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияДенежныеСредстваНаличныеИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти