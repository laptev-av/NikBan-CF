#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	БлокироватьДляИзменения = Истина;

	// Для регистра контроль выполняется на наличие положительных остатков.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.РаспоряжениеНаРасходованиеДенежныхСредств КАК РаспоряжениеНаРасходованиеДенежныхСредств,
	|	Таблица.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Таблица.ДокументРасчета КАК ДокументРасчета,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -Таблица.Сумма
	|		ИНАЧЕ Таблица.Сумма
	|	КОНЕЦ КАК СуммаПередЗаписью
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваКВыплатеПередЗаписью
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваКВыплате КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияДенежныеСредстваКВыплатеИзменение".

	Запрос = Новый Запрос;
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.РаспоряжениеНаРасходованиеДенежныхСредств КАК РаспоряжениеНаРасходованиеДенежныхСредств,
	|	ТаблицаИзменений.СтатьяДвиженияДенежныхСредств             КАК СтатьяДвиженияДенежныхСредств,
	|	ТаблицаИзменений.ДокументРасчета                           КАК ДокументРасчета,
	|	СУММА(ТаблицаИзменений.СуммаИзменение)                     КАК СуммаИзменение
	|
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваКВыплатеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.РаспоряжениеНаРасходованиеДенежныхСредств КАК РаспоряжениеНаРасходованиеДенежныхСредств,
	|		Таблица.СтатьяДвиженияДенежныхСредств             КАК СтатьяДвиженияДенежныхСредств,
	|		Таблица.ДокументРасчета                           КАК ДокументРасчета,
	|		Таблица.СуммаПередЗаписью                         КАК СуммаИзменение
	|	ИЗ
	|		ДвиженияДенежныеСредстваКВыплатеПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.РаспоряжениеНаРасходованиеДенежныхСредств КАК РаспоряжениеНаРасходованиеДенежныхСредств,
	|		Таблица.СтатьяДвиженияДенежныхСредств             КАК СтатьяДвиженияДенежныхСредств,
	|		Таблица.ДокументРасчета                           КАК ДокументРасчета,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				Таблица.Сумма
	|			ИНАЧЕ
	|				-Таблица.Сумма
	|		КОНЕЦ                                             КАК СуммаИзменение
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваКВыплате КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.РаспоряжениеНаРасходованиеДенежныхСредств,
	|	ТаблицаИзменений.СтатьяДвиженияДенежныхСредств,
	|	ТаблицаИзменений.ДокументРасчета
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаИзменение) > 0
	|;
	|УНИЧТОЖИТЬ ДвиженияДенежныеСредстваКВыплатеПередЗаписью
	|";
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();

	СтруктураВременныеТаблицы.Вставить("ДвиженияДенежныеСредстваКВыплатеИзменение", Выборка.Количество > 0);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
