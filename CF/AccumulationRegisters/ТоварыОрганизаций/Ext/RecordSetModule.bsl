#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства)  Тогда
		Возврат;
	КонецЕсли;
		
	ТребуетсяКонтроль = Ложь;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если (Запись.ВидДвижения = ВидДвиженияНакопления.Расход И Запись.Количество <> 0) Тогда
			ТребуетсяКонтроль = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ТребуетсяКонтроль Тогда
		ДополнительныеСвойства.РассчитыватьИзменения = Ложь;
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый", ДополнительныеСвойства.ЭтоНовый);
	Запрос.УстановитьПараметр("Приход", ВидДвиженияНакопления.Приход);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Номенклатура              КАК Номенклатура,
	|	Таблица.Характеристика            КАК Характеристика,
	|	Таблица.Склад                     КАК Склад,
	|	Таблица.Организация               КАК Организация,
	|	Таблица.Поставщик                 КАК Поставщик,
	|	Таблица.Договор                   КАК Договор,
	|	Таблица.НомерГТД                  КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = &Приход
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ -Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияТоварыОрганизацийПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И (НЕ &ЭтоНовый)";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства)  Тогда
		Возврат;
	КонецЕсли;
		
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаИзменений.Номенклатура           КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика         КАК Характеристика,
	|	ТаблицаИзменений.Склад                  КАК Склад,
	|	ТаблицаИзменений.Организация            КАК Организация,
	|	ТаблицаИзменений.Поставщик              КАК Поставщик,
	|	ТаблицаИзменений.Договор                КАК Договор,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение,
	|	ТаблицаИзменений.НомерГТД                   КАК НомерГТД
	|ПОМЕСТИТЬ ДвиженияТоварыОрганизацийИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.Склад КАК Склад,
	|		Таблица.Организация КАК Организация,
	|		Таблица.Поставщик КАК Поставщик,
	|		Таблица.Договор КАК Договор,
	|		Таблица.НомерГТД КАК НомерГТД,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияТоварыОрганизацийПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Номенклатура,
	|		Таблица.Характеристика,
	|		Таблица.Склад,
	|		Таблица.Организация,
	|		Таблица.Поставщик,
	|		Таблица.Договор,
	|		Таблица.НомерГТД,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Склад,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Поставщик,
	|	ТаблицаИзменений.Договор,
	|	ТаблицаИзменений.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияТоварыОрганизацийПередЗаписью";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатЗапроса[0].Выбрать();
	Выборка.Следующий();
	
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияТоварыОрганизацийИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
