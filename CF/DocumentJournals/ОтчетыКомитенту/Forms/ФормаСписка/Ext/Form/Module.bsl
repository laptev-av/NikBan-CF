#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереСписокДокументов(Список);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	ТолькоПросмотр = (НЕ ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ОтчетКомитентуОПродажах))
						И (НЕ ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ОтчетКомитентуОСписании));
	Элементы.СписокОформитьСчетФактуруНаВознаграждение.Доступность = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.СчетФактураВыданный);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборМагазинПриИзменении(Элемент)
	
	УстановитьОтборДинамическогоСписка("Магазин");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	УстановитьОтборДинамическогоСписка("Организация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтветственныйПриИзменении(Элемент)
	
	УстановитьОтборДинамическогоСписка("Ответственный");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	
	УстановитьОтборДинамическогоСписка("Контрагент");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОформитьСчетФактуруНаВознаграждение(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущаяСтрока.Ссылка) = Тип("ДокументСсылка.ОтчетКомитентуОСписании") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для документа ""Отчет комитенту о списании"" ввод счет-фактуры не предусмотрен'"),,"Внимание");
		Возврат;
	КонецЕсли;
	
	ПараметрыСчетФактуры = Новый Структура;
	ПараметрыСчетФактуры.Вставить("Проведен",ТекущаяСтрока.Проведен);
	ПараметрыСчетФактуры.Вставить("Ссылка",  ТекущаяСтрока.Ссылка);

	СтруктураСчетФактуры = Новый Структура();
	СтруктураСчетФактуры.Вставить("Объект",             ПараметрыСчетФактуры);
	СтруктураСчетФактуры.Вставить("Модифицированность", Ложь);
	
	ПродажиКлиент.ВвестиСчетФактуру(СтруктураСчетФактуры, ТекущаяСтрока, Истина);

	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДинамическогоСписка(ИмяРеквизита)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
	Список, 
	ИмяРеквизита, 
	ЭтаФорма[ИмяРеквизита], 
	ЗначениеЗаполнено(ЭтаФорма[ИмяРеквизита]));
	
КонецПроцедуры


#КонецОбласти
