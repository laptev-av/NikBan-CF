#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТаблицаНоменклатуры = Новый ТаблицаЗначений;
	ТаблицаНоменклатуры.Колонки.Добавить("ВидНоменклатуры",				Новый ОписаниеТипов("СправочникСсылка.ВидыНоменклатуры"));
	ТаблицаНоменклатуры.Колонки.Добавить("Номенклатура",				Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаНоменклатуры.Колонки.Добавить("НаименованиеПоПравилу",		Новый ОписаниеТипов("Строка"));
	ТаблицаНоменклатуры.Колонки.Добавить("ПараметрИменования",			Новый ОписаниеТипов("Строка"));
	ТаблицаНоменклатуры.Колонки.Добавить("ЗначениеПараметраИменования",	Новый ОписаниеТипов("Строка"));
	
	ТаблицаДоступныхПараметров = РаботаСПравиламиИменования.ПолучитьДоступныеПараметры();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
	               |	ВидыНоменклатуры.ПравилоИменования КАК ПравилоИменования,
	               |	ВидыНоменклатуры.НаборСвойств КАК НаборСвойств
	               |ИЗ
	               |	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	               |ГДЕ
	               |	НЕ ВидыНоменклатуры.ПометкаУдаления
	               |	И ВидыНоменклатуры.ПравилоИменования <> ЗНАЧЕНИЕ(Справочник.ПравилаИменованияНоменклатуры.ПустаяСсылка)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		РезультатПроверкиПравила = РаботаСПравиламиИменования.ПроверитьИспользованиеПравилаИменования(Выборка.ПравилоИменования,ТаблицаДоступныхПараметров,Выборка.НаборСвойств);
		Если РезультатПроверкиПравила.ЕстьОшибки Или РезультатПроверкиПравила.Свойство("ПравилоНельзяИспользоватьДляНабора") Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПараметров = РаботаСПравиламиИменования.ПолучитьСтруктуруПараметровПравилаИменования(Выборка.ВидНоменклатуры);		
		Правило = РаботаСПравиламиИменованияКлиентСервер.ПреобразоватьСтрокуПоПравилу(Выборка.ПравилоИменования.Правило,СтруктураПараметров.ПараметрыПравилаИменования,"ИмяПараметра","ИмяЭлемента",Ложь);

		РезультатЗапроса = РаботаСПравиламиИменования.ПолучитьЗначенияПараметровНоменклатуры(Выборка.ВидНоменклатуры,СтруктураПараметров.ПараметрыПравилаИменования);
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			МассивСтрок				= Новый Массив;
			СтруктураНаименование	= Новый Структура;
			ПараметрыЗаполнены		= Истина;
			Для Каждого СтрокаПараметр из СтруктураПараметров.ПараметрыПравилаИменования Цикл
				СтруктураСтрока = Новый Структура();
				СтруктураСтрока.Вставить("ВидНоменклатуры",				Выборка.ВидНоменклатуры);
				СтруктураСтрока.Вставить("Номенклатура",				ВыборкаДетальныеЗаписи.Номенклатура);
				СтруктураСтрока.Вставить("ПараметрИменования",			СтрокаПараметр.ИмяПараметра);
				СтруктураСтрока.Вставить("ЗначениеПараметраИменования",	Формат(ВыборкаДетальныеЗаписи[СтрокаПараметр.ИмяЭлемента],"ДЛФ=Д; БЛ='Ложь'; БИ='" + СтрокаПараметр.ИмяПараметра + "'"));
				
				МассивСтрок.Добавить(СтруктураСтрока);
				Если СтруктураСтрока.ЗначениеПараметраИменования = "" тогда
					ПараметрыЗаполнены = Ложь;
				КонецЕсли;
				Если ПараметрыЗаполнены Тогда 
					СтруктураНаименование.Вставить(СтрокаПараметр.ИмяЭлемента,СтруктураСтрока.ЗначениеПараметраИменования);
				КонецЕсли;
			КонецЦикла;
			
			Если ПараметрыЗаполнены Тогда
				НаименованиеПоПравилу = РаботаСПравиламиИменованияКлиентСервер.ПреобразоватьСтрокуПоПравилу(Правило,СтруктураНаименование,"Ключ","Значение");
			Иначе
				НаименованиеПоПравилу = "";
			КонецЕсли;
			Если ВыборкаДетальныеЗаписи.НоменклатураНаименование <> НаименованиеПоПравилу Тогда
				Для Каждого ЭлементМассива из МассивСтрок Цикл
					СтрокаТаблицы = ТаблицаНоменклатуры.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы,ЭлементМассива);
					СтрокаТаблицы.НаименованиеПоПравилу = НаименованиеПоПравилу;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

    Схема = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
    
    Настройки = КомпоновщикНастроек.ПолучитьНастройки();
    ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
    
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, ДанныеРасшифровки);
    
    ВнешниеНаборыДанных = Новый Структура;
    ВнешниеНаборыДанных.Вставить("ТаблицаНоменклатуры",ТаблицаНоменклатуры);
    
    ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
    ДокументРезультат.Очистить();
    
    ПроцессорВывода = Новый
    ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	ДокументРезультат.ПоказатьУровеньГруппировокСтрок(1);
	
КонецПроцедуры

#КонецЕсли