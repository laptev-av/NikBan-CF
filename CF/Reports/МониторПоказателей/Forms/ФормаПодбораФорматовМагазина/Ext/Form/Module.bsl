
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СписокФорматовМагазина") Тогда 
		ЗаполнитьФорматыМагазинов(Параметры.СписокФорматовМагазина);
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда 
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), 
						НСтр("ru='Настройки были изменены. Записать изменения?'"), 
						РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
					
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьОтмеченные(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	СнятьОтметитьРазделы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	СнятьОтметитьРазделы(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФорматыМагазинов(СписокФорматовМагазина)
	
	ФорматыМагазинов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФорматыМагазинов.Ссылка КАК Значение,
	|	ВЫБОР
	|		КОГДА ФорматыМагазинов.Ссылка В (&СписокФорматовМагазина)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Справочник.ФорматыМагазинов КАК ФорматыМагазинов
	|ГДЕ
	|	НЕ ФорматыМагазинов.ЭтоГруппа
	|	И НЕ ФорматыМагазинов.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФорматыМагазинов.Наименование";
	
	Запрос.УстановитьПараметр("СписокФорматовМагазина", СписокФорматовМагазина);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ФорматыМагазинов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметитьРазделы(Флаг)
	
	Для Каждого Строка Из ФорматыМагазинов Цикл 
		Строка.Пометка = Флаг;	
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	Модифицированность = Ложь;
	
	ОтмеченныеЭлементы = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(ФорматыМагазинов);
	ОповеститьОВыборе(ОтмеченныеЭлементы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Модифицированность = Ложь;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СохранитьИЗакрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Закрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда 
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
