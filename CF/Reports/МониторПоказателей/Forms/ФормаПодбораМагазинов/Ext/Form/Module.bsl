
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СписокМагазинов") Тогда 
		ЗаполнитьМагазины(Параметры.СписокМагазинов);
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда 
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект), 
						НСтр("ru='Настройки были изменены. Записать изменения?'"), 
						РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьОтмеченные(Команда)
	
	СохранитьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	СнятьОтметитьРазделы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	СнятьОтметитьРазделы(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьМагазины(СписокМагазинов)
	
	Магазины.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Магазины.Ссылка КАК Значение,
	|	ВЫБОР
	|		КОГДА Магазины.Ссылка В (&СписокМагазинов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Справочник.Магазины КАК Магазины
	|ГДЕ
	|	НЕ Магазины.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Магазины.Наименование";
	
	Запрос.УстановитьПараметр("СписокМагазинов", СписокМагазинов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Магазины.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметитьРазделы(Флаг)
	
	Для Каждого Строка Из Магазины Цикл 
		Строка.Пометка = Флаг;	
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	Модифицированность = Ложь;
	
	ОтмеченныеЭлементы = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(Магазины);
	ОповеститьОВыборе(ОтмеченныеЭлементы);
	
КонецПроцедуры	

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Ответ = РезультатВопроса;
	Модифицированность = Ложь;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СохранитьИЗакрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Закрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда 
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
