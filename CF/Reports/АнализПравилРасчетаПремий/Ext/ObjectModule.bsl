#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОбщегоНазначенияРТ.ВывестиДатуФормированияОтчета(ДокументРезультат);
		
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - Структура - см. возвращаемое значение
//       ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ФормироватьСразу = Истина;
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных -
//       Пользовательские настройки для загрузки в компоновщик настроек.
//
// См. также:
//   "Расширение управляемой формы для отчета.ПриЗагрузкеПользовательскихНастроекНаСервере"
//    в синтакс-помощнике.
//
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Перем АдресХранилищаНастроекДляРасшифровки;
	
	Если Форма.Параметры.Свойство("ЭтоРасшифровка") И Форма.Параметры.Свойство("АдресХранилищаНастроекДляРасшифровки", АдресХранилищаНастроекДляРасшифровки) Тогда
			
			НастройкаРасшифровки = ПолучитьИзВременногоХранилища(АдресХранилищаНастроекДляРасшифровки);
			
			Если НастройкаРасшифровки <> Неопределено Тогда
				
				Для каждого ПараметрДанныхИсточника Из НастройкаРасшифровки.ПараметрыДанных.Элементы Цикл
					
					Если 	ТипЗнч(ПараметрДанныхИсточника) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
						ПараметрДанных = ЭтотОбъект.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти(ПараметрДанныхИсточника.Параметр);
						Если ПараметрДанных <> Неопределено Тогда
							ЗаполнитьЗначенияСвойств(ПараметрДанных, ПараметрДанныхИсточника);
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
				
				Для каждого ПараметрДанныхПриемника Из ЭтотОбъект.КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.Элементы Цикл
					
					Для каждого ПараметрДанныхИсточника Из НастройкаРасшифровки.Отбор.Элементы Цикл
						
						Если ТипЗнч(ПараметрДанныхИсточника) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
							Если ПараметрДанныхПриемника.Поле = ПараметрДанныхИсточника.ЛевоеЗначение Тогда
								Если НЕ ПараметрДанныхИсточника.Использование = Ложь Тогда
									НовыйЭлементОтбора = ЭтотОбъект.КомпоновщикНастроек.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
									ЗаполнитьЗначенияСвойств(НовыйЭлементОтбора, ПараметрДанныхИсточника, "ВидСравнения, Использование, ЛевоеЗначение, ПравоеЗначение, Применение");
								КонецЕсли;
							КонецЕсли;
					    КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла;
			КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
