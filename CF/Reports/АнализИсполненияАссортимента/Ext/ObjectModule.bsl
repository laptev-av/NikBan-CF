#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ЦветКрасный;
Перем ЦветАвто;
Перем ЦветСерый;

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТабличныйДокументРезультат = СформироватьОтчет();
	
	ОбщегоНазначенияРТ.ВывестиДатуФормированияОтчета(ТабличныйДокументРезультат);
	
	ДокументРезультат.АвтоМасштаб = Истина;
	ДокументРезультат.Вывести(ТабличныйДокументРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьОтчет()
	
	ТабличныйДокументРезультат = Новый ТабличныйДокумент;
	
	Запрос=Новый Запрос;
	
	ТекстЗапроса="ВЫБРАТЬ
	|	МАКСИМУМ(Квоты.Период) КАК Период,
	|	Квоты.ОбъектПланирования КАК ОбъектПланирования
	|ПОМЕСТИТЬ МаксимальныеПериодыКвот
	|ИЗ
	|	РегистрСведений.КвотыАссортимента.СрезПоследних(&Период, ) КАК Квоты
	|СГРУППИРОВАТЬ ПО
	|	Квоты.ОбъектПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Квоты.ОбъектПланирования КАК ФорматМагазина,
	|	Квоты.ТоварнаяКатегория.Владелец КАК ВидНоменклатуры,
	|	Квоты.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|	ВЫБОР КОГДА Квоты.Марка = ЗНАЧЕНИЕ(Справочник.Марки.ПустаяСсылка) ТОГДА &ПрочиеМарки ИНАЧЕ Квоты.Марка КОНЕЦ КАК Марка,
	|	Квоты.Квота КАК Квота,
	|	Квоты.ПроцентОтклонения КАК ПроцентОтклонения
	|ПОМЕСТИТЬ втТекущиеКвотыБезМагазинов
	|ИЗ
	|	РегистрСведений.КвотыАссортимента.СрезПоследних(&Период, //УсловиеКвоты
	|	) КАК Квоты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальныеПериодыКвот КАК МаксимальныеПериодыКвот
	|		ПО МаксимальныеПериодыКвот.ОбъектПланирования = Квоты.ОбъектПланирования
	|		И МаксимальныеПериодыКвот.Период = Квоты.Период
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТекущиеКвотыБезМагазинов.ФорматМагазина КАК ФорматМагазина,
	|	Магазины.Ссылка КАК Магазин,
	|	втТекущиеКвотыБезМагазинов.ВидНоменклатуры КАК ВидНоменклатуры,
	|	втТекущиеКвотыБезМагазинов.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|	втТекущиеКвотыБезМагазинов.Марка КАК Марка,
	|	втТекущиеКвотыБезМагазинов.Квота КАК Квота,
	|	втТекущиеКвотыБезМагазинов.ПроцентОтклонения КАК ПроцентОтклонения
	|ПОМЕСТИТЬ втТекущиеКвоты
	|ИЗ
	|	втТекущиеКвотыБезМагазинов КАК втТекущиеКвотыБезМагазинов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Магазины КАК Магазины
	|		ПО (Магазины.ФорматМагазина = втТекущиеКвотыБезМагазинов.ФорматМагазина)
	|		//УсловиеМагазиныКвотыИАссортимент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущиеКвоты.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|	ТекущиеКвоты.Марка КАК Марка,
	|	МАКСИМУМ(ТекущиеКвоты.Квота) КАК Квота,
	|	СРЕДНЕЕ(ТекущиеКвоты.ПроцентОтклонения) КАК ПроцентОтклонения
	|ПОМЕСТИТЬ втТекущиеКвотыБезФорматов
	|ИЗ
	|	втТекущиеКвоты КАК ТекущиеКвоты
	|
	|СГРУППИРОВАТЬ ПО
	|	ТекущиеКвоты.ТоварнаяКатегория,
	|	ТекущиеКвоты.Марка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТекущиеКвотыБезМагазинов.ФорматМагазина КАК ФорматМагазина,
	|	СУММА(втТекущиеКвотыБезМагазинов.Квота) КАК Квота,
	|	СРЕДНЕЕ(втТекущиеКвотыБезМагазинов.ПроцентОтклонения) КАК ПроцентОтклонения
	|ПОМЕСТИТЬ втКвотыИтогФормат
	|ИЗ
	|	втТекущиеКвотыБезМагазинов КАК втТекущиеКвотыБезМагазинов
	|СГРУППИРОВАТЬ ПО
	|	втТекущиеКвотыБезМагазинов.ФорматМагазина
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ТекущиеКвотыБезФорматов.Квота) КАК Квота,
	|	СРЕДНЕЕ(ТекущиеКвотыБезФорматов.ПроцентОтклонения) КАК ПроцентОтклонения
	|ПОМЕСТИТЬ втКвотыИтогОбщие
	|ИЗ
	|	втТекущиеКвотыБезФорматов КАК ТекущиеКвотыБезФорматов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Ассортимент.Номенклатура КАК Номенклатура,
	|	ВЫБОР КОГДА Ассортимент.Номенклатура.ТоварнаяКатегория.Код ЕСТЬ NULL ТОГДА 
	|		Ассортимент.Номенклатура.ВидНоменклатуры 
	|	ИНАЧЕ
	|		Ассортимент.Номенклатура.ТоварнаяКатегория.Владелец 
	|	КОНЕЦ КАК ВидНоменклатуры,
	|	Ассортимент.Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|	Ассортимент.Номенклатура.Марка КАК Марка,
	|	Ассортимент.ОбъектПланирования КАК ОбъектПланирования,
	|	Ассортимент.РольАссортимента КАК РольАссортимента,
	|	Ассортимент.РазрешеныЗакупки КАК РазрешеныЗакупки,
	|	Ассортимент.РазрешеныПродажи КАК РазрешеныПродажи
	|ПОМЕСТИТЬ втТекущаяНаполненностьФорматов
	|ИЗ
	|	РегистрСведений.Ассортимент.СрезПоследних(КОНЕЦПЕРИОДА(&Период, ДЕНЬ), %УсловиеАссортимент%) КАК Ассортимент
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ассортимент.ОбъектПланирования КАК ФорматМагазина,
	|	Магазины.Ссылка КАК Магазин,
	|	Ассортимент.ВидНоменклатуры КАК ВидНоменклатуры,
	|	Ассортимент.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|	Ассортимент.Марка КАК Марка,
	|	Ассортимент.Номенклатура КАК Номенклатура,
	|	Ассортимент.РольАссортимента КАК РольАссортимента
	|ПОМЕСТИТЬ втТекущаяНаполненность
	|ИЗ
	|	втТекущаяНаполненностьФорматов КАК Ассортимент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Магазины КАК Магазины
	|		ПО (Магазины.ФорматМагазина = Ассортимент.ОбъектПланирования)
	|		//УсловиеМагазиныКвотыИАссортимент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Склад.Магазин.ФорматМагазина КАК ФорматМагазина,
	|	ТоварыНаСкладахОстатки.Склад.Магазин КАК Магазин,
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ВЫБОР КОГДА ТоварыНаСкладахОстатки.Номенклатура.ТоварнаяКатегория.Код ЕСТЬ NULL ТОГДА 
	|		ТоварыНаСкладахОстатки.Номенклатура.ВидНоменклатуры 
	|	ИНАЧЕ
	|		ТоварыНаСкладахОстатки.Номенклатура.ТоварнаяКатегория.Владелец 
	|	КОНЕЦ КАК ВидНоменклатуры,
	|	ТоварыНаСкладахОстатки.Номенклатура.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|	ТоварыНаСкладахОстатки.Номенклатура.Марка КАК Марка,
	|	СУММА(ТоварыНаСкладахОстатки.КоличествоОстаток) КАК ВНаличииОстаток
	|ПОМЕСТИТЬ втТекущиеОстатки
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(КОНЕЦПЕРИОДА(&Период, ДЕНЬ), //УсловиеОстатки
	|	) КАК ТоварыНаСкладахОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыНаСкладахОстатки.Склад.Магазин,
	|	ТоварыНаСкладахОстатки.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыНаСкладахОстатки.КоличествоОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъединенныйОбщий.ФорматМагазина КАК ФорматМагазина,
	|	ОбъединенныйОбщий.Магазин КАК Магазин,
	|	ОбъединенныйОбщий.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ОбъединенныйОбщий.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|	ОбъединенныйОбщий.Марка КАК Марка,
	|	ОбъединенныйОбщий.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ОбъединенныйОбщий.НоменклатураАссортимента) КАК НоменклатураАссортимента,
	|	МАКСИМУМ(ОбъединенныйОбщий.НоменклатураОстатка) КАК НоменклатураОстатка,
	|	МАКСИМУМ(ОбъединенныйОбщий.РольАссортимента) КАК РольАссортимента,
	|	МАКСИМУМ(ОбъединенныйОбщий.ЭтоНеИтог) КАК ЭтоНеИтог,
	|	МАКСИМУМ(ОбъединенныйОбщий.Квота) КАК Квота,
	|	МАКСИМУМ(ОбъединенныйОбщий.ПроцентОтклонения) КАК ПроцентОтклонения,
	|	МАКСИМУМ(ОбъединенныйОбщий.Наполненность) КАК Наполненность,
	|	МАКСИМУМ(ОбъединенныйОбщий.Представленность) КАК Представленность,
	|	МАКСИМУМ(ОбъединенныйОбщий.КвотаИтогФормат) КАК КвотаИтогФормат,
	|	МАКСИМУМ(ОбъединенныйОбщий.КвотаИтог) КАК КвотаИтог,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ОбъединенныйОбщий.Представленность) > МАКСИМУМ(ОбъединенныйОбщий.Наполненность)
	|			ТОГДА 1
	|		КОГДА МАКСИМУМ(ОбъединенныйОбщий.Наполненность) > МАКСИМУМ(ОбъединенныйОбщий.Представленность)
	|				И МАКСИМУМ(ОбъединенныйОбщий.РольАссортимента) = ЗНАЧЕНИЕ(Перечисление.РолиАссортимента.ПостоянныйАссортимент)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НарушениеНаполненности
	|ПОМЕСТИТЬ втИтоговаяТаблица
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОбщийЗапросОбъединение.ФорматМагазина КАК ФорматМагазина,
	|		ОбщийЗапросОбъединение.Магазин КАК Магазин,
	|		ОбщийЗапросОбъединение.ВидНоменклатуры КАК ВидНоменклатуры,
	|		ОбщийЗапросОбъединение.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|		ВЫБОР КОГДА ТекущиеКвоты.Марка.Код ЕСТЬ NULL ТОГДА &ПрочиеМарки ИНАЧЕ ТекущиеКвоты.Марка КОНЕЦ КАК Марка,
	|		ВЫБОР
	|			КОГДА ОбщийЗапросОбъединение.Номенклатура ЕСТЬ NULL 
	|				ТОГДА ОбщийЗапросОбъединение.НоменклатураОстатка
	|			ИНАЧЕ ОбщийЗапросОбъединение.Номенклатура
	|		КОНЕЦ КАК Номенклатура,
	|		ОбщийЗапросОбъединение.Номенклатура КАК НоменклатураАссортимента,
	|		ОбщийЗапросОбъединение.НоменклатураОстатка КАК НоменклатураОстатка,
	|		МАКСИМУМ(ОбщийЗапросОбъединение.РольАссортимента) КАК РольАссортимента,
	|		МАКСИМУМ(ОбщийЗапросОбъединение.ЭтоНеИтог) КАК ЭтоНеИтог,
	|		МАКСИМУМ(ОбщийЗапросОбъединение.Квота) КАК Квота,
	|		МАКСИМУМ(ОбщийЗапросОбъединение.ПроцентОтклонения) КАК ПроцентОтклонения,
	|		МАКСИМУМ(ОбщийЗапросОбъединение.Наполненность) КАК Наполненность,
	|		МАКСИМУМ(ОбщийЗапросОбъединение.Представленность) КАК Представленность,
	|		МАКСИМУМ(ОбщийЗапросОбъединение.КвотаИтогФормат) КАК КвотаИтогФормат,
	|		МАКСИМУМ(ОбщийЗапросОбъединение.КвотаИтог) КАК КвотаИтог
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТекущиеКвоты.ФорматМагазина КАК ФорматМагазина,
	|			ТекущиеКвоты.Магазин КАК Магазин,
	|			ТекущиеКвоты.ВидНоменклатуры КАК ВидНоменклатуры,
	|			ТекущиеКвоты.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|			ТекущиеКвоты.Марка КАК Марка,
	|			NULL КАК Номенклатура,
	|			NULL КАК РольАссортимента,
	|			NULL КАК НоменклатураОстатка,
	|			ИСТИНА КАК ЭтоНеИтог,
	|			ТекущиеКвоты.Квота КАК Квота,
	|			ТекущиеКвоты.ПроцентОтклонения КАК ПроцентОтклонения,
	|			0 КАК Наполненность,
	|			0 КАК Представленность,
	|			ЕСТЬNULL(КвотыИтогФормат.Квота, 0) КАК КвотаИтогФормат,
	|			ЕСТЬNULL(КвотыИтогОбщие.Квота, 0) КАК КвотаИтог
	|		ИЗ
	|			втТекущиеКвоты КАК ТекущиеКвоты
	|				ЛЕВОЕ СОЕДИНЕНИЕ втКвотыИтогФормат КАК КвотыИтогФормат
	|				ПО (КвотыИтогФормат.ФорматМагазина = ТекущиеКвоты.ФорматМагазина)
	|				ЛЕВОЕ СОЕДИНЕНИЕ втКвотыИтогОбщие КАК КвотыИтогОбщие
	|				ПО (ИСТИНА)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТекущаяНаполненность.ФорматМагазина,
	|			ТекущаяНаполненность.Магазин,
	|			ТекущаяНаполненность.ВидНоменклатуры,
	|			ТекущаяНаполненность.ТоварнаяКатегория,
	|			ТекущаяНаполненность.Марка,
	|			ТекущаяНаполненность.Номенклатура,
	|			ТекущаяНаполненность.РольАссортимента,
	|			NULL,
	|			ИСТИНА,
	|			0,
	|			0,
	|			1,
	|			0,
	|			0,
	|			0
	|		ИЗ
	|			втТекущаяНаполненность КАК ТекущаяНаполненность
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТекущаяПредставленность.ФорматМагазина,
	|			ТекущаяПредставленность.Магазин,
	|			ТекущаяПредставленность.ВидНоменклатуры,
	|			ТекущаяПредставленность.ТоварнаяКатегория,
	|			ТекущаяПредставленность.Марка,
	|			NULL,
	|			NULL,
	|			ТекущаяПредставленность.Номенклатура,
	|			ЛОЖЬ,
	|			0,
	|			0,
	|			0,
	|			1,
	|			0,
	|			0
	|		ИЗ
	|			втТекущиеОстатки КАК ТекущаяПредставленность) КАК ОбщийЗапросОбъединение
	|			ЛЕВОЕ СОЕДИНЕНИЕ втТекущиеКвоты КАК ТекущиеКвоты
	|			ПО (ТекущиеКвоты.ТоварнаяКатегория = ОбщийЗапросОбъединение.ТоварнаяКатегория)
	|				И (ТекущиеКвоты.Марка = ОбщийЗапросОбъединение.Марка)
	|				И (ТекущиеКвоты.Магазин = ОбщийЗапросОбъединение.Магазин)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ОбщийЗапросОбъединение.ФорматМагазина,
	|		ОбщийЗапросОбъединение.Магазин,
	|		ОбщийЗапросОбъединение.ВидНоменклатуры,
	|		ОбщийЗапросОбъединение.ТоварнаяКатегория,
	|		ВЫБОР КОГДА ТекущиеКвоты.Марка.Код ЕСТЬ NULL ТОГДА &ПрочиеМарки ИНАЧЕ ТекущиеКвоты.Марка КОНЕЦ,
	|		ОбщийЗапросОбъединение.Номенклатура,
	|		ОбщийЗапросОбъединение.НоменклатураОстатка) КАК ОбъединенныйОбщий
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъединенныйОбщий.ФорматМагазина,
	|	ОбъединенныйОбщий.Магазин,
	|	ОбъединенныйОбщий.ВидНоменклатуры,
	|	ОбъединенныйОбщий.ТоварнаяКатегория,
	|	ОбъединенныйОбщий.Марка,
	|	ОбъединенныйОбщий.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИтоговаяТаблица.ФорматМагазина,
	|	втИтоговаяТаблица.Магазин,
	|	втИтоговаяТаблица.ТоварнаяКатегория,
	|	втИтоговаяТаблица.Марка,
	|	СУММА(втИтоговаяТаблица.НарушениеНаполненности) КАК НарушениеНаполненности
	|ПОМЕСТИТЬ втИтогиНаполненностьПромежуточные1
	|ИЗ
	|	втИтоговаяТаблица КАК втИтоговаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	втИтоговаяТаблица.ФорматМагазина,
	|	втИтоговаяТаблица.Магазин,
	|	втИтоговаяТаблица.ТоварнаяКатегория,
	|	втИтоговаяТаблица.Марка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИтогиНаполненностьПромежуточные1.ФорматМагазина,
	|	втИтогиНаполненностьПромежуточные1.ТоварнаяКатегория,
	|	втИтогиНаполненностьПромежуточные1.Марка,
	|	МАКСИМУМ(втИтогиНаполненностьПромежуточные1.НарушениеНаполненности) КАК НарушениеНаполненности
	|ПОМЕСТИТЬ втИтогиНаполненностьПромежуточные
	|ИЗ
	|	втИтогиНаполненностьПромежуточные1 КАК втИтогиНаполненностьПромежуточные1
	|
	|СГРУППИРОВАТЬ ПО
	|	втИтогиНаполненностьПромежуточные1.ФорматМагазина,
	|	втИтогиНаполненностьПромежуточные1.ТоварнаяКатегория,
	|	втИтогиНаполненностьПромежуточные1.Марка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втИтогиНаполненностьПромежуточные.ФорматМагазина,
	|	СУММА(втИтогиНаполненностьПромежуточные.НарушениеНаполненности) КАК НарушениеНаполненности
	|ПОМЕСТИТЬ втИтогиНарушениеНаполненностиФормат
	|ИЗ
	|	втИтогиНаполненностьПромежуточные КАК втИтогиНаполненностьПромежуточные
	|
	|СГРУППИРОВАТЬ ПО
	|	втИтогиНаполненностьПромежуточные.ФорматМагазина
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтоговаяТаблица.ФорматМагазина КАК ФорматМагазина,
	|	ИтоговаяТаблица.ФорматМагазина.Наименование КАК НаименованиеФорматМагазина,
	|	ИтоговаяТаблица.ФорматМагазина.ПометкаУдаления КАК ПометкаУдаленияФорматМагазина,
	|	ИтоговаяТаблица.Магазин КАК Магазин,
	|	ИтоговаяТаблица.Магазин.Наименование КАК НаименованиеМагазин,
	|	ИтоговаяТаблица.Магазин.ПометкаУдаления КАК ПометкаУдаленияМагазин,
	|	ИтоговаяТаблица.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ИтоговаяТаблица.ВидНоменклатуры.Наименование КАК НаименованиеВидНоменклатуры,
	|	ИтоговаяТаблица.ВидНоменклатуры.ПометкаУдаления КАК ПометкаУдаленияВидНоменклатуры,
	|	ИтоговаяТаблица.ТоварнаяКатегория КАК ТоварнаяКатегория,
	|	ИтоговаяТаблица.ТоварнаяКатегория.Наименование КАК НаименованиеТоварнаяКатегория,
	|	ИтоговаяТаблица.ТоварнаяКатегория.ПометкаУдаления КАК ПометкаУдаленияТоварнаяКатегория,
	|	ИтоговаяТаблица.Марка КАК Марка,
	|	ВЫБОР
	|		КОГДА ИтоговаяТаблица.Марка ССЫЛКА Справочник.Марки
	|			ТОГДА ИтоговаяТаблица.Марка.Наименование
	|		ИНАЧЕ ИтоговаяТаблица.Марка
	|	КОНЕЦ КАК НаименованиеМарка,
	|	ВЫБОР
	|		КОГДА ИтоговаяТаблица.Марка ССЫЛКА Справочник.Марки
	|			ТОГДА ИтоговаяТаблица.Марка.ПометкаУдаления
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПометкаУдаленияМарка,
	|	ИтоговаяТаблица.Номенклатура,
	|	ИтоговаяТаблица.Номенклатура.Наименование КАК НаименованиеНоменклатура,
	|	ИтоговаяТаблица.Номенклатура.ПометкаУдаления КАК ПометкаУдаленияНоменклатура,
	|	ИтоговаяТаблица.НоменклатураАссортимента КАК НоменклатураАссортимента,
	|	ИтоговаяТаблица.НоменклатураОстатка КАК НоменклатураОстатка,
	|	ИтоговаяТаблица.РольАссортимента КАК РольАссортимента,
	|	ИтоговаяТаблица.ЭтоНеИтог КАК ЭтоНеИтог,
	|	ИтоговаяТаблица.Квота КАК Квота,
	|	ИтоговаяТаблица.ПроцентОтклонения КАК ПроцентОтклонения,
	|	ИтоговаяТаблица.Наполненность КАК Наполненность,
	|	ИтоговаяТаблица.Представленность КАК Представленность,
	|	ИтоговаяТаблица.КвотаИтогФормат КАК КвотаИтогФормат,
	|	ИтоговаяТаблица.КвотаИтог КАК КвотаИтог,
	|	ИтоговаяТаблица.НарушениеНаполненности КАК НарушениеНаполненности,
	|	втИтогиНарушениеНаполненностиФормат.НарушениеНаполненности КАК НарушениеНаполненностиФормат
	|ИЗ
	|	втИтоговаяТаблица КАК ИтоговаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИтогиНарушениеНаполненностиФормат КАК втИтогиНарушениеНаполненностиФормат
	|		ПО (втИтогиНарушениеНаполненностиФормат.ФорматМагазина = ИтоговаяТаблица.ФорматМагазина)
	|ГДЕ
	|	НЕ (ИтоговаяТаблица.Квота = 0
	|	И ИтоговаяТаблица.Наполненность = 0
	|	И ИтоговаяТаблица.Представленность = 0)
	|УПОРЯДОЧИТЬ ПО
	|	НаименованиеФорматМагазина,
	|	НаименованиеМагазин,
	|	НаименованиеВидНоменклатуры,
	|	НаименованиеТоварнаяКатегория,
	|	НаименованиеМарка,
	|	НаименованиеНоменклатура
	|ИТОГИ
	|	ЛОЖЬ КАК ЭтоНеИтог,
	|	СУММА(Квота) КАК Квота,
	|	МАКСИМУМ(ПроцентОтклонения) КАК ПроцентОтклонения,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИтоговаяТаблица.НоменклатураАссортимента) КАК Наполненность,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИтоговаяТаблица.НоменклатураОстатка) КАК Представленность,
	|	МАКСИМУМ(КвотаИтогФормат) КАК КвотаИтогФормат,
	|	МАКСИМУМ(КвотаИтог) КАК КвотаИтог,
	|	СУММА(НарушениеНаполненности) КАК НарушениеНаполненности,
	|	МАКСИМУМ(НарушениеНаполненностиФормат) КАК НарушениеНаполненностиФормат
	|ПО
	|	ОБЩИЕ,
	|	ФорматМагазина,
	|	Магазин,
	|	ВидНоменклатуры ИЕРАРХИЯ,
	|	ТоварнаяКатегория ИЕРАРХИЯ,
	|	Марка";
	
	ЭлементПараметров = Неопределено;
	ПараметрПериод = "";
	Для Каждого ЭлементНастроек Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастроек) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			Если СокрЛП(ЭлементНастроек.Параметр) = "Период" Тогда
				ЭлементПараметров = ЭлементНастроек;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ПериодПроверен = Истина;
	Если ЭлементПараметров = Неопределено Тогда
		ПериодПроверен = Ложь;
	Иначе
		Если ЭлементПараметров.Использование Тогда
			Если ТипЗнч(ЭлементПараметров.Значение) = Тип("Дата") Тогда
				Запрос.Параметры.Вставить(СокрЛП(ЭлементПараметров.Параметр),ЭлементПараметров.Значение);
				ПараметрПериод = ЭлементПараметров.Значение;
			Иначе
				Запрос.Параметры.Вставить(СокрЛП(ЭлементПараметров.Параметр),ЭлементПараметров.Значение.Дата);
				ПараметрПериод = ЭлементПараметров.Значение.Дата;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ПараметрПериод) Тогда
				ПериодПроверен = Ложь;
			КонецЕсли;
		Иначе
			ПериодПроверен = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ПериодПроверен Тогда
		СтрокаСообщенияПериода = НСтр("ru = 'Не задано значение параметра ""Период"".'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщенияПериода);
		Возврат ТабличныйДокументРезультат;
	КонецЕсли;
	
	ЭлементОтбора = КомпоновщикНастроек.ПолучитьНастройки().Отбор;
	Если ЭлементОтбора <> Неопределено Тогда
		ДобавитьУсловияИПараметры(Запрос, ТекстЗапроса, ЭлементОтбора);
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ПрочиеМарки", НСтр("ru = 'Прочие марки'"));
	Результат=Запрос.Выполнить();
	//
	ВыборкаУровень0=Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//
	СоответствиеПоказателей=Новый Соответствие;
	СоответствиеПоказателей.Вставить("Наполненность","Наполненность");
	СоответствиеПоказателей.Вставить("Квота","Квота");
	СоответствиеПоказателей.Вставить("Представленность","Представленность");
	СоответствиеПоказателей.Вставить("НарушениеНаполненности","НарушениеНаполненности");
	//
	СоответствиеПоказателейУровень1=Новый Соответствие;
	СоответствиеПоказателейУровень1.Вставить("Наполненность","Наполненность");
	СоответствиеПоказателейУровень1.Вставить("Квота","КвотаИтогФормат");
	СоответствиеПоказателейУровень1.Вставить("Представленность","Представленность");
	СоответствиеПоказателейУровень1.Вставить("НарушениеНаполненности","НарушениеНаполненностиФормат");
	//
	СоответствиеПоказателейУровень0=Новый Соответствие;
	СоответствиеПоказателейУровень0.Вставить("Наполненность","Наполненность");
	СоответствиеПоказателейУровень0.Вставить("Квота","КвотаИтог");
	СоответствиеПоказателейУровень0.Вставить("Представленность","Представленность");
	СоответствиеПоказателейУровень0.Вставить("НарушениеНаполненности","НарушениеНаполненностиФормат");
	//
	ЦветКрасный = Новый Цвет(255, 0, 0);
	ЦветАвто = Новый Цвет();
	ЦветСерый = Новый Цвет(160, 160, 164);
	//
	Макет = ПолучитьМакет("Макет");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = НСтр("ru = 'Анализ исполнения ассортимента'");
	ТабличныйДокументРезультат.Вывести(ОбластьЗаголовок);
	//
	ТабличныйДокументРезультат.НачатьАвтогруппировкуСтрок();
	ОбластьПараметрыОтчета = Макет.ПолучитьОбласть("ПараметрыОтчета");
	ТекстПараметров = НСтр("ru = 'Дата отчета: %1'");
	ТекстПараметров = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПараметров, ПараметрПериод);
	ОбластьПараметрыОтчета.Параметры.ПараметрыОтчета = ТекстПараметров;
	ТабличныйДокументРезультат.Вывести(ОбластьПараметрыОтчета, 1);
	//
	ПредставлениеОтбора = СокрЛП(ЭлементОтбора);
	Если НЕ ПустаяСтрока(ПредставлениеОтбора) Тогда
		ОбластьОтборыОтчета = Макет.ПолучитьОбласть("ОтборыОтчета");
		ОбластьОтборыОтчета.Параметры.ОтборыОтчета = ПредставлениеОтбора;
		ТабличныйДокументРезультат.Вывести(ОбластьОтборыОтчета, 1);
	КонецЕсли;
	//
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.ЗаголовокГруппировок = НСтр("ru = 'Формат / Магазин'")
													+ Символы.ПС + НСтр("ru = 'Вид номенклатуры / Категория'")
													+ Символы.ПС + НСтр("ru = 'Марка / Номенклатура'");
	ТабличныйДокументРезультат.Вывести(ОбластьШапка, 0);
	//
	ОбластьУровень1 = Макет.ПолучитьОбласть("ОбластьУровень1");
	ОбластьУровень2 = Макет.ПолучитьОбласть("ОбластьУровень2");
	ОбластьУровень3 = Макет.ПолучитьОбласть("ОбластьУровень3");
	ОбластьУровень4 = Макет.ПолучитьОбласть("ОбластьУровень4");
	ОбластьУровень5 = Макет.ПолучитьОбласть("ОбластьУровень5");
	ОбластьУровень6 = Макет.ПолучитьОбласть("ОбластьУровень6");
	ОбластьИтого = Макет.ПолучитьОбласть("Итого");

	
	Пока ВыборкаУровень0.Следующий() Цикл
		
		ВыборкаУровень1 = ВыборкаУровень0.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаУровень1.Следующий() Цикл
			ОбластьУровень1.Параметры.ЗначениеГруппировки = ВыборкаУровень1.ФорматМагазина;
			ЗаполнитьПараметрыИВывестиОбласть(
				ТабличныйДокументРезультат,
				ОбластьУровень1,
				ВыборкаУровень1,
				"ФорматМагазина",
				СоответствиеПоказателейУровень1);
			
			Если ВыборкаУровень1.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
				Продолжить;
			КонецЕсли;
			
			ВыборкаУровень2 = ВыборкаУровень1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаУровень2.Следующий() Цикл
				ЗаполнитьПараметрыИВывестиОбласть(
					ТабличныйДокументРезультат,
					ОбластьУровень2,
					ВыборкаУровень2,
					"Магазин",
					СоответствиеПоказателей);
				
				Если ВыборкаУровень2.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
					Продолжить;
				КонецЕсли;
				
				ВыборкаУровень3 = ВыборкаУровень2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаУровень3.Следующий() Цикл
					ЗаполнитьПараметрыИВывестиОбласть(
						ТабличныйДокументРезультат,
						ОбластьУровень3,
						ВыборкаУровень3,
						"ВидНоменклатуры",
						СоответствиеПоказателей);
					
					Если ВыборкаУровень3.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
						Продолжить;
					КонецЕсли;
					
					ВыборкаУровень4 = ВыборкаУровень3.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Пока ВыборкаУровень4.Следующий() Цикл
						ЗаполнитьПараметрыИВывестиОбласть(
							ТабличныйДокументРезультат,
							ОбластьУровень4,
							ВыборкаУровень4,
							"ТоварнаяКатегория",
							СоответствиеПоказателей);
						
						Если ВыборкаУровень4.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
							Продолжить;
						КонецЕсли;
						
						ВыборкаУровень5 = ВыборкаУровень4.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						
						Пока ВыборкаУровень5.Следующий() Цикл
							ЗаполнитьПараметрыИВывестиОбласть(
								ТабличныйДокументРезультат,
								ОбластьУровень5,
								ВыборкаУровень5,
								"Марка",
								СоответствиеПоказателей);
							
							Если ВыборкаУровень5.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
								Продолжить;
							КонецЕсли;
							
							ВыборкаУровень6 = ВыборкаУровень5.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							
							Пока ВыборкаУровень6.Следующий() Цикл
								Если ВыборкаУровень6.ЭтоНеИтог И ВыборкаУровень6.РольАссортимента = Null Тогда
									Продолжить;
								КонецЕсли;
								ЗаполнитьПараметрыИВывестиОбласть(
									ТабличныйДокументРезультат,
									ОбластьУровень6,
									ВыборкаУровень6,
									"Номенклатура",
									СоответствиеПоказателей);
							КонецЦикла;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		ЗаполнитьПараметрыИВывестиОбласть(
			ТабличныйДокументРезультат,
			ОбластьИтого,
			ВыборкаУровень0,
			"",
			СоответствиеПоказателейУровень0);
		
	КонецЦикла;
	ТабличныйДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
	
	Возврат ТабличныйДокументРезультат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИВывестиОбласть(ТабличныйДокументРезультат, Область, Выборка, ИмяГруппировки, СоответствиеПоказателей)
	УровеньГруппировки = Выборка.Уровень();
	ДобавитьПробелов = "";
	Для СчетчикПробелов = 2 По УровеньГруппировки Цикл
		ДобавитьПробелов = ДобавитьПробелов + " ";
	КонецЦикла;
	Если НЕ ПустаяСтрока(ИмяГруппировки) Тогда
		Область.Параметры.ЗначениеГруппировки = Выборка[ИмяГруппировки];
		Если ИмяГруппировки = "Номенклатура" Тогда
			Область.Параметры.ПредставлениеГруппировки = ДобавитьПробелов + Выборка.НаименованиеНоменклатура;
			Область.Параметры.РольАссортимента = Выборка.РольАссортимента;
		Иначе
			Область.Параметры.ПредставлениеГруппировки = ДобавитьПробелов + Выборка["Наименование" + ИмяГруппировки];
		КонецЕсли;
	КонецЕсли;
	Для Каждого Показатель Из СоответствиеПоказателей Цикл
		Область.Параметры[Показатель.Ключ] = Выборка[Показатель.Значение];
	КонецЦикла;
	Если ИмяГруппировки = "Номенклатура" Тогда
		Область.Параметры.ОтклонениеКвотыАбсолютное = 0;
		Область.Параметры.ОтклонениеКвотыОтносительное = 0;
	Иначе
		НаполненностьКвоты = Мин(Область.Параметры.Квота, Область.Параметры.Наполненность);
		ОтклонениеКвотыАбсолютное = Область.Параметры.Представленность-НаполненностьКвоты;
		Область.Параметры.ОтклонениеКвотыАбсолютное = ОтклонениеКвотыАбсолютное;
		Если НаполненностьКвоты = 0 Тогда
			Область.Параметры.ОтклонениеКвотыОтносительное = 0;
		Иначе
			Область.Параметры.ОтклонениеКвотыОтносительное = Окр(ОтклонениеКвотыАбсолютное * 100 / НаполненностьКвоты, 2);
		КонецЕсли;
		Область.Параметры.ОтклонениеКвотыРасшифровка = НСтр("ru = 'Текущее отклонение:'") + " "
		+ Область.Параметры.ОтклонениеКвотыОтносительное + "%" + Символы.ПС
		+ НСтр("ru = 'Допустимое отклонение:'") + " " + ?(Область.Параметры.ОтклонениеКвотыОтносительное >= 0, "", "-")
		+ Выборка.ПроцентОтклонения + "%";
	КонецЕсли;
	
	ЦветОбласти = ЦветАвто;
	
	Если Область.Параметры.НарушениеНаполненности <> 0 Тогда
		ЦветОбласти = ЦветКрасный;
	КонецЕсли;
	Если ИмяГруппировки <> "Номенклатура" Тогда
		Если ИмяГруппировки = "ТоварнаяКатегория" Тогда
			Если НЕ ЗначениеЗаполнено(Выборка.ТоварнаяКатегория) Тогда
				ЦветОбласти = ЦветСерый;
				Область.Параметры.ПредставлениеГруппировки = НСтр("ru = '<Категория не заполнена>'");
			КонецЕсли;
		КонецЕсли;
		Если Область.Параметры.ОтклонениеКвотыОтносительное > Выборка.ПроцентОтклонения
			ИЛИ Область.Параметры.ОтклонениеКвотыОтносительное < -Выборка.ПроцентОтклонения Тогда
			ЦветОбласти = ЦветКрасный;
		ИначеЕсли ОтклонениеКвотыАбсолютное <> 0 И Область.Параметры.Квота = 0 Тогда
			ЦветОбласти = ЦветКрасный;
		КонецЕсли;
	КонецЕсли;
	Для Каждого ПодчиненнаяОбласть Из Область.Области Цикл
		ПодчиненнаяОбласть.ЦветТекста = ЦветОбласти;
	КонецЦикла;
	ТабличныйДокументРезультат.Вывести(Область, УровеньГруппировки);
КонецПроцедуры

Процедура ДобавитьУсловияИПараметры(Запрос, ТекстЗапроса, ЭлементОтбора)
	
	ДополнительныеПараметрыУсловия = АссортиментКлиентСервер.ПараметрыДляУсловия();
	
	ДополнительныеПараметрыУсловия.СчетчикУсловий = 0;
	ДополнительныеПараметрыУсловия.СчетчикПараметров = 0;
	///////////////////////////////////////////////////////////////////////////////
	// Условие квоты
	ДополнительныеПараметрыУсловия.ТекстУсловия = "";
	ДополнительныеПараметрыУсловия.ТекстНачальногоУсловия = "";
	ДополнительныеПараметрыУсловия.УсловиеГруппы = "";
	
	СоответствиеПолейУсловия = Новый Соответствие;
	СоответствиеПолейУсловия.Вставить("ФорматМагазина", "ОбъектПланирования");
	СоответствиеПолейУсловия.Вставить("ТоварнаяКатегория", "ТоварнаяКатегория");
	СоответствиеПолейУсловия.Вставить("Марка", "Марка");
	ДополнительныеПараметрыУсловия.СоответствиеПолейУсловия = СоответствиеПолейУсловия;
	
	АссортиментСервер.СформироватьТекстУсловия(Запрос, ЭлементОтбора.Элементы, ДополнительныеПараметрыУсловия);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//УсловиеКвоты", СокрЛП(ДополнительныеПараметрыУсловия.ТекстУсловия));
	
	///////////////////////////////////////////////////////////////////////////////
	// УсловиеМагазиныКвотыИАссортимент
	ДополнительныеПараметрыУсловия.СчетчикУсловий = 0;
	ДополнительныеПараметрыУсловия.ТекстУсловия = "";
	ДополнительныеПараметрыУсловия.ТекстНачальногоУсловия = "И";
	ДополнительныеПараметрыУсловия.УсловиеГруппы = "";
	
	СоответствиеПолейУсловия.Очистить();
	СоответствиеПолейУсловия.Вставить("Магазин", "Магазины.Ссылка");
	СоответствиеПолейУсловия.Вставить("Магазин.", "Магазины.");
	ДополнительныеПараметрыУсловия.СоответствиеПолейУсловия = СоответствиеПолейУсловия;
	
	АссортиментСервер.СформироватьТекстУсловия(Запрос, ЭлементОтбора.Элементы, ДополнительныеПараметрыУсловия);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//УсловиеМагазиныКвотыИАссортимент", СокрЛП(ДополнительныеПараметрыУсловия.ТекстУсловия));
	
	///////////////////////////////////////////////////////////////////////////////
	// УсловиеАссортимент
	ДополнительныеПараметрыУсловия.СчетчикУсловий = 0;
	ДополнительныеПараметрыУсловия.ТекстУсловия = "";
	ДополнительныеПараметрыУсловия.ТекстНачальногоУсловия = "";
	ДополнительныеПараметрыУсловия.УсловиеГруппы = "";
	
	СоответствиеПолейУсловия.Очистить();
	СоответствиеПолейУсловия.Вставить("ФорматМагазина", "ОбъектПланирования");
	СоответствиеПолейУсловия.Вставить("ТоварнаяКатегория", "Номенклатура.ТоварнаяКатегория");
	СоответствиеПолейУсловия.Вставить("Марка", "Номенклатура.Марка");
	ДополнительныеПараметрыУсловия.СоответствиеПолейУсловия = СоответствиеПолейУсловия;
	
	АссортиментСервер.СформироватьТекстУсловия(Запрос, ЭлементОтбора.Элементы, ДополнительныеПараметрыУсловия);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%УсловиеАссортимент%", СокрЛП(ДополнительныеПараметрыУсловия.ТекстУсловия));
	
	///////////////////////////////////////////////////////////////////////////////
	// УсловиеОстатки
	ДополнительныеПараметрыУсловия.СчетчикУсловий = 0;
	ДополнительныеПараметрыУсловия.ТекстУсловия = "";
	ДополнительныеПараметрыУсловия.ТекстНачальногоУсловия = "";
	ДополнительныеПараметрыУсловия.УсловиеГруппы = "";
	
	СоответствиеПолейУсловия = Новый Соответствие;
	СоответствиеПолейУсловия.Вставить("ФорматМагазина", "Склад.Магазин.ФорматМагазина");
	СоответствиеПолейУсловия.Вставить("Магазин", "Склад.Магазин");
	СоответствиеПолейУсловия.Вставить("ТоварнаяКатегория", "Номенклатура.ТоварнаяКатегория");
	СоответствиеПолейУсловия.Вставить("Марка", "Номенклатура.Марка");
	ДополнительныеПараметрыУсловия.СоответствиеПолейУсловия = СоответствиеПолейУсловия;
	
	АссортиментСервер.СформироватьТекстУсловия(Запрос, ЭлементОтбора.Элементы, ДополнительныеПараметрыУсловия);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//УсловиеОстатки", СокрЛП(ДополнительныеПараметрыУсловия.ТекстУсловия));
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
