#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеЭлементамиФормыНаСервере();
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ИспользоватьОплатуБанковскимиКредитами = ПолучитьФункциональнуюОпцию("ИспользоватьОплатуБанковскимиКредитами");
	
	Если НЕ ИспользоватьОплатуБанковскимиКредитами Тогда
	
		УдаляемыйЭлемент = Элементы.ТипОплаты.СписокВыбора.НайтиПоЗначению(Перечисления.ТипыОплатЧекаККМ.БанковскийКредит);
		
		Если НЕ УдаляемыйЭлемент = Неопределено Тогда
			Элементы.ТипОплаты.СписокВыбора.Удалить(УдаляемыйЭлемент);
		КонецЕсли;
	КонецЕсли;
	
	ИспользоватьОплатуПлатежнымиКартами = ПолучитьФункциональнуюОпцию("ИспользоватьОплатуПлатежнымиКартами");
	
	Если НЕ ИспользоватьОплатуПлатежнымиКартами Тогда
		
		Если НЕ Объект.Предопределенный Тогда
			УдалитьЭлементСпискаТипОплаты(Перечисления.ТипыОплатЧекаККМ.ПлатежнаяКарта);
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Объект.Предопределенный Тогда
		
		УдалитьЭлементСпискаТипОплаты(Перечисления.ТипыОплатЧекаККМ.Наличные);
		УдалитьЭлементСпискаТипОплаты(Перечисления.ТипыОплатЧекаККМ.ПодарочныйСертификат);
		УдалитьЭлементСпискаТипОплаты(Перечисления.ТипыОплатЧекаККМ.Бонусы);
		УдалитьЭлементСпискаТипОплаты(Перечисления.ТипыОплатЧекаККМ.ЗачетАванса);
		УдалитьЭлементСпискаТипОплаты(Перечисления.ТипыОплатЧекаККМ.ВРассрочку);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭлементамиФормыНаКлиенте();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОплатыПриИзменении(Элемент)
	
	УправлениеЭлементамиФормыНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементамиФормыНаСервере()
	
	Элементы.ТипОплаты.ТолькоПросмотр = Объект.Предопределенный;
		
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭлементамиФормыНаКлиенте()
	
	Если Объект.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатЧекаККМ.БанковскийКредит") Тогда
		
		Элементы.ГруппаБанковскийКредит.ТекущаяСтраница = Элементы.БанковскийКредит;
		
	Иначе
		
		Элементы.ГруппаБанковскийКредит.ТекущаяСтраница = Элементы.Пустышка;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементСпискаТипОплаты(ТипОплаты)
	
	УдаляемыйЭлемент = Элементы.ТипОплаты.СписокВыбора.НайтиПоЗначению(ТипОплаты);
	
	Если НЕ УдаляемыйЭлемент = Неопределено Тогда
		Элементы.ТипОплаты.СписокВыбора.Удалить(УдаляемыйЭлемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
