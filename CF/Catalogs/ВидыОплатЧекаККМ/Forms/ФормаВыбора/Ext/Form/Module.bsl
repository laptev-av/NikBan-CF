#Область ОбработчикиСобытийФормы

// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ПараметрыОтбора;
	Перем Отрицание;
	Перем ЭквайринговыйТерминал;
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СтруктураПараметрыОтбора", ПараметрыОтбора)  Тогда
		ИмяПоляОтбораЛевоеЗначение = ПараметрыОтбора.ИмяПоляОтбораЛевоеЗначение;
		ПравоеЗначение = ПараметрыОтбора.ПравоеЗначение;
		
		Если НЕ ПараметрыОтбора.Свойство("Отрицание", Отрицание) Тогда
			Отрицание = Ложь;
		КонецЕсли; 
		УстановитьОтборСписку(ИмяПоляОтбораЛевоеЗначение, ПравоеЗначение, Отрицание);
		
		ПараметрыОтбора.Свойство("ЭквайринговыйТерминал", ЭквайринговыйТерминал);
		Если ЗначениеЗаполнено(ЭквайринговыйТерминал) Тогда
			МассивВидовОплаты = ЭквайринговыйТерминал.ТарифыЗаРасчетноеОбслуживание.ВыгрузитьКолонку("ВидОплаты");
			НуженОтборПоВидамОплат = МассивВидовОплаты.Количество() > 0;
			
			Если НуженОтборПоВидамОплат 
				И МассивВидовОплаты.Количество() = 1 
				И МассивВидовОплаты[0] = Справочники.ВидыОплатЧекаККМ.ПустаяСсылка() Тогда
				НуженОтборПоВидамОплат = Ложь;
			КонецЕсли;
			ПравоеЗначение = Новый СписокЗначений;
			ПравоеЗначение.ЗагрузитьЗначения(МассивВидовОплаты);
			УстановитьОтборСписку("Ссылка", ПравоеЗначение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает отбор списку выбора.
// Параметры:
//           ИмяПоляОтбораЛевоеЗначение - Строка - Наименование поля отбора на которое устанавливается отбор.
//           ПравоеЗначение - СправочникСсылка.ВидыОплатЧекаККМ или СписокЗначений - Значение отбора.
//
&НаСервере
Процедура УстановитьОтборСписку(ИмяПоляОтбораЛевоеЗначение, ПравоеЗначение, Отрицание = Ложь)
	
	Если Список.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяПоляОтбораЛевоеЗначение) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыОтбора = Список.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	
	ЭлементОтбораДанных = Неопределено;
	
	ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоляОтбораЛевоеЗначение);
	
	Для каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		Если ЭлементОтбора.ЛевоеЗначение = ЛевоеЗначение Тогда
			Если ЭлементОтбораДанных = Неопределено Тогда
				ЭлементОтбораДанных = ЭлементОтбора;
			Иначе
				ЭлементОтбора.Использование = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭлементОтбораДанных = Неопределено Тогда
		ЭлементОтбораДанных = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение = ЛевоеЗначение;
	КонецЕсли;
	Если НЕ Отрицание Тогда
		Если ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
			ПеремВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			ПеремВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
	Иначе
		Если ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
			ПеремВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
		Иначе
			ПеремВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		КонецЕсли;
	КонецЕсли;
	
	ЭлементОтбораДанных.ВидСравнения = ПеремВидСравнения;
	ЭлементОтбораДанных.ПравоеЗначение = ПравоеЗначение;
	ЭлементОтбораДанных.Использование = Истина;
	
КонецПроцедуры // УстановитьОтборСписку()

#КонецОбласти