
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыФормы = ПодготовитьСтруктуруПечати(ПараметрКоманды);
	Если ПараметрыФормы <> Неопределено Тогда
		ОткрытьФорму(
			"Обработка.ПечатьЭтикетокИЦенников.Форма.Форма",
			ПараметрыФормы,
			ПараметрыВыполненияКоманды.Источник,
			ПараметрыВыполненияКоманды.Уникальность,
			ПараметрыВыполненияКоманды.Окно,
			ПараметрыВыполненияКоманды.НавигационнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодготовитьСтруктуруПечати(ПараметрКоманды)
	
	ТекущийМагазин = ОбщегоНазначенияРТ.ОпределитьТекущийМагазин();
	
	Если ЗначениеЗаполнено(ТекущийМагазин) Тогда
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийМагазин, "СкладПродажи.Организация");
	Иначе
		Организация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатуры.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатуры.Характеристика КАК Характеристика,
	|	Справки2.Ссылка КАК Справка2,
	|	1 КАК Количество,
	|	&Организация КАК Организация
	|ИЗ
	|	Справочник.Справки2ЕГАИС КАК Справки2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатуры
	|		ПО Справки2.АлкогольнаяПродукция = СоответствиеНоменклатуры.АлкогольнаяПродукция
	|ГДЕ
	|	Справки2.Ссылка В(&МассивСправок2)";

	Запрос.УстановитьПараметр("МассивСправок2", ПараметрКоманды);
	Запрос.УстановитьПараметр("Организация", Организация);
	Справки = Запрос.Выполнить().Выгрузить();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьМагазин", ТекущийМагазин);
	СтруктураДействий.Вставить("ИспользоватьСправкиБ", Истина);
	СтруктураДействий.Вставить("ЗаполнитьТаблицуТоваров", Истина);
	СтруктураДействий.Вставить("ЗаполнитьКоличествоЭтикетокПоДокументу", Истина);
	
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("Справки2ЕГАИС", Справки);
	СтруктураРезультат.Вставить("СтруктураДействий", СтруктураДействий);
	СтруктураПараметры = Новый Структура("АдресВХранилище", ПоместитьВоВременноеХранилище(СтруктураРезультат));
	
	Возврат СтруктураПараметры;
	
КонецФункции

#КонецОбласти