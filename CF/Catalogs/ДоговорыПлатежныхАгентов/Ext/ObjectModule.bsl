
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ТипДоговора = Перечисления.ТипыДоговоровПлатежныхАгентов.Субагентский Тогда
		Если ЗначениеЗаполнено(Агент) И Агент = Посредник Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'Поставщик услуг и посредник не должны совпадать'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				"Посредник");
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Посредник");
	КонецЕсли;
	
	Если ПризнакАгента = Перечисления.ПризнакиАгента.Комиссионер Тогда
		Если ЗначениеЗаполнено(Агент) Тогда
			СистемаАгента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Агент, "СистемаНалогообложения");
			Если НЕ ЗначениеЗаполнено(СистемаАгента) Тогда
				Отказ = Истина;
				ТекстСообщения = НСтр("ru = 'Не заполнена система налогообложения у комитента'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"Агент");
			КонецЕсли;
		КонецЕсли;
		МассивНепроверяемыхРеквизитов.Добавить("ИННПоставщикаУслуг");
		
		Если НЕ ЗначениеЗаполнено(ИННПоставщикаУслуг)  Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'Поле ""ИНН поставщика товаров"" не заполнено'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект, 
				"ИННПоставщикаТоваров");
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("СтавкаНДС");
		МассивНепроверяемыхРеквизитов.Добавить("СпособРасчетаКомиссионногоВознаграждения");
		МассивНепроверяемыхРеквизитов.Добавить("УслугаКомиссионногоВознаграждения");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		Организация   = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация, ТекущийПользователь);
		БанковскийСчет = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(Организация, , БанковскийСчет);
		ТипДоговора = Перечисления.ТипыДоговоровПлатежныхАгентов.Прямой;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
