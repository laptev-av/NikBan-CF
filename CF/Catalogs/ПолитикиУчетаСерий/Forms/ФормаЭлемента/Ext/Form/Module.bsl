#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	УстановитьДоступность(ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УказыватьПриПриемкеПриИзменении(Элемент)
	Если Не Объект.УказыватьПриПриемке Тогда
		
		Объект.УказыватьПриПриемкеКомплектующихПослеРазборки = Ложь;
		Объект.УказыватьПриПриемкеОтПоставщика               = Ложь;
		Объект.УказыватьПриПриемкеПоВозвратуОтКлиента        = Ложь ИЛИ Объект.УказыватьПриМаркировкеПродукцииДляГИСМ;
		Объект.УказыватьПриПриемкеПоПеремещению              = Ложь;
		Объект.УказыватьПриПриемкеСобранныхКомплектов        = Ложь;
		
	КонецЕсли;
	
	УстановитьДоступность(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура УказыватьПриОтгрузкеПриИзменении(Элемент)
	Если Не Объект.УказыватьПриОтгрузке Тогда
		
		Объект.УказыватьПриОтгрузкеКлиенту                = Ложь;
		Объект.УказыватьПриОтгрузкеВРозницу               = Ложь ИЛИ Объект.УказыватьПриМаркировкеПродукцииДляГИСМ;
		Объект.УказыватьПриОтгрузкеКомплектовДляРазборки  = Ложь;
		Объект.УказыватьПриОтгрузкеКомплектующихДляСборки = Ложь;
		Объект.УказыватьПриОтгрузкеПоВозвратуПоставщику   = Ложь;
		Объект.УказыватьПриОтгрузкеПоПеремещению          = Ложь;
		
		НастройкаУказанияСерийПриОтгрузке = 0;
	Иначе
		НастройкаУказанияСерийПриОтгрузке = 3;
	КонецЕсли;
	
	УстановитьДоступность(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура УказыватьПриМаркировкеПродукцииДляГИСМПриИзменении(Элемент)
	
	Если Объект.УказыватьПриМаркировкеПродукцииДляГИСМ Тогда
		
		Объект.УказыватьПриПриемкеОтПоставщика = Истина;
		Объект.УказыватьПриПриемкеПоВозвратуОтКлиента = Истина;
		Объект.УказыватьПриОтраженииИзлишковНедостачПорчи = Истина;
		Объект.УказыватьПриОтгрузкеВРозницу = Истина;
		
	КонецЕсли;
	
	УстановитьДоступнотьПоМаркировке(ЭтаФорма, Объект.УказыватьПриМаркировкеПродукцииДляГИСМ);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ОбработкаОповещения = Новый ОписаниеОповещения("РазблокировкаРезвизитов", ЭтотОбъект);
		ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект, ОбработкаОповещения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступность(Форма)
	
	Форма.Элементы.УказыватьПриПриемкеПоВозвратуОтКлиента.Доступность        = Форма.Объект.УказыватьПриПриемке;
	Форма.Элементы.УказыватьПриПриемкеОтПоставщика.Доступность               = Форма.Объект.УказыватьПриПриемке;
	Форма.Элементы.УказыватьПриПриемкеПоПеремещению.Доступность              = Форма.Объект.УказыватьПриПриемке;
	Форма.Элементы.УказыватьПриПриемкеСобранныхКомплектов.Доступность        = Форма.Объект.УказыватьПриПриемке;
	Форма.Элементы.УказыватьПриПриемкеКомплектующихПослеРазборки.Доступность = Форма.Объект.УказыватьПриПриемке;
	
	Форма.Элементы.УказыватьПриОтгрузкеКлиенту.Доступность                = Форма.Объект.УказыватьПриОтгрузке;
	Форма.Элементы.УказыватьПриОтгрузкеВРозницу.Доступность               = Форма.Объект.УказыватьПриОтгрузке;
	Форма.Элементы.УказыватьПриОтгрузкеКомплектующихДляСборки.Доступность = Форма.Объект.УказыватьПриОтгрузке;
	Форма.Элементы.УказыватьПриОтгрузкеПоВозвратуПоставщику.Доступность   = Форма.Объект.УказыватьПриОтгрузке;
	Форма.Элементы.УказыватьПриОтгрузкеПоПеремещению.Доступность          = Форма.Объект.УказыватьПриОтгрузке;
	Форма.Элементы.УказыватьПриОтгрузкеКомплектовДляРазборки.Доступность  = Форма.Объект.УказыватьПриОтгрузке;
	
	Форма.Элементы.УказыватьПриОтраженииИзлишковНедостачПорчи.Доступность = Истина;
	Форма.Элементы.УказыватьПриПересчетеТоваров.Доступность               = Истина;
	Форма.Элементы.УказыватьПриПриемке.Доступность                        = Истина;
	Форма.Элементы.УказыватьПриОтгрузке.Доступность                       = Истина;
	
	УстановитьДоступнотьПоМаркировке(Форма, Форма.Объект.УказыватьПриМаркировкеПродукцииДляГИСМ);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступнотьПоМаркировке(Форма, УказыватьПриМаркировкеПродукцииДляГИСМ)
	
	Форма.Элементы.УказыватьПриПриемкеПоВозвратуОтКлиента.ТолькоПросмотр     = УказыватьПриМаркировкеПродукцииДляГИСМ;
	Форма.Элементы.УказыватьПриОтраженииИзлишковНедостачПорчи.ТолькоПросмотр = УказыватьПриМаркировкеПродукцииДляГИСМ;
	Форма.Элементы.УказыватьПриОтгрузкеВРозницу.ТолькоПросмотр               = УказыватьПриМаркировкеПродукцииДляГИСМ;
	
КонецПроцедуры

&НаКлиенте
Процедура РазблокировкаРезвизитов(Знач РезультатЗакрытия, Знач ДополнительныеПараметры) Экспорт
	
	УстановитьДоступнотьПоМаркировке(ЭтаФорма, Объект.УказыватьПриМаркировкеПродукцииДляГИСМ)
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти