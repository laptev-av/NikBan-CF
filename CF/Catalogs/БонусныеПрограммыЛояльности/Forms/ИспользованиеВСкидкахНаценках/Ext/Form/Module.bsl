
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "БонуснаяПрограммаЛояльности", Параметры.БонуснаяПрограммаЛояльности, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	ТекущаяДата = ТекущаяДатаСеанса();
	ПеренестиОтборыВДинамическийСписок(ЭтотОбъект, Список);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "Справочник.СкидкиНаценки.Форма.ФормаЭлемента.Открытие");
	
	ПараметрыОткрытия = Новый Структура("Ключ", Элементы.Список.ТекущиеДанные.СкидкаНаценка);
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.СкидкиНаценки.Форма.ФормаЭлемента", ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

Процедура ПеренестиОтборыВДинамическийСписок(Форма, ДинамическийСписок = Неопределено)
	Перем ВидСравнения;
	
	Если Не Форма.Параметры.Свойство("Отбор") Тогда
		Возврат;
	КонецЕсли;
	Если ДинамическийСписок = Неопределено Тогда
		ДинамическийСписок = Форма.Список;
	КонецЕсли;
	
	ОтборыИзПараметров = Форма.Параметры.Отбор;
	ОтборыДинамическогоСписка = ДинамическийСписок.КомпоновщикНастроек.Настройки.Отбор;
	
	Использование = Истина;
	РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	Для Каждого КлючИЗначение Из ОтборыИзПараметров Цикл
		ИмяПоля = КлючИЗначение.Ключ;
		ПравоеЗначение = КлючИЗначение.Значение;
		
		Если ТипЗнч(ПравоеЗначение) = Тип("Массив") Тогда
			ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ИначеЕсли ТипЗнч(ПравоеЗначение) = Тип("СписокЗначений") Тогда
			ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		Иначе
			ВидСравнения = Неопределено;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ОтборыДинамическогоСписка,
			ИмяПоля,
			ПравоеЗначение,
			ВидСравнения,
			,
			Использование,
			РежимОтображения);
	КонецЦикла;
	
	ОтборыИзПараметров.Очистить();
КонецПроцедуры

#КонецОбласти
