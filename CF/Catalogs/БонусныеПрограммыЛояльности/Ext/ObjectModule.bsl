#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если НЕ ЗначениеЗаполнено(ВидОплаты) Тогда
		ВидОплаты = Справочники.ВидыОплатЧекаККМ.ОплатаБонусамиКакСкидкой;
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если МаксимальныйПроцентОплатыБонусами > 100 Тогда
		СтрокаСообщения = НСтр("ru = 'Максимальная сумма оплаты бонусами должна быть не более 100%'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрокаСообщения,
			,
			"МаксимальныйПроцентОплатыБонусами",
			"Объект",
			Отказ);
	КонецЕсли;
	
	Если ЦеновыеГруппы.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ЦеновыеГруппы.НомерСтроки КАК НомерСтроки,
		|	ЦеновыеГруппы.ЦеноваяГруппа КАК ЦеноваяГруппа
		|ПОМЕСТИТЬ ВременнаяТаблицаЦеновыеГруппы
		|ИЗ
		|	&ЦеновыеГруппы КАК ЦеновыеГруппы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеновыеГруппы.НомерСтроки КАК НомерСтроки,
		|	ЦеновыеГруппы.ЦеноваяГруппа КАК ЦеноваяГруппа
		|ИЗ
		|	ВременнаяТаблицаЦеновыеГруппы КАК ЦеновыеГруппы
		|ГДЕ
		|	ЦеновыеГруппы.ЦеноваяГруппа <> ЗНАЧЕНИЕ(Справочник.ЦеновыеГруппы.ПустаяСсылка)
		|	И ЦеновыеГруппы.ЦеноваяГруппа В
		|			(ВЫБРАТЬ
		|				Т.ЦеноваяГруппа
		|			ИЗ
		|				ВременнаяТаблицаЦеновыеГруппы КАК Т
		|			СГРУППИРОВАТЬ ПО
		|				Т.ЦеноваяГруппа
		|			ИМЕЮЩИЕ
		|				КОЛИЧЕСТВО(*) > 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		Запрос.УстановитьПараметр("ЦеновыеГруппы", ЦеновыеГруппы.Выгрузить());
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстОшибки = НСтр("ru = 'Ценовая группа ""%1"" повторяется'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Выборка.ЦеноваяГруппа);
					
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				Ценообразование.ПолучитьТекстДляВыдачиСообщенийПоСтрокеТЧ("ЦеновыеГруппы", Выборка.НомерСтроки, "ЦеноваяГруппа"),
				,
				Отказ);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли