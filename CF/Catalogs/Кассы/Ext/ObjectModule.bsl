#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	
	ОбщегоНазначенияРТ.ПроверитьИспользованиеОрганизации(,,Владелец);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	КассаУправляющейСистемы = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Перем МассивВсехРеквизитов;
	Перем МассивРеквизитов;
	
	Справочники.Кассы.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ЭтотОбъект,
		МассивВсехРеквизитов,
		МассивРеквизитов);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ОбщегоНазначенияРТКлиентСервер.ЗаполнитьМассивНепроверяемыхРеквизитов(
		МассивВсехРеквизитов,
		МассивРеквизитов,
		МассивНепроверяемыхРеквизитов);
	
	Если ПробиватьЧекиПоКассеККМ Тогда
		Если ЗначениеЗаполнено(КассаККМ) Тогда
			ТипОборудования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КассаККМ, "ТипОборудования");
			Если ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ Тогда
				УправлениеШаблонами.ПроверитьТипШаблона(ЭтотОбъект, "ШаблонРКО", Перечисления.ТипыШаблонов.ЧекККТ, Отказ);
				УправлениеШаблонами.ПроверитьТипШаблона(ЭтотОбъект, "ШаблонПКО", Перечисления.ТипыШаблонов.ЧекККТ, Отказ);
			ИначеЕсли ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ФискальныйРегистратор Тогда
				УправлениеШаблонами.ПроверитьТипШаблона(ЭтотОбъект, "ШаблонРКО", Перечисления.ТипыШаблонов.ФискальныйЧек, Отказ);
				УправлениеШаблонами.ПроверитьТипШаблона(ЭтотОбъект, "ШаблонПКО", Перечисления.ТипыШаблонов.ФискальныйЧек, Отказ);
			КонецЕсли;
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("КассаККМ")
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
