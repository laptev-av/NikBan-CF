
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	Если Параметры.Ключ = Неопределено Или Параметры.Ключ.Пустая() Тогда
		
		Объект.ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияСкидокНаценок.Максимум;
		Объект.Наименование = Строка(Объект.ВариантСовместногоПрименения);
		
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.СпособПредоставления = Перечисления.СпособыПредоставленияСкидокНаценок.ПустаяСсылка();
	КонецЕсли;
	
	Элементы.ГруппаПараметрыОграничения.Доступность = Объект.ОграничениеРазмераПодчиненныхСкидок;
	Элементы.СпособПредоставления.АвтоОтметкаНезаполненного = Объект.ОграничениеРазмераПодчиненныхСкидок;
	Элементы.ЗначениеСкидкиНаценки.АвтоОтметкаНезаполненного = Объект.ОграничениеРазмераПодчиненныхСкидок;
	
	Если НЕ Объект.ОграничениеРазмераПодчиненныхСкидок Тогда
		Объект.ЗначениеСкидкиНаценки = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.СкидкиНаценки.Форма.ФормаГруппы.Открытие");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОграничениеРазмераПодчиненныхСкидокПриИзменении(Элемент)
	
	Элементы.ГруппаПараметрыОграничения.Доступность = Объект.ОграничениеРазмераПодчиненныхСкидок;
	Элементы.СпособПредоставления.АвтоОтметкаНезаполненного = Объект.ОграничениеРазмераПодчиненныхСкидок;
	Элементы.ЗначениеСкидкиНаценки.АвтоОтметкаНезаполненного = Объект.ОграничениеРазмераПодчиненныхСкидок;
	Если НЕ Объект.ОграничениеРазмераПодчиненныхСкидок Тогда
		Объект.СпособПредоставления = Неопределено;
		Объект.ЗначениеСкидкиНаценки = 0;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантСовместногоПримененияСкидокНаценокПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = Строка(Объект.ВариантСовместногоПрименения);
	Иначе
		Для Каждого ЭлементСписка Из Элементы.ВариантСовместногоПримененияСкидокНаценок.СписокВыбора Цикл
			Если Строка(ЭлементСписка.Значение) = Объект.Наименование Тогда
				Объект.Наименование = Строка(Объект.ВариантСовместногоПрименения);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
