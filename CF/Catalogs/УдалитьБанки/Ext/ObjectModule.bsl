
// Процедура - обработчик события "ПриКопировании".
//
Процедура ПриКопировании(ОбъектКопирования)
	
	Код = "";
	
КонецПроцедуры // ПриКопировании()

// Процедура - обработчик события "ОбработкаПроверкиЗаполнения".
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
			
	Если НЕ ЭтоГруппа Тогда
		
		Если СтрДлина(СокрЛП(Код)) <> 9 Тогда
			ТекстСообщения = НСтр("ru = 'БИК банка должен иметь 9 знаков.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Код",, Отказ);
		КонецЕсли;

		Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(СокрЛП(Код)) Тогда
			ТекстСообщения = НСтр("ru = 'В составе БИК банка должны быть только цифры.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Код",, Отказ);
		КонецЕсли;
		
	Иначе
		
		ПорядковыйНомер = ПроверяемыеРеквизиты.Найти("Код");
		Если ПорядковыйНомер <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ПорядковыйНомер);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()
