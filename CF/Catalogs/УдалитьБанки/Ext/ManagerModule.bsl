
///////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ //
///////////////////////////////////

// Процедура добавляет новый банк из классификатора
// по значению БИК или корреспондентскому счету.
//
// Параметры:
//	БИК - Строка (9) - БИК банка
//	КоррСчет - Строка (20) - Корреспондентский счет банка
//	ТаблицаБанков - ТаблицаЗначений - Таблица банков
//
Процедура ДобавитьБанкиИзКлассификатора(
	БИК,
	КоррСчет,
	ТаблицаБанков
	)
	
	Возврат
	
КонецПроцедуры // ДобавитьБанкиИзКлассификатора()

// Функция получает таблицу ссылок на банки по БИКу или корреспондетскому счету.
//
// Параметры:
//	Поле - Строка - Имя поля (БИК или КоррСчет)
//	Значение - Строка - Значение БИК или Корреспондентского счета
//
// Возвращаемое значение:
//	ТаблицаЗначений - Найденые банки
//
Функция ПолучитьТаблицуБанковПоРеквизитам(
	Поле,
	Значение
	) Экспорт
	
	ТаблицаБанков = Новый ТаблицаЗначений;
	Колонки = ТаблицаБанков.Колонки;
	Колонки.Добавить("Ссылка");
	Колонки.Добавить("Код");
	Колонки.Добавить("КоррСчет");
	
	ЭтоБИК = Ложь;
	ЭтоКоррСчет = Ложь;
	Если Найти(Поле, "БИК") <> 0 Тогда
		ЭтоБИК = Истина;
	ИначеЕсли Найти(Поле, "КоррСчет") <> 0 Тогда
		ЭтоКоррСчет = Истина;
	КонецЕсли;
	Значение = СокрЛП(Значение);
	Если (ЭтоБИК И СтрДлина(Значение) = 9)
	 ИЛИ (ЭтоКоррСчет И СтрДлина(Значение) = 20)
	Тогда
		
		Если ЭтоБИК Тогда
			СтруктураОтбора = Новый Структура("Код", Значение);
			
		ИначеЕсли ЭтоКоррСчет Тогда
			СтруктураОтбора = Новый Структура("КоррСчет", Значение);
			
		КонецЕсли;
		
		Выборка = Справочники.УдалитьБанки.Выбрать(,, СтруктураОтбора, "Код Возр");
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаБанков.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
		
		Если ТаблицаБанков.Количество() = 0 Тогда
			ДобавитьБанкиИзКлассификатора(
				?(ЭтоБИК, Значение, ""), // БИК
				?(ЭтоКоррСчет, Значение, ""), // КоррСчет
				ТаблицаБанков
			);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаБанков;
	
КонецФункции // ПолучитьТаблицуБанковПоРеквизитам()

Функция ПолучитьРеквизитыБанка(БанковскийСчет) Экспорт
	Перем СтруктураРеквизитов;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Банки.Код          КАК БИК,
	|	Банки.Наименование КАК НаименованиеБанка
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	Справочник.УдалитьБанки КАК Банки
	|ПО
	|	БанковскиеСчетаОрганизаций.Банк = Банки.Ссылка
	|ГДЕ
	|	БанковскиеСчетаОрганизаций.Ссылка = &БанковскийСчет
	|";
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		БИКБанка          = "";
		НаименованиеБанка = "";
	Иначе
		ВыборкаЗапроса = РезультатЗапроса.Выбрать();
		ВыборкаЗапроса.Следующий();
		БИКБанка          = ВыборкаЗапроса.БИК;
		НаименованиеБанка = ВыборкаЗапроса.НаименованиеБанка;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("БИКБанка, НаименованиеБанка", БИКБанка, НаименованиеБанка);
	
	Возврат СтруктураРеквизитов;
	
КонецФункции // ПолучитьРеквизитыБанка()
