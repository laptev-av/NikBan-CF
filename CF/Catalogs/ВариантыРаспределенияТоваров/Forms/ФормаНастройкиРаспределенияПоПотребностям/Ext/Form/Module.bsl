
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Магазин") Тогда
		Магазин = Параметры.Магазин;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("СохранениеНастроек") Тогда
		Элементы.ОткрыватьНастройкиПередРаспределением.Видимость = Параметры.СохранениеНастроек;
	Иначе
		Элементы.ОткрыватьНастройкиПередРаспределением.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокОсновнойКнопки") Тогда
		Элементы.ФормаСохранитьИЗакрыть.Заголовок = Параметры.ЗаголовокОсновнойКнопки;
	КонецЕсли;
	
	ВариантРаспределенияТоваров = Справочники.ВариантыРаспределенияТоваров.ПриПоступленииПоПотребностям;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Настройки.НастройкиВарианта,
	|	Настройки.ПрименятьАвтоматически
	|ИЗ
	|	РегистрСведений.НастройкиВариантовРаспределенияТоваров КАК Настройки
	|ГДЕ
	|	Настройки.ВариантРаспределенияТоваров = &ВариантРаспределенияТоваров
	|	И Настройки.Магазин = &Магазин";
	Запрос.УстановитьПараметр("ВариантРаспределенияТоваров", ВариантРаспределенияТоваров);
	Запрос.УстановитьПараметр("Магазин", Магазин);
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		ОткрыватьНастройкиПередРаспределением = не Результат.ПрименятьАвтоматически;
		НастройкиВарианта.Загрузить(Результат.НастройкиВарианта.Получить());
	КонецЕсли;
	
	ИспользованныеСклады = Новый Массив();
	Для Каждого Строка Из НастройкиВарианта Цикл
		ИспользованныеСклады.Добавить(Строка.Склад);
	КонецЦикла;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Магазин = &Магазин
	|	И Не Склады.Ссылка в (&ИспользованныеСклады)
	|	И НЕ Склады.ПометкаУдаления";
	Запрос.УстановитьПараметр("Магазин", Магазин);
	Запрос.УстановитьПараметр("ИспользованныеСклады", ИспользованныеСклады);
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		НовСтр = НастройкиВарианта.Добавить();
		НовСтр.Склад = Результат.Склад;
		НовСтр.ПериодАнализаПродаж = Новый СтандартныйПериод(ВариантСтандартногоПериода.Месяц);
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	Если ЭтаФорма.ПроверитьЗаполнение() Тогда
		Закрыть(СтруктураВозвратаФормы());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого Строка Из НастройкиВарианта Цикл
		
		Если Строка.ИспользоватьДляРаспределения Тогда
			
			Если не ЗначениеЗаполнено(Строка.ПериодАнализаПродаж.ДатаНачала) Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = Нстр("ru = 'Не заполнен период анализа продаж по складу %1'");
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Сообщение.Текст, Строка.Склад);
				Сообщение.Поле = "НастройкиВарианта[" + Строка(НастройкиВарианта.Индекс(Строка)) + "].ПериодАнализаПродаж";
				Сообщение.Сообщить();
			КонецЕсли;
			
			Если не ЗначениеЗаполнено(Строка.Максимум) Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = Нстр("ru = 'Не заполнен максимум по складу %1'");
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Сообщение.Текст, Строка.Склад);
				Сообщение.Поле = "ПараметрыРаспределения[" + Строка(НастройкиВарианта.Индекс(Строка)) + "].Максимум";
				Сообщение.Сообщить();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СтруктураВозвратаФормы()
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("НастройкиВарианта", Новый ХранилищеЗначения(НастройкиВарианта.Выгрузить()));
	СтруктураРезультат.Вставить("ПрименятьАвтоматически", не ОткрыватьНастройкиПередРаспределением);
	
	Возврат СтруктураРезультат;
	
КонецФункции
