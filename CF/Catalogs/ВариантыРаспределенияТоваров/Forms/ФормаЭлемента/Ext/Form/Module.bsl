
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийМагазин = ПараметрыСеанса.ТекущийМагазин;
	
	Если ЗначениеЗаполнено(Объект.ИмяПредопределенныхДанных) Тогда
		
		Элементы.ГруппаКодИНаименование.ТолькоПросмотр = Ложь;
		Элементы.ГруппаВнешняяОбработка.Видимость = Ложь;
		Элементы.ГруппаПрочиеНастройки.ТолькоПросмотр = Истина;
		
		Объект.ИспользоватьПриОтгрузке = Объект.ИмяПредопределенныхДанных = "ПриОтгрузкеПоОстаткам";
		Объект.ИспользоватьПриПоступлении = Объект.ИмяПредопределенныхДанных = "ПриПоступленииПоПотребностям";
		Объект.ИндивидуальныеНастройкиДляМагазинов = Истина;
		
	КонецЕсли;
	
	Элементы.ФормаНастроитьВариант.Видимость = не Объект.ИндивидуальныеНастройкиДляМагазинов;
	Элементы.ГруппаНастройкиПоМагазинам.Видимость = Объект.ИндивидуальныеНастройкиДляМагазинов;
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокМагазинов, "ВариантРаспределенияТоваров", Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИндивидуальныеНастройкиДляМагазиновПриИзменении(Элемент)
	
	Элементы.ФормаНастроитьВариант.Видимость = не Объект.ИндивидуальныеНастройкиДляМагазинов;
	Элементы.ГруппаНастройкиПоМагазинам.Видимость = Объект.ИндивидуальныеНастройкиДляМагазинов;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВариант(Команда)
	
	Если ЭтаФорма.Модифицированность Тогда
		ТекстПредупреждения = НСтр("ru = 'Перед настройкой необходимо записать вариант'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
	Иначе
		НастроитьВариантВыполнить(ПредопределенноеЗначение("Справочник.Магазины.ПустаяСсылка"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьВариантВыполнить(Магазин) Экспорт
	
	Если Магазин = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ВнешняяОбработка) и не Объект.Предопределенный Тогда
		ТекстСообщения = НСтр("ru = 'Выберите внешнюю обработку'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыРаспределения = Новый Структура();
	ПараметрыРаспределения.Вставить("Магазин", Магазин);
	ПараметрыРаспределения.Вставить("ВариантРаспределенияТоваров", Объект.Ссылка);
	
	ДополнитьПараметрыРаспределенияНаСервере(Магазин, Объект.Ссылка, ПараметрыРаспределения);
	
	ОповещениеОЗакрытииФормыНастройки = Новый ОписаниеОповещения("ПослеЗакрытияФормыНастроек", ЭтотОбъект, ПараметрыРаспределения);
	
    Если Объект.ИмяПредопределенныхДанных = "ПриПоступленииПоПотребностям" Тогда
        
        // &ЗамерПроизводительности
        ОценкаПроизводительностиРТКлиент.НачатьЗамер(
                 Истина, "Справочник.ВариантыРаспределенияТоваров.Форма.ФормаНастройкиРаспределенияПоПотребностям.Открытие");
         
        ОткрытьФорму("Справочник.ВариантыРаспределенияТоваров.Форма.ФормаНастройкиРаспределенияПоПотребностям", ПараметрыРаспределения,,,,, ОповещениеОЗакрытииФормыНастройки, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    ИначеЕсли Объект.ИмяПредопределенныхДанных = "ПриОтгрузкеПоОстаткам" Тогда
        
        // &ЗамерПроизводительности
        ОценкаПроизводительностиРТКлиент.НачатьЗамер(
                 Истина, "Справочник.ВариантыРаспределенияТоваров.Форма.ФормаНастройкиРаспределенияПоОстаткам.Открытие");
        
        ОткрытьФорму("Справочник.ВариантыРаспределенияТоваров.Форма.ФормаНастройкиРаспределенияПоОстаткам", ПараметрыРаспределения,,,,, ОповещениеОЗакрытииФормыНастройки, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    Иначе
		
		ВнешняяОбработка = ОбщегоНазначенияРТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ВнешняяОбработка");
		ИмяОбработки = ДополнительныеОтчетыИОбработкиВызовСервера.ПодключитьВнешнююОбработку(ВнешняяОбработка);
		ИмяФормыНастроек = "ВнешняяОбработка." + ИмяОбработки + ".Форма.Настройки";
		ОткрытьФорму(ИмяФормыНастроек, ПараметрыРаспределения,,,,, ОповещениеОЗакрытииФормыНастройки, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДополнитьПараметрыРаспределенияНаСервере(Магазин, ВариантРаспределения, СтруктураПараметров)
	
	СтруктураПараметров.Вставить("СохранениеНастроек", Пользователи.РолиДоступны("ДобавлениеИзменениеВариантовРаспределения"));
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиВариантовРаспределенияТоваров.НастройкиВарианта,
	|	НастройкиВариантовРаспределенияТоваров.ПрименятьАвтоматически
	|ИЗ
	|	РегистрСведений.НастройкиВариантовРаспределенияТоваров КАК НастройкиВариантовРаспределенияТоваров
	|ГДЕ
	|	НастройкиВариантовРаспределенияТоваров.ВариантРаспределенияТоваров = &Вариант
	|	И НастройкиВариантовРаспределенияТоваров.Магазин = &Магазин";
	Запрос.УстановитьПараметр("Вариант", ВариантРаспределения);
	Запрос.УстановитьПараметр("Магазин", Магазин);
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		СтруктураПараметров.Вставить("НастройкиВарианта", Результат.НастройкиВарианта);
		СтруктураПараметров.Вставить("ПрименятьАвтоматически", Результат.ПрименятьАвтоматически);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияФормыНастроек(РезультатЗакрытия, ПараметрыВарианта) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиНаСервере(РезультатЗакрытия, ПараметрыВарианта);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере(РезультатЗакрытия, ПараметрыВарианта)
	
	МенеджерНастроек = РегистрыСведений.НастройкиВариантовРаспределенияТоваров.СоздатьМенеджерЗаписи();
	
	МенеджерНастроек.ВариантРаспределенияТоваров = ПараметрыВарианта.ВариантРаспределенияТоваров;
	Если Объект.ИндивидуальныеНастройкиДляМагазинов Тогда
		МенеджерНастроек.Магазин = ПараметрыВарианта.Магазин;
	КонецЕсли;
	
	МенеджерНастроек.НастройкиВарианта = РезультатЗакрытия.НастройкиВарианта;
	МенеджерНастроек.ПрименятьАвтоматически = РезультатЗакрытия.ПрименятьАвтоматически;
	
	МенеджерНастроек.Записать(Истина);
	
	Элементы.СписокМагазинов.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокМагазиновВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если не Элементы.СписокМагазинов.ТекущиеДанные = Неопределено Тогда
		Если ЭтаФорма.Модифицированность Тогда
			ТекстПредупреждения = НСтр("ru = 'Перед настройкой необходимо записать вариант'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
		Иначе
			НастроитьВариантВыполнить(Элементы.СписокМагазинов.ТекущиеДанные.Магазин);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
