
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьВсеВидыОплаты(Команда)
	
	Если Объект.СоответствиеВидовОплаты.Количество() > 0 Тогда
		
		ОповещенияПриЗавершении = Новый ОписаниеОповещения("ОбработатьОтветПользователя", ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru='В табличной части ""Соответствие видов оплаты"" уже содержатся строки.
							|Очистить табличную часть перед заполнением?'");
		
		ПоказатьВопрос(ОповещенияПриЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ДобавитьВсеВидыОплатыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКодыВидовОплатыККМOfflineИдентификаторами(Команда)
	
	ЗаполнитьКодыВидовОплатыККМOfflineИдентификаторамиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьОтветПользователя(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.СоответствиеВидовОплаты.Очистить();
	КонецЕсли;
	
	ДобавитьВсеВидыОплатыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВсеВидыОплатыНаСервере()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
		|	ВидыОплатЧекаККМ.Ссылка КАК ВидОплаты
		|ИЗ
		|	Справочник.ВидыОплатЧекаККМ КАК ВидыОплатЧекаККМ
		|ГДЕ
		|	ВидыОплатЧекаККМ.ТипОплаты В (ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.Наличные),
		|ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.ПлатежнаяКарта),
		|ЗНАЧЕНИЕ(Перечисление.ТипыОплатЧекаККМ.БанковскийКредит))";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Объект.СоответствиеВидовОплаты.Добавить();
		
		НоваяСтрока.ВидОплаты = Выборка.ВидОплаты;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКодыВидовОплатыККМOfflineИдентификаторамиНаСервере()
	
	Для Каждого ТекСтрока Из Элементы.СоответствиеВидовОплаты.ВыделенныеСтроки Цикл
		
		ДанныеТекущейСтроки = Объект.СоответствиеВидовОплаты.НайтиПоИдентификатору(ТекСтрока);
		
		ДанныеТекущейСтроки.КодВидаОплатыККМOffline = ДанныеТекущейСтроки.ВидОплаты.УникальныйИдентификатор();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти