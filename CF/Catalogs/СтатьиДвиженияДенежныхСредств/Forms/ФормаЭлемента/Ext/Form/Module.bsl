
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Подсистема запрета редактирования ключевых реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	Элементы.Наименование.ТолькоПросмотр = Объект.Предопределенный;
	Если Объект.Предопределенный Тогда
		Элементы.ХозяйственнаяОперация.СписокВыбора.Очистить();
		Элементы.ХозяйственнаяОперация.СписокВыбора.Добавить(Справочники.СтатьиДвиженияДенежныхСредств.ХозяйственнаяОперацияПредопределеннойСтатьиДДС(Объект.Ссылка));
	КонецЕсли;
	
	ЗаполнитьСписокВыбораКоррСчета();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Подсистема запрета редактирования ключевых реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ХозяйственнаяОперацияПриИзменении(Элемент)
	
	Объект.КорреспондирующийСчет = "";
	ЗаполнитьСписокВыбораКоррСчета();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораКоррСчета()
	
	СписокСчетов = Элементы.КорреспондирующийСчет.СписокВыбора;
	СписокСчетов.Очистить();
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы Тогда
		СписокСчетов.Добавить("91.01", НСтр("ru = 'Прочие доходы (91.01)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы Тогда
		СписокСчетов.Добавить("25", НСтр("ru = 'Общепроизводственные расходы (25)'"));
		СписокСчетов.Добавить("26", НСтр("ru = 'Общехозяйственные расходы (26)'"));
		СписокСчетов.Добавить("44.01", НСтр("ru = 'Издержки обращения (44.01)'"));
		СписокСчетов.Добавить("44.02", НСтр("ru = 'Коммерческие расходы (44.02)'"));
		СписокСчетов.Добавить("70", НСтр("ru = 'Расчеты с персоналом по оплате труда (70)'"));
		СписокСчетов.Добавить("91.02", НСтр("ru = 'Прочие расходы (91.02)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику Тогда
		СписокСчетов.Добавить("60.01", НСтр("ru = 'Расчеты с поставщиками и подрядчиками (60.01)'"));
		СписокСчетов.Добавить("76.05", НСтр("ru = 'Расчеты с прочими поставщиками и подрядчиками (76.05)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
		СписокСчетов.Добавить("62.01", НСтр("ru = 'Расчеты с покупателями и заказчиками (62.01)'"));
		СписокСчетов.Добавить("76.06", НСтр("ru = 'Расчеты с прочими покупателями и заказчиками (76.06)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика Тогда
		СписокСчетов.Добавить("60.01", НСтр("ru = 'Расчеты с поставщиками и подрядчиками (60.01)'"));
		СписокСчетов.Добавить("76.05", НСтр("ru = 'Расчеты с прочими поставщиками и подрядчиками (76.05)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту Тогда
		СписокСчетов.Добавить("62.01", НСтр("ru = 'Расчеты с покупателями и заказчиками (62.01)'"));
		СписокСчетов.Добавить("76.06", НСтр("ru = 'Расчеты с прочими покупателями и заказчиками (76.06)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу Тогда
		СписокСчетов.Добавить("50.01", НСтр("ru = 'Касса организации (50.01)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка Тогда
		СписокСчетов.Добавить("51", НСтр("ru = 'Расчетные счета (51)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СдачаДенежныхСредствВБанк Тогда
		СписокСчетов.Добавить("51", НСтр("ru = 'Расчетные счета (51)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюОрганизацию Тогда
		СписокСчетов.Добавить("60.01", НСтр("ru = 'Расчеты с поставщиками и подрядчиками (60.01)'"));
		СписокСчетов.Добавить("60.02", НСтр("ru = 'Расчеты по авансам выданным (60.02)'"));
		СписокСчетов.Добавить("76.05", НСтр("ru = 'Расчеты с прочими поставщиками и подрядчиками (76.05)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствВКассуККМ Тогда
		СписокСчетов.Добавить("50.01", НСтр("ru = 'Касса организации (50.01)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ Тогда
		СписокСчетов.Добавить("50.01", НСтр("ru = 'Касса организации (50.01)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗаработнойПлатыПоВедомостям
		ИЛИ Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыплатаЗаработнойПлатыРаботнику Тогда
		СписокСчетов.Добавить("70", НСтр("ru = 'Расчеты с персоналом по оплате труда (70)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств Тогда
		СписокСчетов.Добавить("50.01", НСтр("ru = 'Касса организации (50.01)'"));
		
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВознаграждениеОтКомитента Тогда
		СписокСчетов.Добавить("76.09", НСтр("ru = 'Прочие расчеты с разными дебиторами и кредиторами (76.09)'"));	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
