#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтапыОплаты.Итог("ПроцентПлатежа") <> 100 Тогда
		
		ТекстОшибки = НСтр("ru='Процент платежей по всем этапам должен быть равен 100%'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"Этапы",
			,
			Отказ);
		
	КонецЕсли;
	
	Для Каждого Строка Из ЭтапыОплаты Цикл
		Если Строка.ВидПлатежа = Перечисления.ВидПлатежа.Предоплата И Строка.Срок = 0 Тогда
			
			НомерСтроки = Строка.НомерСтроки;
			
			ТекстОшибки = НСтр("ru='Срок в строке %НомерСтроки% не может быть меньше одного дня.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%", НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				,
				"Объект.ЭтапыОплаты[" + (Строка.НомерСтроки - 1) + "].Срок"
				,
				Отказ);
			
		КонецЕсли;
	КонецЦикла;
	
	КоличествоЭтапов = ЭтапыОплаты.Количество();
	
	Если КоличествоЭтапов >= 2 Тогда
		
		Для ВнешнийСчетчик = 2 По КоличествоЭтапов Цикл
			
			ИндексПредыдущегоЭтапа           = ВнешнийСчетчик - 2;
			ИндексТекущегоЭтапа              = ВнешнийСчетчик - 1;
			ПредыдущееЗначениеВариантаОплаты = ЭтапыОплаты[ИндексПредыдущегоЭтапа].ВидПлатежа;
			ПредыдущееЗначениеСдвига         = ЭтапыОплаты[ИндексПредыдущегоЭтапа].Срок;
			ТекущееЗначениеВариантаОплаты    = ЭтапыОплаты[ИндексТекущегоЭтапа].ВидПлатежа;
			ТекущееЗначениеСдвига            = ЭтапыОплаты[ИндексТекущегоЭтапа].Срок;
			
			// В табличной части ЭтапыОплаты не должно быть строк со значением Предоплата
			// в поле ВидПлатежа, идущих после строк со значением ОтсрочкаПлатежа
			Если (ТекущееЗначениеВариантаОплаты = Перечисления.ВидПлатежа.Предоплата И 
				(ПредыдущееЗначениеВариантаОплаты = Перечисления.ВидПлатежа.ОтсрочкаПлатежа)) Тогда
				
				ТекстОшибки = НСтр("ru='Вариант оплаты ""%ТекущееЗначениеВариантаОплаты%"" в строке %ИндексТекущегоЭтапа%
				|не может идти после варианта оплаты ""%ПредыдущееЗначениеВариантаОплаты%"" в строке %ИндексПредыдущегоЭтапа%'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ПредыдущееЗначениеВариантаОплаты%", ПредыдущееЗначениеВариантаОплаты);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ТекущееЗначениеВариантаОплаты%",    ТекущееЗначениеВариантаОплаты);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИндексТекущегоЭтапа%",              ИндексТекущегоЭтапа + 1);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИндексПредыдущегоЭтапа%",           ИндексПредыдущегоЭтапа + 1);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Этапы",ИндексТекущегоЭтапа+1,"ВариантОплаты"),
					,
					Отказ);
			
			КонецЕсли;
			
			Если ((ТекущееЗначениеВариантаОплаты = Перечисления.ВидПлатежа.Предоплата И 
				ПредыдущееЗначениеВариантаОплаты = Перечисления.ВидПлатежа.Предоплата) И
				ТекущееЗначениеСдвига > ПредыдущееЗначениеСдвига) Тогда
				
				ТекстОшибки = НСтр("ru='Отсрочка в строке %ИндексТекущегоЭтапа% 
				| должна быть не больше, чем в строке %ИндексПредыдущегоЭтапа%'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИндексТекущегоЭтапа%",    ИндексТекущегоЭтапа + 1);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИндексПредыдущегоЭтапа%", ИндексПредыдущегоЭтапа + 1);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Этапы",ИндексТекущегоЭтапа+1,"Сдвиг"),
					,
					Отказ);

				
			ИначеЕсли ((ТекущееЗначениеВариантаОплаты = Перечисления.ВидПлатежа.ОтсрочкаПлатежа И 
				ПредыдущееЗначениеВариантаОплаты = ТекущееЗначениеВариантаОплаты) И 
				ТекущееЗначениеСдвига < ПредыдущееЗначениеСдвига)Тогда
				
				ТекстОшибки = НСтр("ru='Отсрочка в строке %ИндексТекущегоЭтапа% 
				| должна быть не меньше, чем в строке %ИндексПредыдущегоЭтапа%'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИндексТекущегоЭтапа%",    ИндексТекущегоЭтапа + 1);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИндексПредыдущегоЭтапа%", ИндексПредыдущегоЭтапа + 1);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Этапы",ИндексТекущегоЭтапа+1,"Сдвиг"),
					,
					Отказ);
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
