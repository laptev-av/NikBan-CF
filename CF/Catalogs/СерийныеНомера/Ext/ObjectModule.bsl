#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события "ОбработкаПроверкиЗаполнения".
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	СтруктураВладельца = Новый Структура;
	СтруктураВладельца.Вставить("ТипСерийногоНомера", "ТипСерийногоНомера");
	СтруктураВладельца.Вставить("ИспользоватьСерийныеНомера", "ИспользоватьСерийныеНомера");
	
	РеквизитыВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Владелец, СтруктураВладельца);
	
	Если НЕ РеквизитыВладельца.ИспользоватьСерийныеНомера Тогда
		
		ТекстОшибки = НСтр("ru = 'Для элемента ""%1"" номера подарочных сертификатов не используются'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Владелец);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"Владелец",
			,
			Отказ);
		
	КонецЕсли;
	
	Если РеквизитыВладельца.ТипСерийногоНомера = Перечисления.ТипыСерийныхНомеровСертификатов.Штриховой Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КодСерийногоНомера");
	КонецЕсли;
	
	Если НЕ РеквизитыВладельца.ТипСерийногоНомера = Перечисления.ТипыСерийныхНомеровСертификатов.Штриховой Тогда
		
		Если НЕ ЭтоНовый() Тогда
			
			ЗначенияРеквизитовВИнформационнойБазе = ОбщегоНазначенияРТ.ПолучитьЗначенияРеквизитовОбъекта(Ссылка, Новый Структура("КодСерийногоНомера"));
			
		КонецЕсли;
		
		Если (ЭтоНовый() ИЛИ ЗначенияРеквизитовВИнформационнойБазе.КодСерийногоНомера <> КодСерийногоНомера)
			И НЕ ПустаяСтрока(КодСерийногоНомера) Тогда
			
			Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			|	СерийныеНомера.Ссылка
			|ИЗ
			|	Справочник.СерийныеНомера КАК СерийныеНомера
			|ГДЕ
			|	СерийныеНомера.КодСерийногоНомера = &КодСерийногоНомера
			|	И СерийныеНомера.Ссылка <> &Ссылка");
			
			Запрос.УстановитьПараметр("КодСерийногоНомера", КодСерийногоНомера);
			Запрос.УстановитьПараметр("Ссылка"            , Ссылка);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				
				ТекстОшибки = НСтр("ru = 'Номер подарочного сертификата %1 уже существует'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, КодСерийногоНомера);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					"КодСерийногоНомера",
					,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

// Процедура - обработчик события "ПередЗаписью".
//
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;		
	КонецЕсли;
    	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
