#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Очищает неиспользуемые номера сертификатов.
//
Процедура ОчиститьВДокументеНеиспользуемыеСерийныеНомера(ТЧТовары, ТЧСерийныеНомера) Экспорт

	СоответствиеКлючей = Новый Соответствие;
	Количество        = ТЧСерийныеНомера.Количество();
	Для Итератор = 1 По Количество Цикл

		СтрокаТЧ            = ТЧСерийныеНомера[Количество - Итератор];
		НомерНеИспользуется = СоответствиеКлючей[СтрокаТЧ.КлючСвязиСерийныхНомеров];
		Если НомерНеИспользуется = Неопределено Тогда

			НомерНеИспользуется = (ТЧТовары.Найти(СтрокаТЧ.КлючСвязиСерийныхНомеров,
				"КлючСвязиСерийныхНомеров") = Неопределено);

			СоответствиеКлючей.Вставить(СтрокаТЧ.КлючСвязиСерийныхНомеров, НомерНеИспользуется);

		КонецЕсли;

		Если НомерНеИспользуется Тогда
			ТЧСерийныеНомера.Удалить(СтрокаТЧ);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Очищает неиспользуемые номера сертификатов с отбором по ключу связи.
//
Процедура ОчиститьВДокументеНеиспользуемыеСерийныеНомераПоКлючу(КлючСвязи, ТЧСерийныеНомера) Экспорт

	Количество = ТЧСерийныеНомера.Количество();
	Для Итератор = 1 По Количество Цикл

		СтрокаТЧ = ТЧСерийныеНомера[Количество - Итератор];

		Если СтрокаТЧ.КлючСвязиСерийныхНомеров <> КлючСвязи Тогда

			ТЧСерийныеНомера.Удалить(СтрокаТЧ);

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Функция проверки принадлежности серийных номеров владельцу.
//
Функция СерийныеНомераНеПринадлежатВладельцу(Номенклатура, МассивСерийныхНомеров) Экспорт

	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Результат
	|ИЗ
	|	Справочник.СерийныеНомера КАК Спр
	|ГДЕ
	|	Спр.Владелец <> &Номенклатура
	|	И Спр.Ссылка В (&МассивСерийныхНомеров)
	|");
	Запрос.УстановитьПараметр("Номенклатура"         , Номенклатура);
	Запрос.УстановитьПараметр("МассивСерийныхНомеров", МассивСерийныхНомеров);

	Возврат Не Запрос.Выполнить().Пустой();

КонецФункции

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

