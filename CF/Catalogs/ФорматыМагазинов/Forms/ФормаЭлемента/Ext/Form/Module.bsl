
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	//
	ПроверитьДоступностьРеквизитов();
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	ОбновитьСписокВыбораТехнологииРазмещения();
	ОбновитьСписокВыбораЦелевогоМестоположения();
	ОбновитьСписокВыбораЦелевыхГрупп();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьСписокВыбораТехнологииРазмещения();
	ОбновитьСписокВыбораЦелевогоМестоположения();
	ОбновитьСписокВыбораЦелевыхГрупп();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПлощадьТорговогоЗалаМинимальнаяПриИзменении(Элемент)
	Если Объект.ПлощадьТорговогоЗалаМаксимальная < Объект.ПлощадьТорговогоЗалаМинимальная Тогда
		Если Объект.ПлощадьТорговогоЗалаМаксимальная <> 0 Тогда
			ТекстСообщения = НСтр("ru = 'Минимальная площадь не может быть больше максимальной.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Объект.ПлощадьТорговогоЗалаМаксимальная = Объект.ПлощадьТорговогоЗалаМинимальная;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПлощадьТорговогоЗалаМаксимальнаяПриИзменении(Элемент)
	Если Объект.ПлощадьТорговогоЗалаМаксимальная < Объект.ПлощадьТорговогоЗалаМинимальная Тогда
		ТекстСообщения = НСтр("ru = 'Максимальная площадь не может быть меньше минимальной.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Объект.ПлощадьТорговогоЗалаМинимальная = Объект.ПлощадьТорговогоЗалаМаксимальная;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ТехнологияРазмещенияТовараИОбслуживанияПокупателейПриИзменении(Элемент)
	ОбновитьСписокВыбораТехнологииРазмещения();
КонецПроцедуры

&НаКлиенте
Процедура ЦелевоеМестоположениеПриИзменении(Элемент)
	ОбновитьСписокВыбораЦелевогоМестоположения();
КонецПроцедуры


&НаКлиенте
Процедура ПравилоЦенообразованияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "Справочник.ПравилаЦенообразования.Форма.ФормаВыбора.Открытие");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЦелевыеГруппыПокупателей

&НаКлиенте
Процедура ЦелевыеГруппыПокупателейЦелеваяГруппаПокупателейПриИзменении(Элемент)
	ОбновитьСписокВыбораЦелевыхГрупп();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокВыбораЦелевогоМестоположения()
	Элементы.ЦелевоеМестоположение.СписокВыбора.Очистить();
	МассивВариантов = ПолучитьВарианты("ФорматыМагазинов", "ЦелевоеМестоположение", "ЭтоГруппа");
	СписокВыбора = Элементы.ЦелевоеМестоположение.СписокВыбора;
	Для Каждого Вариант Из МассивВариантов Цикл
		СписокВыбора.Добавить(Вариант);
	КонецЦикла;
	Если СписокВыбора.НайтиПоЗначению(Объект.ЦелевоеМестоположение) = Неопределено Тогда
		СписокВыбора.Добавить(Объект.ЦелевоеМестоположение);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВыбораТехнологииРазмещения()
	Элементы.ТехнологияРазмещенияТовараИОбслуживанияПокупателей.СписокВыбора.Очистить();
	МассивВариантов = ПолучитьВарианты("ФорматыМагазинов", "ТехнологияРазмещенияТовараИОбслуживанияПокупателей", "ЭтоГруппа");
	СписокВыбора = Элементы.ТехнологияРазмещенияТовараИОбслуживанияПокупателей.СписокВыбора;
	Для Каждого Вариант Из МассивВариантов Цикл
		СписокВыбора.Добавить(Вариант);
	КонецЦикла;
	Если СписокВыбора.НайтиПоЗначению(Объект.ТехнологияРазмещенияТовараИОбслуживанияПокупателей) = Неопределено Тогда
		СписокВыбора.Добавить(Объект.ТехнологияРазмещенияТовараИОбслуживанияПокупателей);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВыбораЦелевыхГрупп()
	Элементы.ЦелевыеГруппыПокупателейЦелеваяГруппаПокупателей.СписокВыбора.Очистить();
	МассивВариантов = ПолучитьВарианты("ФорматыМагазинов.ЦелевыеГруппыПокупателей", "ЦелеваяГруппаПокупателей", "Ссылка.ЭтоГруппа");
	СписокВыбора = Элементы.ЦелевыеГруппыПокупателейЦелеваяГруппаПокупателей.СписокВыбора;
	Для Каждого Вариант Из МассивВариантов Цикл
		СписокВыбора.Добавить(Вариант);
	КонецЦикла;
	Для Каждого СтрокаЦелевойГруппы Из Объект.ЦелевыеГруппыПокупателей Цикл
		Если СписокВыбора.НайтиПоЗначению(СтрокаЦелевойГруппы.ЦелеваяГруппаПокупателей) = Неопределено Тогда
			СписокВыбора.Добавить(СтрокаЦелевойГруппы.ЦелеваяГруппаПокупателей);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ПолучитьВарианты(ИмяТаблицы, ИмяРеквизита, СтрокаУсловияГруппы)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Таблица."+ИмяРеквизита+" КАК ЗначениеВарианта
	                      |ИЗ
	                      |	Справочник."+ИмяТаблицы+" КАК Таблица
						  |ГДЕ
						  |	(НЕ Таблица."+СтрокаУсловияГруппы+")
						  |	И Таблица."+ИмяРеквизита+"<>""""
	                      |СГРУППИРОВАТЬ ПО
	                      |	Таблица."+ИмяРеквизита+"
	                      |УПОРЯДОЧИТЬ ПО
	                      |	ЗначениеВарианта
						  |");
	РезультатЗапроса=Запрос.Выполнить();
	МассивВариантов = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ЗначениеВарианта");
	
	Возврат МассивВариантов;
КонецФункции

&НаСервере
Процедура ПроверитьДоступностьРеквизитов()
	
	ДоступностьПравилаЦенообразования = ОбщегоНазначенияРТВызовСервера.ПроверитьДоступКРеквизиту(Объект, "ПравилоЦенообразования"  , "Справочник.ПравилаЦенообразования");
	
	Элементы.ПравилоЦенообразования.Видимость   = ДоступностьПравилаЦенообразования;
	
КонецПроцедуры

#КонецОбласти


