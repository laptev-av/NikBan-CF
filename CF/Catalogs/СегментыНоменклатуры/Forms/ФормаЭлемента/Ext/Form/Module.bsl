
#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ОбработкаРедактированияОписания(ИзмененныйТекст, ДополнительныеПараметры) Экспорт
	
	Если ИзмененныйТекст <> Неопределено
		И Объект.Описание <> ИзмененныйТекст Тогда
		Объект.Описание = ИзмененныйТекст;
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОткрытияНастройкиСКД(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЭтаФорма.Модифицированность = Истина;
		Объект.ИмяШаблонаСКД = Результат.ИмяТекущегоШаблонаСКД;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособФормированияПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		СпособФормированияПриИзмененииСервер(Истина);
	Иначе
		Объект.СпособФормирования = ПредыдущийСпособФормирования;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСегментОбработкаВопроса(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Если Записать() Тогда
			СформироватьСегментЗавершение();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	// Подсистема запрета редактирования ключевых реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Элементы.ДатаОчистки.ТолькоПросмотр =
		Объект.СпособФормирования <> Перечисления.СпособыФормированияСегментов.ФормироватьВручную;
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СКД = Объект.Ссылка.СхемаКомпоновкиДанных.Получить();
	Иначе
		СКД = Справочники.СегментыНоменклатуры.ПолучитьМакет("ОсновнаяСхема");
	КонецЕсли;
	АдресСКД = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор);

	Элементы.Сформировать.Доступность = НЕ Объект.СпособФормирования =
										Перечисления.СпособыФормированияСегментов.ФормироватьДинамически;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	Элементы.Сформировать.Доступность = НЕ Объект.СпособФормирования =
										Перечисления.СпособыФормированияСегментов.ФормироватьДинамически;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.СхемаКомпоновкиДанных =
		Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресСКД));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	// Обработчик подсистемы запрета редактирования реквизитов объектов.
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДополнительныеПараметры = Новый Структура;
	ОбработчикОповещения = Новый ОписаниеОповещения("ОбработкаРедактированияОписания", ЭтаФорма, ДополнительныеПараметры);
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(
		ОбработчикОповещения,
		Элемент.ТекстРедактирования,
		НСтр("ru = 'Описание сегмента'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СпособФормированияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПредыдущийСпособФормирования = Объект.СпособФормирования;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособФормированияПриИзменении(Элемент)

	Если Объект.Ссылка.Пустая() Тогда
		СпособФормированияПриИзмененииСервер(Ложь);
		ПредыдущийСпособФормирования = Объект.СпособФормирования;
	Иначе
		Если ПредыдущийСпособФормирования <> Объект.СпособФормирования Тогда
			ДополнительныеПараметры = Новый Структура;
			ОбработчикОповещения = Новый ОписаниеОповещения("СпособФормированияПриИзмененииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(ОбработчикОповещения, НСтр("ru = 'Текущий состав сегмента будет очищен. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьНастройкиСхемыКомпоновкиДанных(Команда)

	ЗаголовокНастройки = НСтр("ru = 'Настройки сегмента ""%ИмяСегмента%""'");
	ЗаголовокНастройки = СтрЗаменить(ЗаголовокНастройки, "%ИмяСегмента%", Объект.Наименование);
	
	ИмяШаблонаСКД = ?(ЗначениеЗаполнено(Объект.ИмяШаблонаСКД), Объект.ИмяШаблонаСКД, "ОсновнаяСхема");
	
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АдресСхемыКомпоновкиДанных", АдресСКД);
	ПараметрыОткрытия.Вставить("ИсточникШаблонов", Объект.Ссылка);
	ПараметрыОткрытия.Вставить("Заголовок", ЗаголовокНастройки);
	ПараметрыОткрытия.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыОткрытия.Вставить("ИмяШаблонаСКД", ИмяШаблонаСКД);
	ПараметрыОткрытия.Вставить("ВозвращатьИмяТекущегоШаблонаСКД", Истина);
	ДополнительныеПараметры = Новый Структура;
	ОбработчикОповещения = Новый ОписаниеОповещения("ОбработкаОткрытияНастройкиСКД", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных", ПараметрыОткрытия, ЭтаФорма, , , , ОбработчикОповещения, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

// Процедура формирует сегмент номенклатуры.
//
&НаКлиенте
Процедура СформироватьСегмент(Команда)

	Если Объект.СпособФормирования = ПредопределенноеЗначение("Перечисление.СпособыФормированияСегментов.ФормироватьДинамически") Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Формирование доступно только для нединамических сегментов.'"));
		Возврат;
	КонецЕсли;
	
	Если (НЕ ЗначениеЗаполнено(Объект.Ссылка))
		ИЛИ ЭтаФорма.Модифицированность Тогда
		ОбработчикОповещения = Новый ОписаниеОповещения("СформироватьСегментОбработкаВопроса", ЭтотОбъект);
		ПоказатьВопрос(ОбработчикОповещения, НСтр("ru='Перед формированием необходимо записать сегмент. Записать?'"), РежимДиалогаВопрос.ДаНет);
	Иначе
		СформироватьСегментЗавершение();
	КонецЕсли;

КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьСегментЗавершение()

	СегментыВызовСервера.Сформировать(Объект.Ссылка);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Формирование сегмента номенклатуры'"),,
								   НСтр("ru = 'Сегмент сформирован.'"));

КонецПроцедуры

&НаСервере
Процедура СпособФормированияПриИзмененииСервер(ОчищатьСостав)
	
	Если ОчищатьСостав Тогда
		СегментыВызовСервера.Очистить(Объект.Ссылка);
	КонецЕсли;
	
	Если Объект.СпособФормирования = Перечисления.СпособыФормированияСегментов.ФормироватьВручную Тогда
		Элементы.ДатаОчистки.ТолькоПросмотр = Ложь;
		Элементы.ДатаОчистки.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	Иначе
		Элементы.ДатаОчистки.ТолькоПросмотр = Истина;
		Объект.ДатаОчистки = '00010101';
		Элементы.ДатаОчистки.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	КонецЕсли;

	Элементы.Сформировать.Доступность = НЕ Объект.СпособФормирования = Перечисления.СпособыФормированияСегментов.ФормироватьДинамически;
	
КонецПроцедуры

#КонецОбласти
