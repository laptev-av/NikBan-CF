
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.НастройкиРМК.Форма.ФормаСписка.Открытие");
	
	#Если Не ВебКлиент Тогда
	мИмяКомпьютера = ИмяКомпьютера();
	#КонецЕсли
	
	ОтборыСписковКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Компьютер", мИмяКомпьютера, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.НастройкиРМК.Форма.ФормаЭлемента.Открытие");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	УстановитьЗаголовокКнопки();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекущуюНастройкуРМК(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	Если НЕ ТекущаяСтрока = Неопределено Тогда
	
		НастройкаРМК = ТекущаяСтрока;
		Если ЗначениеЗаполнено(НастройкаРМК) Тогда
			
			УстановитьТекущуюНастройкуРМКСервер(НастройкаРМК);
			
		КонецЕсли;
	КонецЕсли;
	
	УстановитьЗаголовокКнопки();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
    
    // &ЗамерПроизводительности
	ОценкаПроизводительностиРТКлиент.НачатьЗамер(
		     Истина, "Справочник.НастройкиРМК.Форма.ФормаЭлемента.СозданиеНового");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает текущую настройку для текущего компьютера.
//
// Параметры:
//  НастройкаРМК - Настройка РМК, которая будет установлена как текущая.
//
&НаСервере
Процедура УстановитьТекущуюНастройкуРМКСервер(НастройкаРМК)

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НастройкаРМКРегистр.Компьютер,
	|	НастройкаРМКРегистр.НастройкаРМК
	|ИЗ
	|	РегистрСведений.НастройкаРМКНаКомпьютере КАК НастройкаРМКРегистр
	|ГДЕ
	|	НастройкаРМКРегистр.Компьютер = &Компьютер
	|	И НастройкаРМКРегистр.НастройкаРМК = &НастройкаРМК";
	
	Запрос.УстановитьПараметр("Компьютер"   , мИмяКомпьютера);
	Запрос.УстановитьПараметр("НастройкаРМК", НастройкаРМК);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	МенеджерРегистраНастройкаРМК           = РегистрыСведений.НастройкаРМКНаКомпьютере.СоздатьМенеджерЗаписи();
	МенеджерРегистраНастройкаРМК.Компьютер = мИмяКомпьютера;
	
	Если НЕ Выборка.Следующий() Тогда
		МенеджерРегистраНастройкаРМК.НастройкаРМК = НастройкаРМК;
	КонецЕсли;
	
	МенеджерРегистраНастройкаРМК.Записать(Истина);
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокКнопки()
	
	Если Элементы.Найти("КнопкаУстановитьТекущую") = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		
		Если ТекущаяСтрока.Текущая Тогда
			ЗаголовокКоманды = НСтр("ru = 'Отменить настройку'");
		Иначе
			ЗаголовокКоманды = НСтр("ru = 'Применить настройку'");
		КонецЕсли;
		
		Если НЕ ЗаголовокКоманды = Элементы.КнопкаУстановитьТекущую.Заголовок Тогда
			Элементы.КнопкаУстановитьТекущую.Заголовок = ЗаголовокКоманды;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти