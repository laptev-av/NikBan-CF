#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ПеременныеМодуля

Перем ТаблицаСочетанийКлавишЗадействованныхВРМК;

#КонецОбласти


#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ИспользоватьРасширеннуюНастройкуКнопокНижнейПанели Тогда
		
		Если КнопкиНижнейПанели.Найти("КнопкаВыход", "ИмяКнопки") = Неопределено Тогда
			Текст = НСтр("ru = 'Среди кнопок нижней панели обязательно должна быть кнопка ""Выход""'");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				ЭтотОбъект,
				"КнопкиНижнейПанели" ,
				,
				Отказ
			);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаКнопок.ИмяКнопки,
		|	ТаблицаКнопок.НомерСтроки
		|ПОМЕСТИТЬ ТаблицаКолонок
		|ИЗ
		|	&ТаблицаКнопок КАК ТаблицаКнопок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ИмяКнопки КАК ИмяКнопки,
		|	ВложенныйЗапрос.КоличествоСтрок,
		|	ТаблицаКолонок.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаКолонок.ИмяКнопки КАК ИмяКнопки,
		|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаКолонок.НомерСтроки) КАК КоличествоСтрок
		|	ИЗ
		|		ТаблицаКолонок КАК ТаблицаКолонок
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ТаблицаКолонок.ИмяКнопки) КАК ВложенныйЗапрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКолонок КАК ТаблицаКолонок
		|		ПО ВложенныйЗапрос.ИмяКнопки = ТаблицаКолонок.ИмяКнопки
		|ГДЕ
		|	ВложенныйЗапрос.КоличествоСтрок > 1
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|ИТОГИ ПО
		|	ИмяКнопки";
		
		Запрос.УстановитьПараметр("ТаблицаКнопок", КнопкиНижнейПанели.Выгрузить());
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока Выборка.Следующий() Цикл
			ОписаниеОшибкиПовтора = НСтр("ru = 'Одинаковые кнопки нижней панели - ""%1"" в строках №№'") + " ";
			ОписаниеОшибкиПовтора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ОписаниеОшибкиПовтора,
				Выборка.ИмяКнопки
			);
			
			ВыборкаСтрок = Выборка.Выбрать();
			ПервыйПроход = Истина;
			НомерСтроки = 1;
			Пока ВыборкаСтрок.Следующий() Цикл
				
				Если ПервыйПроход Тогда
					ОписаниеОшибкиПовтора = ОписаниеОшибкиПовтора + ВыборкаСтрок.НомерСтроки;
					ПервыйПроход = Ложь;
				Иначе
					ОписаниеОшибкиПовтора = ОписаниеОшибкиПовтора + ", " + ВыборкаСтрок.НомерСтроки;
				КонецЕсли;
				
				НомерСтроки = ВыборкаСтрок.НомерСтроки;
			КонецЦикла;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ОписаниеОшибкиПовтора,
				ЭтотОбъект,
				"КнопкиНижнейПанели[" + (НомерСтроки - 1) + "]" ,
				,
				Отказ
			);
		КонецЦикла;
		
		// Предупреждение по сочетаниям.
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаКнопок.Клавиша КАК Клавиша,
		|	ВЫРАЗИТЬ(ТаблицаКнопок.АкселераторAlt КАК БУЛЕВО) КАК АкселераторAlt,
		|	ВЫРАЗИТЬ(ТаблицаКнопок.АкселераторCtrl КАК БУЛЕВО) КАК АкселераторCtrl,
		|	ВЫРАЗИТЬ(ТаблицаКнопок.АкселераторShift КАК БУЛЕВО) КАК АкселераторShift,
		|	ТаблицаКнопок.НомерСтроки
		|ПОМЕСТИТЬ ТаблицаКнопок
		|ИЗ
		|	&ТаблицаКнопок КАК ТаблицаКнопок
		|ГДЕ
		|	ТаблицаКнопок.Клавиша <> &КлавишаПусто
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаКнопок.НомерСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаКнопок.АкселераторCtrl
		|			ТОГДА &КлавишаCtrl
		|		ИНАЧЕ &ПустаяСтрока
		|	КОНЕЦ + ВЫБОР
		|		КОГДА ТаблицаКнопок.АкселераторAlt
		|			ТОГДА &КлавишаAlt
		|		ИНАЧЕ &ПустаяСтрока
		|	КОНЕЦ + ВЫБОР
		|		КОГДА ТаблицаКнопок.АкселераторShift
		|			ТОГДА &КлавишаShift
		|		ИНАЧЕ &ПустаяСтрока
		|	КОНЕЦ + ТаблицаКнопок.Клавиша КАК Функциональные,
		|	ТаблицаКнопок.Клавиша,
		|	&СтрокаТабличнойЧасти КАК ТабличнаяЧасть,
		|	&ИмяТабличнаяЧасть КАК ИмяТабличнаяЧасть
		|ПОМЕСТИТЬ ТаблицаКнопокСвернутая
		|ИЗ
		|	ТаблицаКнопок КАК ТаблицаКнопок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСочетанийКлавишЗадействованныхВРМК.Акселератор КАК Акселератор
		|ПОМЕСТИТЬ ТаблицаСочетанийКлавишЗадействованныхВРМК
		|ИЗ
		|	&ТаблицаСочетанийКлавишЗадействованныхВРМК КАК ТаблицаСочетанийКлавишЗадействованныхВРМК
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаКнопокСвернутая.НомерСтроки,
		|	ТаблицаКнопокСвернутая.Функциональные,
		|	ТаблицаКнопокСвернутая.Клавиша,
		|	ТаблицаКнопокСвернутая.ТабличнаяЧасть,
		|	ТаблицаКнопокСвернутая.ИмяТабличнаяЧасть
		|ПОМЕСТИТЬ ТаблицаСБыстрымиТоварами
		|ИЗ
		|	ТаблицаКнопокСвернутая КАК ТаблицаКнопокСвернутая
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	-1,
		|	ТаблицаСочетанийКлавишЗадействованныхВРМК.Акселератор,
		|	&ПустаяСтрока,
		|	&СочетаниеКлавиш,
		|	&ПустаяСтрока
		|ИЗ
		|	ТаблицаСочетанийКлавишЗадействованныхВРМК КАК ТаблицаСочетанийКлавишЗадействованныхВРМК
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Функциональные КАК Функциональные,
		|	ТаблицаСБыстрымиТоварами.ТабличнаяЧасть КАК ТабличнаяЧасть,
		|	ТаблицаСБыстрымиТоварами.НомерСтроки КАК НомерСтроки,
		|	ТаблицаСБыстрымиТоварами.ИмяТабличнаяЧасть КАК ИмяТабличнаяЧасть
		|ИЗ
		|	(ВЫБРАТЬ
		|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаСБыстрымиТоварами.НомерСтроки) КАК КоличествоСтрок,
		|		ТаблицаСБыстрымиТоварами.Функциональные КАК Функциональные,
		|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаСБыстрымиТоварами.ТабличнаяЧасть) КАК КоличествоТаблиц
		|	ИЗ
		|		ТаблицаСБыстрымиТоварами КАК ТаблицаСБыстрымиТоварами
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ТаблицаСБыстрымиТоварами.Функциональные) КАК ВложенныйЗапрос
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСБыстрымиТоварами КАК ТаблицаСБыстрымиТоварами
		|		ПО ВложенныйЗапрос.Функциональные = ТаблицаСБыстрымиТоварами.Функциональные
		|ГДЕ
		|	(ВложенныйЗапрос.КоличествоСтрок > 1
		|			ИЛИ ВложенныйЗапрос.КоличествоТаблиц > 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИмяТабличнаяЧасть,
		|	НомерСтроки
		|ИТОГИ ПО
		|	Функциональные";
		
		Запрос.УстановитьПараметр("ТаблицаКнопок"                            , КнопкиНижнейПанели.Выгрузить());
		Запрос.УстановитьПараметр("КлавишаПусто"                             , НСтр("ru = 'Нет'"));
		Запрос.УстановитьПараметр("ТаблицаСочетанийКлавишЗадействованныхВРМК", ТаблицаСочетанийКлавишЗадействованныхВРМК);
		Запрос.УстановитьПараметр("ПустаяСтрока"                             , "");
		Запрос.УстановитьПараметр("КлавишаCtrl"                              , НСтр("ru = 'Ctrl+'") );
		Запрос.УстановитьПараметр("КлавишаAlt"                               , НСтр("ru = 'Alt+'") );
		Запрос.УстановитьПараметр("КлавишаShift"                             , НСтр("ru = 'Shift+'"));
		Запрос.УстановитьПараметр("СтрокаТабличнойЧасти"                     , НСтр("ru = 'Таблица кнопок нижней панели'") );
		Запрос.УстановитьПараметр("ИмяТабличнаяЧасть"                        , НСтр("ru = 'КнопкиНижнейПанели'"));
		Запрос.УстановитьПараметр("СочетаниеКлавиш"                          , НСтр("ru = 'Сочетание клавиш, используемое в РМК'"));
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока Выборка.Следующий() Цикл
			
			ОписаниеОшибкиПовтора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Одинаковые сочетания клавиш ""%1""'"),
				Выборка.Функциональные
			);
			
			ВыборкаСтрок = Выборка.Выбрать();
			
			ПолеФормы = "";
			
			Пока ВыборкаСтрок.Следующий() Цикл
				Если ВыборкаСтрок.НомерСтроки = - 1 Тогда
					ОписаниеОшибкиПовтора = ОписаниеОшибкиПовтора + Символы.ПС + Символы.Таб
										  + ВыборкаСтрок.ТабличнаяЧасть;
				Иначе
					ОписаниеОшибкиПовтора = ОписаниеОшибкиПовтора + Символы.ПС + Символы.Таб
										  + ВыборкаСтрок.ТабличнаяЧасть + " " + НСтр("ru = 'в строке №'") + " " + ВыборкаСтрок.НомерСтроки;
					ПолеФормы = ВыборкаСтрок.ИмяТабличнаяЧасть + "[" + (ВыборкаСтрок.НомерСтроки - 1) +"]";
				КонецЕсли;
			КонецЦикла;
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ОписаниеОшибкиПовтора,
				ЭтотОбъект,
				ПолеФормы ,
				,
				Отказ
			);
			
		КонецЦикла;
		
		Если Не Отказ Тогда
			Если (КнопкиНижнейПанели.Найти("КнопкаОтложитьЧек", "ИмяКнопки") = Неопределено)
				И (КнопкиНижнейПанели.Найти("КнопкаВвестиАннулирование", "ИмяКнопки") = Неопределено) Тогда
				
				ТекстСообщения = НСтр("ru = 'Среди кнопок нижней панели нет кнопок прекращения набора чека ""Отложить"" и ""Аннулировать""'") + Символы.ПС
							   + НСтр("ru = 'Пользователь не сможет прервать набор чека.'");
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,
					ЭтотОбъект,
					"КнопкиНижнейПанели",
					,
					Ложь
				);
				
			КонецЕсли;
		КонецЕсли;
		
		ПроверяемыеРеквизиты.Добавить("МаксимальноеКоличествоКнопокВОдномРяду");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет строку в таблицу задействованных кнопок.
//
// Параметры:
//  Акселератор - строковое описание акселератора.
//
Процедура ДобавитьСтрокуВТаблицуЗадействованныхКнопок(Акселератор)
	
	СтрокаТаблицы = ТаблицаСочетанийКлавишЗадействованныхВРМК.Добавить();
	СтрокаТаблицы.Акселератор = Акселератор;
	
КонецПроцедуры

// Заполняет таблицу задействованных кнопок.
//
// Параметры:
//  Нет
//
Процедура ЗаполнитьТаблицуЗадействованныхКнопок()

	ТаблицаСочетанийКлавишЗадействованныхВРМК = Новый ТаблицаЗначений;
	ТаблицаСочетанийКлавишЗадействованныхВРМК.Колонки.Добавить("Акселератор", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(13)));
	
	// Клавиши управления чеком
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("Alt+F10");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("Alt+NumDivide");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("NumDivide");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("NumMultiply");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("NumAdd");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("NumSubtract");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("NumDecimal");
	
	
	// клавиши правой панели
	Для Счетчик = 0 По 9 Цикл
		ДобавитьСтрокуВТаблицуЗадействованныхКнопок("Num"+Счетчик);
	КонецЦикла;
	
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("K");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("P");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("D");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("F");
	ДобавитьСтрокуВТаблицуЗадействованныхКнопок("C");
	
	
КонецПроцедуры

#КонецОбласти

#Область ОсновнойБлок

ЗаполнитьТаблицуЗадействованныхКнопок();

#КонецОбласти

#КонецЕсли
