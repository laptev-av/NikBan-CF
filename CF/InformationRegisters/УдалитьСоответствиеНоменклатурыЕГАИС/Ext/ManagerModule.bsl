#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбновлениеИнформационнойБазы

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МАКСИМУМ(УдалитьСоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция) КАК АлкогольнаяПродукция,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	РегистрСведений.УдалитьСоответствиеНоменклатурыЕГАИС КАК УдалитьСоответствиеНоменклатурыЕГАИС
	|
	|СГРУППИРОВАТЬ ПО
	|	УдалитьСоответствиеНоменклатурыЕГАИС.Номенклатура,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УдалитьСоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика,
	|	1 КАК Порядок
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьСоответствиеНоменклатурыЕГАИС КАК УдалитьСоответствиеНоменклатурыЕГАИС
	|		ПО ТаблицаНоменклатуры.АлкогольнаяПродукция = УдалитьСоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|			И ТаблицаНоменклатуры.Номенклатура = УдалитьСоответствиеНоменклатурыЕГАИС.Номенклатура
	|			И ТаблицаНоменклатуры.Характеристика = УдалитьСоответствиеНоменклатурыЕГАИС.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО ТаблицаНоменклатуры.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
	|			И ТаблицаНоменклатуры.Характеристика = СоответствиеНоменклатурыЕГАИС.Характеристика
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура ЕСТЬ NULL");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УдалитьСоответствиеНоменклатурыЕГАИС");
			ЭлементБлокировки.УстановитьЗначение("АлкогольнаяПродукция", Выборка.АлкогольнаяПродукция);
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторУпаковки", Выборка.ИдентификаторУпаковки);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика);
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать запись регистра ""УдалитьСоответствиеНоменклатурыЕГАИС"" по алкогольной продукции %АлкогольнаяПродукция%
			                            |по причине %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%АлкогольнаяПродукция%", Выборка.АлкогольнаяПродукция);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			                         УровеньЖурналаРегистрации.Предупреждение,
			                         Метаданные.РегистрыСведений.УдалитьСоответствиеНоменклатурыЕГАИС,
			                         Выборка.АлкогольнаяПродукция,
			                         ТекстСообщения);
			
		КонецПопытки
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаВерсию2_2_8(Параметры) Экспорт
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика,
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.УдалитьАлкогольнаяПродукция КАК УдалитьАлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки
	|ПОМЕСТИТЬ ПеренесенныеРаннеДанные
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УдалитьСоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	УдалитьСоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика
	|ИЗ
	|	РегистрСведений.УдалитьСоответствиеНоменклатурыЕГАИС КАК УдалитьСоответствиеНоменклатурыЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПеренесенныеРаннеДанные КАК ПеренесенныеРаннеДанные
	|		ПО ПеренесенныеРаннеДанные.Номенклатура = УдалитьСоответствиеНоменклатурыЕГАИС.Номенклатура
	|			И ПеренесенныеРаннеДанные.Характеристика = УдалитьСоответствиеНоменклатурыЕГАИС.Характеристика
	|			И (ПеренесенныеРаннеДанные.АлкогольнаяПродукция = УдалитьСоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|				ИЛИ ПеренесенныеРаннеДанные.УдалитьАлкогольнаяПродукция = УдалитьСоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция)
	|			И ПеренесенныеРаннеДанные.ИдентификаторУпаковки = УдалитьСоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки
	|ГДЕ
	|	ПеренесенныеРаннеДанные.Номенклатура ЕСТЬ NULL");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УдалитьСоответствиеНоменклатурыЕГАИС");
			ЭлементБлокировки.УстановитьЗначение("АлкогольнаяПродукция", Выборка.АлкогольнаяПродукция);
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторУпаковки", Выборка.ИдентификаторУпаковки);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
			НаборЗаписей.Отбор.Характеристика.Установить(Выборка.Характеристика);
			НаборЗаписей.Отбор.АлкогольнаяПродукция.Установить(Выборка.АлкогольнаяПродукция);
			НаборЗаписей.Отбор.ИдентификаторУпаковки.Установить(Выборка.ИдентификаторУпаковки);
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать запись регистра ""УдалитьСоответствиеНоменклатурыЕГАИС"" по алкогольной продукции %АлкогольнаяПродукция%
			                            |по причине %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%АлкогольнаяПродукция%", Выборка.АлкогольнаяПродукция);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			                         УровеньЖурналаРегистрации.Предупреждение,
			                         Метаданные.РегистрыСведений.УдалитьСоответствиеНоменклатурыЕГАИС,
			                         Выборка.АлкогольнаяПродукция,
			                         ТекстСообщения);
			
		КонецПопытки
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли