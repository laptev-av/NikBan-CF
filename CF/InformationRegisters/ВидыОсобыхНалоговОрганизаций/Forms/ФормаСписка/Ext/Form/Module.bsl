#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") Тогда
		
		Если Параметры.Отбор.Свойство("Склад") Тогда
			
			Склад = Параметры.Отбор.Склад;
			Если Склад = Неопределено
				ИЛИ Склад.ТипСклада <> Перечисления.ТипыСкладов.ТорговыйЗал Тогда
				
				ТекстЗаголовка = НСтр("ru = 'Для элемента: ""%Склад%"" ЕНВД не применяется'");
				Элементы.Список.ТолькоПросмотр = Истина;
				
			Иначе
				ТекстЗаголовка = НСтр("ru = 'Применение ЕНВД (%Склад%)'");
				ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
					Список,
					"Организация",
					Склад.Организация,
					ЗначениеЗаполнено(Склад.Организация));
			КонецЕсли;
				
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Склад%", Строка(Склад));
			АвтоЗаголовок  = Ложь;
			Заголовок      = ТекстЗаголовка;
			
		ИначеЕсли Параметры.Отбор.Свойство("Организация") Тогда
			Если ТипЗнч(Параметры.Отбор.Организация) = Тип("СправочникСсылка.Организации") Тогда
				ТекстЗаголовка = "";
				АвтоЗаголовок  = Ложь;
				ТекстЗаголовка = НСтр("ru = 'Применение ЕНВД (%Организация%)'");
				ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%Организация%", Строка(Параметры.Отбор.Организация));
				Заголовок      = ТекстЗаголовка;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") 
		ИЛИ Параметры.Отбор.Свойство("Организация") Тогда
		Элементы.Организация.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "РегистрСведений.ВидыОсобыхНалоговОрганизаций.Форма.ФормаСписка.Открытие");

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "РегистрСведений.ВидыОсобыхНалоговОрганизаций.Форма.ФормаЗаписи.Открытие");

КонецПроцедуры

#КонецОбласти