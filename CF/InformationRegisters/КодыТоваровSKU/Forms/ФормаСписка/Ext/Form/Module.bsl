#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДоступностьРаботыСКодамиТоваровSKU = ПодключаемоеОборудованиеOfflineВызовСервера.ДоступностьРаботыСКодамиТоваровSKU();
	
	Если Параметры.Свойство("Номенклатура") Тогда 
		Номенклатура = Параметры.Номенклатура;
		ОтборыСписковКлиентСервер.УстановитьЭлементОтбораСписка(Список, "Номенклатура", Параметры.Номенклатура);
		
		Элементы.Список.ТолькоПросмотр = НЕ ДоступностьРаботыСКодамиТоваровSKU;
	Иначе
		
		Элементы.ФормаСоздатьКодыSKU.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "РегистрСведений.КодыТоваровSKU.Форма.ФормаСписка.Открытие");

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    // &ЗамерПроизводительности
    ОценкаПроизводительностиРТКлиент.НачатьЗамер(
             Истина, "РегистрСведений.КодыТоваровSKU.Форма.ФормаЗаписи.Открытие");
             
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьКодыSKU(Команда)
	
	Если ДоступностьРаботыСКодамиТоваровSKU Тогда
		
		ЗаполнитьАвтоматическиКодыSKU();
		Элементы.Список.Обновить();
		
	Иначе
		
		ПоказатьПредупреждение(,НСтр("ru = 'Создание и удаление SKU осуществляется в главном узле РИБ'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьАвтоматическиКодыSKU()
	
	ЗакончилисьSKU = Ложь;
	РегистрыСведений.КодыТоваровSKU.СоздатьSKU(Номенклатура, ЗакончилисьSKU);
	
	Если ЗакончилисьSKU Тогда
		
		ТекстСообщения = НСтр("ru = 'В процессе заполнения закончились доступные SKU коды товара. Операция завершена не полностью.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
