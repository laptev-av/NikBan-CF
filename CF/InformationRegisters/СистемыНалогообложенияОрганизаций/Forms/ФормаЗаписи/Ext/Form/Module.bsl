
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияРТ.ПроверитьИспользованиеОрганизации(Отказ,,Запись.Организация);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		Элементы.Организация.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьИЗначениеОбъектаНалогообложения();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	
	УстановитьДоступностьИЗначениеОбъектаНалогообложения();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ИспользуетсяУСН = Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная");
	
	Если ИспользуетсяУСН и 
		Запись.ОбъектНалогообложения.Пустая() Тогда
		
		ТекстСообщения = НСтр("ru = 'Объект налогообложения по УСН должен быть заполнен'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,
			"ОбъектНалогообложения", "Запись", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьИЗначениеОбъектаНалогообложения()
	
	ИспользуетсяУСН = Запись.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная");
	
	Элементы.ОбъектНалогообложения.Доступность 		= ИспользуетсяУСН;
	Элементы.НеЯвляетсяПлательщикомНДС.Доступность 	= НЕ ИспользуетсяУСН;
	
	Если не ИспользуетсяУСН и не Запись.ОбъектНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ПустаяСсылка") Тогда
		Запись.ОбъектНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ПустаяСсылка");
	КонецЕсли;
	
	Если ИспользуетсяУСН Тогда
		Запись.НеЯвляетсяПлательщикомНДС = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектНалогообложения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.СистемаНалогообложения.ПутьКДанным);
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СистемыНалогообложения.Упрощенная;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.ОбъектНалогообложения.ПутьКДанным);
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
КонецПроцедуры

#КонецОбласти
