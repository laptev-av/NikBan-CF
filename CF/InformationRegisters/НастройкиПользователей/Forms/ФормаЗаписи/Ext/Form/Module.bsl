
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Ключ")
		И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		Если ЗначениеЗаполнено(Параметры.Ключ.Магазин)
			И ЗначениеЗаполнено(Параметры.Ключ.Настройка)
			И ЗначениеЗаполнено(Параметры.Ключ.Пользователь) Тогда
			
			УстановитьПараметрыВыбора(Параметры.Ключ.Магазин, Параметры.Ключ.Настройка, Параметры.Ключ.Пользователь);
			
		Иначе
			
			Элементы.Значение.Доступность = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
			
			Если Параметры.ЗначенияЗаполнения.Свойство("Настройка")
				И Параметры.ЗначенияЗаполнения.Свойство("Магазин")
				И Параметры.ЗначенияЗаполнения.Свойство("Пользователь")
				И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Магазин) 
				И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Настройка)
				И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Пользователь) Тогда
				
				УстановитьПараметрыВыбора(Параметры.ЗначенияЗаполнения.Магазин, Параметры.ЗначенияЗаполнения.Настройка, Параметры.ЗначенияЗаполнения.Пользователь);
				
			Иначе
				
				Элементы.Значение.Доступность = Ложь;
				
			КонецЕсли;
			
		Иначе
			
			Элементы.Значение.Доступность = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойСкладПоступления 
		ИЛИ ТекущийОбъект.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойСкладПродажи
		ИЛИ ТекущийОбъект.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяКасса
		ИЛИ ТекущийОбъект.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяКассаККМ Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущийОбъект.Значение, "Магазин");
		
		Если ТекущийОбъект.Магазин <> ЗначенияРеквизитов.Магазин Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Магазин указанного значения настройки не совпадает с магазином настройки'"),
				,
				"Запись.Значение",
				,
				Отказ);
			
		КонецЕсли;
		
		Если ТекущийОбъект.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяКасса
			ИЛИ ТекущийОбъект.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяКассаККМ Тогда
			
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НастройкиПользователей.Значение КАК Организация
			|ИЗ
			|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
			|ГДЕ
			|	НастройкиПользователей.Пользователь = &Пользователь
			|	И НастройкиПользователей.Магазин = &Магазин
			|	И НастройкиПользователей.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация)");
			Запрос.УстановитьПараметр("Пользователь", ТекущийОбъект.Пользователь);
			Запрос.УстановитьПараметр("Магазин", ТекущийОбъект.Магазин);
			Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Выборка.Следующий();
				ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущийОбъект.Значение, "Владелец");
				Если ЗначенияРеквизитов.Владелец <> Выборка.Организация Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						НСтр("ru = 'Организация указанного значения настройки не совпадает с организацией по умолчанию'"),
						,
						"Запись.Значение",
						,
						Отказ);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТекущийОбъект.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НастройкиПользователей.Значение КАК Касса,
		|	Кассы.Владелец КАК Организация
		|ИЗ
		|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Кассы КАК Кассы
		|		ПО НастройкиПользователей.Значение = Кассы.Ссылка
		|ГДЕ
		|	НастройкиПользователей.Пользователь = &Пользователь
		|	И НастройкиПользователей.Магазин = &Магазин
		|	И НастройкиПользователей.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОсновнаяКасса)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиПользователей1.Значение,
		|	Кассы.Владелец
		|ИЗ
		|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей1
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КассыККМ КАК Кассы
		|		ПО НастройкиПользователей1.Значение = Кассы.Ссылка
		|ГДЕ
		|	НастройкиПользователей1.Пользователь = &Пользователь
		|	И НастройкиПользователей1.Магазин = &Магазин
		|	И НастройкиПользователей1.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОсновнаяКассаККМ)");
		Запрос.УстановитьПараметр("Пользователь", ТекущийОбъект.Пользователь);
		Запрос.УстановитьПараметр("Магазин", ТекущийОбъект.Магазин);
		Результат = Запрос.Выполнить();
			Если НЕ Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Пока Выборка.Следующий() Цикл
					Если ТекущийОбъект.Значение <> Выборка.Организация Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
							НСтр("ru = 'Организация, указанная в значении настройки не совпадает с организацией заданной по умолчанию кассы'"),
							,
							"Запись.Значение",
							,
							Отказ);
					КонецЕсли;
					Прервать;
				КонецЦикла;
			КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МагазинПриИзменении(Элемент)
	УстановитьПараметрыВыбора(Запись.Магазин, Запись.Настройка, Запись.Пользователь);
КонецПроцедуры

&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)
	УстановитьПараметрыВыбора(Запись.Магазин, Запись.Настройка, Запись.Пользователь);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПриИзменении(Элемент)
	УстановитьПараметрыВыбора(Запись.Магазин, Запись.Настройка, Запись.Пользователь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает параметры выбора для элемента "Значение".
//
&НаСервере
Процедура УстановитьПараметрыВыбора(Магазин, Настройка, Пользователь)
	
	Если ЗначениеЗаполнено(Настройка) И ЗначениеЗаполнено(Магазин) И ЗначениеЗаполнено(Пользователь) Тогда
		
		Если Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойСкладПоступления Тогда
			
			Параметр = Новый ПараметрВыбора("Отбор.Магазин", Магазин);
			МассивВыбора = Новый Массив;
			МассивВыбора.Добавить(Параметр);
			ПараметрыВыбора = Новый ФиксированныйМассив(МассивВыбора);
			Элементы.Значение.ПараметрыВыбора = ПараметрыВыбора;
			
		ИначеЕсли Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойСкладПродажи Тогда
			
			МассивВыбора = Новый Массив;
			Параметр = Новый ПараметрВыбора("Отбор.Магазин", Магазин);
			МассивВыбора.Добавить(Параметр);
			Параметр = Новый ПараметрВыбора("Отбор.ТипСклада", Перечисления.ТипыСкладов.ТорговыйЗал);
			МассивВыбора.Добавить(Параметр);
			ПараметрыВыбора = Новый ФиксированныйМассив(МассивВыбора);
			Элементы.Значение.ПараметрыВыбора = ПараметрыВыбора;
			
		ИначеЕсли Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяКасса
			ИЛИ Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяКассаККМ Тогда
			
			МассивВыбора = Новый Массив;
			Параметр = Новый ПараметрВыбора("Отбор.Магазин", Магазин);
			МассивВыбора.Добавить(Параметр);
			
			Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	НастройкиПользователей.Значение КАК Организация
			|ИЗ
			|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
			|ГДЕ
			|	НастройкиПользователей.Пользователь = &Пользователь
			|	И НастройкиПользователей.Магазин = &Магазин
			|	И НастройкиПользователей.Настройка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НастройкиПользователей.ОсновнаяОрганизация)");
			Запрос.УстановитьПараметр("Пользователь", Пользователь);
			Запрос.УстановитьПараметр("Магазин", Магазин);
			Результат = Запрос.Выполнить();
			
			Если НЕ Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Выборка.Следующий();
				Параметр = Новый ПараметрВыбора("Отбор.Владелец", Выборка.Организация);
				МассивВыбора.Добавить(Параметр);
			КонецЕсли;
			
			ПараметрыВыбора = Новый ФиксированныйМассив(МассивВыбора);
			Элементы.Значение.ПараметрыВыбора = ПараметрыВыбора;
			
		КонецЕсли;
		
		Элементы.Значение.Доступность = Истина;
		
	Иначе
		
		Элементы.Значение.Доступность = Ложь;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти