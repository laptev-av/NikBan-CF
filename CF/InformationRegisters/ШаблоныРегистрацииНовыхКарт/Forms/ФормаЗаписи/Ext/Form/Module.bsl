
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПересечения(Команда)
	
	ПроверитьПересеченияСервер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьПересеченияСервер()
	
	ТекстСообщения = НСтр("ru = 'Пересечения не обнаружены'"); 
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ВидКарты,
	|	ВложенныйЗапрос.ДлинаКода,
	|	ВложенныйЗапрос.НачалоДиапазона,
	|	ВложенныйЗапрос.КонецДиапазона,
	|	ВложенныйЗапрос.ПроводитьОпросВладельцаПриРегистрации,
	|	ВложенныйЗапрос.ВидДисконтнойКарты,
	|	ВложенныйЗапрос.БонуснаяПрограммаЛояльности,
	|	ВложенныйЗапрос.ГруппаКарты,
	|	ВложенныйЗапрос.НаименованиеШаблона
	|ИЗ
	|	(ВЫБРАТЬ
	|		ШаблоныРегистрацииНовыхКарт.ВидКарты КАК ВидКарты,
	|		ШаблоныРегистрацииНовыхКарт.ДлинаКода КАК ДлинаКода,
	|		ШаблоныРегистрацииНовыхКарт.НачалоДиапазона КАК НачалоДиапазона,
	|		ШаблоныРегистрацииНовыхКарт.КонецДиапазона КАК КонецДиапазона,
	|		ШаблоныРегистрацииНовыхКарт.ПроводитьОпросВладельцаПриРегистрации КАК ПроводитьОпросВладельцаПриРегистрации,
	|		ШаблоныРегистрацииНовыхКарт.ВидДисконтнойКарты КАК ВидДисконтнойКарты,
	|		ШаблоныРегистрацииНовыхКарт.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		ШаблоныРегистрацииНовыхКарт.ГруппаКарты КАК ГруппаКарты,
	|		ВЫБОР
	|			КОГДА ШаблоныРегистрацииНовыхКарт.НачалоДиапазона < &НачалоДиапазона
	|					И ШаблоныРегистрацииНовыхКарт.КонецДиапазона < &НачалоДиапазона
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ШаблоныРегистрацииНовыхКарт.НачалоДиапазона > &КонецДиапазона
	|							И (ШаблоныРегистрацииНовыхКарт.КонецДиапазона > &КонецДиапазона
	|								И &КонецДиапазона <> &ПустойДиапазон)
	|						ТОГДА ЛОЖЬ
	|				КОГДА &КонецДиапазона <> &ПустойДиапазон
	|						И ШаблоныРегистрацииНовыхКарт.КонецДиапазона = &ПустойДиапазон
	|						И ШаблоныРегистрацииНовыхКарт.НачалоДиапазона > &КонецДиапазона
	|					ТОГДА ЛОЖЬ
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|		КОНЕЦ КАК Пересечение,
	|		ШаблоныРегистрацииНовыхКарт.НаименованиеШаблона КАК НаименованиеШаблона
	|	ИЗ
	|		РегистрСведений.ШаблоныРегистрацииНовыхКарт КАК ШаблоныРегистрацииНовыхКарт
	|	ГДЕ
	|		ШаблоныРегистрацииНовыхКарт.ДлинаКода = &ДлинаКода
	|		И (НЕ(ШаблоныРегистрацииНовыхКарт.НачалоДиапазона = &НачалоДиапазона
	|					И ШаблоныРегистрацииНовыхКарт.КонецДиапазона = &КонецДиапазона))
	|		И ШаблоныРегистрацииНовыхКарт.ВидКарты = &ВидКарты) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.Пересечение";
	
	Запрос.УстановитьПараметр("ДлинаКода"      , Запись.ДлинаКода);
	Запрос.УстановитьПараметр("НачалоДиапазона", Запись.НачалоДиапазона);
	Запрос.УстановитьПараметр("КонецДиапазона" , Запись.КонецДиапазона);
	Запрос.УстановитьПараметр("ВидКарты"       , Запись.ВидКарты);
	Запрос.УстановитьПараметр("ПустойДиапазон" , "");
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		
		Если Выборка.Количество() > 1 Тогда
			ТекстСообщения = НСтр("ru = 'Диапазон текущей записи пересекается с диапазонами:'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Диапазон текущей записи пересекается с диапазоном:'");
		КонецЕсли;
		Пока Выборка.Следующий() Цикл
			ТекстШаблона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '- %1, Начало диапазона - %2, Конец диапазона - %3'"),
				Выборка.НаименованиеШаблона,
				Выборка.НачалоДиапазона,
				Выборка.КонецДиапазона);
			
			ТекстСообщения = ТекстСообщения + Символы.ПС + ТекстШаблона;
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			Запись,
			"НачалоДиапазона");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроводитьОпросВладельцаПриРегистрацииПриИзменении(Элемент)
	УстановитьДоступностьВладельца();
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьВладельца()
	Элементы.ГруппаВладельцаКарты.Доступность = Запись.ПроводитьОпросВладельцаПриРегистрации;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьДоступностьВладельца();
КонецПроцедуры

#КонецОбласти