#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет на соответствие введенным шаблонам.
//
// Параметры:
//  КодКарты - числовой или строковый код карты.
//  ВидКарты - Перечисление ВидыИнформационныхКарт 
//  МассивШаблонов - массив возвращаемых шаблонов.
//
// Возвращаемое значение:
//  Булево
//
Функция РезультатПроверкиСоответствияШаблонам(КодКарты, ВидКарты, ТипШтрихкодаРТ, МассивШаблонов) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ШаблоныКарт.ВидКарты,
	|	ШаблоныКарт.ПроводитьОпросВладельцаПриРегистрации,
	|	ШаблоныКарт.ДлинаКода,
	|	ШаблоныКарт.НачалоДиапазона КАК НачалоДиапазона,
	|	ШаблоныКарт.КонецДиапазона,
	|	ШаблоныКарт.ВидДисконтнойКарты,
	|	ШаблоныКарт.БонуснаяПрограммаЛояльности,
	|	ШаблоныКарт.ГруппаКарты,
	|	ШаблоныКарт.ГруппаВладельцаКарты,
	|	ШаблоныКарт.НаименованиеШаблона
	|ИЗ
	|	РегистрСведений.ШаблоныРегистрацииНовыхКарт КАК ШаблоныКарт
	|ГДЕ
	|	(ШаблоныКарт.ВидКарты = &ВидКарты
	|		ИЛИ ШаблоныКарт.ВидКарты = &СмешанныйВидКарты)
	|	И ШаблоныКарт.НачалоДиапазона <= &КодКарты
	|	И ШаблоныКарт.КонецДиапазона >= &КодКарты
	|	И ШаблоныКарт.ДлинаКода = &ДлинаКода
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачалоДиапазона");
	
	Запрос.УстановитьПараметр("ВидКарты", ВидКарты);
	Запрос.УстановитьПараметр("СмешанныйВидКарты", Перечисления.ВидыИнформационныхКарт.Смешанная);
	Запрос.УстановитьПараметр("КодКарты", Строка(КодКарты));
	Запрос.УстановитьПараметр("ДлинаКода", СтрДлина(Строка(КодКарты)));
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Возврат Ложь;
		
	Иначе	
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтруктураПараметров = ОбщегоНазначенияРТ.СоздатьСтруктуруПоСтрокеВыборки(Результат, Выборка);
			СтруктураПараметров.Вставить("КодКарты", КодКарты);
			СтруктураПараметров.Вставить("ТипШтрихкода", ТипШтрихкодаРТ);
			МассивШаблонов.Добавить(СтруктураПараметров);
		КонецЦикла;
		Возврат Истина;
		
	КонецЕсли;
	
КонецФункции 

#КонецОбласти

#КонецЕсли

