#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПередЗаписью".
//
Процедура ПередЗаписью(Отказ)
	
	Если НЕ ИспользоватьОтборПоОрганизациям Тогда
		Организации.Очистить();
	КонецЕсли;
	Если НЕ ИспользоватьОтборПоСкладам Тогда
		Склады.Очистить();
	КонецЕсли;
	РежимВыгрузкиПриНеобходимости = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;

	Если ЭтоНовый() Тогда
		Константы.ИспользуетсяОбменСУправлениемТорговлей.Установить(Истина);
	Иначе
		Если ПометкаУдаления Тогда
			Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			|	УзлыОбмена.Ссылка
			|ИЗ
			|	ПланОбмена.ОбменРозницаУправлениеТорговлей103 КАК УзлыОбмена
			|ГДЕ
			|	УзлыОбмена.Ссылка <> &ЭтотУзел
			|	И УзлыОбмена.Ссылка <> &Ссылка
			|	И (НЕ УзлыОбмена.ПометкаУдаления)");
			Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменРозницаУправлениеТорговлей103.ЭтотУзел());
			Запрос.УстановитьПараметр("Ссылка", Ссылка);
			РезультатЗапроса = Запрос.Выполнить();
			Если РезультатЗапроса.Пустой() Тогда
				Константы.ИспользуетсяОбменСУправлениемТорговлей.Установить(Ложь);
			КонецЕсли;
		Иначе
			Если НЕ Константы.ИспользуетсяОбменСУправлениемТорговлей.Получить() Тогда
				Константы.ИспользуетсяОбменСУправлениемТорговлей.Установить(Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьОбъект(ДанныеЗаполнения);
	
КонецПроцедуры

// Процедура - обработчик события "ПередУдалением".
//
Процедура ПередУдалением(Отказ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	УзлыОбмена.Ссылка
	|ИЗ
	|	ПланОбмена.ОбменРозницаУправлениеТорговлей103 КАК УзлыОбмена
	|ГДЕ
	|	УзлыОбмена.Ссылка <> &ЭтотУзел
	|	И УзлыОбмена.Ссылка <> &Ссылка
	|	И (НЕ УзлыОбмена.ПометкаУдаления)");
	Запрос.УстановитьПараметр("ЭтотУзел", ПланыОбмена.ОбменРозницаУправлениеТорговлей103.ЭтотУзел());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Константы.ИспользуетсяОбменСУправлениемТорговлей.Установить(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОбъект(ДанныеЗаполнения)
	
	Если Не ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДатаНачалаВыгрузкиДокументов	= НачалоГода(ТекущаяДатаСеанса());
	РежимВыгрузкиПриНеобходимости 	= Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПриНеобходимости;

КонецПроцедуры

#КонецОбласти

#КонецЕсли

