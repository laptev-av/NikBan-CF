#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СуффиксыИменЭлементов = Новый Массив;
	
	СуффиксыИменЭлементов.Добавить("Товар");
	СуффиксыИменЭлементов.Добавить("Услуга");
	СуффиксыИменЭлементов.Добавить("ПодарочныйСертификат");
	
	Для Каждого Суффикс из СуффиксыИменЭлементов Цикл
		Элементы["ТипНоменклатуры" + Суффикс].Подсказка = Перечисления.ТипыНоменклатуры.ПодсказкаПоТипуНоменклатуры(
															ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры." + Суффикс));
	КонецЦикла;
		
	ТипНоменклатуры = Параметры.ТипНоменклатуры;
	ПризнакПредметаРасчета = Параметры.ПризнакПредметаРасчета;
	
	ВестиУчетМаркировкиПродукцииВГИСМ = ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиПродукцииВГИСМ");
	ВестиУчетПодконтрольныхТоваровВЕТИС = ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС");
	ВестиСведенияДляДекларацийПоАлкогольнойПродукции = ПолучитьФункциональнуюОпцию("ВестиСведенияДляДекларацийПоАлкогольнойПродукции");
	ВестиУчетМаркировкиТабачнойПродукции = ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиТабачнойПродукции");
	ВестиУчетМаркировкиОбувнойПродукции = ПолучитьФункциональнуюОпцию("ВестиУчетМаркировкиОбувнойПродукции");
	
	Элементы.ОсобенностьУчетаБезОсобенностейУчета.Видимость = ВестиУчетМаркировкиПродукцииВГИСМ
								ИЛИ ВестиУчетПодконтрольныхТоваровВЕТИС
								ИЛИ ВестиСведенияДляДекларацийПоАлкогольнойПродукции
								ИЛИ ВестиУчетМаркировкиТабачнойПродукции
								ИЛИ ВестиУчетМаркировкиОбувнойПродукции;
	
	Элементы.ОсобенностьУчетаАлкогольнаяПродукция.Видимость         = ВестиСведенияДляДекларацийПоАлкогольнойПродукции;
	Элементы.ПродаетсяВРозлив.Видимость                             = ВестиСведенияДляДекларацийПоАлкогольнойПродукции;
	Элементы.ДекорацияОтступАлкоголь.Видимость                      = ВестиСведенияДляДекларацийПоАлкогольнойПродукции;
	Элементы.ОсобенностьУчетаПродукцияМаркируемаяДляГИСМ.Видимость  = ВестиУчетМаркировкиПродукцииВГИСМ;
	Элементы.ОсобенностьУчетаКиЗГИСМ.Видимость                      = ВестиУчетМаркировкиПродукцииВГИСМ;
	Элементы.ОсобенностьУчетаПодконтрольнаяПродукцияВЕТИС.Видимость = ВестиУчетПодконтрольныхТоваровВЕТИС;
	Элементы.ОсобенностьУчетаТабачнаяПродукция.Видимость            = ВестиУчетМаркировкиТабачнойПродукции;
	Элементы.ОсобенностьУчетаОбувнаяПродукция.Видимость            	= ВестиУчетМаркировкиОбувнойПродукции;
	
	СуффиксыИменЭлементов = Новый Массив;
	
	СуффиксыИменЭлементов.Добавить("АлкогольнаяПродукция");
	СуффиксыИменЭлементов.Добавить("БезОсобенностейУчета");
	СуффиксыИменЭлементов.Добавить("КиЗГИСМ");
	СуффиксыИменЭлементов.Добавить("ПодконтрольнаяПродукцияВЕТИС");
	СуффиксыИменЭлементов.Добавить("ПродукцияМаркируемаяДляГИСМ");
	СуффиксыИменЭлементов.Добавить("ТабачнаяПродукция");
	СуффиксыИменЭлементов.Добавить("ОбувнаяПродукция");
	
	Для Каждого Суффикс из СуффиксыИменЭлементов Цикл
		Элементы["ОсобенностьУчета" + Суффикс].Подсказка = Перечисления.ОсобенностиУчетаНоменклатуры.ПодсказкаПоОсобенностиУчетаНоменклатуры(
															Перечисления.ОсобенностиУчетаНоменклатуры[Суффикс]);
	КонецЦикла;

	Если ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
		ОсобенностьУчета = Параметры.ОсобенностьУчета;
		ПродаетсяВРозлив = Параметры.ПродаетсяВРозлив;
		Элементы.ГруппаОсобенностиЗначения.Доступность = Истина;
		Элементы.ПродаетсяВРозлив.Доступность = ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция;
	Иначе
		ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ПустаяСсылка();
		ПродаетсяВРозлив = Ложь;
		Элементы.ГруппаОсобенностиЗначения.Доступность = Ложь;
		Элементы.ПродаетсяВРозлив.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Результат = Новый Структура("ТипНоменклатуры, ОсобенностьУчета, ПродаетсяВРозлив, ПризнакПредметаРасчета");
	Результат.ТипНоменклатуры = ТипНоменклатуры;
	Результат.ПризнакПредметаРасчета = ПризнакПредметаРасчета;
	
	Если ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		Результат.ОсобенностьУчета = ОсобенностьУчета;
	Иначе
		Результат.ОсобенностьУчета = ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета");
	КонецЕсли;
	
	Если Результат.ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция") Тогда
		Результат.ПродаетсяВРозлив = ПродаетсяВРозлив;
	Иначе
		Результат.ПродаетсяВРозлив = Ложь;
	КонецЕсли;
	
	Закрыть(Результат);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипНоменклатурыПриИзменении(Элемент)
	Если ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.БезОсобенностейУчета");
		Элементы.ГруппаОсобенностиЗначения.Доступность = Истина;
	Иначе
		ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ПустаяСсылка");
		ПродаетсяВРозлив = Ложь;
		Элементы.ГруппаОсобенностиЗначения.Доступность = Ложь;
	КонецЕсли;
	Элементы.ПродаетсяВРозлив.Доступность = Ложь;
	
	РасчитатьПризнакПредметаРасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОсобенностьУчетаПриИзменении(Элемент)
	
	ПродаетсяВРозлив = Ложь;
	Если ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция") Тогда
		Элементы.ПродаетсяВРозлив.Доступность = Истина;
	Иначе
		Элементы.ПродаетсяВРозлив.Доступность = Ложь ;
	КонецЕсли; 
	
	ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар");
	
	РасчитатьПризнакПредметаРасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура РасчитатьПризнакПредметаРасчета()
	
	Если ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар") Тогда
		
		Если ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция")
			ИЛИ ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция")
			ИЛИ ОсобенностьУчета = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция")Тогда
			ПризнакПредметаРасчета = ПредопределенноеЗначение("Справочник.ПризнакиПредметовРасчета.ПодакцизныйТовар");
		Иначе
			ПризнакПредметаРасчета = ПредопределенноеЗначение("Справочник.ПризнакиПредметовРасчета.Товар");
		КонецЕсли;
	Иначе
		Если ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга") Тогда
			ПризнакПредметаРасчета = ПредопределенноеЗначение("Справочник.ПризнакиПредметовРасчета.Услуга");
		ИначеЕсли ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПодарочныйСертификат") Тогда
			ПризнакПредметаРасчета = ПредопределенноеЗначение("Справочник.ПризнакиПредметовРасчета.Платеж");
		Иначе
			ПризнакПредметаРасчета = ПредопределенноеЗначение("Справочник.ПризнакиПредметовРасчета.Товар");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


